#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//Возвращает имена блокруемых реквизитов для механизма блокирования реквизитов БСП
//	Возвращаемое значание:
//		Массив - имена блокируемых реквизитов
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт

	Результат = Новый Массив;
	Результат.Добавить("ЕдиницаИзмерения");
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НаборыУпаковок.Ссылка КАК Значение,
	|	НаборыУпаковок.Наименование КАК Представление
	|ИЗ
	|	Справочник.НаборыУпаковок КАК НаборыУпаковок
	|ГДЕ
	|	НаборыУпаковок.Ссылка <> ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.БазовыеЕдиницыИзмерения)
	|	И НаборыУпаковок.Ссылка <> ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ИндивидуальныйДляНоменклатуры)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НаборыУпаковок.Наименование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Значение, Выборка.Представление); 
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы


// Обработчик обновления BAS УТ 3.2.2
Процедура ОбновитьДляУказанияКоличестваВРазличныхЕдиницах() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НаборыУпаковок.Ссылка,
	|	ЕдиницыИзмерения.УдалитьУпаковка КАК ЕдиницаИзмерения,
	|	ЕдиницыДляОтчетов.УдалитьУпаковка КАК ЕдиницаДляОтчетов,
	|	ВЫБОР
	|		КОГДА ЕдиницыИзмерения.УдалитьУпаковка = ЕдиницыДляОтчетов.УдалитьУпаковка
	|			ТОГДА 1
	|		ИНАЧЕ НаборыУпаковок.КоэффициентЕдиницыДляОтчетов
	|	КОНЕЦ КАК КоэффициентЕдиницыДляОтчетов
	|ИЗ
	|	Справочник.НаборыУпаковок КАК НаборыУпаковок
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УдалитьЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ПО (ЕдиницыИзмерения.Ссылка = НаборыУпаковок.ЕдиницаИзмерения)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УдалитьЕдиницыИзмерения КАК ЕдиницыДляОтчетов
	|		ПО (ЕдиницыДляОтчетов.Ссылка = НаборыУпаковок.ЕдиницаИзмерения)
	|ГДЕ
	|	ЕдиницыИзмерения.УдалитьУпаковка <> НаборыУпаковок.ЕдиницаИзмерения";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СпрОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ЗаполнитьЗначенияСвойств(СпрОбъект,Выборка);
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СпрОбъект);
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти

#КонецЕсли
