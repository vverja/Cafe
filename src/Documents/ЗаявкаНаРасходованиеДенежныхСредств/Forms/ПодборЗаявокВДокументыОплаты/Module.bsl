&НаКлиенте
Перем ВыполняетсяЗакрытие;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	ИспользуютсяОбъектыРасчетов = Параметры.ВидимостьОбъектаРасчетов;
	ИспользуютсяДоговорыКредитовДепозитов = Параметры.ВидимостьДоговораКредитаДепозита;
	ИспользуютсяДоговорыЗаймыСотрудникам  = Параметры.ВидимостьДоговораЗаймаСотруднику;
	
	Валюта = Параметры.Валюта;
	СуммаДокумента = Параметры.СуммаДокумента;
	
	Элементы.ТаблицаЗаявокЗаказ.Видимость = ИспользуютсяОбъектыРасчетов;
	Элементы.ТаблицаЗаявокДоговорКредитаДепозита.Видимость = ИспользуютсяДоговорыКредитовДепозитов;
	Элементы.ТаблицаЗаявокДоговорЗаймаСотруднику.Видимость = ИспользуютсяДоговорыЗаймыСотрудникам;
	
	АдресПлатежейВХранилище = Параметры.АдресПлатежейВХранилище;
	
	ЗаполнитьТаблицуЗаявок();
	ПодборТоваровКлиентСервер.СформироватьЗаголовокФормыПодбора(Заголовок, Параметры.Ссылка);
	
	МодификацияКонфигурацииПереопределяемый.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РассчитатьСуммуПлатежей();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	Если Не ВыполняетсяЗакрытие и Модифицированность Тогда
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект), НСтр("ru='Данные были изменены. Перенести изменения в документ?';uk='Дані були змінені. Перенести зміни в документ?'"), РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Ответ = РезультатВопроса;
    
    Если Ответ = КодВозвратаДиалога.Да Тогда
        ВыполняетсяЗакрытие = Истина;
        Модифицированность = Ложь;
        ПеренестиЗаявкиВДокумент();
        
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		ВыполняетсяЗакрытие = Истина;
		Модифицированность = Ложь;
        Закрыть();
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТаблицаЗаявокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.ТаблицаЗаявок.ТекущиеДанные <> Неопределено Тогда
		Если Поле.Имя = "ТаблицаЗаявокПредставлениеЗаявки" Тогда
			ПоказатьЗначение(Неопределено, Элементы.ТаблицаЗаявок.ТекущиеДанные.ЗаявкаНаРасходованиеДенежныхСредств);
		ИначеЕсли Поле.Имя = "ТаблицаЗаявокЗаказ" Тогда
			ПоказатьЗначение(Неопределено, Элементы.ТаблицаЗаявок.ТекущиеДанные.Заказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗаявокПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РассчитатьСуммуПлатежей();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьСтроки(Команда)
	
	ВыбратьВсеЗаявкиНаСервере(Истина);
	РассчитатьСуммуПлатежей();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьСтроки(Команда)
	
	ВыбратьВсеЗаявкиНаСервере(Ложь);
	РассчитатьСуммуПлатежей();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	ПеренестиЗаявкиВДокумент();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаЗаявокПредставлениеЗаявки.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаЗаявокЗаказ.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаЗаявок.ЗаявкаНаРасходованиеДенежныхСредств");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветГиперссылки);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаЗаявокЗаказ.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаЗаявок.Заказ");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветГиперссылки);
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаЗаявок.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаЗаявок.ПрисутствуетВДокументе");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Gray);
	
КонецПроцедуры

&НаСервере
Процедура ВыбратьВсеЗаявкиНаСервере(ЗначениеВыбора = Истина)
	
	Для Каждого СтрокаТаблицы Из ТаблицаЗаявок.НайтиСтроки(Новый Структура("СтрокаВыбрана", Не ЗначениеВыбора)) Цикл
		
		СтрокаТаблицы.СтрокаВыбрана = ЗначениеВыбора;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьЗаявкиВХранилище()
	
	// Формирование таблицы для возврата в документ
	ТаблицаВыбранныхЗаявок = ТаблицаЗаявок.Выгрузить(Новый Структура("СтрокаВыбрана", Истина));
	
	АдресЗаявокВХранилище = ПоместитьВоВременноеХранилище(ТаблицаВыбранныхЗаявок);
	
	Возврат АдресЗаявокВХранилище;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТаблицуЗаявок()
	
	ДанныеОтбора = Новый Структура(
		"Контрагент,
		|ПодотчетноеЛицо,
		|СтатьяРасходов,
		|АналитикаРасходов,
		|Организация,
		|ХозяйственнаяОперация,
		|Валюта,
		|ФормаОплаты,
		|БанковскийСчетКасса,
		|Ссылка,
		|Дата");
		
	ЗаполнитьЗначенияСвойств(ДанныеОтбора, Параметры);
	
	ДенежныеСредстваСервер.ЗаполнитьПоОстаткамЗаявокНаРасходованиеДенежныхСредств(ДанныеОтбора, ТаблицаЗаявок);
	
	УстановитьПризнакиПрисутствияСтрокиВДокументе(ТаблицаЗаявок);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПризнакиПрисутствияСтрокиВДокументе(ТаблицаЗаявок)
	
	Если ЭтоАдресВременногоХранилища(АдресПлатежейВХранилище) Тогда
		
		СтруктураПоиска = Новый Структура("СтатьяДвиженияДенежныхСредств, ЗаявкаНаРасходованиеДенежныхСредств, Сумма");
		Если ИспользуютсяОбъектыРасчетов Тогда
			СтруктураПоиска.Вставить("Заказ");
		КонецЕсли;
		Если ИспользуютсяДоговорыКредитовДепозитов Тогда
			СтруктураПоиска.Вставить("ДоговорКредитаДепозита");
		КонецЕсли;
		Если ИспользуютсяДоговорыЗаймыСотрудникам Тогда
			СтруктураПоиска.Вставить("ДоговорЗаймаСотруднику");
		КонецЕсли;
		
		РасшифровкаПлатежа = ПолучитьИзВременногоХранилища(АдресПлатежейВХранилище);
		
		Для каждого СтрокаПлатежа из ТаблицаЗаявок Цикл
			
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаПлатежа);
			
			Строки = РасшифровкаПлатежа.НайтиСтроки(СтруктураПоиска);
			Если Строки.Количество() Тогда
				СтрокаПлатежа.ПрисутствуетВДокументе = Истина;
			КонецЕсли;
			
			СтрокаПлатежа.СтрокаВыбрана = Не СтрокаПлатежа.ПрисутствуетВДокументе;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиЗаявкиВДокумент()

	// Снятие модифицированности, т.к. перед закрытием признак проверяется
	Модифицированность = Ложь;

	АдресЗаявокВХранилище = ПоместитьЗаявкиВХранилище();

	Закрыть();

	ОповеститьОВыборе(Новый Структура("АдресЗаявокВХранилище", АдресЗаявокВХранилище));

КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуПлатежей()
	
	СуммаПлатежей = 0;
	Для Каждого СтрокаТаблицы Из ТаблицаЗаявок Цикл
		
		Если СтрокаТаблицы.СтрокаВыбрана Тогда
			СуммаПлатежей = СуммаПлатежей + СтрокаТаблицы.Сумма;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

ВыполняетсяЗакрытие = Ложь;