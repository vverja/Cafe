#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.ДокументПоступления КАК ДокументПоступления,
	|	Таблица.Номенклатура        КАК Номенклатура,
	|	Таблица.Характеристика      КАК Характеристика,
	|	Таблица.Партнер             КАК Партнер,
	|	Таблица.ПредусмотренЗалог   КАК ПредусмотренЗалог,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.Количество
	|		ИНАЧЕ
	|			-Таблица.Количество
	|	КОНЕЦ                      КАК КоличествоПередЗаписью,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.Сумма
	|		ИНАЧЕ
	|			-Таблица.Сумма
	|	КОНЕЦ                      КАК СуммаПередЗаписью
	|ПОМЕСТИТЬ ДвиженияПринятаяВозвратнаяТараПередЗаписью
	|ИЗ
	|	РегистрНакопления.ПринятаяВозвратнаяТара КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	|";
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.ДокументПоступления         КАК ДокументПоступления,
	|	ТаблицаИзменений.Номенклатура                КАК Номенклатура,
	|	ТаблицаИзменений.Характеристика              КАК Характеристика,
	|	ТаблицаИзменений.Партнер                     КАК Партнер,
	|	ТаблицаИзменений.ПредусмотренЗалог           КАК ПредусмотренЗалог,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение)  КАК КоличествоИзменение,
	|	СУММА(ТаблицаИзменений.СуммаИзменение)       КАК СуммаИзменение
	|ПОМЕСТИТЬ ДвиженияПринятаяВозвратнаяТараИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.ДокументПоступления    КАК ДокументПоступления,
	|		Таблица.Номенклатура           КАК Номенклатура,
	|		Таблица.Характеристика         КАК Характеристика,
	|		Таблица.Партнер                КАК Партнер,
	|		Таблица.ПредусмотренЗалог      КАК ПредусмотренЗалог,
	|		Таблица.КоличествоПередЗаписью КАК КоличествоИзменение,
	|		Таблица.СуммаПередЗаписью      КАК СуммаИзменение
	|	ИЗ
	|		ДвиженияПринятаяВозвратнаяТараПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.ДокументПоступления КАК ДокументПоступления,
	|		Таблица.Номенклатура        КАК Номенклатура,
	|		Таблица.Характеристика      КАК Характеристика,
	|		Таблица.Партнер             КАК Партнер,
	|		Таблица.ПредусмотренЗалог   КАК ПредусмотренЗалог,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				-Таблица.Количество
	|			ИНАЧЕ
	|				Таблица.Количество
	|		КОНЕЦ                      КАК КоличествоИзменение,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				-Таблица.Сумма
	|			ИНАЧЕ
	|				Таблица.Сумма
	|		КОНЕЦ                      КАК СуммаИзменение
	|	ИЗ
	|		РегистрНакопления.ПринятаяВозвратнаяТара КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.ДокументПоступления,
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.Характеристика,
	|	ТаблицаИзменений.Партнер,
	|	ТаблицаИзменений.ПредусмотренЗалог
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) > 0
	|	ИЛИ СУММА(ТаблицаИзменений.СуммаИзменение) > 0
	|;
	|УНИЧТОЖИТЬ ДвиженияПринятаяВозвратнаяТараПередЗаписью
	|";
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	// Новые изменения были помещены во временную таблицу.
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияПринятаяВозвратнаяТараИзменение", Выборка.Количество > 0);

КонецПроцедуры

#КонецОбласти

#КонецЕсли