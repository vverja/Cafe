
&НаКлиенте
Процедура Выбрать(Команда)
	Если ИндексВыбора=0 Тогда
		Документ = "Реализация товаров и услуг";
	ИначеЕсли ИндексВыбора=1 Тогда
		Документ = "Акт выполненных работ";
	ИначеЕсли ИндексВыбора=2 Тогда
		Документ = "Акт на передачу прав";
	КонецЕсли;	
	Оповестить("ВыбранТипДокументаПродажи",Документ);
	Этаформа.Закрыть();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ИспользоватьАктыВыполненныхРаботПоНесколькимЗаказам 	= ПолучитьФункциональнуюОпцию("ИспользоватьАктыВыполненныхРаботПоНесколькимЗаказам");
	ИспользоватьРеализациюПоНесколькимЗаказам 		= ПолучитьФункциональнуюОпцию("ИспользоватьРеализациюПоНесколькимЗаказам");
	
	//определение количества позиций с различными вариантами оформления услуг
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказыКлиентовОстатки.Номенклатура КАК Номенклатура,
	               |	ЗаказыКлиентовОстатки.ЗаказКлиента КАК ЗаказКлиента,
	               |	ЗаказыКлиентовОстатки.КодСтроки КАК КодСтроки
	               |ИЗ
	               |	РегистрНакопления.ЗаказыКлиентов.Остатки(
	               |			,
	               |			ЗаказКлиента В (&МассивЗаказовДляАктовВыполненныхРабот)
	               |				И Номенклатура.ВариантОформленияПродажи = ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияПродажи.АктВыполненныхРабот)) КАК ЗаказыКлиентовОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаказыКлиентовОстатки.Номенклатура КАК Номенклатура,
	               |	ЗаказыКлиентовОстатки.ЗаказКлиента КАК ЗаказКлиента,
	               |	ЗаказыКлиентовОстатки.КодСтроки КАК КодСтроки
	               |ИЗ
	               |	РегистрНакопления.ЗаказыКлиентов.Остатки(
	               |			,
	               |			ЗаказКлиента В (&МассивЗаказовДляАктНаПередачуПрав)
	               |				И (Склад = &Склад
	               |					ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	               |				И Номенклатура.ВариантОформленияПродажи = ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияПродажи.АктНаПередачуПрав)) КАК ЗаказыКлиентовОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаказыКлиентовОстатки.Номенклатура КАК Номенклатура,
	               |	ЗаказыКлиентовОстатки.ЗаказКлиента КАК ЗаказКлиента,
	               |	ЗаказыКлиентовОстатки.КодСтроки КАК КодСтроки
	               |ИЗ
	               |	РегистрНакопления.ЗаказыКлиентов.Остатки(
	               |			,
	               |			ЗаказКлиента В (&МассивЗаказовДляРеализации)
	               |				И (Склад = &Склад
	               |					ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	               |				И Номенклатура.ВариантОформленияПродажи = ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияПродажи.РеализацияТоваровУслуг)) КАК ЗаказыКлиентовОстатки";
		   
	Запрос.УстановитьПараметр("МассивЗаказовДляАктовВыполненныхРабот", Параметры.СписокЗаказовДляАктовВыполненныхРабот.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("МассивЗаказовДляАктНаПередачуПрав", Параметры.СписокЗаказовДляАктНаПередачуПрав.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("МассивЗаказовДляРеализации", Параметры.СписокЗаказовДляРеализации.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("Склад", Параметры.Склад);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоАктВыполненныхРабот 	= РезультатЗапроса[0].Выгрузить().Количество();
	КоличествоАктНаПередачуПрав	 = РезультатЗапроса[1].Выгрузить().Количество();
	КоличествоРеализацияТоваровУслуг = РезультатЗапроса[2].Выгрузить().Количество();
	ТекстРеализация = НСтр("ru='Реализация товаров и услуг (%количество% поз.)';uk='Реалізація товарів і послуг (%количество% поз.)'");
	ТекстАктВыполненныхРабот = НСтр("ru='Акт выполненных работ (%количество% поз.)';uk='Акт виконаних робіт (%количество% поз.)'");
	ТекстАктНаПередачуПрав = НСтр("ru='Акт на передачу прав (%количество% поз.)';uk='Акт на передачу прав (%количество% поз.)'");
	Если КоличествоРеализацияТоваровУслуг> 0 Тогда
		Элементы.ИндексВыбора.СписокВыбора.Добавить(0, СтрЗаменить(ТекстРеализация,"%количество%",Формат(КоличествоРеализацияТоваровУслуг,"ЧГ=")));
	КонецЕсли;
	Если КоличествоАктВыполненныхРабот > 0 Тогда
		Элементы.ИндексВыбора.СписокВыбора.Добавить(1, СтрЗаменить(ТекстАктВыполненныхРабот,"%количество%",Формат(КоличествоАктВыполненныхРабот,"ЧГ=")));
	КонецЕсли;
	Если КоличествоАктНаПередачуПрав > 0 Тогда
		Элементы.ИндексВыбора.СписокВыбора.Добавить(2, СтрЗаменить(ТекстАктНаПередачуПрав,"%количество%",Формат(КоличествоАктНаПередачуПрав,"ЧГ=")));
	КонецЕсли;
	
	Если Параметры.СписокЗаказовДляРеализации.Количество()>1 
		ИЛИ Параметры.СписокЗаказовДляАктовВыполненныхРабот.Количество()>1
		ИЛИ Параметры.СписокЗаказовДляАктНаПередачуПрав.Количество()>1 Тогда
		Элементы.Декорация1.Заголовок = СтрЗаменить(Элементы.Декорация1.Заголовок,НСтр("ru='заказе';uk='замовленні'"),НСтр("ru='заказах';uk='замовленнях'"));
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьАктыНаПередачуПрав") Тогда
		Элементы.Декорация1.Заголовок = СтрЗаменить(Элементы.Декорация1.Заголовок, "%АктНаПередачуПрав%", НСтр("ru=', ""Акт на передачу прав""';uk=', ""Акт на передачу прав""'"));
	Иначе
		Элементы.Декорация1.Заголовок = СтрЗаменить(Элементы.Декорация1.Заголовок, "%АктНаПередачуПрав%", "");
	КонецЕсли;
	
КонецПроцедуры
