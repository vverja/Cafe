#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ВосстановитьНастройки();

	УстановитьВидимостьФлагов(Параметры.СписокВыбора);
	УстановитьПорядок(ЭтаФорма);
	УстановитьПредставлениеФлагов(Параметры.СписокВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьПодсказку();

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()

	СохранитьНастройки();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтгрузитьПриИзменении(Элемент)

	ПриИзмененииФлага(Элемент);

КонецПроцедуры

&НаКлиенте
Процедура РезервироватьПриИзменении(Элемент)

	ПриИзмененииФлага(Элемент);

КонецПроцедуры

&НаКлиенте
Процедура ОбособленноПриИзменении(Элемент)

	ПриИзмененииФлага(Элемент);

КонецПроцедуры

&НаКлиенте
Процедура РезервироватьКСрокуПриИзменении(Элемент)

	ПриИзмененииФлага(Элемент);

КонецПроцедуры

&НаКлиенте
Процедура ТребуетсяПриИзменении(Элемент)

	ПриИзмененииФлага(Элемент);

КонецПроцедуры

&НаКлиенте
Процедура НеТребуетсяПриИзменении(Элемент)

	ПриИзмененииФлага(Элемент);

КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура Заполнить(Команда)

	ПереченьВариантов = Новый Массив();
	Если Отгрузить Тогда
		

		Если Параметры.СписокВыбора = Неопределено
			Или Параметры.СписокВыбора.НайтиПоЗначению(
				ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить")) <> Неопределено Тогда

			ПереченьВариантов.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить"));

		КонецЕсли;

		Если Параметры.СписокВыбора = Неопределено
			Или Параметры.СписокВыбора.НайтиПоЗначению(
				ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно")) <> Неопределено Тогда

			ПереченьВариантов.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно"));

		КонецЕсли;

	КонецЕсли;

	Если Резервировать Тогда
		ПереченьВариантов.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.СоСклада"));
	КонецЕсли;

	Если РезервироватьКСроку Тогда
		ПереченьВариантов.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.ИзЗаказов"));
	КонецЕсли;

	Если Обособленно Тогда
		ПереченьВариантов.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Обособленно"));
	КонецЕсли;

	Если Требуется Тогда
		ПереченьВариантов.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Требуется"));
	КонецЕсли;

	Если НеТребуется Тогда
		ПереченьВариантов.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.НеТребуется"));
	КонецЕсли;

	Если ПереченьВариантов.Количество() = 0 Тогда
		Текст = НСтр("ru='Для заполнения необходимо установить хотя бы одну отметку';uk='Для заповнення необхідно встановити хоча б одну позначку'");
		ПоказатьПредупреждение(, Текст);
	Иначе
		ОповеститьОВыборе(ПереченьВариантов);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьПодсказку()

	НеобособленныеОстаткиНеУчитываютсяТекст = НСтр("ru='Фактические остатки необособленных товаров не анализируются.
                                                         |Для заполнения по остаткам нужно указать второе действие,
                                                         |которое будет установлено при отсутствии товара на складе:
                                                         |к обеспечению, обеспечивать обособленно, либо не обеспечивать.'
                                                         |;uk='Фактичні залишки невідокремлених товарів не аналізуються.
                                                         |Для заповнення за залишками потрібно вказати другу дію,
                                                         |яка буде встановлена при відсутності товару на складі:
                                                         |до забезпечення, забезпечувати відокремлено, або не забезпечувати.'");
	ОстаткиНеУчитываютсяТекст = НСтр("ru='Фактические остатки товаров не анализируются.
                                           |Для заполнения по остаткам нужно указать второе действие,
                                           |которое будет установлено при отстуствии товара на складе:
                                           |к обеспечению либо не обеспечивать).'
                                           |;uk='Фактичні залишки товарів не аналізуються.
                                           |Для заповнення за залишками потрібно вказати другу дію,
                                           |яка буде встановлена при відсутності товару на складі:
                                           |до забезпечення або не забезпечувати).'");

	ЗаполнениеДопустимо  = Истина;
	ЗаполнениеПоОстаткам = Истина;

	// Варианты заполнения без учета остатков.
	Если Отгрузить И Не РезервироватьКСроку И Не Обособленно И Не Требуется И Не НеТребуется Тогда

		ЗаполнениеПоОстаткам = Ложь;
		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru='Если есть обособленный запас - отгрузить обособленно.
                                   |Если ожидается обособленная поставка - обеспечивать обособленно.
                                   |Остальное количество - отгрузить.'
                                   |;uk='Якщо є відокремлений запас - відвантажити відокремлено.
                                   |Якщо очікується відокремлена поставка - забезпечувати відокремлено.
                                   |Решта - відвантажити.'");

			Предупреждение = НеобособленныеОстаткиНеУчитываютсяТекст;

		Иначе

			Подсказка = НСтр("ru='Все количество - отгрузить.';uk='Вся кількість - відвантажити.'");
			Предупреждение = ОстаткиНеУчитываютсяТекст;

		КонецЕсли;

	ИначеЕсли Резервировать И Не РезервироватьКСроку И Не Обособленно И Не Требуется И Не НеТребуется Тогда

		ЗаполнениеПоОстаткам = Ложь;
		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru='Если есть обособленный запас или ожидается
                                   |обособленная поставка - обеспечивать обособленно.
                                   |Остальное количество - резервировать на складе.'
                                   |;uk='Якщо є відокремлений запас або очікується
                                   |відокремлена поставка - забезпечувати відокремлено.
                                   |Решта - резервувати на складі.'");

			Предупреждение = НеобособленныеОстаткиНеУчитываютсяТекст;

		Иначе

			Подсказка = НСтр("ru='Все количество - резервировать на складе.';uk='Вся кількість - резервувати на складі.'");
			Предупреждение = ОстаткиНеУчитываютсяТекст;

		КонецЕсли;

	ИначеЕсли РезервироватьКСроку И Не Обособленно И Не Требуется И Не НеТребуется Тогда

		ЗаполнениеПоОстаткам = Ложь;
		ЗаполнениеДопустимо  = Ложь;
		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Предупреждение = НСтр("ru='Выбор действия резервировать к дате возможен только
                                        |совместно с одним из действий: обеспечивать обособленно,
                                        |к обеспечению, не обеспечивать.'
                                        |;uk='Вибір дії резервувати до дати можливий тільки
                                        |спільно з однією з дій: забезпечувати відокремлено,
                                        |до забезпечення, не забезпечувати.'");

		Иначе

			Предупреждение = НСтр("ru='Выбор действия резервировать к дате возможен только
                                        |совместно с одним из действий: к обеспечению, не обеспечивать.'
                                        |;uk='Вибір дії резервувати до дати можливий тільки
                                        |спільно з однією з дій: до забезпечення, не забезпечувати.'");

		КонецЕсли;

	// Варианты заполнения с учетом остатков.
	ИначеЕсли Отгрузить И Не РезервироватьКСроку И Обособленно Тогда

		Подсказка = НСтр("ru='Если есть обособленный запас - отгрузить обособленно.
                               |Если ожидается обособленная поставка - обеспечивать обособленно.
                               |Если нет обособленного запаса, ожидаемой обособленной поставки
                               |и товар есть на складе - отгрузить.
                               |Остальное количество - обеспечивать обособленно.'
                               |;uk='Якщо є відокремлений запас - відвантажити відокремлено.
                               |Якщо очікується відокремлена поставка - забезпечувати відокремлено.
                               |Якщо немає відокремленого запасу, очікуваної відокремленої поставки
                               |і товар є на складі - відвантажити.
                               |Решта - забезпечувати відокремлено.'");

	ИначеЕсли Отгрузить И Не РезервироватьКСроку И Требуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru='Если есть обособленный запас - отгрузить обособленно.
                                   |Если ожидается обособленная поставка - обеспечивать обособленно.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки
                                   |и товар есть на складе - отгрузить.
                                   |Остальное количество - к обеспечению.'
                                   |;uk='Якщо є відокремлений запас - відвантажити відокремлено.
                                   |Якщо очікується відокремлена поставка - забезпечувати відокремлено.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки
                                   |і товар є на складі - відвантажити.
                                   |Решта - до забезпечення.'");

		Иначе

			Подсказка = НСтр("ru='Если товар в наличии на складе - отгрузить.
                                   |Остальное количество - к обеспечению.'
                                   |;uk='Якщо товар в наявності на складі - відвантажити.
                                   |Решта - до забезпечення.'");

		КонецЕсли;

	ИначеЕсли Отгрузить И Не РезервироватьКСроку И НеТребуется Тогда


		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru='Если есть обособленный запас - отгрузить обособленно.
                                   |Если ожидается обособленная поставка - обеспечивать обособленно.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки
                                   |и товар есть на складе - отгрузить.
                                   |Остальное количество - не обеспечивать.'
                                   |;uk='Якщо є відокремлений запас - відвантажити відокремлено.
                                   |Якщо очікується відокремлена поставка - забезпечувати відокремлено.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки
                                   |і товар є на складі - відвантажити.
                                   |Решта - не забезпечувати.'");

		Иначе

			Подсказка = НСтр("ru='Если товар в наличии на складе - отгрузить.
                                   |Остальное количество - не обеспечивать.'
                                   |;uk='Якщо товар в наявності на складі - відвантажити.
                                   |Решта - не забезпечувати.'");

		КонецЕсли;

	ИначеЕсли Отгрузить И РезервироватьКСроку И Обособленно Тогда

		Подсказка = НСтр("ru='Если есть обособленный запас - отгрузить обособленно.
                               |Если ожидается обособленная поставка - обеспечивать обособленно.
                               |Если нет обособленного запаса, ожидаемой обособленной поставки
                               |и товар есть на складе - отгрузить.
                               |Если нет обособленного запаса, ожидаемой обособленной поставки,
                               |товара на складе и ожидается поставка - резервировать по мере поступления.
                               |Остальное количество - обеспечивать обособленно.'
                               |;uk='Якщо є відокремлений запас - відвантажити відокремлено.
                               |Якщо очікується відокремлена поставка - забезпечувати відокремлено.
                               |Якщо немає відокремленого запасу, очікуваної відокремленої поставки
                               |і товар є на складі - відвантажити.
                               |Якщо немає відокремленого запасу, очікуваної відокремленої поставки,
                               |товару на складі і очікується поставка - резервувати по мірі надходження.
                               |Решта - забезпечувати відокремлено.'");

	ИначеЕсли Отгрузить И РезервироватьКСроку И Требуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru='Если есть обособленный запас - отгрузить обособленно.
                                   |Если ожидается обособленная поставка - обеспечивать обособленно.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки
                                   |и товар есть на складе - отгрузить.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки,
                                   |товара на складе и ожидается поставка - резервировать по мере поступления.
                                   |Остальное количество - принять к обеспечению.'
                                   |;uk='Якщо є відокремлений запас - відвантажити відокремлено.
                                   |Якщо очікується відокремлена поставка - забезпечувати відокремлено.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки
                                   |і товар є на складі - відвантажити.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки,
                                   |товару на складі і очікується поставка - резервувати по мірі надходження.
                                   |Решта - прийняти до забезпечення.'");

		Иначе
		
			Подсказка = НСтр("ru='Если есть в наличии - отгрузить.
                            |Если нет в наличии, но ожидается поставка - резервировать к дате поступления.
                            |Если нет в наличии и поставка не ожидается - принять к обеспечению.'
                            |;uk='Якщо є в наявності - відвантажити.
                            |Якщо немає в наявності, але очікується поставка - резервувати до дати надходження.
                            |Якщо немає в наявності та поставка не очікується - прийняти до забезпечення.'");

		КонецЕсли;

	ИначеЕсли Отгрузить И РезервироватьКСроку И НеТребуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru='Если есть обособленный запас - отгрузить обособленно.
                                   |Если ожидается обособленная поставка - обеспечивать обособленно.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки
                                   |и товар есть на складе - отгрузить.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки,
                                   |товара на складе и ожидается поставка - резервировать по мере поступления.
                                   |Остальное количество - не обеспечивать.'
                                   |;uk='Якщо є відокремлений запас - відвантажити відокремлено.
                                   |Якщо очікується відокремлена поставка - забезпечувати відокремлено.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки
                                   |і товар є на складі - відвантажити.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки,
                                   |товару на складі і очікується поставка - резервувати по мірі надходження.
                                   |Решта - не забезпечувати.'");
			
		Иначе
		
			Подсказка = НСтр("ru='Если есть в наличии - отгрузить.
                                   |Если нет в наличии, но ожидается поставка - резервировать к дате поступления.
                                   |Если нет в наличии и поставка не ожидается - не обеспечивать.'
                                   |;uk='Якщо є в наявності - відвантажити.
                                   |Якщо немає в наявності, але очікується поставка - резервувати до дати надходження.
                                   |Якщо немає в наявності та поставка не очікується - не забезпечувати.'");

		КонецЕсли;

	ИначеЕсли Резервировать И Не РезервироватьКСроку И Обособленно Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru='Если есть обособленный запас или ожидается
                                   |обособленная поставка - обеспечивать обособленно.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки
                                   |и товар есть на складе - резервировать на складе.
                                   |Остальное количество - обеспечивать обособленно.'
                                   |;uk='Якщо є відокремлений запас або очікується
                                   |відокремлена поставка - забезпечувати відокремлено.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки
                                   |і товар є на складі - резервувати на складі.
                                   |Решта - забезпечувати відокремлено.'");

		Иначе

			Подсказка = НСтр("ru='Если есть в наличии - резервировать на складе.
                                   |Если нет в наличии - обеспечивать обособленно.'
                                   |;uk='Якщо є в наявності - резервувати на складі.
                                   |Якщо немає в наявності - забезпечувати відокремлено.'");

		КонецЕсли;

	ИначеЕсли Резервировать И Не РезервироватьКСроку И Требуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru='Если есть обособленный запас или ожидается
                                   |обособленная поставка - обеспечивать обособленно.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки
                                   |и товар есть на складе - резервировать на складе.
                                   |Остальное количество - принять к обеспечению.'
                                   |;uk='Якщо є відокремлений запас або очікується
                                   |відокремлена поставка - забезпечувати відокремлено.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки
                                   |і товар є на складі - резервувати на складі.
                                   |Решта - прийняти до забезпечення.'");

		Иначе

			Подсказка = НСтр("ru='Если есть в наличии - резервировать на складе.
                                   |Если нет в наличии - принять к обеспечению.'
                                   |;uk='Якщо є в наявності - резервувати на складі.
                                   |Якщо немає в наявності - прийняти до забезпечення.'");

		КонецЕсли;

	ИначеЕсли Резервировать И Не РезервироватьКСроку И НеТребуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru='Если есть обособленный запас или ожидается
                                   |обособленная поставка - обеспечивать обособленно.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки
                                   |и товар есть на складе - резервировать на складе.
                                   |Остальное количество - не обеспечивать.'
                                   |;uk='Якщо є відокремлений запас або очікується
                                   |відокремлена поставка - забезпечувати відокремлено.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки
                                   |і товар є на складі - резервувати на складі.
                                   |Решта - не забезпечувати.'");

		Иначе

			Подсказка = НСтр("ru='Если есть в наличии - резервировать на складе.
                                   |Если нет в наличии - не обеспечивать'
                                   |;uk='Якщо є в наявності - резервувати на складі.
                                   |Якщо немає в наявності - не забезпечувати'");

		КонецЕсли;

	ИначеЕсли Резервировать И РезервироватьКСроку И Не Обособленно И Не Требуется И Не НеТребуется Тогда

		ЗаполнениеПоОстаткам = Ложь;
		ЗаполнениеДопустимо  = Ложь;
		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Предупреждение = НСтр("ru='Выбор действия резервировать к дате возможен только
                                        |совместно с одним из действий: обеспечивать обособленно,
                                        |к обеспечению, не обеспечивать.'
                                        |;uk='Вибір дії резервувати до дати можливий тільки
                                        |спільно з однією з дій: забезпечувати відокремлено,
                                        |до забезпечення, не забезпечувати.'");

		Иначе

			Предупреждение = НСтр("ru='Выбор действия резервировать к дате возможен только
                                        |совместно с одним из действий: к обеспечению, не обеспечивать.'
                                        |;uk='Вибір дії резервувати до дати можливий тільки
                                        |спільно з однією з дій: до забезпечення, не забезпечувати.'");

		КонецЕсли;

	ИначеЕсли Резервировать И РезервироватьКСроку И Обособленно Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru='Если есть обособленный запас или ожидается
                                   |обособленная поставка - обеспечивать обособленно.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки
                                   |и товар есть на складе - резервировать на складе.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки,
                                   |товара на складе и ожидается поставка - резервировать к дате поступления.
                                   |Остальное количество - обеспечивать обособленно.'
                                   |;uk='Якщо є відокремлений запас або очікується
                                   |відокремлена поставка - забезпечувати відокремлено.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки
                                   |і товар є на складі - резервувати на складі.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки,
                                   |товару на складі і очікується поставка - резервувати до дати надходження.
                                   |Решта - забезпечувати відокремлено.'");

		Иначе

			Подсказка = НСтр("ru='Если есть в наличии - резервировать на складе.
                                   |Если нет в наличии, но ожидается поставка - резервировать к дате поступления.
                                   |Если нет в наличии и поставка не ожидается - обеспечивать обособленно.'
                                   |;uk='Якщо є в наявності - резервувати на складі.
                                   |Якщо немає в наявності, але очікується поставка - резервувати до дати надходження.
                                   |Якщо немає в наявності та поставка не очікується - забезпечувати відокремлено.'");

		КонецЕсли;

	ИначеЕсли Резервировать И РезервироватьКСроку И Требуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru='Если есть обособленный запас или ожидается
                                   |обособленная поставка - обеспечивать обособленно.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки
                                   |и товар есть на складе - резервировать на складе.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки,
                                   |товара на складе и ожидается поставка - резервировать к дате поступления.
                                   |Остальное количество - принять к обеспечению.'
                                   |;uk='Якщо є відокремлений запас або очікується
                                   |відокремлена поставка - забезпечувати відокремлено.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки
                                   |і товар є на складі - резервувати на складі.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки,
                                   |товару на складі і очікується поставка - резервувати до дати надходження.
                                   |Решта - прийняти до забезпечення.'");

		Иначе

			Подсказка = НСтр("ru='Если есть в наличии - резервировать на складе.
                                   |Если нет в наличии, но ожидается поставка - резервировать к дате поступления.
                                   |Если нет в наличии и поставка не ожидается - принять к обеспечению.'
                                   |;uk='Якщо є в наявності - резервувати на складі.
                                   |Якщо немає в наявності, але очікується поставка - резервувати до дати надходження.
                                   |Якщо немає в наявності та поставка не очікується - прийняти до забезпечення.'");

		КонецЕсли;

	ИначеЕсли Резервировать И РезервироватьКСроку И НеТребуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru='Если есть обособленный запас или ожидается
                                   |обособленная поставка - обеспечивать обособленно.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки
                                   |и товар есть на складе - резервировать на складе.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки,
                                   |товара на складе и ожидается поставка - резервировать к дате поступления.
                                   |Остальное количество - не обеспечивать.'
                                   |;uk='Якщо є відокремлений запас або очікується
                                   |відокремлена поставка - забезпечувати відокремлено.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки
                                   |і товар є на складі - резервувати на складі.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки,
                                   |товару на складі і очікується поставка - резервувати до дати надходження.
                                   |Решта - не забезпечувати.'");

		Иначе

			Подсказка = НСтр("ru='Если есть в наличии - резервировать на складе.
                                   |Если нет в наличии, но ожидается поставка - резервировать к дате поступления.
                                   |Если нет в наличии и поставка не ожидается - не обеспечивать.'
                                   |;uk='Якщо є в наявності - резервувати на складі.
                                   |Якщо немає в наявності, але очікується поставка - резервувати до дати надходження.
                                   |Якщо немає в наявності та поставка не очікується - не забезпечувати.'");

		КонецЕсли;

	ИначеЕсли РезервироватьКСроку И Обособленно Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru='Если есть обособленный запас или ожидается
                                   |обособленная поставка - обеспечивать обособленно.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки и товар
                                   |есть на складе или ожидается поставка - резервировать к дате поступления.
                                   |Остальное количество - обеспечивать обособленно.'
                                   |;uk='Якщо є відокремлений запас або очікується
                                   |відокремлена поставка - забезпечувати відокремлено.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки і товар
                                   |є на складі або очікується поставка - резервувати до дати надходження.
                                   |Решта - забезпечувати відокремлено.'");

		Иначе

			Подсказка = НСтр("ru='Если есть в наличии или ожидается поставка - резервировать к дате поступления.
                                   |Если есть в наличии и поставка не ожидается - обеспечивать обособленно.'
                                   |;uk='Якщо є в наявності або очікується поставка - резервувати до дати надходження.
                                   |Якщо є в наявності і поставка не очікується - забезпечувати відокремлено.'");

		КонецЕсли;

	ИначеЕсли РезервироватьКСроку И Требуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru='Если есть обособленный запас или ожидается
                                   |обособленная поставка - обеспечивать обособленно.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки и товар
                                   |есть на складе или ожидается поставка - резервировать к дате поступления.
                                   |Остальное количество - принять к обеспечению.'
                                   |;uk='Якщо є відокремлений запас або очікується
                                   |відокремлена поставка - забезпечувати відокремлено.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки і товар
                                   |є на складі або очікується поставка - резервувати до дати надходження.
                                   |Решта - прийняти до забезпечення.'");

		Иначе

			Подсказка = НСтр("ru='Если есть в наличии или ожидается поставка - резервировать к дате поступления.
                                   |Если есть в наличии и поставка не ожидается - принять к обеспечению.'
                                   |;uk='Якщо є в наявності або очікується поставка - резервувати до дати надходження.
                                   |Якщо є в наявності і поставка не очікується - прийняти до забезпечення.'");

		КонецЕсли;

	ИначеЕсли РезервироватьКСроку И НеТребуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru='Если есть обособленный запас или ожидается
                                   |обособленная поставка - обеспечивать обособленно.
                                   |Если нет обособленного запаса, ожидаемой обособленной поставки и товар
                                   |есть на складе или ожидается поставка - резервировать к дате поступления.
                                   |Остальное количество - не обеспечивать.'
                                   |;uk='Якщо є відокремлений запас або очікується
                                   |відокремлена поставка - забезпечувати відокремлено.
                                   |Якщо немає відокремленого запасу, очікуваної відокремленої поставки і товар
                                   |є на складі або очікується поставка - резервувати до дати надходження.
                                   |Решта - не забезпечувати.'");

		Иначе

			Подсказка = НСтр("ru='Если есть в наличии или ожидается поставка - резервировать к дате поступления.
                                   |Если есть в наличии и поставка не ожидается - не обеспечивать.'
                                   |;uk='Якщо є в наявності або очікується поставка - резервувати до дати надходження.
                                   |Якщо є в наявності і поставка не очікується - не забезпечувати.'");

		КонецЕсли;

	ИначеЕсли Обособленно Тогда

		Подсказка = НСтр("ru='Все строки - обеспечивать обособленно.';uk='Всі рядки - забезпечувати відокремлено.'");

	ИначеЕсли Требуется Тогда

		Подсказка = НСтр("ru='Все строки - принять к обеспечению.';uk='Всі рядки - прийняти до забезпечення.'");

	ИначеЕсли НеТребуется Тогда

		Подсказка = НСтр("ru='Все строки - не обеспечивать.';uk='Всі рядки - не забезпечувати.'");
		
	Иначе

		Если Параметры.СписокВыбора <> Неопределено Тогда

			Подсказка = НСтр("ru='Не выбраны действия для заполнения.
                        |Необходимо установить отметку/отметки.'
                        |;uk='Не вибрані дії для заповнення.
                        |Необхідно встановити позначку/позначки.'");

		Иначе

			Подсказка = НСтр("ru='Не выбраны действия для заполнения.
                        |Необходимо установить отметки (не более трех).'
                        |;uk='Не вибрані дії для заповнення.
                        |Необхідно встановити позначки (не більше трьох).'");

		КонецЕсли;

	КонецЕсли;

	Элементы.СтраницыПодсказки.ТекущаяСтраница = ?(ЗаполнениеПоОстаткам, Элементы.СтраницаПодсказкаПоОстаткам,
	                                             ?(ЗаполнениеДопустимо,  Элементы.СтраницаПодсказкаБезОстатков,
	                                                                     Элементы.СтраницаНедопустимыйВариант));

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииФлага(Элемент)

	Отгрузить = ?(Элемент = Элементы.Резервировать, Ложь, Отгрузить);
	
	Резервировать = ?(Элемент = Элементы.Отгрузить, Ложь, Резервировать);
						
	Обособленно = ?(Элемент = Элементы.Требуется 
					Или Элемент = Элементы.НеТребуется, Ложь, Обособленно);
					
	Требуется = ?(Элемент = Элементы.Обособленно 
					Или Элемент = Элементы.НеТребуется, Ложь, Требуется);
					
	НеТребуется = ?(Элемент = Элементы.Обособленно 
					Или Элемент = Элементы.Требуется, Ложь, НеТребуется);
					
	УстановитьПорядок(ЭтаФорма);
	УстановитьПодсказку();

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьФлагов(Список)

	Если Параметры.СписокВыбора <> Неопределено Тогда

		Элементы.ГруппаОтгрузить.Видимость =
			Список.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.Отгрузить) <> Неопределено
			Или Список.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.ОтгрузитьОбособленно) <> Неопределено;

		Элементы.ГруппаРезервировать.Видимость =
			Список.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.СоСклада) <> Неопределено;

		Элементы.ГруппаРезервироватьКСроку.Видимость =
			Список.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.ИзЗаказов) <> Неопределено;

		Элементы.ГруппаОбособленно.Видимость =
			Список.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.Обособленно) <> Неопределено;

		Элементы.ГруппаТребуется.Видимость =
			Список.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.Требуется) <> Неопределено;

		Элементы.ГруппаНеТребуется.Видимость =
			Список.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.НеТребуется) <> Неопределено;

	КонецЕсли;

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОбособленноеОбеспечениеЗаказов") Тогда

		Элементы.ГруппаОбособленно.Видимость = Ложь;

	Иначе

		Элементы.Отгрузить.Заголовок = НСтр("ru='Отгрузить (при необходимости обособленно)';uk='Відвантажити (при необхідності відокремлено)'");

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеФлагов(Список)
	
	Если Список = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ЭлементСписка Из Список Цикл
		Если НЕ ЗначениеЗаполнено(ЭлементСписка.Представление) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементСписка.Значение = Перечисления.ВариантыОбеспечения.Отгрузить Тогда
			Элементы.Отгрузить.Заголовок = ЭлементСписка.Представление;
		ИначеЕсли ЭлементСписка.Значение = Перечисления.ВариантыОбеспечения.СоСклада Тогда
			Элементы.Резервировать.Заголовок = ЭлементСписка.Представление;
		ИначеЕсли ЭлементСписка.Значение = Перечисления.ВариантыОбеспечения.ИзЗаказов Тогда
			Элементы.РезервироватьКСроку.Заголовок = ЭлементСписка.Представление;
		ИначеЕсли ЭлементСписка.Значение = Перечисления.ВариантыОбеспечения.Обособленно Тогда
			Элементы.Обособленно.Заголовок = ЭлементСписка.Представление;
		ИначеЕсли ЭлементСписка.Значение = Перечисления.ВариантыОбеспечения.Требуется Тогда
			Элементы.Требуется.Заголовок = ЭлементСписка.Представление;
		ИначеЕсли ЭлементСписка.Значение = Перечисления.ВариантыОбеспечения.НеТребуется Тогда
			Элементы.НеТребуется.Заголовок = ЭлементСписка.Представление;
		КонецЕсли; 
	КонецЦикла; 

	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетЗатратПоНаправлениямДеятельности") Тогда
		
		Если ЗначениеЗаполнено(Параметры.НаправлениеДеятельности) Тогда
			
			ЗаголовокОбособленно = НСтр("ru='Обеспечивать обособленно (под заказ)';uk='Забезпечувати відокремлено (під замовлення)'");
			
			УстановитьПривилегированныйРежим(Истина);
			ВариантОбособления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.НаправлениеДеятельности, "ВариантОбособления");
			УстановитьПривилегированныйРежим(Ложь);
			
			Если ВариантОбособления = Перечисления.ВариантыОбособленияПоНаправлениюДеятельности.ПоНаправлениюВЦелом Тогда
				
				ЗаголовокОбособленно = НСтр("ru='Обеспечивать обособленно (под направление деятельности)';uk='Забезпечувати відокремлено (під напрям діяльності)'");
				
			ИначеЕсли ВариантОбособления = Перечисления.ВариантыОбособленияПоНаправлениюДеятельности.ПоЗаказамНаправления Тогда
				
				ЗаголовокОбособленно = НСтр("ru='Обеспечивать обособленно (под заказ направления деятельности)';uk='Забезпечувати відокремлено (під замовлення напряму діяльності)'");
				
			КонецЕсли;
			
			Элементы.Обособленно.Заголовок = ЗаголовокОбособленно;
			
		КонецЕсли;
		
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПорядок(ЭтаФорма)

	СчетчикРезервироватьКСроку = 0;
	СчетчикНеобеспечено        = 0;

	Если ЭтаФорма.Отгрузить Тогда

		СчетчикРезервироватьКСроку = СчетчикРезервироватьКСроку + 1;
		СчетчикНеобеспечено        = СчетчикНеобеспечено + 1;

		ЭтаФорма.ОтгрузитьПорядок = 1;

	Иначе

		ЭтаФорма.ОтгрузитьПорядок = 0;

	КонецЕсли;

	Если ЭтаФорма.Резервировать Тогда

		СчетчикРезервироватьКСроку = СчетчикРезервироватьКСроку + 1;
		СчетчикНеобеспечено        = СчетчикНеобеспечено + 1;

		ЭтаФорма.РезервироватьПорядок = 1;

	Иначе

		ЭтаФорма.РезервироватьПорядок = 0;

	КонецЕсли;

	Если ЭтаФорма.РезервироватьКСроку Тогда

		СчетчикНеобеспечено = СчетчикНеобеспечено + 1;

		ЭтаФорма.РезервироватьКСрокуПорядок = 1 + СчетчикРезервироватьКСроку;

	Иначе

		ЭтаФорма.РезервироватьКСрокуПорядок = 0;

	КонецЕсли;

	ЭтаФорма.ОбособленноПорядок = ?(ЭтаФорма.Обособленно, 1 + СчетчикНеобеспечено, 0);
	ЭтаФорма.ТребуетсяПорядок   = ?(ЭтаФорма.Требуется,   1 + СчетчикНеобеспечено, 0);
	ЭтаФорма.НеТребуетсяПорядок = ?(ЭтаФорма.НеТребуется, 1 + СчетчикНеобеспечено, 0);

КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()

	Настройки = Новый Структура("ИзЗаказов, СоСклада, Обособленно, Отгрузить, Требуется, НеТребуется",
		РезервироватьКСроку, Резервировать, Обособленно, Отгрузить, Требуется, НеТребуется);

	Шаблон = НСтр("ru='Заполнение варианта обеспечения %1';uk='Заповнення варіанта забезпечення %1'");
	КлючНастройки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, Параметры.Тип);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("Перечисление.ВариантыОбеспечения", КлючНастройки, Настройки);

КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройки()

	Шаблон = НСтр("ru='Заполнение варианта обеспечения %1';uk='Заповнення варіанта забезпечення %1'");
	КлючНастройки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, Параметры.Тип);

	ЗначениеНастроек = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("Перечисление.ВариантыОбеспечения", КлючНастройки);

	Если ТипЗнч(ЗначениеНастроек) = Тип("Структура") Тогда

		ЗаполнитьЗначенияСвойств(ЭтаФорма, ЗначениеНастроек);
		РезервироватьКСроку = ЗначениеНастроек.ИзЗаказов;
		Резервировать = ЗначениеНастроек.СоСклада;

	КонецЕсли;

	Если Параметры.СписокВыбора <> Неопределено Тогда

		Если Параметры.СписокВыбора.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.ИзЗаказов) = Неопределено Тогда
			РезервироватьКСроку = Ложь;
		КонецЕсли;

		Если Параметры.СписокВыбора.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.СоСклада) = Неопределено Тогда
			Резервировать = Ложь;
		КонецЕсли;

		Если Параметры.СписокВыбора.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.Обособленно) = Неопределено Тогда
			Обособленно = Ложь;
		КонецЕсли;

		Если Параметры.СписокВыбора.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.Отгрузить) = Неопределено Тогда
			Отгрузить = Ложь;
		КонецЕсли;

		Если Параметры.СписокВыбора.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.Требуется) = Неопределено Тогда
			Требуется = Ложь;
		КонецЕсли;

		Если Параметры.СписокВыбора.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.НеТребуется) = Неопределено Тогда
			НеТребуется = Ложь;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти


