&НаКлиенте
Перем ВыполняетсяЗакрытие;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ДокументОбъект") 
		Или Не ТипЗнч(Параметры.ДокументОбъект) = Тип("ДанныеФормыСтруктура")
		Или НЕ Параметры.ДокументОбъект.Свойство("Ссылка") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ДокументОбъект,,"ПриемкаТоваров");
	ПриемкаТоваров.Загрузить(Параметры.ДокументОбъект.ПриемкаТоваров.Выгрузить());
	
	НастроитьФормуВЗависимостиОтТипаДокумента(Параметры.ДокументОбъект);
	
	Если ОбщегоНазначенияУТКлиентСервер.АвторизованВнешнийПользователь() 
		И Параметры.ДокументОбъект.Статус <> Перечисления.СтатусыАктаОРасхождениях.НеСогласовано Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
	ДолжностиДляПечатиКлиентСервер.ЗаполнитьСпискиВыбораДолжностейЧленовКомиссии(Элементы, ЭтотОбъект);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Не ВыполняетсяЗакрытие и Модифицированность И Не СохранитьПараметры Тогда
		
		СписокКнопок = Новый СписокЗначений();
		СписокКнопок.Добавить("Закрыть", НСтр("ru='Закрыть';uk='Закрити'"));
		СписокКнопок.Добавить("НеЗакрывать", НСтр("ru='Не закрывать';uk='Не закривати'"));
		
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект), НСтр("ru='Реквизиты печати акта приемки были изменены. Закрыть форму без сохранения реквизитов?';uk='Реквізити друку акта приймання були змінені. Закрити форму без збереження реквізитів?'"), СписокКнопок);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ОтветНаВопрос = РезультатВопроса;
	
	Если ОтветНаВопрос = "НеЗакрывать" Тогда
		СохранитьПараметры = Ложь;
	Иначе
		ВыполняетсяЗакрытие = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если СохранитьПараметры Тогда
		
		ОповеститьОВыборе(СтруктраВозвращаемыхДанных());
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЧленКомиссии1ПриИзменении(Элемент)

	ДолжностиДляПечатиКлиентСервер.ЗаполнитьСписокВыбораДолжностей(Элементы.ДолжностьЧленаКомиссии1,
		ЧленКомиссии1, Организация, Дата, ДолжностьЧленаКомиссии1);
	
КонецПроцедуры

&НаКлиенте
Процедура ЧленКомиссии2ПриИзменении(Элемент)

	ДолжностиДляПечатиКлиентСервер.ЗаполнитьСписокВыбораДолжностей(Элементы.ДолжностьЧленаКомиссии2,
		ЧленКомиссии2, Организация, Дата, ДолжностьЧленаКомиссии2);
	
КонецПроцедуры

&НаКлиенте
Процедура ЧленКомиссии3ПриИзменении(Элемент)

	ДолжностиДляПечатиКлиентСервер.ЗаполнитьСписокВыбораДолжностей(Элементы.ДолжностьЧленаКомиссии3,
		ЧленКомиссии3, Организация, Дата, ДолжностьЧленаКомиссии3);
	
КонецПроцедуры
 
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ТолькоПросмотр Тогда
		СохранитьПараметры = Истина;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


Функция СтруктраВозвращаемыхДанных()
	
	СтруктураВозврата = Новый Структура;
	
	РеквизитыФормы = ПолучитьРеквизиты();
	Для Каждого РеквизитФормы Из РеквизитыФормы Цикл
		
		СтруктураВозврата.Вставить(РеквизитФормы.Имя, ЭтотОбъект[РеквизитФормы.Имя]);
		
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

Процедура НастроитьФормуВЗависимостиОтТипаДокумента(ДокументОбъект)
	
	Если ТипЗнч(ДокументОбъект.Ссылка) = Тип("ДокументСсылка.АктОРасхожденияхПослеОтгрузки") Тогда
		
		Элементы.Руководитель.ОграничениеТипа     = Новый ОписаниеТипов("Строка");
		Элементы.ГлавныйБухгалтер.ОграничениеТипа = Новый ОписаниеТипов("Строка");
		
	ИначеЕсли ТипЗнч(ДокументОбъект.Ссылка) = Тип("ДокументСсылка.АктОРасхожденияхПослеПриемки") Тогда
		
		Элементы.Руководитель.ОграничениеТипа     = Новый ОписаниеТипов("СправочникСсылка.ОтветственныеЛицаОрганизаций");
		Элементы.ГлавныйБухгалтер.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.ОтветственныеЛицаОрганизаций");
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

ВыполняетсяЗакрытие = Ложь;