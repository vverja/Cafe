#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//Возвращает вариант классификации по умолчанию.
// Если в ИБ есть только один вариант, который не помечен на удаление,
// то будет возвращена ссылка на него, иначе будет возвращена ссылка на первый созданный в справочнике элемент.
//
// Возвращаемое значение:
//     СправочникСсылка.ВариантыКлассификацииЗадолженности - ссылка на вариант
//
Функция ЭлементПоУмолчанию(ТолькоЕслиОдин = Ложь) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	ЭлементПоУмолчанию = Справочники.ВариантыКлассификацииЗадолженности.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ВариантыКлассификацииЗадолженности.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВариантыКлассификацииЗадолженности КАК ВариантыКлассификацииЗадолженности
	|ГДЕ
	|	НЕ ВариантыКлассификацииЗадолженности.ПометкаУдаления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если ТолькоЕслиОдин И Выборка.Количество() = 1 
			Или Не ТолькоЕслиОдин Тогда
			ЭлементПоУмолчанию = Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ЭлементПоУмолчанию;
КонецФункции

#Область ОбновлениеИнформационнойБазы

//Выполняет первоначальное заполнение справочника
//
Процедура СоздатьЭлементПоУмолчаниюМонопольно() Экспорт
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Поле
	|ИЗ
	|	Справочник.ВариантыКлассификацииЗадолженности КАК ВариантыКлассификацииЗадолженности");
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		// Режим отсрочки - по календарным дням
		ЭлементПоУмолчанию = Справочники.ВариантыКлассификацииЗадолженности.СоздатьЭлемент();
		
		ЭлементПоУмолчанию.Наименование = НСтр("ru='Стандартная классификация';uk='Стандартна класифікація'");
		
		НовыйИнтервал = ЭлементПоУмолчанию.Интервалы.Добавить();
		НовыйИнтервал.НижняяГраницаИнтервала = 1;
		НовыйИнтервал.ВерхняяГраницаИнтервала = 2;
		НовыйИнтервал.НаименованиеИнтервала = НСтр("ru='От 1 до 2 дней';uk='Від 1 до 2 днів'");
		
		НовыйИнтервал = ЭлементПоУмолчанию.Интервалы.Добавить();
		НовыйИнтервал.НижняяГраницаИнтервала = 3;
		НовыйИнтервал.ВерхняяГраницаИнтервала = 14;
		НовыйИнтервал.НаименованиеИнтервала = НСтр("ru='От 3 до 14 дней';uk='Від 3 до 14 днів'");
		
		НовыйИнтервал = ЭлементПоУмолчанию.Интервалы.Добавить();
		НовыйИнтервал.НижняяГраницаИнтервала = 15;
		НовыйИнтервал.ВерхняяГраницаИнтервала = 29;
		НовыйИнтервал.НаименованиеИнтервала = НСтр("ru='От 15 до 29 дней';uk='Від 15 до 29 днів'");
		
		НовыйИнтервал = ЭлементПоУмолчанию.Интервалы.Добавить();
		НовыйИнтервал.НижняяГраницаИнтервала = 30;
		НовыйИнтервал.ВерхняяГраницаИнтервала = 44;
		НовыйИнтервал.НаименованиеИнтервала = НСтр("ru='От 30 до 44 дней';uk='Від 30 до 44 днів'");
		
		НовыйИнтервал = ЭлементПоУмолчанию.Интервалы.Добавить();
		НовыйИнтервал.НижняяГраницаИнтервала = 45;
		НовыйИнтервал.ВерхняяГраницаИнтервала = 59;
		НовыйИнтервал.НаименованиеИнтервала = НСтр("ru='От 45 до 59 дней';uk='Від 45 до 59 днів'");
		
		НовыйИнтервал = ЭлементПоУмолчанию.Интервалы.Добавить();
		НовыйИнтервал.НижняяГраницаИнтервала = 60;
		НовыйИнтервал.ВерхняяГраницаИнтервала = 179;
		НовыйИнтервал.НаименованиеИнтервала = НСтр("ru='От 60 до 179 дней';uk='Від 60 до 179 днів'");
		
		НовыйИнтервал = ЭлементПоУмолчанию.Интервалы.Добавить();
		НовыйИнтервал.НижняяГраницаИнтервала = 180;
		НовыйИнтервал.ВерхняяГраницаИнтервала = 9999999999;
		НовыйИнтервал.НаименованиеИнтервала = НСтр("ru='Свыше 180 дней';uk='Понад 180 днів'");
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ЭлементПоУмолчанию);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	Если ВидФормы = "ФормаСписка" 
		И НЕ Константы.ИспользоватьНесколькоКлассификацийЗадолженности.Получить() Тогда
		
		Параметры.Вставить("Ключ", ЭлементПоУмолчанию());
		ВыбраннаяФорма = "ФормаЭлемента";
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли