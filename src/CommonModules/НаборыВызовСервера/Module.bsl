////////////////////////////////////////////////////////////////////////////////
// Модуль "НаборыВызовСервера", содержит процедуры и функции для
// работы с наборами
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Функция возвращает вариант комплектации номенклатуры по-умолчанию
// 
// Параметры:
//  Номенклатура - Позиция номенклатуры
//  Характеристика - Характеристика номенклатуры
//
// Возвращаемое значение:
//  СправочникСсылка.ВариантыКомплектацииНоменклатуры
Функция ВариантКомплектацииНоменклатурыПоУмолчанию(Знач Номенклатура, Знач Характеристика) Экспорт
	
	ВариантКомплектацииНоменклатуры = Неопределено;
	
	Если Характеристика = Неопределено Тогда
		Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВариантыКомплектацииНоменклатуры.Ссылка КАК ВариантКомплектацииНоменклатуры
	|ИЗ
	|	Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
	|ГДЕ
	|	ВариантыКомплектацииНоменклатуры.Владелец = &Владелец
	|	И ВариантыКомплектацииНоменклатуры.Характеристика = &Характеристика
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВариантыКомплектацииНоменклатуры.Основной УБЫВ,
	|	ВариантыКомплектацииНоменклатуры.ПометкаУдаления");
	
	Запрос.УстановитьПараметр("Владелец", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ВариантКомплектацииНоменклатуры = Выборка.ВариантКомплектацииНоменклатуры;
	КонецЕсли;
	
	Возврат ВариантКомплектацииНоменклатуры;
	
КонецФункции

// Функция возвращает параметры варианта комплектации номенклатуры
// 
// Параметры:
//  Номенклатура - Позиция номенклатуры
//  Характеристика - Характеристика номенклатуры
//
// Возвращаемое значение:
//  Структура параметров или Неопределено
Функция ПараметрыВариантаКомплектацииНоменклатуры(Знач Номенклатура, Знач Характеристика) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Характеристика = Неопределено Тогда
		Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВариантыКомплектацииНоменклатуры.Ссылка                                    КАК ВариантКомплектацииНоменклатуры,
	|	ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора                  КАК ВариантРасчетаЦеныНабора,
	|	ВариантыКомплектацииНоменклатуры.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ВариантыКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента           КАК НоменклатураОсновногоКомпонента,
	|	ВариантыКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента         КАК ХарактеристикаОсновногоКомпонента,
	|	ВариантыКомплектацииНоменклатуры.Товары.(
	|		Ссылка,
	|		НомерСтроки,
	|		Номенклатура,
	|		Характеристика,
	|		Упаковка,
	|		КоличествоУпаковок,
	|		ДоляСтоимости,
	|		Количество
	|	) КАК Комплектующие
	|ИЗ
	|	Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
	|ГДЕ
	|	ВариантыКомплектацииНоменклатуры.Владелец = &Владелец
	|	И ВариантыКомплектацииНоменклатуры.Характеристика = &Характеристика
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВариантыКомплектацииНоменклатуры.Основной УБЫВ");
	
	Запрос.УстановитьПараметр("Владелец", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	
	Результат = Запрос.Выполнить().Выгрузить();
	Если Результат.Количество() > 0 Тогда
		
		ВозвращаемоеЗначение = Новый Структура;
		ВозвращаемоеЗначение.Вставить("ВариантКомплектацииНоменклатуры",           Результат[0].ВариантКомплектацииНоменклатуры);
		ВозвращаемоеЗначение.Вставить("ВариантРасчетаЦеныНабора",                  Результат[0].ВариантРасчетаЦеныНабора);
		ВозвращаемоеЗначение.Вставить("ВариантПредставленияНабораВПечатныхФормах", Результат[0].ВариантПредставленияНабораВПечатныхФормах);
		ВозвращаемоеЗначение.Вставить("Комплектующие",                             Результат[0].Комплектующие);
		ВозвращаемоеЗначение.Вставить("НоменклатураОсновногоКомпонента",           Результат[0].НоменклатураОсновногоКомпонента);
		ВозвращаемоеЗначение.Вставить("ХарактеристикаОсновногоКомпонента",         Результат[0].ХарактеристикаОсновногоКомпонента);
		
		Возврат ВозвращаемоеЗначение;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Функция возвращает комплектующие набора, их цены и остатки на складах
// 
// Параметры:
//  Параметры - Описание параметров набора
//  ДополнительныеПараметры - Прочие параметры (Расчет цен и остатков)
//
// Возвращаемое значение:
//  Массив с комплектующими набора
Функция Комплектующие(Параметры, ДополнительныеПараметры) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Параметры.ВариантКомплектацииНоменклатуры) Тогда
		ВариантКомплектацииНоменклатуры = ВариантКомплектацииНоменклатурыПоУмолчанию(
			Параметры.НоменклатураНабора,
			Параметры.ХарактеристикаНабора);
	Иначе
		ВариантКомплектацииНоменклатуры = Параметры.ВариантКомплектацииНоменклатуры;
	КонецЕсли;
	
	Если Не Параметры.Свойство("КоличествоУпаковок") Тогда
		Параметры.Вставить("КоличествоУпаковок", 1);
	КонецЕсли;
	
	ПодобранныеТовары = Новый Массив;
	
	Если Не ЗначениеЗаполнено(ВариантКомплектацииНоменклатуры) Тогда
		ТекстОшибки = НСтр("ru='Для номенклатуры %1 не определен состав набора';uk='Для номенклатури %1 не визначено склад набору'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстОшибки,
			Параметры.НоменклатураНабора,
			Параметры.ХарактеристикаНабора);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Возврат ПодобранныеТовары;
	КонецЕсли;
	
	Комплектующие = НаборыСервер.СоздатьТаблицуСоставаНабора(КолонкиНабора());
	Комплектующие.Колонки.Добавить("ТипНоменклатуры", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыНоменклатуры"));
	Комплектующие.Колонки.Добавить("ВидНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.ВидыНоменклатуры"));
	Комплектующие.Колонки.Добавить("ЕдиницаИзмерения", Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"));
	
	ВариантРасчетаЦеныНабора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВариантКомплектацииНоменклатуры, "ВариантРасчетаЦеныНабора");
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Т.Номенклатура КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	СУММА(Т.Количество) КАК Количество
		|ПОМЕСТИТЬ ОстаткиИзЗаказов
		|ИЗ
		|	РегистрСведений.ДоступныеОстаткиПланируемыхПоступлений КАК Т
		|ГДЕ
		|	Т.ДатаДоступности = ДАТАВРЕМЯ(1, 1, 1)
		|	И Т.Количество < 0
		|	И &ФильтрСкладыТ
		|СГРУППИРОВАТЬ ПО
		|	Т.Номенклатура,
		|	Т.Характеристика
		|////////////////////////////////////////////////////////////////////////////////////
		|
		|;
		|ВЫБРАТЬ
		|	ВариантыКомплектацииНоменклатурыТовары.НомерСтроки КАК НомерСтроки,
		|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура КАК Номенклатура,
		|	ВариантыКомплектацииНоменклатурыТовары.Характеристика КАК Характеристика,
		|	ВариантыКомплектацииНоменклатурыТовары.Упаковка КАК Упаковка,
		|	ВариантыКомплектацииНоменклатурыТовары.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ВариантыКомплектацииНоменклатурыТовары.Количество КАК Количество,
		|	ВариантыКомплектацииНоменклатурыТовары.ДоляСтоимости КАК ДоляСтоимости
		|ПОМЕСТИТЬ
		|	ВариантыКомплектацииНоменклатурыТовары
		|ИЗ
		|	Справочник.ВариантыКомплектацииНоменклатуры.Товары КАК ВариантыКомплектацииНоменклатурыТовары
		|ГДЕ
		|	ВариантыКомплектацииНоменклатурыТовары.Ссылка = &Ссылка
		|;
		|ВЫБРАТЬ
		|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура КАК Номенклатура,
		|	ВариантыКомплектацииНоменклатурыТовары.Характеристика КАК Характеристика,
		|	СУММА(ЕСТЬNULL(СвободныеОстатки.ВНаличииОстаток, 0) - ЕСТЬNULL(СвободныеОстатки.ВРезервеПодЗаказОстаток, 0) -
		|	ЕСТЬNULL(СвободныеОстатки.ВРезервеСоСкладаОстаток, 0) + ЕСТЬNULL(ОстаткиИзЗаказов.Количество, 0)) КАК Доступно,
		|	СУММА(ЕСТЬNULL(СвободныеОстатки.ВНаличииОстаток, 0)) КАК ВНаличии
		|ПОМЕСТИТЬ
		|	ДоступностьТоваров
		|ИЗ
		|	ВариантыКомплектацииНоменклатурыТовары КАК ВариантыКомплектацииНоменклатурыТовары
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.СвободныеОстатки.Остатки(
		|			,
		|			(Номенклатура,Характеристика) В
		|				(ВЫБРАТЬ
		|					ВариантыКомплектацииНоменклатурыТовары.Номенклатура КАК Номенклатура,
		|					ВариантыКомплектацииНоменклатурыТовары.Характеристика КАК Характеристика
		|				ИЗ
		|					ВариантыКомплектацииНоменклатурыТовары КАК ВариантыКомплектацииНоменклатурыТовары)
		|			И &ФильтрСклады
		|		) КАК СвободныеОстатки
		|	ПО СвободныеОстатки.Номенклатура = ВариантыКомплектацииНоменклатурыТовары.Номенклатура
		|		И СвободныеОстатки.Характеристика = ВариантыКомплектацииНоменклатурыТовары.Характеристика
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ОстаткиИзЗаказов КАК ОстаткиИзЗаказов
		|		ПО ОстаткиИзЗаказов.Номенклатура = ВариантыКомплектацииНоменклатурыТовары.Номенклатура
		|		 И ОстаткиИзЗаказов.Характеристика = ВариантыКомплектацииНоменклатурыТовары.Характеристика
		|СГРУППИРОВАТЬ ПО
		|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура,
		|	ВариантыКомплектацииНоменклатурыТовары.Характеристика
		|;
		|ВЫБРАТЬ
		|	ВариантыКомплектацииНоменклатурыТовары.НомерСтроки КАК НомерСтроки,
		|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура КАК Номенклатура,
		|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
		|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВариантыКомплектацииНоменклатурыТовары.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ВариантыКомплектацииНоменклатурыТовары.Характеристика КАК Характеристика,
		|	ВариантыКомплектацииНоменклатурыТовары.Упаковка КАК Упаковка,
		|	ВариантыКомплектацииНоменклатурыТовары.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ВариантыКомплектацииНоменклатурыТовары.Количество КАК Количество,
		|	ВариантыКомплектацииНоменклатурыТовары.ДоляСтоимости КАК ДоляСтоимости,
		|	ДоступностьТоваров.Доступно / ЕстьNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК Доступно,
		|	ДоступностьТоваров.ВНаличии / ЕстьNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК ВНаличии
		|ИЗ
		|	ВариантыКомплектацииНоменклатурыТовары КАК ВариантыКомплектацииНоменклатурыТовары
		|ЛЕВОЕ СОЕДИНЕНИЕ ДоступностьТоваров КАК ДоступностьТоваров
		|	ПО ДоступностьТоваров.Номенклатура = ВариантыКомплектацииНоменклатурыТовары.Номенклатура
		|	И ДоступностьТоваров.Характеристика = ВариантыКомплектацииНоменклатурыТовары.Характеристика
		|");
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ВЫРАЗИТЬ(ВариантыКомплектацииНоменклатурыТовары.Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения)",
		"ВЫРАЗИТЬ(ВариантыКомплектацииНоменклатурыТовары.Номенклатура КАК Справочник.Номенклатура)"));
		
	Запрос.УстановитьПараметр("Ссылка",  ВариантКомплектацииНоменклатуры);
	
	Если ДополнительныеПараметры.Свойство("Склады") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ФильтрСкладыТ", "Т.Склад В(&Склады)");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ФильтрСклады",  "Склад В (&Склады)");
		Запрос.УстановитьПараметр("Склады",  ДополнительныеПараметры.Склады);
	Иначе
		Запрос.УстановитьПараметр("ФильтрСклады",  Истина);
		Запрос.УстановитьПараметр("ФильтрСкладыТ", Истина);
	КонецЕсли;
		
	Выборка = Запрос.ВыполнитьПакет()[3].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Комплектующие.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.КоличествоУпаковок   = НоваяСтрока.КоличествоУпаковок;
		НоваяСтрока.Количество           = НоваяСтрока.Количество;
		НоваяСтрока.НоменклатураНабора   = Параметры.НоменклатураНабора;
		НоваяСтрока.ХарактеристикаНабора = Параметры.ХарактеристикаНабора;
		
	КонецЦикла;
	
	Если ВариантРасчетаЦеныНабора = Перечисления.ВариантыРасчетаЦенНаборов.РассчитываетсяИзЦенКомплектующих Тогда
		
		Если ДополнительныеПараметры.Свойство("ВидЦены") Тогда
			ЦеныРассчитаны = ПродажиСервер.ЗаполнитьЦены(
				Комплектующие,
				Неопределено, // Массив строк или структура отбора
				Новый Структура( // Параметры заполнения
					"Дата, Валюта, ВидЦены, РасчитыватьНаборы, ПоляЗаполнения",
					ДополнительныеПараметры.Дата,
					ДополнительныеПараметры.Валюта,
					ДополнительныеПараметры.ВидЦены,
					Истина,
					"Цена, ВидЦены"
				));
		ИначеЕсли ДополнительныеПараметры.Свойство("Соглашение") Тогда
			ЦеныРассчитаны = ПродажиСервер.ЗаполнитьЦены(
				Комплектующие,
				Неопределено, // Массив строк или структура отбора
				Новый Структура( // Параметры заполнения
					"Дата, Валюта, Соглашение, РасчитыватьНаборы, ПоляЗаполнения",
					ДополнительныеПараметры.Дата,
					ДополнительныеПараметры.Валюта,
					ДополнительныеПараметры.Соглашение,
					Истина,
					"Цена, ВидЦены"
				));
		КонецЕсли;
		
		Для Каждого СтрокаТЧ Из Комплектующие Цикл
			
			ПараметрыТовара = ПодборТоваровКлиентСервер.ПараметрыТовара();
			ЗаполнитьЗначенияСвойств(ПараметрыТовара, Параметры);
			
			ПараметрыТовара.Номенклатура       = СтрокаТЧ.Номенклатура;
			ПараметрыТовара.Характеристика     = СтрокаТЧ.Характеристика;
			ПараметрыТовара.Упаковка           = СтрокаТЧ.Упаковка;
			ПараметрыТовара.ТипНоменклатуры    = СтрокаТЧ.ТипНоменклатуры;
			ПараметрыТовара.ВидНоменклатуры    = СтрокаТЧ.ВидНоменклатуры;
			ПараметрыТовара.ВидЦены            = СтрокаТЧ.ВидЦены;
			ПараметрыТовара.Цена               = СтрокаТЧ.Цена;
			ПараметрыТовара.КоличествоУпаковок = СтрокаТЧ.КоличествоУпаковок * Параметры.КоличествоУпаковок;
			ПараметрыТовара.Количество         = СтрокаТЧ.Количество * Параметры.КоличествоУпаковок;
			ПараметрыТовара.Доступно           = СтрокаТЧ.Доступно;
			ПараметрыТовара.ВНаличии           = СтрокаТЧ.ВНаличии;
			
			Если ПараметрыТовара.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга
				ИЛИ ПараметрыТовара.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа Тогда
				ПараметрыТовара.Склад = Неопределено;
			КонецЕсли;
			
			ПараметрыТовара.ХарактеристикиИспользуются = ?(ЗначениеЗаполнено(ПараметрыТовара.Характеристика), Истина, Ложь);
			
			ПодобранныеТовары.Добавить(ПараметрыТовара);
			
		КонецЦикла;
		
	Иначе
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами") И ДополнительныеПараметры.Свойство("Соглашение") Тогда
			ПараметрыРасчета = Новый Структура( // Параметры заполнения
				"Дата, Валюта, Соглашение, ПоляЗаполнения",
				ДополнительныеПараметры.Дата,
				ДополнительныеПараметры.Валюта,
				ДополнительныеПараметры.Соглашение,
				"Цена");
		Иначе
			ПараметрыРасчета = Новый Структура( // Параметры заполнения
				"Дата, Валюта, ВидЦены, ПоляЗаполнения",
				ДополнительныеПараметры.Дата,
				ДополнительныеПараметры.Валюта,
				ДополнительныеПараметры.ВидЦены,
				"Цена");
		КонецЕсли;
		
		КоэффициентыПропорциональностиРасчетаЦенНаборов = НаборыСервер.КоэффициентыРаспределения(
			Комплектующие.Скопировать(), ПараметрыРасчета);
		
		ОбщаяСтоимость = 0;
		Для Каждого ТекущаяСтрока Из КоэффициентыПропорциональностиРасчетаЦенНаборов Цикл
			ОбщаяСтоимость = ОбщаяСтоимость + ТекущаяСтрока.Цена;
		КонецЦикла;

		СуммаКРаспределению = ДополнительныеПараметры.Цена * Параметры.КоличествоУпаковок;
		
		СлужебнаяТЧ = Новый ТаблицаЗначений;
		СлужебнаяТЧ.Колонки.Добавить("СтрокаТЧ");
		СлужебнаяТЧ.Колонки.Добавить("НомерСтроки");
		
		Комплектующие.Сортировать("КоличествоУпаковок Убыв");
		Для Каждого ТекущаяСтрока Из Комплектующие Цикл
			
			Стоимость = КоэффициентыПропорциональностиРасчетаЦенНаборов.Найти(ТекущаяСтрока.НомерСтроки, "НомерСтроки").Цена;
			
			ПараметрыТовара = ПодборТоваровКлиентСервер.ПараметрыТовара();
			ЗаполнитьЗначенияСвойств(ПараметрыТовара, Параметры);
			
			ПараметрыТовара.Номенклатура       = ТекущаяСтрока.Номенклатура;
			ПараметрыТовара.Характеристика     = ТекущаяСтрока.Характеристика;
			ПараметрыТовара.Упаковка           = ТекущаяСтрока.Упаковка;
			ПараметрыТовара.ТипНоменклатуры    = ТекущаяСтрока.ТипНоменклатуры;
			ПараметрыТовара.ВидНоменклатуры    = ТекущаяСтрока.ВидНоменклатуры;
			ПараметрыТовара.КоличествоУпаковок = ТекущаяСтрока.КоличествоУпаковок * Параметры.КоличествоУпаковок;
			ПараметрыТовара.Количество         = ТекущаяСтрока.Количество * Параметры.КоличествоУпаковок;
			
			Если ПараметрыТовара.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга
				ИЛИ ПараметрыТовара.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа Тогда
				ПараметрыТовара.Склад = Неопределено;
			КонецЕсли;
			
			Если ДополнительныеПараметры.Свойство("ВидЦены") Тогда
				ПараметрыТовара.ВидЦены = ДополнительныеПараметры.ВидЦены;
			Иначе
				ПараметрыТовара.ВидЦены = Справочники.ВидыЦен.ПустаяСсылка();
			КонецЕсли;
			
			Если ПараметрыТовара.КоличествоУпаковок <> 0 И ОбщаяСтоимость <> 0 Тогда
				ПараметрыТовара.Цена = Окр(((Стоимость / ОбщаяСтоимость) * СуммаКРаспределению) / ПараметрыТовара.КоличествоУпаковок, 2);
			Иначе
				ПараметрыТовара.Цена = 0;
			КонецЕсли;
			
			ОбщаяСтоимость = ОбщаяСтоимость - Стоимость;
			СуммаКРаспределению = СуммаКРаспределению - ПараметрыТовара.Цена * ПараметрыТовара.КоличествоУпаковок;
			
			ПараметрыТовара.Доступно = ТекущаяСтрока.Доступно;
			ПараметрыТовара.ВНаличии = ТекущаяСтрока.ВНаличии;
			
			ПараметрыТовара.ХарактеристикиИспользуются = ?(ЗначениеЗаполнено(ПараметрыТовара.Характеристика), Истина, Ложь);
			
			СлужебнаяСтрока = СлужебнаяТЧ.Добавить();
			СлужебнаяСтрока.СтрокаТЧ = ПараметрыТовара;
			СлужебнаяСтрока.НомерСтроки = ТекущаяСтрока.НомерСтроки;
			
		КонецЦикла;
		
		Если СуммаКРаспределению <> 0 И СлужебнаяТЧ.Количество() > 0 Тогда
			
			ПоследняяСтрока = СлужебнаяТЧ[СлужебнаяТЧ.Количество() - 1];
			ИсходнаяСтрока = ПоследняяСтрока.СтрокаТЧ;
			
			Если ИсходнаяСтрока.КоличествоУпаковок > 1 Тогда
				НоваяСтрока = ПодборТоваровКлиентСервер.ПараметрыТовара();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ИсходнаяСтрока);
				
				ИсходнаяСтрока.КоличествоУпаковок = ИсходнаяСтрока.КоличествоУпаковок - 1;
				НоваяСтрока.КоличествоУпаковок = 1;
				НоваяСтрока.Цена = НоваяСтрока.Цена + СуммаКРаспределению;
				
				СлужебнаяСтрока = СлужебнаяТЧ.Добавить();
				СлужебнаяСтрока.СтрокаТЧ = НоваяСтрока;
				СлужебнаяСтрока.НомерСтроки = ПоследняяСтрока.НомерСтроки;
				
			Иначе
				
				ИсходнаяСтрока.Погрешность = СуммаКРаспределению;
				
			КонецЕсли;
			
		КонецЕсли;
		
		СлужебнаяТЧ.Сортировать("НомерСтроки Возр");
		Для Каждого СтрокаТЧ Из СлужебнаяТЧ Цикл
			ПодобранныеТовары.Добавить(СтрокаТЧ.СтрокаТЧ);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ПодобранныеТовары;

КонецФункции

// Процедура дополняет массив строк табличной части оставшимися строками наборов,
// если указанные наборы в массиве строк представлены не полностью.
// 
// Параметры:
//  ТабличнаяЧасть - ТабличнаяЧасть
//  МассивСтрок - Массив строк табличной часи
//
// Возвращаемое значение:
//  Нет
Процедура ДополнитьДоПолногоНабора(ТабличнаяЧасть, МассивСтрок, УчитыватьКодСтроки = Ложь) Экспорт
	
	Если МассивСтрок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДобавляемыеСтроки = Новый Массив;
	
	ТаблицаНоменклатура = Новый ТаблицаЗначений;
	ТаблицаНоменклатура.Колонки.Добавить("НоменклатураНабора", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаНоменклатура.Колонки.Добавить("ХарактеристикаНабора", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Для Каждого СтрокаТЧ Из МассивСтрок Цикл
		Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(СтрокаТЧ, "НоменклатураНабора")
			И ЗначениеЗаполнено(СтрокаТЧ.НоменклатураНабора) Тогда
			НоваяСтрока = ТаблицаНоменклатура.Добавить();
			НоваяСтрока.НоменклатураНабора = СтрокаТЧ.НоменклатураНабора;
			НоваяСтрока.ХарактеристикаНабора = СтрокаТЧ.ХарактеристикаНабора;
		КонецЕсли;
	КонецЦикла;
	ТаблицаНоменклатура.Свернуть("НоменклатураНабора,ХарактеристикаНабора");
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Т.НоменклатураНабора,
	|	Т.ХарактеристикаНабора
	|ПОМЕСТИТЬ Таблица
	|ИЗ
	|	&Таблица КАК Т
	|;
	|ВЫБРАТЬ
	|	Т.НоменклатураНабора,
	|	Т.ХарактеристикаНабора,
	|	ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора
	|ИЗ
	|	Таблица КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
	|		ПО ВариантыКомплектацииНоменклатуры.Владелец = Т.НоменклатураНабора
	|		И ВариантыКомплектацииНоменклатуры.Характеристика = Т.ХарактеристикаНабора
	|		И ВариантыКомплектацииНоменклатуры.Основной
	|");
	
	Запрос.УстановитьПараметр("Таблица", ТаблицаНоменклатура);
	
	Данные = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТЧ Из МассивСтрок Цикл
		
		Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(СтрокаТЧ, "НоменклатураНабора")
			И ЗначениеЗаполнено(СтрокаТЧ.НоменклатураНабора) Тогда
			
			Отбор = Новый Структура;
			Отбор.Вставить("НоменклатураНабора", СтрокаТЧ.НоменклатураНабора);
			Отбор.Вставить("ХарактеристикаНабора", СтрокаТЧ.ХарактеристикаНабора);
			
			НайденныеСтроки = Данные.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество() > 0 Тогда
				Если НайденныеСтроки[0].ВариантРасчетаЦеныНабора = Перечисления.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоДолям
					ИЛИ НайденныеСтроки[0].ВариантРасчетаЦеныНабора = Перечисления.ВариантыРасчетаЦенНаборов.ЦенаЗадаетсяЗаНаборРаспределяетсяПоЦенам Тогда
					НайденныеСтрокиТабличнойЧасти = ТабличнаяЧасть.НайтиСтроки(Отбор);
					Для Каждого НайденнаяСтрока Из НайденныеСтрокиТабличнойЧасти Цикл
						
						Если УчитыватьКодСтроки И НайденнаяСтрока.КодСтроки <> 0 Тогда
							Продолжить;	
						КонецЕсли;
						
						Если МассивСтрок.Найти(НайденнаяСтрока) = Неопределено Тогда
							 ДобавляемыеСтроки.Добавить(НайденнаяСтрока);
						КонецЕсли;
						 
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из ДобавляемыеСтроки Цикл
		МассивСтрок.Добавить(СтрокаТЧ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КолонкиНабора()
	
	Колонки = Новый Массив;
	Колонки.Добавить("НомерСтроки");
	Колонки.Добавить("НоменклатураКомплекта");
	Колонки.Добавить("ХарактеристикаКомплекта");
	Колонки.Добавить("Номенклатура");
	Колонки.Добавить("Характеристика");
	Колонки.Добавить("Цена");
	Колонки.Добавить("ВидЦены");
	Колонки.Добавить("Упаковка");
	Колонки.Добавить("Количество");
	Колонки.Добавить("КоличествоУпаковок");
	Колонки.Добавить("ПроцентРучнойСкидки");
	Колонки.Добавить("СуммаРучнойСкидки");
	Колонки.Добавить("Доступно");
	Колонки.Добавить("ВНаличии");
	
	Возврат Колонки;
	
КонецФункции

#КонецОбласти
