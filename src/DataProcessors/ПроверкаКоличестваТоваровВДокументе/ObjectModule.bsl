#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не СкладыСервер.ИспользоватьАдресноеХранение(Склад,Помещение,ДатаОтгрузки) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Упаковка");
		ПолеЕдиницыИзмерения = "Упаковка";
	Иначе
		НоменклатураСервер.ПроверитьЗаполнениеУпаковок(ЭтотОбъект,МассивНепроверяемыхРеквизитов,Отказ);
		ПолеЕдиницыИзмерения = "ЕдиницаИзмерения";
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Товары.КоличествоУпаковок");
	
	Для Каждого СтрТабл из Товары Цикл
		Если СтрТабл.НеОтгружать = 1
			И СтрТабл.КоличествоУпаковок = 0 Тогда
		
			ТекстСообщения = НСтр("ru='Не заполнено количество в неотгружаемой строке номер %НомерСтроки%.';uk='Не заповнена кількість в рядку, який не відвантажується, номер %НомерСтроки%.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", СтрТабл.НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрТабл.НомерСтроки, "КоличествоУпаковок");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,"Объект",Отказ);
			
		КонецЕсли;
	
		Если СтрТабл.Количество = 0 
			И СтрТабл.КоличествоУпаковок <> 0 Тогда
			
			ТекстСообщения = НСтр("ru='Обнаружено нулевое количество при пересчете в единицу хранения в строке %НомерСтроки% списка ""%ПредставлениеТЧ%""';uk='Виявлена нульова кількість при перерахунку на одиницю зберігання в рядку %НомерСтроки% списку ""%ПредставлениеТЧ%""'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Строка(СтрТабл.НомерСтроки));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПредставлениеТЧ%", НСтр("ru='Товары';uk='Товари'"));
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрТабл.НомерСтроки, "КоличествоУпаковок");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,"Объект",Отказ);
			
		КонецЕсли;
		
		Если СтрТабл.СтатусПроблемы = 4 Тогда
			
			Если СтрТабл.КоличествоУпаковокВДокументе > 0 Тогда
				ТекстСообщения = НСтр("ru='Отобрано на больше, чем требуется по ордеру. Необходимо для %КоличествоОтобраноБольше% %ЕдиницаИзмерения% отметить, что это количество не нужно отгружать.';uk='Відібрано на більше, ніж потрібно по ордеру. Необхідно для %КоличествоОтобраноБольше% %ЕдиницаИзмерения% зазначити, що цю кількість не треба відвантажувати.'");
				
				ТекстСообщения = СтрЗаменить(ТекстСообщения,"%КоличествоОтобраноБольше%", СтрТабл.КоличествоУпаковокСумма - СтрТабл.КоличествоУпаковокВДокументе);  
				ТекстСообщения = СтрЗаменить(ТекстСообщения,"%ЕдиницаИзмерения%", СтрТабл[ПолеЕдиницыИзмерения]);  
			Иначе
				Если СтрТабл.КоличествоУпаковокСумма > 0 Тогда
					ТекстСообщения = НСтр("ru='В ордере нет такого товара. Необходимо отметить, что эту строку не нужно отгружать.';uk='В ордері немає такого товару. Необхідно зазначити, що цей рядок не треба відвантажувати.'");
				Иначе
					ТекстСообщения = НСтр("ru='В ордере нет такого товара. Необходимо заполнить количество и отметить, что эту строку не нужно отгружать.';uk='В ордері немає такого товару. Необхідно заповнити кількість і відзначити, що цей рядок не треба відвантажувати.'");
				КонецЕсли;
			КонецЕсли;
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрТабл.НомерСтроки, "СтатусПроблемы");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,"Объект",Отказ);
		КонецЕсли;
		
	КонецЦикла;

	
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект,МассивНепроверяемыхРеквизитов,Отказ);
	
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект,
		НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Обработки.ПроверкаКоличестваТоваровВДокументе),
		Отказ,
		МассивНепроверяемыхРеквизитов);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗавершитьПроверку(УчитываемыеПоля) Экспорт
	
	//1. Формируем таблицу по данным документа.
	// - если упаковки учитываются, то по упаковкам, иначе в базовых
	// - серии учитываем, если это не серии, которые указываются по факту отбора
	// - неотгружаемые строки игнорируем - их всегда будем брать из обработки
	//2. Формируем таблицу по данным обработки.
	// - если упаковки учитываются, то по упаковкам, иначе в базовых
	// - серии учитываем, если это не серии, которые указываются по факту отбора
	//3. Сравниваем сумму отружаемых строк с документом - получаем таблицу недостач
	//4. Все неотгружаемые строки разделяем
	// - на излишки (по ним потом нужно будет делать документ результата проверки и их нужно перенести в документ)
	// - просто неотружаемые (их просто нужно просто перенести в документ)
	//5. Если излишков и недостач нет, то проверяем расхождения по сериям (по факту отбора с учетом остатков) - по
	// таким сериям нужно сформировать документ результата проверки
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Ордер);
	Запрос.УстановитьПараметр("ТоварыИзОбработки",Товары.Выгрузить());
	Запрос.УстановитьПараметр("УчитыватьУпаковки", УчитываемыеПоля.УчитыватьУпаковки);
	Запрос.УстановитьПараметр("УчитыватьСерии", УчитываемыеПоля.УчитыватьСерии);
	Запрос.УстановитьПараметр("НеУчитываемыеСтатусыСерий", УчитываемыеПоля.НеУчитываемыеСтатусыСерий);
	
	Запрос.УстановитьПараметр("ПоВсейТЧ",Истина);
	Запрос.УстановитьПараметр("ЭтоУдаление",Ложь);
	Запрос.УстановитьПараметр("НомерСтроки",Неопределено);
	Запрос.УстановитьПараметр("Номенклатура",Неопределено);
	Запрос.УстановитьПараметр("Характеристика",Неопределено);
	Запрос.УстановитьПараметр("Назначение",Неопределено);
	Запрос.УстановитьПараметр("Упаковка",Неопределено);
	Запрос.УстановитьПараметр("Серия",Неопределено);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапросаВременныеТаблицыДокументаИОбработки();
	Запрос.ВыполнитьПакет();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.Серия,
	|	СУММА(ВложенныйЗапрос.КоличествоВДокументе - ВложенныйЗапрос.КоличествоКОтгрузке) КАК КоличествоНедобор,
	|	СУММА(ВложенныйЗапрос.КоличествоКОтгрузке) КАК КоличествоКОтгрузке,
	|	СУММА(ВложенныйЗапрос.КоличествоВДокументе) КАК КоличествоВДокументе
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыИзОрдера.Номенклатура КАК Номенклатура,
	|		ТоварыИзОрдера.Характеристика КАК Характеристика,
	|		ТоварыИзОрдера.Назначение КАК Назначение,
	|		ТоварыИзОрдера.Серия КАК Серия,
	|		0 КАК КоличествоКОтгрузке,
	|		ТоварыИзОрдера.Количество КАК КоличествоВДокументе
	|	ИЗ
	|		ТоварыИзОрдера КАК ТоварыИзОрдера
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТоварыИзОбработки КАК ТоварыИзОбработки
	|			ПО ТоварыИзОрдера.Номенклатура = ТоварыИзОбработки.Номенклатура
	|				И ТоварыИзОрдера.Характеристика = ТоварыИзОбработки.Характеристика
	|				И ТоварыИзОрдера.Назначение = ТоварыИзОбработки.Назначение
	|				И ТоварыИзОрдера.Упаковка = ТоварыИзОбработки.Упаковка
	|				И ТоварыИзОрдера.Серия = ТоварыИзОбработки.Серия
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыИзОбработки.Номенклатура,
	|		ТоварыИзОбработки.Характеристика,
	|		ТоварыИзОбработки.Назначение,
	|		ТоварыИзОбработки.Серия,
	|		ТоварыИзОбработки.Количество,
	|		0
	|	ИЗ
	|		ТоварыИзОбработки КАК ТоварыИзОбработки) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Серия,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.КоличествоВДокументе - ВложенныйЗапрос.КоличествоКОтгрузке) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыИзОбработки.Номенклатура,
	|	ТоварыИзОбработки.Характеристика,
	|	ТоварыИзОбработки.Назначение,
	|	ТоварыИзОбработки.Упаковка,
	|	ТоварыИзОбработки.Серия,
	|	ТоварыИзОбработки.КоличествоНеОтгружать КАК Количество,
	|	ТоварыИзОбработки.КоличествоУпаковокНеОтгружать КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыИзОбработки КАК ТоварыИзОбработки
	|ГДЕ
	|	ТоварыИзОбработки.КоличествоУпаковокНеОтгружать > 0";
	
	Результат = Запрос.ВыполнитьПакет();
	
	Недобор.Загрузить(Результат[0].Выгрузить());
	Неотгружаемые.Загрузить(Результат[1].Выгрузить());
	
	ВсеХорошо = Истина;
	
	Если Неотгружаемые.Количество() = 0
		И Недобор.Количество() = 0 Тогда
		
		ВсеХорошо = ПеренестиРезультатыВОрдер(УчитываемыеПоля);
		
	КонецЕсли;
	
	Возврат ВсеХорошо;
	
КонецФункции

Функция ОтразитьРасхожденияВУчете(УчитываемыеПоля) Экспорт

	ВсеХорошо = ПеренестиРезультатыВОрдер(УчитываемыеПоля);
	
	Возврат ВсеХорошо;
	
КонецФункции

Функция ПеренестиРезультатыВОрдер(УчитываемыеПоля)
	
	ЭтоОрдерНаПеремещение = ТипЗнч(Ордер) = Тип("ДокументСсылка.ОрдерНаПеремещениеТоваров");
	
	ДокументОбъект = Ордер.ПолучитьОбъект();
	
	Если РежимИсправления Тогда
		
		Если ЭтоОрдерНаПеремещение Тогда
			ДокументОбъект.ОтгружаемыеТовары.Очистить();
		Иначе
			
			УсловиеОтбораСтрок = Новый Структура;
			УсловиеОтбораСтрок.Вставить("ЭтоУпаковочныйЛист", Ложь);
			УсловиеОтбораСтрок.Вставить("УпаковочныйЛистРодитель", Документы.УпаковочныйЛист.ПустаяСсылка());
			
			НайденныеСтроки = ДокументОбъект.ОтгружаемыеТовары.НайтиСтроки(УсловиеОтбораСтрок);
			Для Каждого СтрМас из НайденныеСтроки Цикл
				ДокументОбъект.ОтгружаемыеТовары.Удалить(СтрМас);
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		УсловиеОтбораСтрок = Новый Структура;
		УсловиеОтбораСтрок.Вставить("Действие", Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать);
		
		НайденныеСтроки = ДокументОбъект.ОтгружаемыеТовары.НайтиСтроки(УсловиеОтбораСтрок);
		Для Каждого СтрМас из НайденныеСтроки Цикл
			ДокументОбъект.ОтгружаемыеТовары.Удалить(СтрМас);
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого СтрТабл из Товары Цикл
		
		Если СтрТабл.КоличествоУпаковок = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТовары = ДокументОбъект.ОтгружаемыеТовары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТовары, СтрТабл);
		Если Не ЭтоОрдерНаПеремещение Тогда
			СтрокаТовары.УпаковочныйЛистРодитель = СтрТабл.УпаковочныйЛист;
			СтрокаТовары.УпаковочныйЛист = Документы.УпаковочныйЛист.ПустаяСсылка();
		КонецЕсли;
		СтрокаТовары.СтатусУказанияСерий = 0;
		Если СтрТабл.НеОтгружать Тогда
			СтрокаТовары.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать;
		Иначе	
			СтрокаТовары.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить;
		КонецЕсли;
		
	КонецЦикла;
	
	ЕстьСтрокиКОтбору = Ложь;
	
	Если ВариантУчетаНедобора = "Добрать" Тогда
		Для Каждого СтрТабл из Недобор Цикл
			СтрокаТовары = ДокументОбъект.ОтгружаемыеТовары.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТовары, СтрТабл);
			СтрокаТовары.СтатусУказанияСерий = 0;
			СтрокаТовары.Количество          = СтрТабл.КоличествоНедобор;
			СтрокаТовары.КоличествоУпаковок  = СтрТабл.КоличествоНедобор;
			СтрокаТовары.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать;
			ЕстьСтрокиКОтбору = Истина;
		КонецЦикла;
	КонецЕсли;
	
	Если ЭтоОрдерНаПеремещение Тогда
		Если Не ЕстьСтрокиКОтбору Тогда
			ДокументОбъект.Статус = Перечисления.СтатусыОрдеровНаПеремещение.КОтгрузке;
		Иначе
			ДокументОбъект.Статус = Перечисления.СтатусыОрдеровНаПеремещение.КОтбору;
		КонецЕсли;
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ДокументОбъект,Документы.ОрдерНаПеремещениеТоваров);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокументОбъект, ПараметрыУказанияСерий);
	Иначе
		Если Не ЕстьСтрокиКОтбору Тогда
			ДокументОбъект.Статус = Перечисления.СтатусыРасходныхОрдеров.КОтгрузке;
		Иначе
			ДокументОбъект.Статус = Перечисления.СтатусыРасходныхОрдеров.КОтбору;
		КонецЕсли;
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ДокументОбъект,Документы.РасходныйОрдерНаТовары);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокументОбъект, ПараметрыУказанияСерий.ОтгружаемыеТовары);
		
		Если Недобор.Количество() <> 0
			И ВариантУчетаНедобора = "УменьшитьОрдер" Тогда
			Документы.РасходныйОрдерНаТовары.ИзменитьТоварыПоРаспоряжениямПоОтгружаемымТоварам(ДокументОбъект);
		КонецЕсли;
		
		ДокументОбъект.ВсегоМест = УпаковочныеЛистыСервер.КоличествоМестВТЧ(ДокументОбъект.ОтгружаемыеТовары);
	КонецЕсли;
	
	ДокументРасхожденийОбъект = СформироватьДокументРасхождений(ДокументОбъект);
	
	ВсеХорошо = Истина;
	
	Если Не ЭтоОрдерНаПеремещение Тогда
		ДополнитьОрдерСтрокамиУпаковочныхЛистов(ДокументОбъект);
	КонецЕсли;
	
	Попытка
		НачатьТранзакцию();
		Если ДокументРасхожденийОбъект <> Неопределено Тогда
			ДокументРасхожденийОбъект.Записать(РежимЗаписиДокумента.Проведение);
			ДокументОтраженияРезультатов = ДокументРасхожденийОбъект.Ссылка;
		КонецЕсли;	
		
		Если Не ЭтоОрдерНаПеремещение 
			И ДокументОбъект.ОтгрузкаПоЗаданиюНаПеревозку
			И Не ЗначениеЗаполнено(ДокументОбъект.ЗаданиеНаПеревозку) Тогда
			
			Если Не ЗначениеЗаполнено(ЭтотОбъект.ЗаданиеНаПеревозку)
				И ДокументОбъект.Статус = Перечисления.СтатусыРасходныхОрдеров.КОтгрузке Тогда
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("ru = 'Не выбрано задание на перевозку. Невозможно завершить проверку.'");
				ДокументОтраженияРезультатов = Документы.КорректировкаПоОрдеруНаТовары.ПустаяСсылка();
				ВсеХорошо = Ложь;
				ОтменитьТранзакцию();
				Возврат ВсеХорошо;
			КонецЕсли;	
			
			ДокументОбъект.ЗаданиеНаПеревозку = ЭтотОбъект.ЗаданиеНаПеревозку;
		КонецЕсли;
		
		Если ДокументОбъект.ОтгружаемыеТовары.Количество() = 0 Тогда
			ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ИнформацияОбОшибке().Описание);
		ДокументОтраженияРезультатов = Документы.КорректировкаПоОрдеруНаТовары.ПустаяСсылка();
		ВсеХорошо = Ложь;
		ОтменитьТранзакцию();
	КонецПопытки;
	
	Если Всехорошо
		И Не ЭтоОрдерНаПеремещение Тогда
		УпаковочныеЛистыСервер.СинхронизироватьУпаковочныеЛистыСРасходнымОрдером(ДокументОбъект.Ссылка, УчитываемыеПоля.УчитыватьУпаковки);
	КонецЕсли;
	
	Возврат ВсеХорошо;
	
КонецФункции

Функция СформироватьДокументРасхождений(ОрдерОбъект)
	
	//1. Посчитаем по заданиям на отбор, сколько отобрали
	//2. Посчитаем то, что показывает обработка
	//3. Посчитаем, сколько уже учтено в документах расхождений
	//4. Посчитаем, сколько в ордере отгружаемых строк и неотгружаемых
	//5. На разницу создадим новый документ расхождений
	
	Если Не СкладыСервер.ИспользоватьАдресноеХранение(Склад, Помещение, ОрдерОбъект.Дата) Тогда
		Возврат Неопределено;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТоварыИзОбработки.Номенклатура,
	|	ТоварыИзОбработки.Характеристика,
	|	ТоварыИзОбработки.Назначение,
	|	ТоварыИзОбработки.Упаковка,
	|	ВЫБОР
	|		КОГДА НЕ ТоварыИзОбработки.СтатусУказанияСерий В (0, 1, 2)
	|			ТОГДА ТоварыИзОбработки.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Серия,
	|	ТоварыИзОбработки.Количество КАК Количество,
	|	ТоварыИзОбработки.КоличествоУпаковок КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ТоварыИзОбработки
	|ИЗ
	|	&ТоварыИзОбработки КАК ТоварыИзОбработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.Серия,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
	|	СУММА(ВложенныйЗапрос.КоличествоУпаковок) КАК КоличествоУпаковок
	|ПОМЕСТИТЬ Расхождения
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыОтбор.Номенклатура КАК Номенклатура,
	|		ТоварыОтбор.Характеристика КАК Характеристика,
	|		ТоварыОтбор.Назначение КАК Назначение,
	|		ТоварыОтбор.Упаковка КАК Упаковка,
	|		ТоварыОтбор.Серия КАК Серия,
	|		ТоварыОтбор.КоличествоОтобрано КАК Количество,
	|		ТоварыОтбор.КоличествоУпаковокОтобрано КАК КоличествоУпаковок
	|	ИЗ
	|		Документ.ОтборРазмещениеТоваров.ТоварыОтбор КАК ТоварыОтбор
	|	ГДЕ
	|		ТоварыОтбор.Ссылка.Проведен
	|		И ТоварыОтбор.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтбораРазмещенияТоваров.Отбор)
	|		И ТоварыОтбор.Ссылка.Распоряжение = &Ордер
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыИзОбработки.Номенклатура,
	|		ТоварыИзОбработки.Характеристика,
	|		ТоварыИзОбработки.Назначение,
	|		ТоварыИзОбработки.Упаковка,
	|		ТоварыИзОбработки.Серия,
	|		-ТоварыИзОбработки.Количество,
	|		-ТоварыИзОбработки.КоличествоУпаковок
	|	ИЗ
	|		ТоварыИзОбработки КАК ТоварыИзОбработки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Товары.Номенклатура,
	|		Товары.Характеристика,
	|		Товары.Назначение,
	|		Товары.Упаковка,
	|		Товары.Серия,
	|		ВЫБОР
	|			КОГДА Товары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировкиОрдераНаТовары.ПеренестиВДругойОрдер)
	|				ТОГДА ВЫБОР
	|						КОГДА Товары.Ссылка.Ордер = &Ордер
	|							ТОГДА -Товары.Количество
	|						ИНАЧЕ Товары.Количество
	|					КОНЕЦ
	|			КОГДА Товары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировкиОрдераНаТовары.ОтразитьНедостачу)
	|				ТОГДА -Товары.Количество
	|			ИНАЧЕ Товары.Количество
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Товары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировкиОрдераНаТовары.ПеренестиВДругойОрдер)
	|				ТОГДА ВЫБОР
	|						КОГДА Товары.Ссылка.Ордер = &Ордер
	|							ТОГДА -Товары.КоличествоУпаковок
	|						ИНАЧЕ Товары.КоличествоУпаковок
	|					КОНЕЦ
	|			КОГДА Товары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировкиОрдераНаТовары.ОтразитьНедостачу)
	|				ТОГДА -Товары.КоличествоУпаковок
	|			ИНАЧЕ Товары.КоличествоУпаковок
	|		КОНЕЦ
	|	ИЗ
	|		Документ.КорректировкаПоОрдеруНаТовары.Товары КАК Товары
	|	ГДЕ
	|		Товары.Ссылка.Проведен
	|		И (Товары.Ссылка.Ордер = &Ордер
	|				ИЛИ Товары.Ссылка.ОрдерПолучатель = &Ордер)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОтгружаемыеТовары.Номенклатура,
	|		ОтгружаемыеТовары.Характеристика,
	|		ОтгружаемыеТовары.Назначение,
	|		ОтгружаемыеТовары.Упаковка,
	|		ВЫБОР
	|			КОГДА НЕ ОтгружаемыеТовары.СтатусУказанияСерий В (0, 1, 2)
	|				ТОГДА ОтгружаемыеТовары.Серия
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ,
	|		-ОтгружаемыеТовары.Количество,
	|		-ОтгружаемыеТовары.КоличествоУпаковок
	|	ИЗ
	|		Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК ОтгружаемыеТовары
	|	ГДЕ
	|		ОтгружаемыеТовары.Ссылка = &Ордер
	|		И НЕ ОтгружаемыеТовары.ЭтоУпаковочныйЛист
	|		И ОтгружаемыеТовары.Действие В (ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить), ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать))
	|		И НЕ &РежимИсправления
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОтгружаемыеТовары.Номенклатура,
	|		ОтгружаемыеТовары.Характеристика,
	|		ОтгружаемыеТовары.Назначение,
	|		ОтгружаемыеТовары.Упаковка,
	|		ВЫБОР
	|			КОГДА НЕ ОтгружаемыеТовары.СтатусУказанияСерий В (0, 1, 2)
	|				ТОГДА ОтгружаемыеТовары.Серия
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ,
	|		-ОтгружаемыеТовары.Количество,
	|		-ОтгружаемыеТовары.КоличествоУпаковок
	|	ИЗ
	|		Документ.ОрдерНаПеремещениеТоваров.ОтгружаемыеТовары КАК ОтгружаемыеТовары
	|	ГДЕ
	|		ОтгружаемыеТовары.Ссылка = &Ордер
	|		И ОтгружаемыеТовары.Действие В (ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить), ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать))
	|		И НЕ &РежимИсправления) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Серия,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расхождения.Номенклатура,
	|	Расхождения.Характеристика,
	|	Расхождения.Назначение,
	|	Расхождения.Упаковка,
	|	Расхождения.Серия,
	|	ВЫБОР
	|		КОГДА Расхождения.КоличествоУпаковок < 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировкиОрдераНаТовары.ОтразитьИзлишек)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировкиОрдераНаТовары.ОтразитьНедостачу)
	|	КОНЕЦ КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА Расхождения.Количество < 0
	|			ТОГДА -Расхождения.Количество
	|		ИНАЧЕ Расхождения.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА Расхождения.КоличествоУпаковок < 0
	|			ТОГДА -Расхождения.КоличествоУпаковок
	|		ИНАЧЕ Расхождения.КоличествоУпаковок
	|	КОНЕЦ КАК КоличествоУпаковок
	|ИЗ
	|	Расхождения КАК Расхождения
	|ГДЕ
	|	Расхождения.КоличествоУпаковок <> 0";
	
	Запрос.УстановитьПараметр("ТоварыИзОбработки", Товары.Выгрузить());
	Запрос.УстановитьПараметр("Ордер", ОрдерОбъект.Ссылка);
	Запрос.УстановитьПараметр("РежимИсправления", ЭтотОбъект.РежимИсправления);
	
	ТаблицаРасхождений = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаРасхождений.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДокументРасхожденийОбъект = Документы.КорректировкаПоОрдеруНаТовары.СоздатьДокумент();
	
	ДокументРасхожденийОбъект.Дата = ТекущаяДатаСеанса();
	ДокументРасхожденийОбъект.Ответственный = Пользователи.ТекущийПользователь();
	ДокументРасхожденийОбъект.Ордер = ОрдерОбъект.Ссылка;        
	ДокументРасхожденийОбъект.Склад = Склад;
	ДокументРасхожденийОбъект.Помещение = Помещение;
	ДокументРасхожденийОбъект.ЗонаОтгрузки = ОрдерОбъект.ЗонаОтгрузки;
	
	ДокументРасхожденийОбъект.Товары.Загрузить(ТаблицаРасхождений);
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ДокументРасхожденийОбъект, Документы.КорректировкаПоОрдеруНаТовары);
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокументРасхожденийОбъект, ПараметрыУказанияСерий); 
	
	Возврат ДокументРасхожденийОбъект;
	
КонецФункции

Процедура ДополнитьОрдерСтрокамиУпаковочныхЛистов(ДокументОбъект);
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУпаковочныеЛисты") Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаТовары = ДокументОбъект.ОтгружаемыеТовары.Выгрузить(,"УпаковочныйЛистРодитель");
	ТаблицаТовары.Свернуть("УпаковочныйЛистРодитель");
	
	Для Каждого СтрТабл Из ТаблицаТовары Цикл
		
		Если ЗначениеЗаполнено(СтрТабл.УпаковочныйЛистРодитель) Тогда
			
			ПараметрыПоиска = Новый Структура;
			ПараметрыПоиска.Вставить("УпаковочныйЛист", СтрТабл.УпаковочныйЛистРодитель);
			ПараметрыПоиска.Вставить("ЭтоУпаковочныйЛист", Истина);
			
			НайденныеСтроки = ДокументОбъект.ОтгружаемыеТовары.НайтиСтроки(ПараметрыПоиска);
			
			Если НайденныеСтроки.Количество() = 0 Тогда
				НоваяСтрока = ДокументОбъект.ОтгружаемыеТовары.Добавить();
				НоваяСтрока.УпаковочныйЛист  = СтрТабл.УпаковочныйЛистРодитель;
				НоваяСтрока.ЭтоУпаковочныйЛист = Истина;
				НоваяСтрока.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить;
				НоваяСтрока.Количество = 1;
				НоваяСтрока.КоличествоУпаковок = 1;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ЗаполнитьСтатусыПроблем(УчитываемыеПоля, ТекущиеДанные, ЭтоУдаление, ЗаполнятьТЧ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	ТекстЗапросаВременныеТаблицыДокументаИОбработки() +
	"
	|ВЫБРАТЬ
	|	ТоварыИзОбработкиБезГруппировки.НомерСтроки,
	|	ТоварыИзОбработки.КоличествоУпаковок КАК КоличествоУпаковокСумма,
	|	ТоварыИзОбработки.КоличествоУпаковокНеОтгружать,
	|	ЕСТЬNULL(ТоварыИзОрдера.Количество, 0) КАК КоличествоВДокументе,
	|	ЕСТЬNULL(ТоварыИзОрдера.КоличествоУпаковок, 0) КАК КоличествоУпаковокВДокументе,
	|	ВЫБОР
	|		КОГДА ТоварыИзОбработки.КоличествоУпаковок < ЕСТЬNULL(ТоварыИзОрдера.КоличествоУпаковок, 0)
	|				И ЕСТЬNULL(ТоварыИзОрдера.КоличествоУпаковок, 0) > 0
	|				И ТоварыИзОбработкиБезГруппировки.НеОтгружать = 0
	|			ТОГДА 0
	|		КОГДА ТоварыИзОбработки.КоличествоУпаковок = ЕСТЬNULL(ТоварыИзОрдера.КоличествоУпаковок, 0)
	|				И ТоварыИзОрдера.КоличествоУпаковок > 0
	|				И ТоварыИзОбработки.НеОтгружать = 0
	|			ТОГДА 1
	|		КОГДА ТоварыИзОбработки.КоличествоУпаковок = ЕСТЬNULL(ТоварыИзОрдера.КоличествоУпаковок, 0)
	|				И ТоварыИзОбработки.НеОтгружать = 1
	|				И ТоварыИзОбработкиБезГруппировки.НеОтгружать = 0
	|			ТОГДА 2
	|		КОГДА ТоварыИзОбработкиБезГруппировки.НеОтгружать = 1
	|			ТОГДА 3
	|		КОГДА ТоварыИзОбработки.КоличествоУпаковок > ЕСТЬNULL(ТоварыИзОрдера.КоличествоУпаковок, 0)
	|				ИЛИ ЕСТЬNULL(ТоварыИзОрдера.КоличествоУпаковок, 0) = 0
	|			ТОГДА 4
	|		ИНАЧЕ 404
	|	КОНЕЦ КАК СтатусПроблемы
	|ПОМЕСТИТЬ ТаблицаПроблем
	|ИЗ
	|	ТоварыИзОбработкиБезГруппировки КАК ТоварыИзОбработкиБезГруппировки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыИзОбработки КАК ТоварыИзОбработки
	|		ПО ТоварыИзОбработкиБезГруппировки.Номенклатура = ТоварыИзОбработки.Номенклатура
	|			И ТоварыИзОбработкиБезГруппировки.Характеристика = ТоварыИзОбработки.Характеристика
	|			И ТоварыИзОбработкиБезГруппировки.Назначение = ТоварыИзОбработки.Назначение
	|			И ТоварыИзОбработкиБезГруппировки.Упаковка = ТоварыИзОбработки.Упаковка
	|			И ТоварыИзОбработкиБезГруппировки.Серия = ТоварыИзОбработки.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыИзОрдера КАК ТоварыИзОрдера
	|		ПО ТоварыИзОбработкиБезГруппировки.Номенклатура = ТоварыИзОрдера.Номенклатура
	|			И ТоварыИзОбработкиБезГруппировки.Характеристика = ТоварыИзОрдера.Характеристика
	|			И ТоварыИзОбработкиБезГруппировки.Назначение = ТоварыИзОрдера.Назначение
	|			И ТоварыИзОбработкиБезГруппировки.Серия = ТоварыИзОрдера.Серия
	|			И ТоварыИзОбработкиБезГруппировки.Упаковка = ТоварыИзОрдера.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПроблем.НомерСтроки КАК НомерСтроки,
	|	ТаблицаПроблем.КоличествоУпаковокСумма КАК КоличествоУпаковокСумма,
	|	ТаблицаПроблем.КоличествоВДокументе КАК КоличествоВДокументе,
	|	ТаблицаПроблем.КоличествоУпаковокВДокументе КАК КоличествоУпаковокВДокументе,
	|	ТаблицаПроблем.КоличествоУпаковокНеОтгружать КАК КоличествоУпаковокНеОтгружать,
	|	ТаблицаПроблем.СтатусПроблемы
	|ИЗ
	|	ТоварыИзОбработкиБезГруппировки КАК ТоварыИзОбработкиБезГруппировки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПроблем КАК ТаблицаПроблем
	|		ПО ТоварыИзОбработкиБезГруппировки.НомерСтроки = ТаблицаПроблем.НомерСтроки
	|			И (ТаблицаПроблем.СтатусПроблемы <> ТоварыИзОбработкиБезГруппировки.СтатусПроблемы
	|				ИЛИ ТаблицаПроблем.КоличествоУпаковокСумма <> ТоварыИзОбработкиБезГруппировки.КоличествоУпаковокСумма
	|				ИЛИ ТаблицаПроблем.КоличествоУпаковокНеОтгружать <> ТоварыИзОбработкиБезГруппировки.КоличествоУпаковокНеОтгружать
	|				ИЛИ ТаблицаПроблем.КоличествоУпаковокВДокументе <> ТоварыИзОбработкиБезГруппировки.КоличествоУпаковокВДокументе)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";	
	Запрос.УстановитьПараметр("Ссылка",Ордер);
	Запрос.УстановитьПараметр("ТоварыИзОбработки",Товары.Выгрузить());
	Запрос.УстановитьПараметр("УчитыватьУпаковки", УчитываемыеПоля.УчитыватьУпаковки);
	Запрос.УстановитьПараметр("УчитыватьСерии", УчитываемыеПоля.УчитыватьСерии);
	Запрос.УстановитьПараметр("НеУчитываемыеСтатусыСерий", УчитываемыеПоля.НеУчитываемыеСтатусыСерий);
	
	Если ТекущиеДанные = Неопределено Тогда
		Запрос.УстановитьПараметр("ПоВсейТЧ",Истина);
		Запрос.УстановитьПараметр("ЭтоУдаление",Ложь);
		Запрос.УстановитьПараметр("НомерСтроки",Неопределено);
		Запрос.УстановитьПараметр("Номенклатура",Неопределено);
		Запрос.УстановитьПараметр("Характеристика",Неопределено);
		Запрос.УстановитьПараметр("Назначение", Неопределено);
		Запрос.УстановитьПараметр("Упаковка",Неопределено);
		Запрос.УстановитьПараметр("Серия",Неопределено);
	Иначе
		Запрос.УстановитьПараметр("ПоВсейТЧ",Ложь);
		Запрос.УстановитьПараметр("ЭтоУдаление",ЭтоУдаление);
		Запрос.УстановитьПараметр("НомерСтроки",ТекущиеДанные.НомерСтроки);
		Запрос.УстановитьПараметр("Номенклатура",ТекущиеДанные.Номенклатура);
		Запрос.УстановитьПараметр("Характеристика",ТекущиеДанные.Характеристика);
		Запрос.УстановитьПараметр("Назначение", ТекущиеДанные.Назначение);
		Запрос.УстановитьПараметр("Упаковка",ТекущиеДанные.Упаковка);
		Запрос.УстановитьПараметр("Серия",ТекущиеДанные.Серия);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если ЗаполнятьТЧ Тогда
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Товары[Выборка.НомерСтроки - 1], Выборка);
		КонецЦикла;
		Возврат Неопределено;
	Иначе
		Возврат Выборка;
	КонецЕсли;
		
КонецФункции

Функция РазбитьПоНеотгружаемым(УчитываемыеПоля) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	ТекстЗапросаВременныеТаблицыДокументаИОбработки() +
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыИзОбработки.Номенклатура,
	|	ТоварыИзОбработки.Характеристика,
	|	ТоварыИзОбработки.Назначение,
	|	ТоварыИзОбработки.Упаковка,
	|	ЕСТЬNULL(ТоварыИзОрдера.Количество, 0) КАК КоличествоВДокументе,
	|	ЕСТЬNULL(ТоварыИзОрдера.КоличествоУпаковок, 0) КАК КоличествоУпаковокВДокументе,
	|	ТоварыИзОбработки.Серия,
	|	ТоварыИзОбработки.СтатусУказанияСерий
	|ИЗ
	|	ТоварыИзОбработки КАК ТоварыИзОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыИзОрдера КАК ТоварыИзОрдера
	|		ПО ТоварыИзОбработки.Номенклатура = ТоварыИзОрдера.Номенклатура
	|			И ТоварыИзОбработки.Характеристика = ТоварыИзОрдера.Характеристика
	|			И ТоварыИзОбработки.Назначение = ТоварыИзОрдера.Назначение
	|			И ТоварыИзОбработки.Упаковка = ТоварыИзОрдера.Упаковка
	|			И ТоварыИзОбработки.Серия = ТоварыИзОрдера.Серия
	|ГДЕ
	|	ТоварыИзОбработки.КоличествоУпаковок > ЕСТЬNULL(ТоварыИзОрдера.КоличествоУпаковок, 0)";	
	Запрос.УстановитьПараметр("Ссылка",Ордер);
	Запрос.УстановитьПараметр("ТоварыИзОбработки",Товары.Выгрузить());
	Запрос.УстановитьПараметр("УчитыватьУпаковки", УчитываемыеПоля.УчитыватьУпаковки);
	Запрос.УстановитьПараметр("УчитыватьСерии", УчитываемыеПоля.УчитыватьСерии);
	Запрос.УстановитьПараметр("НеУчитываемыеСтатусыСерий", УчитываемыеПоля.НеУчитываемыеСтатусыСерий);
	
	Запрос.УстановитьПараметр("ПоВсейТЧ",Истина);
	Запрос.УстановитьПараметр("ЭтоУдаление",Ложь);
	Запрос.УстановитьПараметр("НомерСтроки",Неопределено);
	Запрос.УстановитьПараметр("Номенклатура",Неопределено);
	Запрос.УстановитьПараметр("Характеристика",Неопределено);
	Запрос.УстановитьПараметр("Назначение",Неопределено);
	Запрос.УстановитьПараметр("Упаковка",Неопределено);
	Запрос.УстановитьПараметр("Серия",Неопределено);

	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Функция ТекстЗапросаВременныеТаблицыДокументаИОбработки()
	                             
	ЭтоОрдерНаПеремещение = ТипЗнч(Ордер) = Тип("ДокументСсылка.ОрдерНаПеремещениеТоваров");
	Если ЭтоОрдерНаПеремещение Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаТовары.Характеристика КАК Характеристика,
		|	ТаблицаТовары.Назначение КАК Назначение,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СтатусУказанияСерий В (&НеУчитываемыеСтатусыСерий)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|		ИНАЧЕ ТаблицаТовары.Серия
		|	КОНЕЦ КАК Серия,
		|	ВЫБОР
		|		КОГДА &УчитыватьУпаковки
		|			ТОГДА ТаблицаТовары.Упаковка
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|	КОНЕЦ КАК Упаковка,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать)
		|			ТОГДА 0
		|		ИНАЧЕ ТаблицаТовары.Количество
		|	КОНЕЦ КАК Количество,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать)
		|			ТОГДА 0
		|		КОГДА &УчитыватьУпаковки
		|			ТОГДА ТаблицаТовары.КоличествоУпаковок
		|		ИНАЧЕ ТаблицаТовары.Количество
		|	КОНЕЦ КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ТоварыИзОрдераДляЗапроса
		|ИЗ
		|	Документ.ОрдерНаПеремещениеТоваров.ОтгружаемыеТовары КАК ТаблицаТовары
		|ГДЕ
		|	ИСТИНА
		|	И ТаблицаТовары.Ссылка = &Ссылка
		|	И &УсловиеОтбораСтрок
		|	И (&ПоВсейТЧ
		|			ИЛИ ТаблицаТовары.Номенклатура = &Номенклатура
		|				И ТаблицаТовары.Характеристика = &Характеристика
		|				И ТаблицаТовары.Назначение = &Назначение
		|				И (ТаблицаТовары.Упаковка = &Упаковка
		|					ИЛИ НЕ &УчитыватьУпаковки)
		|				И (ТаблицаТовары.Серия = &Серия
		|					ИЛИ НЕ &УчитыватьСерии))";
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаТовары.Характеристика КАК Характеристика,
		|	ТаблицаТовары.Назначение КАК Назначение,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СтатусУказанияСерий В (&НеУчитываемыеСтатусыСерий)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|		ИНАЧЕ ТаблицаТовары.Серия
		|	КОНЕЦ КАК Серия,
		|	ВЫБОР
		|		КОГДА &УчитыватьУпаковки
		|			ТОГДА ТаблицаТовары.Упаковка
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|	КОНЕЦ КАК Упаковка,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать)
		|			ТОГДА 0
		|		ИНАЧЕ ТаблицаТовары.Количество
		|	КОНЕЦ КАК Количество,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать)
		|			ТОГДА 0
		|		КОГДА &УчитыватьУпаковки
		|			ТОГДА ТаблицаТовары.КоличествоУпаковок
		|		ИНАЧЕ ТаблицаТовары.Количество
		|	КОНЕЦ КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ТоварыИзОрдераДляЗапроса
		|ИЗ
		|	Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК ТаблицаТовары
		|ГДЕ
		|	ИСТИНА
		|	И ТаблицаТовары.Ссылка = &Ссылка
		|	И &УсловиеОтбораСтрок
		|	И (&ПоВсейТЧ
		|			ИЛИ ТаблицаТовары.Номенклатура = &Номенклатура
		|				И ТаблицаТовары.Характеристика = &Характеристика
		|				И ТаблицаТовары.Назначение = &Назначение
		|				И (ТаблицаТовары.Упаковка = &Упаковка
		|					ИЛИ НЕ &УчитыватьУпаковки)
		|				И (ТаблицаТовары.Серия = &Серия
		|					ИЛИ НЕ &УчитыватьСерии))";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|;";
	
	ТекстЗапроса = ТекстЗапроса + "
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Назначение,
	|	ТаблицаТовары.Упаковка КАК Упаковка,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество,
	|	СУММА(ТаблицаТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	ТаблицаТовары.Серия КАК Серия
	|ПОМЕСТИТЬ ТоварыИзОрдера
	|ИЗ
	|	ТоварыИзОрдераДляЗапроса КАК ТаблицаТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Назначение,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыИзОбработки.Номенклатура,
	|	ТоварыИзОбработки.Характеристика,
	|	ТоварыИзОбработки.Назначение,
	|	ВЫБОР
	|		КОГДА &УчитыватьУпаковки
	|			ТОГДА ТоварыИзОбработки.Упаковка
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|	КОНЕЦ КАК Упаковка,
	|	ТоварыИзОбработки.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА &УчитыватьУпаковки
	|			ТОГДА ТоварыИзОбработки.КоличествоУпаковок
	|		ИНАЧЕ ТоварыИзОбработки.Количество
	|	КОНЕЦ КАК КоличествоУпаковок,
	|	ТоварыИзОбработки.КоличествоУпаковокНеОтгружать КАК КоличествоУпаковокНеОтгружать,
	|	ТоварыИзОбработки.КоличествоУпаковокСумма КАК КоличествоУпаковокСумма,
	|	ТоварыИзОбработки.КоличествоУпаковокВДокументе КАК КоличествоУпаковокВДокументе,
	|	ВЫБОР
	|		КОГДА НЕ ТоварыИзОбработки.СтатусУказанияСерий В (&НеУчитываемыеСтатусыСерий)
	|			ТОГДА ТоварыИзОбработки.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Серия,
	|	ТоварыИзОбработки.СтатусУказанияСерий,
	|	ТоварыИзОбработки.НомерСтроки,
	|	ТоварыИзОбработки.СтатусПроблемы,
	|	ТоварыИзОбработки.НеОтгружать
	|ПОМЕСТИТЬ ТоварыИзОбработкиБезГруппировки
	|ИЗ
	|	&ТоварыИзОбработки КАК ТоварыИзОбработки
	|ГДЕ
	|	(&ПоВсейТЧ
	|			ИЛИ ТоварыИзОбработки.Номенклатура = &Номенклатура
	|				И ТоварыИзОбработки.Характеристика = &Характеристика
	|				И ТоварыИзОбработки.Назначение = &Назначение
	|				И (ТоварыИзОбработки.Упаковка = &Упаковка
	|					ИЛИ НЕ &УчитыватьУпаковки)
	|				И (ТоварыИзОбработки.Серия = &Серия
	|					ИЛИ НЕ &УчитыватьСерии))
	|	И (НЕ &ЭтоУдаление
	|			ИЛИ ТоварыИзОбработки.НомерСтроки <> &НомерСтроки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыИзОбработкиБезГруппировки.Номенклатура,
	|	ТоварыИзОбработкиБезГруппировки.Характеристика,
	|	ТоварыИзОбработкиБезГруппировки.Назначение,
	|	ТоварыИзОбработкиБезГруппировки.Упаковка,
	|	СУММА(ВЫБОР
	|			КОГДА ТоварыИзОбработкиБезГруппировки.НеОтгружать = 0
	|				ТОГДА ТоварыИзОбработкиБезГруппировки.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА ТоварыИзОбработкиБезГруппировки.НеОтгружать = 0
	|				ТОГДА ТоварыИзОбработкиБезГруппировки.КоличествоУпаковок
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоУпаковок,
	|	СУММА(ВЫБОР
	|			КОГДА ТоварыИзОбработкиБезГруппировки.НеОтгружать = 1
	|				ТОГДА ТоварыИзОбработкиБезГруппировки.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоНеОтгружать,
	|	СУММА(ВЫБОР
	|			КОГДА ТоварыИзОбработкиБезГруппировки.НеОтгружать = 1
	|				ТОГДА ТоварыИзОбработкиБезГруппировки.КоличествоУпаковок
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоУпаковокНеОтгружать,
	|	ТоварыИзОбработкиБезГруппировки.Серия,
	|	МАКСИМУМ(ТоварыИзОбработкиБезГруппировки.НеОтгружать) КАК НеОтгружать,
	|	МАКСИМУМ(ТоварыИзОбработкиБезГруппировки.СтатусУказанияСерий) КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ ТоварыИзОбработки
	|ИЗ
	|	ТоварыИзОбработкиБезГруппировки КАК ТоварыИзОбработкиБезГруппировки
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыИзОбработкиБезГруппировки.Номенклатура,
	|	ТоварыИзОбработкиБезГруппировки.Характеристика,
	|	ТоварыИзОбработкиБезГруппировки.Назначение,
	|	ТоварыИзОбработкиБезГруппировки.Упаковка,
	|	ТоварыИзОбработкиБезГруппировки.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТоварыИзОрдераДляЗапроса;";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораСтрок", Обработки.ПроверкаКоличестваТоваровВДокументе.УсловиеОтбораСтрок(ЭтотОбъект)); 
	
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

#КонецЕсли
