#Область ПрограммныйИнтерфейс

Процедура ОчиститьРеквизитыВЗависимостиОтУсловияПрименения(Объект) Экспорт
	
	Если Объект.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаВсеРеквизиты = "ВариантОпределенияПериодаНакопительнойСкидки, ВариантНакопления, 
	                      |КритерийОграниченияПримененияЗаОбъемПродаж, ВалютаОграничения, ГрафикОплаты, ФормаОплаты,
	                      |ЗначениеУсловияОграничения, СегментНоменклатурыОграничения, ПериодНакопления,
	                      |ТипСравнения, ГруппаПользователей, ВидКартыЛояльности, 
	                      |СегментПартнеров, КоличествоПериодовНакопления, ВремяДействия,
	                      |КоличествоДнейДоДняРождения, КоличествоДнейПослеДняРождения, ВключатьТекущуюПродажуВНакопленныйОбъемПродаж,
	                      |ВариантОтбораНоменклатуры";
	
	Если Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ВхождениеПартнераВСегмент") Тогда
		
		СтрокаИспользуемыеРеквизиты = "СегментПартнеров";
		
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаВремяПродажи") Тогда
		
		СтрокаИспользуемыеРеквизиты = "ВремяДействия";
	
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаГрафикОплаты") Тогда
		
		СтрокаИспользуемыеРеквизиты = "ГрафикОплаты";
		
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаДеньРожденияКлиента") Тогда
		
		СтрокаИспользуемыеРеквизиты = "КоличествоДнейДоДняРождения,КоличествоДнейПослеДняРождения";
		
	ИначеЕсли ТипЗнч(Объект.УсловиеПредоставления) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") Тогда
		
		СтрокаИспользуемыеРеквизиты = "";
		
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаНакопленныйОбъемПродаж") Тогда
		
		СтрокаИспользуемыеРеквизиты = "ВариантОпределенияПериодаНакопительнойСкидки, ВариантНакопления, КритерийОграниченияПримененияЗаОбъемПродаж,
		                              |ВалютаОграничения,ТипСравнения,
		                              |ЗначениеУсловияОграничения, СегментНоменклатурыОграничения, ПериодНакопления,
		                              |ТипСравнения, СегментПартнеров, КоличествоПериодовНакопления, ВключатьТекущуюПродажуВНакопленныйОбъемПродаж";
		
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаНаличиеКартыЛояльности") Тогда
		
		СтрокаИспользуемыеРеквизиты = "ВидКартыЛояльности";
		
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаРазовыйОбъемПродаж") Тогда
		
		СтрокаИспользуемыеРеквизиты = "КритерийОграниченияПримененияЗаОбъемПродаж,
		                             |ВалютаОграничения,ТипСравнения,
		                             |ЗначениеУсловияОграничения, СегментНоменклатурыОграничения,
		                             |ТипСравнения, СегментПартнеров";
		
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаФормуОплаты") Тогда
		
		СтрокаИспользуемыеРеквизиты = "ФормаОплаты";
		
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.КартаЛояльностиНеЗарегистрирована") Тогда
		
		СтрокаИспользуемыеРеквизиты = "ВидКартыЛояльности";
		
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ОграничениеПоГруппеПользователей") Тогда
		
		СтрокаИспользуемыеРеквизиты = "ГруппаПользователей";
		
	КонецЕсли;
	
	ИспользуемыеРеквизиты = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаИспользуемыеРеквизиты, ",", Истина, Истина);
	ВсеРеквизиты = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаВсеРеквизиты, ",", Истина, Истина);
	
	Для Каждого ЭлементМассива Из ИспользуемыеРеквизиты Цикл
	
		ПорядковыйНомер = ВсеРеквизиты.Найти(ЭлементМассива);
		Если ПорядковыйНомер <> Неопределено Тогда
			ВсеРеквизиты.Удалить(ПорядковыйНомер);
		КонецЕсли;
	
	КонецЦикла;
	
	Для каждого РеквизитКОчистке Из ВсеРеквизиты Цикл
	
		Если РеквизитКОчистке = "ВариантОпределенияПериодаНакопительнойСкидки" Тогда
			
			Объект.ВариантОпределенияПериодаНакопительнойСкидки = ПредопределенноеЗначение("Перечисление.ВариантыОпределенияПериодаНакопительнойСкидки.ПустаяСсылка");
			
		ИначеЕсли РеквизитКОчистке = "ВариантНакопления" Тогда
			
			Объект.ВариантНакопления = ПредопределенноеЗначение("Перечисление.ВариантыНакопленияКумулятивнойСкидкиНаценки.ПустаяСсылка");
			
		ИначеЕсли РеквизитКОчистке = "КритерийОграниченияПримененияЗаОбъемПродаж" Тогда
			
			Объект.КритерийОграниченияПримененияЗаОбъемПродаж = ПредопределенноеЗначение("Перечисление.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.ПустаяСсылка");
			
		ИначеЕсли РеквизитКОчистке = "ВалютаОграничения" Тогда
			
			Объект.ВалютаОграничения = ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");
			
		ИначеЕсли РеквизитКОчистке = "ГрафикОплаты" Тогда
			
			Объект.ГрафикОплаты = ПредопределенноеЗначение("Справочник.ГрафикиОплаты.ПустаяСсылка");
			
		ИначеЕсли РеквизитКОчистке = "ФормаОплаты" Тогда
			
			Объект.ФормаОплаты = ПредопределенноеЗначение("Перечисление.ФормыОплаты.ПустаяСсылка");
			
		ИначеЕсли РеквизитКОчистке = "ЗначениеУсловияОграничения" Тогда
			
			Объект.ЗначениеУсловияОграничения = 0;
			
		ИначеЕсли РеквизитКОчистке = "СегментНоменклатурыОграничения" Тогда
			
			Объект.СегментНоменклатурыОграничения = ПредопределенноеЗначение("Справочник.СегментыНоменклатуры.ПустаяСсылка");
			
		ИначеЕсли РеквизитКОчистке = "ПериодНакопления" Тогда
			
			Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.ПустаяСсылка");
			
		ИначеЕсли РеквизитКОчистке = "ТипСравнения" Тогда
			
			Объект.ТипСравнения = ПредопределенноеЗначение("Перечисление.ТипыСравненияЗначенийСкидокНаценок.ПустаяСсылка");
			
		ИначеЕсли РеквизитКОчистке = "ГруппаПользователей" Тогда
			
			Объект.ГруппаПользователей = ПредопределенноеЗначение("Справочник.ГруппыПользователей.ПустаяСсылка");
			
		ИначеЕсли РеквизитКОчистке = "ВидКартыЛояльности" Тогда
			
			Объект.ВидКартыЛояльности = ПредопределенноеЗначение("Справочник.ВидыКартЛояльности.ПустаяСсылка");
			
		ИначеЕсли РеквизитКОчистке = "СегментПартнеров" Тогда
			
			Объект.СегментПартнеров = ПредопределенноеЗначение("Справочник.СегментыПартнеров.ПустаяСсылка");
			
		ИначеЕсли РеквизитКОчистке = "КоличествоПериодовНакопления" Тогда
			
			Объект.КоличествоПериодовНакопления = 0;
			
		ИначеЕсли РеквизитКОчистке = "ВремяДействия" Тогда
			
			Объект.ВремяДействия.Очистить();
			
		КонецЕсли;
	
	КонецЦикла;

КонецПроцедуры

Функция АвтоНаименованиеУсловияПредоставленияСкидки(Форма) Экспорт
	
	Форма.Элементы.Наименование.СписокВыбора.Очистить();
	
	Объект = Форма.Объект;
	
	ПоКакойНоменклатуре = "";
	Если Объект.ВариантОтбораНоменклатуры = ПредопределенноеЗначение("Перечисление.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок.СписокНоменклатуры") Тогда
		Если Строка(Форма.СписокНоменклатурныхПозиций) <> НСтр("ru='Добавить';uk='Додати'") Тогда
			ПоКакойНоменклатуре = СтрШаблон(НСтр("ru='по номенклатуре из списка: %1';uk='по номенклатурі зі списку: %1'"), Форма.СписокНоменклатурныхПозиций);
		КонецЕсли
	ИначеЕсли Объект.ВариантОтбораНоменклатуры = ПредопределенноеЗначение("Перечисление.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок.СегментНоменклатуры") Тогда
		ПоКакойНоменклатуре = СтрШаблон(НСтр("ru='по номенклатуре из сегмента %1';uk='по номенклатурі з сегмента %1'"), Объект.СегментНоменклатурыОграничения);
	ИначеЕсли Объект.ВариантОтбораНоменклатуры = ПредопределенноеЗначение("Перечисление.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок.ОтборКомпоновкиДанных") Тогда
		ПоКакойНоменклатуре = НСтр("ru='по отобранной номенклатуре';uk='за відібраною номенклатурою'");
	КонецЕсли;
	
	Если Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаВремяПродажи") Тогда
		СтрокаНаименования = НСтр("ru='Время продажи:';uk='Час продажу:'");
		ПервыйДень = Истина;
		Для Каждого СтрокаТаблицы Из Объект.ВремяДействия Цикл
			СтрокаНаименования = СтрокаНаименования + 
			                     ?(ПервыйДень," ", ", ")+ СтрокаТаблицы.ДеньНедели + 
			                     СтрШаблон(НСтр("ru='(с %1 до %2)';uk='(з %1 до %2)'"), Формат(СтрокаТаблицы.ВремяНачала,"ДФ=ЧЧ:мм"), Формат(СтрокаТаблицы.ВремяОкончания,"ДФ=ЧЧ:мм"));
			ПервыйДень = Ложь;
		КонецЦикла;
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаФормуОплаты") Тогда
		СтрокаНаименования = СтрШаблон(НСтр("ru='Форма оплаты: %1';uk='Форма оплати: %1'"), Объект.ФормаОплаты);
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаДеньРожденияКлиента") Тогда
		СтрокаНаименования = НСтр("ru='День рождения клиента';uk='День народження клієнта'");
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ВхождениеПартнераВСегмент") Тогда
		СтрокаНаименования = СтрШаблон(НСтр("ru='Клиент входит в сегмент: %1';uk='Клієнт входить до сегменту: %1'"), Объект.СегментПартнеров);
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаГрафикОплаты") Тогда
		СтрокаНаименования = СтрШаблон(НСтр("ru='График оплаты: %1';uk='Графік оплати: %1'"), Объект.ГрафикОплаты);
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.КартаЛояльностиНеЗарегистрирована") Тогда
		СтрокаНаименования = СтрШаблон(НСтр("ru='Карта лояльности ""%1"" не зарегистрирована';uk='Карта лояльності ""%1"" не зареєстрована'"), Объект.ВидКартыЛояльности);
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаНаличиеКартыЛояльности") Тогда
		СтрокаНаименования = СтрШаблон(НСтр("ru='Клиент владелец карты лояльности ""%1""';uk='Клієнт власник карти лояльності ""%1""'"), Объект.ВидКартыЛояльности);
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ОграничениеПоГруппеПользователей") Тогда
		СтрокаНаименования = СтрШаблон(НСтр("ru='Группа пользователей: %1';uk='Група користувачів: %1'"), Объект.ГруппаПользователей);
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаРазовыйОбъемПродаж") Тогда
		
		Если Объект.КритерийОграниченияПримененияЗаОбъемПродаж = ПредопределенноеЗначение("Перечисление.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.Количество") Тогда
			СтрокаНаименования = СтрШаблон(
				НСтр("ru='Количество в документе %1 %2 ед. %3';uk='Кількість в документі %1 %2 од. %3'"),
				Объект.ТипСравнения,
				Объект.ЗначениеУсловияОграничения,
				ПоКакойНоменклатуре);
		ИначеЕсли Объект.КритерийОграниченияПримененияЗаОбъемПродаж = ПредопределенноеЗначение("Перечисление.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.КоличествоРазличных") Тогда
			СтрокаНаименования = СтрШаблон(
				НСтр("ru='Количество различных наименований в документе %1 %2 шт. %3';uk='Кількість різних найменувань у документі %1 %2 шт. %3'"),
				Объект.ТипСравнения,
				Объект.ЗначениеУсловияОграничения,
				ПоКакойНоменклатуре);
		ИначеЕсли Объект.КритерийОграниченияПримененияЗаОбъемПродаж = ПредопределенноеЗначение("Перечисление.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.КоличествоОдинаковых") Тогда
			СтрокаНаименования = СтрШаблон(
				НСтр("ru='Количество одинаковых позиций в документе %1 %2 ед. %3';uk='Кількість однакових позицій в документі %1 %2 од. %3'"),
				Объект.ТипСравнения,
				Объект.ЗначениеУсловияОграничения,
				ПоКакойНоменклатуре);
		ИначеЕсли Объект.КритерийОграниченияПримененияЗаОбъемПродаж = ПредопределенноеЗначение("Перечисление.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.Сумма") Тогда
			СтрокаНаименования = СтрШаблон(
				НСтр("ru='Сумма в документе %1 %2 %3 %4';uk='Сума в документі %1 %2 %3 %4'"),
				Объект.ТипСравнения,
				Объект.ЗначениеУсловияОграничения,
				Объект.ВалютаОграничения,
				ПоКакойНоменклатуре);
		КонецЕсли;
		
	ИначеЕсли Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаНакопленныйОбъемПродаж") Тогда
	
		Если Объект.ВариантОпределенияПериодаНакопительнойСкидки = ПредопределенноеЗначение("Перечисление.ВариантыОпределенияПериодаНакопительнойСкидки.ВесьПериод") Тогда
			
			Период = НСтр("ru='за весь период';uk='за весь період'");
			
		ИначеЕсли Объект.ВариантОпределенияПериодаНакопительнойСкидки = ПредопределенноеЗначение("Перечисление.ВариантыОпределенияПериодаНакопительнойСкидки.ПрошлыйПериод") Тогда
			
			Строка = "";
			Если Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
				Строка = НСтр("ru='прошлый год,прошлых года,прошлых лет';uk='минулий рік,минулого року,минулих років'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Декада") Тогда
				Строка = НСтр("ru='прошлую декаду,прошлых декады,прошлых декад';uk='минулу декаду,минулої декади,минулих декад'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.День") Тогда
				Строка = НСтр("ru='прошлый день,прошлых дня,прошлых дней';uk='минулий день,минулого дня,минулих днів'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал") Тогда
				Строка = НСтр("ru='прошлый квартал,прошлых квартала,прошлых кварталов';uk='минулий квартал,минулого кварталу,минулих кварталів'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц") Тогда
				Строка = НСтр("ru='прошлый месяц,прошлых месяца,прошлых месяцев';uk='минулий місяць,минулого місяця,минулих місяців'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Неделя") Тогда
				Строка = НСтр("ru='прошлую неделю,прошлых недели,прошлых недель';uk='минулий тиждень,минулого тижня,минулих тижнів'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Полугодие") Тогда
				Строка = НСтр("ru='прошлое полугодие,прошлых полугодия,прошлых полугодий';uk='минуле півріччя,минулого півріччя,минулих півріч'");
			КонецЕсли;
			
			Период = СтрШаблон(НСтр("ru='за %1';uk='за %1'"), СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(Объект.КоличествоПериодовНакопления, Строка));
			
		ИначеЕсли Объект.ВариантОпределенияПериодаНакопительнойСкидки = ПредопределенноеЗначение("Перечисление.ВариантыОпределенияПериодаНакопительнойСкидки.СНачалаТекущегоПериода") Тогда
			
			Если Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
				Период = НСтр("ru='с начала текущего года';uk='з початку поточного року'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Декада") Тогда
				Период = НСтр("ru='с начала текущей декады';uk='з початку поточної декади'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.День") Тогда
				Период = НСтр("ru='с начала текущего дня';uk='з початку поточного дня'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал") Тогда
				Период = НСтр("ru='с начала текущего квартала';uk='з початку поточного кварталу'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц") Тогда
				Период = НСтр("ru='с начала текущего месяца';uk='з початку поточного місяця'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Неделя") Тогда
				Период = НСтр("ru='с начала текущей недели';uk='з початку поточного тижня'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Полугодие") Тогда
				Период = НСтр("ru='с начала текущего полугодия';uk='з початку поточного півріччя'");
			КонецЕсли;
			
		ИначеЕсли Объект.ВариантОпределенияПериодаНакопительнойСкидки = ПредопределенноеЗначение("Перечисление.ВариантыОпределенияПериодаНакопительнойСкидки.ПрошлыйСкользящийПериод") Тогда
			
			Строка = "";
			Если Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
				Строка = НСтр("ru='предыдущий год,продыдущих года,предыдущих лет';uk='попередній рік, попереднього року,попередніх років'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Полугодие") Тогда
				Строка = НСтр("ru='предыдущее полугодие,предыдущих полугодия,предыдущих полугодий';uk='попереднє півріччя,попереднього півріччя,попередніх піврічь'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал") Тогда
				Строка = НСтр("ru='предыдущий квартал,предыдущих квартала,предыдущих кварталов';uk='попередній квартал,попереднього квартала,попередніх кварталів'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц") Тогда
				Строка = НСтр("ru='предыдущий месяц,предыдущих месяца,предыдущих месяцев';uk='попередній місяць,попереднього місяця,попередніх місяців'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Декада") Тогда
				Строка = НСтр("ru='предыдущую декаду,предыдущих декады,предыдущих декад';uk='попередню декаду,попередньої декади,попередніх декад'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.Неделя") Тогда
				Строка = НСтр("ru='предыдущую неделю,предыдущих недель,предыдущих недель';uk='попередній тиждень,попередніх тижнів,попередніх тижнів'");
			ИначеЕсли Объект.ПериодНакопления = ПредопределенноеЗначение("Перечисление.Периодичность.День") Тогда
				Строка = НСтр("ru='предыдущий день,предыдущих дня,предыдущих дней';uk='попередній день,попереднього дня,попередніх днів'");
			КонецЕсли;
			
			Период = СтрШаблон(НСтр("ru='за %1';uk='за %1'"), СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(Объект.КоличествоПериодовНакопления, Строка));
			
		КонецЕсли;
		
		ВариантНакопления = ?(Объект.ВариантНакопления = ПредопределенноеЗначение("Перечисление.ВариантыНакопленияКумулятивнойСкидкиНаценки.ПоПартнеру"), НСтр("ru='по клиенту';uk='по клієнту'"), НСтр("ru='по торговому соглашению';uk='за торговельною офертою'"));
		
		Если Объект.КритерийОграниченияПримененияЗаОбъемПродаж = ПредопределенноеЗначение("Перечисление.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.Количество") Тогда
			СтрокаНаименования = СтрШаблон(
				НСтр("ru='Количество проданной номенклатуры %4 %1 %2 ед. %3';uk='Кількість проданої номенклатури %4 %1 %2 од. %3'"),
				Объект.ТипСравнения,
				Объект.ЗначениеУсловияОграничения,
				ПоКакойНоменклатуре,
				ВариантНакопления);
		ИначеЕсли Объект.КритерийОграниченияПримененияЗаОбъемПродаж = ПредопределенноеЗначение("Перечисление.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.КоличествоРазличных") Тогда
			СтрокаНаименования = СтрШаблон(
				НСтр("ru='Количество различных наименований проданной номенклатуры %4 %1 %2 шт. %3';uk='Кількість різних найменувань проданої номенклатури %4 %1 %2 шт. %3'"),
				Объект.ТипСравнения,
				Объект.ЗначениеУсловияОграничения,
				ПоКакойНоменклатуре,
				ВариантНакопления);
		ИначеЕсли Объект.КритерийОграниченияПримененияЗаОбъемПродаж = ПредопределенноеЗначение("Перечисление.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.КоличествоОдинаковых") Тогда
			СтрокаНаименования = СтрШаблон(
				НСтр("ru='Количество одинаковых позиций проданной номенклатуры %4 %1 %2 ед. %3';uk='Кількість однакових позицій проданої номенклатури %4 %1 %2 од. %3'"),
				Объект.ТипСравнения,
				Объект.ЗначениеУсловияОграничения,
				ПоКакойНоменклатуре,
				ВариантНакопления);
		ИначеЕсли Объект.КритерийОграниченияПримененияЗаОбъемПродаж = ПредопределенноеЗначение("Перечисление.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.Сумма") Тогда
			СтрокаНаименования = СтрШаблон(
				НСтр("ru='Сумма проданной номенклатуры %5 %1 %2 %3 %4';uk='Сума проданої номенклатури %5 %1 %2 %3 %4'"),
				Объект.ТипСравнения,
				Объект.ЗначениеУсловияОграничения,
				Объект.ВалютаОграничения,
				ПоКакойНоменклатуре, 
				ВариантНакопления);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект.УсловиеПредоставления) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") Тогда
		
		СтрокаНаименования = Форма.АвтонаименованиеВнешнейОбработки;
		
	КонецЕсли;
	
	Форма.Элементы.Наименование.СписокВыбора.Добавить(СтрокаНаименования);
	
	Если Форма.ИндексАвтонаименования <= Форма.Элементы.Наименование.СписокВыбора.Количество() - 1 Тогда
		Возврат Форма.Элементы.Наименование.СписокВыбора.Получить(Форма.ИндексАвтонаименования).Значение;
	Иначе
		Возврат СтрокаНаименования;
	КонецЕсли;

КонецФункции

Процедура ОбновитьРеквизитыАвтонаименования(Форма) Экспорт
	
	Форма.ИндексАвтонаименования = 0;
	Форма.НаименованиеИзмененоПользователем = Истина;
	Форма.ИспользуетсяАвтоНаименование = Ложь;
	Для Каждого ЭлементСписка Из Форма.Элементы.Наименование.СписокВыбора Цикл
		
		Если ЭлементСписка.Значение = Форма.Объект.Наименование Тогда
			Форма.ИспользуетсяАвтоНаименование = Истина;
			Форма.НаименованиеИзмененоПользователем = Ложь;
			Форма.ИндексАвтонаименования = Форма.Элементы.Наименование.СписокВыбора.Индекс(ЭлементСписка);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПредставлениеПоляОтбора(Поле, Отрицание = Ложь, НРег = Ложь)
	
	Если Строка(Поле) = "ВидЦены" Тогда
		ВозвращаемоеЗначение = НСтр("ru='Вид цены';uk='Вид ціни'");
	ИначеЕсли Строка(Поле) = "СегментНоменклатуры" Тогда
		ВозвращаемоеЗначение = НСтр("ru='Номенклатура входит в сегмент';uk='Номенклатура входить у сегмент'");
	ИначеЕсли Строка(Поле) = "КлиентПокупаетТоварВПервыйРаз" Тогда
		
		Если Отрицание Тогда
			ВозвращаемоеЗначение = НСтр("ru='Клиент покупает товар не в первый раз';uk='Клієнт купує товар не вперше'");
		Иначе
			ВозвращаемоеЗначение = НСтр("ru='Клиент покупает товар в первый раз';uk='Клієнт купує товар вперше'");
		КонецЕсли;
		
	ИначеЕсли Строка(Поле) = "КоличествоБезУчетаХарактеристик" Тогда
		
		ВозвращаемоеЗначение = НСтр("ru='Количество одинаковых';uk='Кількість однакових'");
		
	ИначеЕсли Строка(Поле) = "СуммаБезУчетаХарактеристик" Тогда
		
		ВозвращаемоеЗначение = НСтр("ru='Сумма по одинаковым';uk='Сума по однакових'");
		
	ИначеЕсли Строка(Поле) = "Количество" Тогда
		
		ВозвращаемоеЗначение = НСтр("ru='Количество одинаковых (с учетом характеристик)';uk='Кількість однакових (з урахуванням характеристик)'");
		
	ИначеЕсли Строка(Поле) = "Сумма" Тогда
		
		ВозвращаемоеЗначение = НСтр("ru='Сумма по одинаковым (с учетом характеристик)';uk='Сума по однакових (з урахуванням характеристик)'");
		
	ИначеЕсли Строка(Поле) = "ЗаданаПроизвольнаяЦена" Тогда
		
		Если Отрицание Тогда
			ВозвращаемоеЗначение = НСтр("ru='Не задана произвольная цена';uk='Не задана довільна ціна'");
		Иначе
			ВозвращаемоеЗначение = НСтр("ru='Задана произвольная цена';uk='Задана довільна ціна'");
		КонецЕсли;
		
	ИначеЕсли Строка(Поле) = "ДоступностьТовараДляКлиента" Тогда
		
		ВозвращаемоеЗначение = НСтр("ru='Доступность товара для клиента';uk='Доступність товару для клієнта'");
		
	ИначеЕсли Строка(Поле) = "ЦенаСовпадаетСЦенойСоглашения" Тогда
		
		Если Отрицание Тогда
			ВозвращаемоеЗначение = НСтр("ru='Цена не совпадает с уточненной ценой из соглашения';uk='Ціна не збігається з уточненою ціною оферти'");
		Иначе
			ВозвращаемоеЗначение = НСтр("ru='Цена совпадает с уточненной ценой из соглашения';uk='Ціна збігається з уточненою ціною з оферти'");
		КонецЕсли;
		
	Иначе
		ВозвращаемоеЗначение = Строка(Поле);
	КонецЕсли;
	
	Если НРег Тогда
		ВозвращаемоеЗначение = НРег(ВозвращаемоеЗначение);
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ПредставлениеОтбора(ЭлементыОтбора, НРег, РазделительЭлементов = ", ") Экспорт
	
	ПредставлениеОтбора = "";
	
	Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		
		Если ПредставлениеОтбора = "" Тогда
			Разделитель = "";
		Иначе
			Разделитель = РазделительЭлементов;
		КонецЕсли;
		
		Если ЭлементОтбора.Использование Тогда
			Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
				Если (ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно ИЛИ ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно)
					И ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("Булево") Тогда
					
					Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
						
						Если ЭлементОтбора.ПравоеЗначение = Истина Тогда
							СтрокаОтбора = СтрШаблон(
								НСтр("ru='%1';uk='%1'"), ПредставлениеПоляОтбора(ЭлементОтбора.ЛевоеЗначение, Ложь, НРег));
						Иначе
							СтрокаОтбора = СтрШаблон(
								НСтр("ru='%1';uk='%1'"), ПредставлениеПоляОтбора(ЭлементОтбора.ЛевоеЗначение, Истина, НРег));
						КонецЕсли;
						
					Иначе
						
						Если ЭлементОтбора.ПравоеЗначение = Истина Тогда
							СтрокаОтбора = СтрШаблон(
								НСтр("ru='%1';uk='%1'"), ПредставлениеПоляОтбора(ЭлементОтбора.ЛевоеЗначение, Истина, НРег));
						Иначе
							СтрокаОтбора = СтрШаблон(
								НСтр("ru='%1';uk='%1'"), ПредставлениеПоляОтбора(ЭлементОтбора.ЛевоеЗначение, Ложь, НРег));
						КонецЕсли;
						
					КонецЕсли;
					
				Иначе
					
					СтрокаОтбора = СтрШаблон(
						НСтр("ru='%1 %2 ""%3""';uk='%1 %2 ""%3""'"),
						ПредставлениеПоляОтбора(ЭлементОтбора.ЛевоеЗначение, Ложь, НРег), НРег(ЭлементОтбора.ВидСравнения), ЭлементОтбора.ПравоеЗначение);
					
				КонецЕсли;
			Иначе
				
				Если ЭлементОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ Тогда
					СтрокаОтбора = "(" + ПредставлениеОтбора(ЭлементОтбора.Элементы, НРег) + ")";
				ИначеЕсли ЭлементОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли Тогда
					СтрокаОтбора = "(" + ПредставлениеОтбора(ЭлементОтбора.Элементы, НРег," " + "Или") + ")";
				ИначеЕсли ЭлементОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе Тогда
					СтрокаОтбора = "Не (" + ПредставлениеОтбора(ЭлементОтбора.Элементы, НРег) + ")";
				КонецЕсли;
				
			КонецЕсли;
			ПредставлениеОтбора = ПредставлениеОтбора
			                    + Разделитель
			                    + СтрокаОтбора;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПредставлениеОтбора;
	
КонецФункции

Процедура НастроитьГиперссылкуОтбора(Форма, Имя, Знач ЦветОтборНеУстановлен = Неопределено, Знач ТекстОтборНеУстановлен = Неопределено, Знач Префикс = "")
	
	НРег = ЗначениеЗаполнено(Префикс);
	
#Если Клиент Тогда
	ЦветГиперссылки = ОбщегоНазначенияКлиентПовтИсп.ЦветСтиля("ЦветГиперссылки");
#Иначе
	ЦветГиперссылки = ЦветаСтиля.ЦветГиперссылки;
#КонецЕсли

#Если Клиент Тогда
	ПоясняющийОшибкуТекст = ОбщегоНазначенияКлиентПовтИсп.ЦветСтиля("ПоясняющийОшибкуТекст");
#Иначе
	ПоясняющийОшибкуТекст = ЦветаСтиля.ПоясняющийОшибкуТекст;
#КонецЕсли
	
	ПредставлениеОтбора = ПредставлениеОтбора(Форма[Имя].Настройки.Отбор.Элементы, НРег);
	
	Если ЦветОтборНеУстановлен = Неопределено Тогда
		Если Форма.Объект.ВариантОтбораНоменклатуры = ПредопределенноеЗначение("Перечисление.ВариантыОтбораНоменклатурыДляРасчетаСкидокНаценок.ОтборКомпоновкиДанных") Тогда
			ЦветОтборНеУстановлен = ПоясняющийОшибкуТекст;
		Иначе
			ЦветОтборНеУстановлен = ЦветГиперссылки;
		КонецЕсли;
	КонецЕсли;
	Если ТекстОтборНеУстановлен = Неопределено Тогда
		ТекстОтборНеУстановлен = НСтр("ru='Отбор не установлен';uk='Відбір не встановлений'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПредставлениеОтбора) И Префикс <> "" Тогда
		ПредставлениеОтбора = Префикс + " " + ПредставлениеОтбора;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПредставлениеОтбора) Тогда
		Форма[Имя + "Представление"] = Новый ФорматированнаяСтрока(
			ПредставлениеОтбора,
			Новый Шрифт(,, Ложь,,,Ложь),
			ЦветГиперссылки,,Имя);
	Иначе
		Форма[Имя + "Представление"] = Новый ФорматированнаяСтрока(
			ТекстОтборНеУстановлен,
			Новый Шрифт(,, Ложь,,,Ложь),
			ЦветОтборНеУстановлен,,Имя);
	КонецЕсли;
	
КонецПроцедуры

// Настраивает текст гиперссылок на отбор номенклатуры.
//
// Параметры:
//	Форма
Процедура НастроитьГиперссылкиОтборов(Форма) Экспорт
	
	Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Форма, "КомпоновщикНастроекОтборПоНоменклатуре") Тогда
		НастроитьГиперссылкуОтбора(Форма, "КомпоновщикНастроекОтборПоНоменклатуре");
	КонецЕсли;
	
#Если Клиент Тогда
	ЦветГиперссылки = ОбщегоНазначенияКлиентПовтИсп.ЦветСтиля("ЦветГиперссылки");
#Иначе
	ЦветГиперссылки = ЦветаСтиля.ЦветГиперссылки;
#КонецЕсли
	
	Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Форма, "КомпоновщикНастроекДополнительныеУсловия") Тогда
		НастроитьГиперссылкуОтбора(
			Форма,
			"КомпоновщикНастроекДополнительныеУсловия",
			ЦветГиперссылки,
			НСтр("ru='Без уточнений';uk='Без уточнень'"),
			НСтр("ru='По которой';uk='За якою'"));
	КонецЕсли;
	
КонецПроцедуры

// Настраивает текст гиперссылок на список номенклатурных позиций.
//
// Параметры:
//	Форма
Процедура НастроитьГиперссылкуНаСписокНоменклатурныхПозиций(Форма) Экспорт
	
	ВсегоПозиций = 0;
	КоличествоПервыхПозиций = 0;
	
	Если ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
		ДанныеДляПредставленияСпискаТоваров = СкидкиНаценкиВызовСервера.ДанныеДляПредставленияСпискаТоваров(Форма.Объект.Ссылка);
		КоличествоПервыхПозиций = ДанныеДляПредставленияСпискаТоваров.ПервыеПозиции.Количество();
		ВсегоПозиций = ДанныеДляПредставленияСпискаТоваров.ВсегоПозиций;
	КонецЕсли;
	
	ПредставлениеСпискаТоваров = "";
	Если КоличествоПервыхПозиций > 0 Тогда
		ПредставлениеСпискаТоваров = СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(ДанныеДляПредставленияСпискаТоваров.ПервыеПозиции, ", ");
	Иначе
		ПредставлениеСпискаТоваров = НСтр("ru='Добавить';uk='Додати'");
	КонецЕсли;
	Если КоличествоПервыхПозиций < ВсегоПозиций Тогда
		ПредставлениеСпискаТоваров = ПредставлениеСпискаТоваров
		                           + " " + НСтр("ru='и еще';uk='і ще'") + " "
		                           + СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(ВсегоПозиций - КоличествоПервыхПозиций, НСтр("ru='позиция,позиции,позиций';uk='позиція,позиції,позицій'"));
	КонецЕсли;
	
#Если Клиент Тогда
	ЦветГиперссылки = ОбщегоНазначенияКлиентПовтИсп.ЦветСтиля("ЦветГиперссылки");
#Иначе
	ЦветГиперссылки = ЦветаСтиля.ЦветГиперссылки;
#КонецЕсли
	
	Форма.СписокНоменклатурныхПозиций = Новый ФорматированнаяСтрока(
		ПредставлениеСпискаТоваров,
		Новый Шрифт(,, Ложь,,,Ложь),
		ЦветГиперссылки,,"Изменить");
	
КонецПроцедуры

Процедура ВариантОпределенияПериодаНакопительнойСкидкиПриИзмененииНаСервере(Форма) Экспорт
	
	Если Форма.Объект.ВариантОпределенияПериодаНакопительнойСкидки = ПредопределенноеЗначение("Перечисление.ВариантыОпределенияПериодаНакопительнойСкидки.ВесьПериод") Тогда
		Форма.Элементы.ПериодНакопления.Видимость = Ложь;
		Форма.Элементы.ДекорацияРавный.Видимость = Ложь;
		Форма.Элементы.КоличествоПериодовНакопления.Видимость = Ложь;
	ИначеЕсли Форма.Объект.ВариантОпределенияПериодаНакопительнойСкидки = ПредопределенноеЗначение("Перечисление.ВариантыОпределенияПериодаНакопительнойСкидки.СНачалаТекущегоПериода") Тогда
		Форма.Элементы.ПериодНакопления.Видимость = Истина;
		Форма.Элементы.ДекорацияРавный.Видимость = Истина;
		Форма.Элементы.КоличествоПериодовНакопления.Видимость = Ложь;
		Форма.Объект.КоличествоПериодовНакопления = 1;
	Иначе
		Форма.Элементы.ПериодНакопления.Видимость = Истина;
		Форма.Элементы.ДекорацияРавный.Видимость = Истина;
		Форма.Элементы.КоличествоПериодовНакопления.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура КритерийПримененияЗаОбъемПродажПриИзмененииНаСервере(Форма) Экспорт
	
	КоличествоРазличных = ПредопределенноеЗначение("Перечисление.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.КоличествоРазличных");
	КоличествоОдинаковых = ПредопределенноеЗначение("Перечисление.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.КоличествоОдинаковых");
	Количество = ПредопределенноеЗначение("Перечисление.КритерииОграниченияПримененияСкидкиНаценкиЗаОбъемПродаж.Количество");
	
	Форма.Элементы.УчитыватьХарактеристики.Видимость = (Форма.Объект.КритерийОграниченияПримененияЗаОбъемПродаж = КоличествоРазличных
		ИЛИ Форма.Объект.КритерийОграниченияПримененияЗаОбъемПродаж = КоличествоОдинаковых);
	Форма.Элементы.УчитыватьХарактеристикиЗаНакопленныйОбъемПродаж.Видимость = (Форма.Объект.КритерийОграниченияПримененияЗаОбъемПродаж = КоличествоРазличных
		ИЛИ Форма.Объект.КритерийОграниченияПримененияЗаОбъемПродаж = КоличествоОдинаковых);
	
	Если Форма.Объект.КритерийОграниченияПримененияЗаОбъемПродаж = Количество
	ИЛИ Форма.Объект.КритерийОграниченияПримененияЗаОбъемПродаж = КоличествоОдинаковых
	ИЛИ Форма.Объект.КритерийОграниченияПримененияЗаОбъемПродаж = КоличествоРазличных Тогда
		
		Форма.Элементы.ВалютаОграничения.Видимость = Ложь;
		Форма.Элементы.ВалютаОграниченияЗаНакопленныйОбъемПродаж.Видимость = Ложь;
		Форма.Элементы.ВалютаОграниченияНеИспользоватьНесколькоВалютЗаНакопленныйОбъемПродаж.Видимость = Ложь;
		Форма.Элементы.ВалютаОграниченияНеИспользоватьНесколькоВалют.Видимость = Ложь;
		
	Иначе
		
		Форма.Элементы.ВалютаОграничения.Видимость = Истина;
		Форма.Элементы.ВалютаОграниченияЗаНакопленныйОбъемПродаж.Видимость = Истина;
		Форма.Элементы.ВалютаОграниченияНеИспользоватьНесколькоВалютЗаНакопленныйОбъемПродаж.Видимость = Истина;
		Форма.Элементы.ВалютаОграниченияНеИспользоватьНесколькоВалют.Видимость = Истина;
		
	КонецЕсли;
	
	Форма.Элементы.ДекорацияПозицийВДокументе.Видимость = (Форма.Объект.КритерийОграниченияПримененияЗаОбъемПродаж = КоличествоРазличных
		ИЛИ Форма.Объект.КритерийОграниченияПримененияЗаОбъемПродаж = КоличествоОдинаковых);
	Форма.Элементы.ДекорацияВДокументе.Видимость = Не (Форма.Объект.КритерийОграниченияПримененияЗаОбъемПродаж = КоличествоРазличных
		ИЛИ Форма.Объект.КритерийОграниченияПримененияЗаОбъемПродаж = КоличествоОдинаковых);
	
	Форма.Элементы.ДекорацияПозицийПроданногоТовара.Видимость = (Форма.Объект.КритерийОграниченияПримененияЗаОбъемПродаж = КоличествоРазличных
		ИЛИ Форма.Объект.КритерийОграниченияПримененияЗаОбъемПродаж = КоличествоОдинаковых);
	Форма.Элементы.ДекорацияПроданногоТовара.Видимость = Не (Форма.Объект.КритерийОграниченияПримененияЗаОбъемПродаж = КоличествоРазличных
		ИЛИ Форма.Объект.КритерийОграниченияПримененияЗаОбъемПродаж = КоличествоОдинаковых);
	
КонецПроцедуры

Процедура УсловиеПредоставленияПриИзмененииНаСервере(Форма) Экспорт
	
	Форма.Элементы.ЗаРазовыйОбъемПродаж.Видимость              = (Форма.Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаРазовыйОбъемПродаж"));
	Форма.Элементы.ЗаНакопленныйОбъемПродаж.Видимость          = (Форма.Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаНакопленныйОбъемПродаж"));
	Форма.Элементы.ЗаГрафикОплаты.Видимость                    = (Форма.Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаГрафикОплаты"));
	Форма.Элементы.ЗаПробнуюПродажу.Видимость                  = (Форма.Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаПробнуюПродажу"));
	Форма.Элементы.ЗаВремяДействия.Видимость                   = (Форма.Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаВремяПродажи"));
	Форма.Элементы.ЗаПервуюПродажуПартнеру.Видимость           = (Форма.Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаПервуюПродажуПартнеру"));
	Форма.Элементы.ЗаФормуОплаты.Видимость                     = (Форма.Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаФормуОплаты"));
	Форма.Элементы.ОграничениеПоГруппеПользователей.Видимость  = (Форма.Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ОграничениеПоГруппеПользователей"));
	Форма.Элементы.КартаЛояльностиНеЗарегистрирована.Видимость = (Форма.Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.КартаЛояльностиНеЗарегистрирована"));
	Форма.Элементы.ЗаНаличиеКартыЛояльности.Видимость          = (Форма.Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаНаличиеКартыЛояльности"));
	Форма.Элементы.ВхождениеПартнераВСегмент.Видимость         = (Форма.Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ВхождениеПартнераВСегмент"));
	Форма.Элементы.ЗаДеньРожденияПартнера.Видимость            = (Форма.Объект.УсловиеПредоставления = ПредопределенноеЗначение("Перечисление.УсловияПредоставленияСкидокНаценок.ЗаДеньРожденияКлиента"));
	Форма.Элементы.ВнешняяОбработка.Видимость                  = (ТипЗнч(Форма.Объект.УсловиеПредоставления) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки"));
	
	Если ТипЗнч(Форма.Объект.УсловиеПредоставления) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") Тогда
		
		ОписаниеВнешнейОбработки = СкидкиНаценкиВызовСервера.ОписаниеВнешнейОбработки(
				Форма.Объект.УсловиеПредоставления,
				Форма.АдресНастроекВнешнейОбработки);
		
		Форма.ОписаниеДействияВнешнейОбработки = ОписаниеВнешнейОбработки.ОписаниеДействия;
		Форма.АвтонаименованиеВнешнейОбработки = ОписаниеВнешнейОбработки.Автонаименование;
		Форма.ИмяФормыНастроекВнешнейОбработки = ОписаниеВнешнейОбработки.ИмяФормыНастроек;
		Форма.Элементы.ПоказатьФормуНастроекВнешнейОбработки.Видимость = ЗначениеЗаполнено(Форма.ИмяФормыНастроекВнешнейОбработки);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
