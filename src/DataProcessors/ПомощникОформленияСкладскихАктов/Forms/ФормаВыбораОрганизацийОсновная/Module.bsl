
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("МассивОрганизаций") Тогда
		Для Каждого Организация Из Параметры.МассивОрганизаций Цикл
			Строка = Объект.ВыбранныеОрганизации.Добавить();
			Строка.Организация = Организация;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Для Каждого Организация Из ВыбранноеЗначение Цикл
		МассивСтрок = Объект.ВыбранныеОрганизации.НайтиСтроки(Новый Структура("Организация",Организация));
		Если МассивСтрок.Количество() = 0 Тогда
			НоваяСтрока = Объект.ВыбранныеОрганизации.Добавить();
			НоваяСтрока.Организация = Организация;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыбранныеОрганизации

&НаКлиенте
Процедура ВыбранныеОрганизацииОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если НЕ Объект.ВыбранныеОрганизации.НайтиСтроки(Новый Структура("Организация",ВыбранноеЗначение)).Количество() = 0 Тогда
		СтандартнаяОбработка = Ложь;
		ПолеОрганизация = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТоварыРаспределенные", Элементы.ВыбранныеОрганизации.ТекущиеДанные.НомерСтроки, "Организация");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Вводимое значение уже присутсвует в списке. Выберите другую организацию.';uk='Введене значення вже присутнє в списку. Виберіть іншу організацію.'"),,"Объект."+ПолеОрганизация);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КнопкаОК(Команда)
	
	МассивОрганизаций = Новый Массив;
	Для Каждого Строка Из Объект.ВыбранныеОрганизации Цикл
		МассивОрганизаций.Добавить(Строка.Организация); 
	КонецЦикла;
	Закрыть(МассивОрганизаций);
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаОтмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаПодбор(Команда)

	ОткрытьФорму("Справочник.Организации.ФормаВыбора",Новый Структура("РежимВыбора,МножественныйВыбор,ЗакрыватьПриВыборе",Истина,Истина,Ложь), ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
#КонецОбласти

