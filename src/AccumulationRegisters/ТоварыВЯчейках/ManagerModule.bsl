#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы


Процедура ИсправитьДвижения_ДанныеДляОбновления(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.ТоварыВЯчейках";
	ИмяРегистра = "ТоварыВЯчейках";

#Область ОрдерНаПеремещениеТоваров
	ТекстЗапросаМеханизмаПроведения = Документы.ОрдерНаПеремещениеТоваров.АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра);
	Регистраторы = ОбновлениеИнформационнойБазыУТ.РегистраторыДляПерепроведения(
        ТекстЗапросаМеханизмаПроведения,
		ПолноеИмяРегистра,
		"Документ.ОрдерНаПеремещениеТоваров"
    );
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	СУММА(ВложенныйЗапрос.Контроль) КАК Контроль
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ОрдерНаПеремещениеУдалитьТовары.Ссылка КАК Регистратор,
	|		1 КАК Контроль
	|	ИЗ
	|		Документ.ОрдерНаПеремещениеТоваров.УдалитьТовары КАК ОрдерНаПеремещениеУдалитьТовары
	|	ГДЕ
	|		ОрдерНаПеремещениеУдалитьТовары.Ссылка.Проведен
	|		И ОрдерНаПеремещениеУдалитьТовары.Ссылка.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыОрдеровНаПеремещение.Принят)
	|		И ОрдерНаПеремещениеУдалитьТовары.НеОтгружать <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТоварыВЯчейках.Регистратор,
	|		-1
	|	ИЗ
	|		РегистрНакопления.ТоварыВЯчейках КАК ТоварыВЯчейках
	|	ГДЕ
	|		ТоварыВЯчейках.Регистратор ССЫЛКА Документ.ОрдерНаПеремещениеТоваров) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Регистратор
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Контроль) <> 0";
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);

#КонецОбласти

#Область РасходныйОрдерНаТовары
    ТекстЗапросаМеханизмаПроведения = Документы.РасходныйОрдерНаТовары.АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра);
	Регистраторы = ОбновлениеИнформационнойБазыУТ.РегистраторыДляПерепроведения(
        ТекстЗапросаМеханизмаПроведения,
		ПолноеИмяРегистра,
		"Документ.РасходныйОрдерНаТовары"
    );
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	СУММА(ВложенныйЗапрос.Контроль) КАК Контроль
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		РасходныйОрдерНаТоварыУдалитьТовары.Ссылка КАК Регистратор,
	|		1 КАК Контроль
	|	ИЗ
	|		Документ.РасходныйОрдерНаТовары.УдалитьТовары КАК РасходныйОрдерНаТоварыУдалитьТовары
	|	ГДЕ
	|		РасходныйОрдерНаТоварыУдалитьТовары.Ссылка.Проведен
	|		И РасходныйОрдерНаТоварыУдалитьТовары.Ссылка.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.Отгружен)
	|		И РасходныйОрдерНаТоварыУдалитьТовары.НеОтгружать <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТоварыВЯчейках.Регистратор,
	|		-1
	|	ИЗ
	|		РегистрНакопления.ТоварыВЯчейках КАК ТоварыВЯчейках
	|	ГДЕ
	|		ТоварыВЯчейках.Регистратор ССЫЛКА Документ.РасходныйОрдерНаТовары) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Регистратор
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Контроль) <> 0";
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	
#КонецОбласти

#Область ВводОстатков

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТоварыВЯчейках.Регистратор
	|ИЗ
	|	РегистрНакопления.ТоварыВЯчейках КАК ТоварыВЯчейках
	|ГДЕ
	|	ТоварыВЯчейках.Регистратор ССЫЛКА Документ.ВводОстатков
	|	И ТоварыВЯчейках.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|	И ТоварыВЯчейках.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|	И ТоварыВЯчейках.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины В (ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.КоличествоШтук), ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.ПустаяСсылка))";
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	
#КонецОбласти

КонецПроцедуры

Процедура ИсправитьДвижения(Параметры) Экспорт
	
	Регистраторы = Новый Массив;
	Регистраторы.Добавить("Документ.ОрдерНаПеремещениеТоваров");
	Регистраторы.Добавить("Документ.РасходныйОрдерНаТовары");
	Регистраторы.Добавить("Документ.ВводОстатков");
	
	ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
        Регистраторы,
	    "РегистрНакопления.ТоварыВЯчейках",
	    Параметры.Очередь
    );
	
	Параметры.ОбработкаЗавершена = ОбработкаЗавершена;
	
КонецПроцедуры



#КонецОбласти

#КонецОбласти

#КонецЕсли