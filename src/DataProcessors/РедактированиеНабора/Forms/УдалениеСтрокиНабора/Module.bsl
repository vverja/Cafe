
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Наборы.Количество() <= 1 Тогда
		Элементы.УдалитьВсеСтрокиНаборов.Заголовок = НСтр("ru='Удалить все строки набора';uk='Видалити всі рядки набору'");
		Сообщение = НСтр("ru='Удаляемые строки входят в набор:';uk='Рядки, що видаляються, входять у набір:'");
	Иначе
		Элементы.УдалитьВсеСтрокиНаборов.Заголовок = НСтр("ru='Удалить все строки наборов';uk='Видалити всі рядки наборів'");
		Сообщение = НСтр("ru='Удаляемые строки входят в наборы:';uk='Рядки, що видаляються, входять у набори:'");
	КонецЕсли;
	
	Если Параметры.Прочее.Количество() > 0 Тогда
		Элементы.РедактироватьНабор.Видимость         = Ложь;
		Если Параметры.Наборы.Количество() <= 1 Тогда
			Сообщение = НСтр("ru='Некоторые из удаляемых строк входят в состав набора:';uk='Деякі з рядків, які вилучаються, входять в склад набору:'");
		Иначе
			Сообщение = НСтр("ru='Некоторые из удаляемых строк входят в состав наборов:';uk='Деякі з рядків, які вилучаються, входять в склад наборів:'");
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Копирование Тогда
		Элементы.РедактироватьНабор.Видимость = Истина;
		Элементы.УдалитьВсеСтрокиНаборов.Видимость = Ложь;
		Если Параметры.Наборы.Количество() <= 1 Тогда
			Сообщение = НСтр("ru='Выделенная строка входит в набор:';uk='Виділена рядок входить в набір:'");
		Иначе
			Сообщение = НСтр("ru='Выделенная строки входят в наборы:';uk='Виділені рядки входять у набори:'");
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Наборы.Количество() > 1 Тогда
		Элементы.РедактироватьНабор.Видимость = Ложь;
	КонецЕсли;
	
	НомерСтроки = 0;
	Для Каждого СтрокаТЧ Из Параметры.Наборы Цикл
		НомерСтроки = НомерСтроки + 1;
		СообщениеНаборы = СообщениеНаборы
		                + ?(НомерСтроки <> 1, Символы.ПС, "")
		                + НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
							СтрокаТЧ.НоменклатураНабора,СтрокаТЧ.ХарактеристикаНабора);
	КонецЦикла;
	Элементы.СообщениеНаборы.Высота = НомерСтроки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РедактироватьНабор(Команда)
	Закрыть("РедактироватьНабор");
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть("Отмена");
КонецПроцедуры

&НаКлиенте
Процедура УдалитьВсеСтроки(Команда)
	Закрыть("УдалитьВесьНабор");
КонецПроцедуры

#КонецОбласти
