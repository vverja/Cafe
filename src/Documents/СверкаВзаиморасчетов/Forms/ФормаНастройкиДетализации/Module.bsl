
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Партнер = Параметры.Партнер;
	
	Параметры.Свойство("Организация", Организация);
	Параметры.Свойство("Контрагент",  Контрагент);
	
	ДоступнаФильтрацияПоДоговору = Параметры.Свойство("Договор", Договор);
	ДоступнаФильтрацияПоПартнерам = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов");
	
	РасшифровкаПоЗаказам = Параметры.РасшифровкаПоЗаказам;
	Если ЗначениеЗаполнено(Договор) Тогда
		ВидРасшифровкиПоПартнерамДоговорам = 4;
	ИначеЕсли Параметры.РасшифровкаПоДоговорам Тогда
		ВидРасшифровкиПоПартнерамДоговорам = 3;
	ИначеЕсли ЗначениеЗаполнено(Партнер) Тогда
		ВидРасшифровкиПоПартнерамДоговорам = 2;
	ИначеЕсли Параметры.РасшифровкаПоПартнерам Тогда
		ВидРасшифровкиПоПартнерамДоговорам = 1;
	Иначе
		ВидРасшифровкиПоПартнерамДоговорам = 0;
	КонецЕсли; 
	
	Элементы.Договор.Видимость	 = ДоступнаФильтрацияПоДоговору;
	Элементы.Договор.Доступность = ВидРасшифровкиПоПартнерамДоговорам = 4;
	Элементы.Партнер.Доступность = ВидРасшифровкиПоПартнерамДоговорам = 2;
	
	Если НЕ ДоступнаФильтрацияПоДоговору Тогда
		Элементы.ВидРасшифровкиПоПартнерамДоговорам.СписокВыбора.Удалить(
		    Элементы.ВидРасшифровкиПоПартнерамДоговорам.СписокВыбора.НайтиПоЗначению(4));
	КонецЕсли;
	
	Если Не ДоступнаФильтрацияПоПартнерам Тогда
		Элементы.ВидРасшифровкиПоПартнерамДоговорам.СписокВыбора.Удалить(
		    Элементы.ВидРасшифровкиПоПартнерамДоговорам.СписокВыбора.НайтиПоЗначению(1));
		Элементы.ВидРасшифровкиПоПартнерамДоговорам.СписокВыбора.Удалить(
		    Элементы.ВидРасшифровкиПоПартнерамДоговорам.СписокВыбора.НайтиПоЗначению(2));
		Элементы.Декорация2.Видимость = Ложь;
		Элементы.Партнер.Видимость    = Ложь;
		Элементы.ГруппаДетализацияВзаиморасчетов.Заголовок = НСтр("ru='Расшифровка расчетов по договорам';uk='Розшифровка розрахунків за договорами'");
	КонецЕсли;

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидРасшифровкиПоПартнерамДоговорамПриИзменении(Элемент)
		
	Элементы.Партнер.Доступность = ВидРасшифровкиПоПартнерамДоговорам = 2;
	Элементы.Договор.Доступность = ВидРасшифровкиПоПартнерамДоговорам = 4;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	Если ВидРасшифровкиПоПартнерамДоговорам = 2 Тогда
		Если Партнер.Пустая() Тогда
			Текст = НСтр("ru='Поле ""Фильтр по партнеру"" не заполнено';uk='Полі ""Фільтр за партнером"" не заповнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				,
				"Партнер",
				,
				Отказ);
		КонецЕсли; 
	Иначе		
		Партнер = ПредопределенноеЗначение("Справочник.Партнеры.ПустаяСсылка");
	КонецЕсли; 
	
	Если ВидРасшифровкиПоПартнерамДоговорам = 4 Тогда
		Если Договор.Пустая() Тогда
			Текст = НСтр("ru='Поле ""Фильтр по договору"" не заполнено';uk='Полі ""Фільтр за договором"" не заповнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				,
				"Договор",
				,
				Отказ);
		КонецЕсли; 
	Иначе		
		Договор = ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка");
	КонецЕсли; 
	
	Если НЕ Отказ Тогда
		
		РасшифровкаПоПартнерам = ВидРасшифровкиПоПартнерамДоговорам = 1;
		РасшифровкаПоДоговорам = ВидРасшифровкиПоПартнерамДоговорам = 3;
		
		Закрыть(Новый Структура("РасшифровкаПоЗаказам, РасшифровкаПоПартнерам, РасшифровкаПоДоговорам, Партнер, Договор",
							 	 РасшифровкаПоЗаказам, РасшифровкаПоПартнерам, РасшифровкаПоДоговорам, Партнер, Договор));
	КонецЕсли;	 
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти
