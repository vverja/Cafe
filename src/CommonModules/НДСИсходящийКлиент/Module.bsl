////////////////////////////////////////////////////////////////////////////////
// АВТОРАСЧЕТ НДС

// Процедура сбрасывает флаг СкидкиРассчитаны и делает недоступными колонки табличной части.
//
Процедура СброситьПроверитьФлагПерерасчетПроизведен(Форма, Проверять = Ложь, ИмяРеквизита = "ПерерасчетПроизведен") Экспорт
	
	Форма[ИмяРеквизита] = Ложь;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИНТЕРАКТИВНАЯ РАБОТА С ПОЛЬЗОВАТЕЛЕМ

Процедура ЗаполнитьОтборНастроек(ОтборПользовательскихНастроек, ПараметрыОтбора, ЗначенияЗаполнения)
	
	Для Каждого ЭлементОтбора Из ОтборПользовательскихНастроек.Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") 
			И ЭлементОтбора.Использование = Истина
			И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно
			И ПараметрыОтбора.Найти(Строка(ЭлементОтбора.ЛевоеЗначение)) <> Неопределено Тогда
			
				ЗначенияЗаполнения.Вставить(Строка(ЭлементОтбора.ЛевоеЗначение), ЭлементОтбора.ПравоеЗначение);
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПараметрыОтбораСпискаНалоговыхДокументов()
	
	Массив = Новый Массив();
	Массив.Добавить("Валюта");
	Массив.Добавить("Организация");
	Массив.Добавить("Партнер");
	Массив.Добавить("Контрагент");
	
	Возврат Массив;
	
КонецФункции

// Возвращает данные для заполнения формы нового налогового документа
//
// Параметры:
//  ТекущаяСтрока				 - ДокументСсылка.НалоговаяНакладная, ДокументСсылка.Приложение2КНалоговойНакладной - 
//  ПользовательскиеНастройки	 - КоллекцияЭлементовПользовательскихНастроекКомпоновкиДанных - Настройки формы списка документов
//  ФиксированныеНастройки		 - НастройкиКомпоновкиДанных - Настройки формы списка документов
//  ПараметрыОтбора				 - Массив - Массив строк с именами ключевых полей шапки документа
//  Копирование					 - Булево - 
// 
// Возвращаемое значение:
//   - Структура
//
Функция ПодготовитьПараметрыЗаполненияНалоговогоДокумента(ТекущаяСтрока, ПользовательскиеНастройки, ФиксированныеНастройки, ПараметрыОтбора, Копирование) Экспорт
	
	ПараметрыФормы = Новый Структура;
	
	Если Копирование И ТекущаяСтрока <> Неопределено Тогда
		ПараметрыФормы.Вставить("ЗначениеКопирования", ТекущаяСтрока);
	Иначе
		
		ОтборПользовательскихНастроек = Неопределено;
		
		Для Каждого ЭлементНастроек Из ПользовательскиеНастройки Цикл
			Если ТипЗнч(ЭлементНастроек) = Тип("ОтборКомпоновкиДанных") Тогда
				ОтборПользовательскихНастроек = ЭлементНастроек;
			КонецЕсли;
		КонецЦикла;
		
		ЗначенияЗаполнения = Новый Структура;
		
		Если ОтборПользовательскихНастроек <> Неопределено Тогда
			ЗаполнитьОтборНастроек(ОтборПользовательскихНастроек, ПараметрыОтбора, ЗначенияЗаполнения);
		КонецЕсли;
		
		ЗаполнитьОтборНастроек(ФиксированныеНастройки.Отбор, ПараметрыОтбора, ЗначенияЗаполнения);
		
		Если ЗначенияЗаполнения.Количество() > 0 Тогда
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		КонецЕсли;
	
	КонецЕсли;
	
	Возврат ПараметрыФормы;
	
КонецФункции

Процедура ПредупредитьПередСозданиемНалоговогоДокументаВручную(Форма, Элемент, Отказ, Копирование, Параметр = Неопределено, ИмяДинамическогоСписока = "Список") Экспорт

	Отказ = Истина;
	
	Если Параметр = Тип("ДокументСсылка.Приложение2КНалоговойНакладной") Тогда
		ИмяОткрываемойФормы = "Документ.Приложение2КНалоговойНакладной.Форма.ФормаДокумента";
	Иначе
		ИмяОткрываемойФормы = "Документ.НалоговаяНакладная.Форма.ФормаДокумента";
	КонецЕсли;
	
	ПараметрыОтбора = ПараметрыОтбораСпискаНалоговыхДокументов();
	ПараметрыФормы = ПодготовитьПараметрыЗаполненияНалоговогоДокумента(Элемент.ТекущаяСтрока, Форма[ИмяДинамическогоСписока].КомпоновщикНастроек.ПользовательскиеНастройки.Элементы,
		Форма[ИмяДинамическогоСписока].КомпоновщикНастроек.ФиксированныеНастройки, ПараметрыОтбора, Копирование);
	
	ДополнительныеСвойства = Новый Структура("ИмяОткрываемойФормы, ПараметрыФормы, Владелец", ИмяОткрываемойФормы, ПараметрыФормы, Форма.Элементы[ИмяДинамическогоСписока]);
	Оповещение = Новый ОписаниеОповещения("ПредупредитьПередСозданиемНалоговогоДокументаВручнуюЗавершение", ЭтотОбъект, ДополнительныеСвойства);
	
	ТекстВопроса = НСтр("ru='Большинство видов налоговых документов формируются автоматически в журнале 
                                                  |""Исходящие налоговые документы"" на закладке ""Оформление налоговых документов"".
                                                  |
                                                  |Создать документ вручную?'
                                                  |;uk='Більшість видів податкових документів формуються автоматично в журналі 
                                                  |""Вихідні податкові документи"" на закладці ""Оформлення податкових документів"".
                                                  |
                                                  |Створити документ вручну?'");
	
	ПоказатьВопрос(Оповещение,
		ТекстВопроса,
		РежимДиалогаВопрос.ОКОтмена,
		, 
		КодВозвратаДиалога.ОК, 
		НСтр("ru='Создание нового налогового документа';uk='Створення нового податкового документа'")
	);
	
КонецПроцедуры

Процедура ПредупредитьПередСозданиемНалоговогоДокументаВручнуюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	ИмяОткрываемойФормы = ДополнительныеПараметры.ИмяОткрываемойФормы;
	ПараметрыФормы      = ДополнительныеПараметры.ПараметрыФормы;
	Владелец            = ДополнительныеПараметры.Владелец;
	
	ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыФормы, Владелец);
	
КонецПроцедуры

Процедура ПредупредитьПередСозданиемВводаОстатков(Дата, Отказ) Экспорт
		
 	ТекстВопроса = НСтр("ru='ВНИМАНИЕ! Зарегистрированы авансы покупателей, по которым выписаны НН.
                         |При первом запуске формирования налоговых документов в журнале «Исходящие налоговые документы», автоматически будут сформированы НН на сумму аванса. 
                         |Все НН будут сформированы на дату %1. НН формируются для контроля соответствия номенклатурного состава НН на аванс и будущей отгрузки.
                         |Проверьте дату автоматически сформированных НН. Дату %1 замените на дату, которой были выписаны НН.
                         |Подробнее о формировании НН по авансам покупателей см. справку к документу «Ввод начальных остатков»'
                         |;uk='УВАГА! Зареєстровані аванси покупців, за якими виписані ПН.
                         |При першому запуску формування податкових документів в журналі «Вихідні податкові документи», автоматично будуть сформовані ПН на суму авансу. 
                         |Всі ПН будуть сформовані на дату %1. ПН формуються для контролю відповідності номенклатурного складу ПН на аванс і майбутнього відвантаження.
                         |Перевірте дату автоматично сформованих ПН. Дату %1 замініть на дату, якою були виписані ПН.
                         |Детальніше про формування ПН за авансами покупців див. довідку до документа «Введення початкових залишків»'");
    ТекстВопроса = СтрШаблон(ТекстВопроса,Формат(Дата, "ДФ=dd.MM.yyyy"));

	ПоказатьПредупреждение(,ТекстВопроса);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ РАБОТЫ С ФОРМАМИ НАЛОГОВЫХ ДОКУМЕНТОВ

Процедура ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения, ОбработатьРеквизитыРегл = Ложь) Экспорт
	
	ТекущаяСтрокаДляОбработки = НДСИсходящийКлиентСервер.СоздатьСтруктуруДляОбработкиПоТекущейСтроке(ТекущаяСтрока, ОбработатьРеквизитыРегл);
	
    // Для услуг измеряемых только в суммовом выражении, количество может быть не задано
	Если СтруктураДействий.Свойство("ПересчитатьСумму") И Не ЗначениеЗаполнено(ТекущаяСтрокаДляОбработки.КоличествоУпаковок) Тогда
		ИмяКолонкиКоличествоУпаковок = "КоличествоУпаковок_УсловнаяЕдиница";
		ТекущаяСтрокаДляОбработки.Вставить(ИмяКолонкиКоличествоУпаковок, 1);
		СтруктураДействий.Вставить("ПересчитатьСумму", ИмяКолонкиКоличествоУпаковок);
	КонецЕсли;

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрокаДляОбработки, СтруктураДействий, КэшированныеЗначения);
	
	НДСИсходящийКлиентСервер.ЗаполнитьТекущуюСтрокуПоСтруктуреДляОбработки(ТекущаяСтрока, ТекущаяСтрокаДляОбработки, ОбработатьРеквизитыРегл);
	
КонецПроцедуры

Процедура ОткрытьФормуОформленияНалоговыхДокументовПоРегистратору(Параметры, Владелец, Уникальность) Экспорт
	
	ОткрытьФорму("ЖурналДокументов.ИсходящиеНалоговыеДокументы.Форма.ОформлениеНалоговыхДокументовПоРегистратору", Параметры, Владелец, Уникальность);
	
КонецПроцедуры

Функция ОткрытьФормуНалоговогоДокумента(ОписаниеКоманды) Экспорт
	
	ПараметрыВыполненияКоманды = Новый Структура("Источник,Уникальность,Окно,НавигационнаяСсылка");
	ЗаполнитьЗначенияСвойств(ПараметрыВыполненияКоманды, ОписаниеКоманды.ДополнительныеПараметры);
	
	Основание = ОписаниеКоманды.ОбъектыОснований[0];
	
	ПараметрыФормы = Новый Структура("ДокументОснование", Основание);
	НДСИсходящийКлиент.ОткрытьФормуОформленияНалоговыхДокументовПоРегистратору(ПараметрыФормы, Неопределено, Основание);
	
КонецФункции
