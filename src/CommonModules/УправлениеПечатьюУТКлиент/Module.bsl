
#Область ПрограммныйИнтерфейс

#Область ЭтикеткиИЦенники

Функция ПечатьЭтикетокИЦенников(ОписаниеКоманды) Экспорт
	
	ДополнительныеПараметры = Новый Структура("МенеджерПечати", ОписаниеКоманды.МенеджерПечати);
	
	АдресВХранилище =
		УправлениеПечатьюУТВызовСервера.ДанныеДляПечатиЦенниковИЭтикеток(
			ОписаниеКоманды.Идентификатор,
			ОписаниеКоманды.ОбъектыПечати,
			ДополнительныеПараметры);
	
	ОткрытьФорму(
		"Обработка.ПечатьЭтикетокИЦенников.Форма.ФормаТовары",
		Новый Структура("АдресВХранилище, НазначениеШаблона", АдресВХранилище, Неопределено),
		ОписаниеКоманды.Форма,
		Новый УникальныйИдентификатор);
	
КонецФункции

Функция ПечатьЭтикетокСкладскиеЯчейки(ОписаниеКоманды) Экспорт
	
	АдресВХранилище =
		УправлениеПечатьюУТВызовСервера.ДанныеДляПечатиЭтикетокСкладскиеЯчейки(
			ОписаниеКоманды.Идентификатор,
			ОписаниеКоманды.ОбъектыПечати);
	
	ОткрытьФорму(
		"Обработка.ПечатьЭтикетокИЦенников.Форма.ФормаСкладскиеЯчейки",
		Новый Структура("АдресВХранилище, НазначениеШаблона", АдресВХранилище, Неопределено),
		ОписаниеКоманды.Форма,
		Новый УникальныйИдентификатор);
	
КонецФункции

Функция ПечатьЭтикетокДоставки(ОписаниеКоманды) Экспорт
	
	ОчиститьСообщения();
	
	СтруктураВозврата = УправлениеПечатьюУТВызовСервера.ДанныеДляПечатиЭтикетокДоставки(
			ОписаниеКоманды.Идентификатор,
			ОписаниеКоманды.ОбъектыПечати);
		
	Если СтруктураВозврата.МассивСсылокСОшибками.Количество() > 0 Тогда
		ТекстСообщения = НСтр("ru='Не настроен общий шаблон этикетки доставки. Обратитесь к администратору.';uk='Не настроєний загальний шаблон етикетки доставки. Зверніться до адміністратора.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Для Каждого СсылкаПеревозчик Из СтруктураВозврата.МассивПеревозчиковБезШаблоновЭтикетки Цикл
		Если ЗначениеЗаполнено(СсылкаПеревозчик) Тогда
			ТекстСообщения = НСтр("ru='Для перевозчика ""%Перевозчик%"" не заполнен шаблон этикетки доставки.';uk='Для перевізника ""%Перевозчик%"" не заповнений шаблон етикетки доставки.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Перевозчик%", СсылкаПеревозчик);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СсылкаРаспоряжение Из СтруктураВозврата.МассивСсылокСОшибками Цикл
		ТекстСообщения = НСтр("ru='Не удалось напечатать этикетку доставки для документа ""%Распоряжение%"".';uk='Не вдалося надрукувати етикетку доставки документа ""%Распоряжение%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Распоряжение%", СсылкаРаспоряжение);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЦикла;
	
	Если СтруктураВозврата.ЕстьЭтикеткиДляПечати Тогда
		
		ПараметрКоманды = Новый Массив;
		ПараметрКоманды.Добавить(ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
		
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			"Обработка.ПечатьЭтикетокИЦенников",
			"ЭтикеткаДоставки",
			ПараметрКоманды,
			Неопределено,
			Новый Структура(
				"АдресВХранилище, СтруктураМакетаШаблона",
				СтруктураВозврата.АдресВХранилище, Неопределено));
	Иначе
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Печатная форма недоступна.';uk='Друкована форма недоступна.'"));
		
	КонецЕсли;
		
КонецФункции

Функция ПечатьЭтикетокУпаковочныхЛистов(ОписаниеКоманды) Экспорт
	
	СтруктураВозврата = УправлениеПечатьюУТВызовСервера.ДанныеДляПечатиЭтикетокУпаковочныеЛисты(ОписаниеКоманды.ОбъектыПечати);
		
	Если Не СтруктураВозврата.ЕстьШаблонЭтикетки Тогда
		ТекстСообщения = НСтр("ru='Не настроен общий шаблон этикетки упаковочного листа. Обратитесь к администратору.';uk='Не настроєний загальний шаблон етикетки пакувального листа. Зверніться до адміністратора.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Если НЕ СтруктураВозврата.ЕстьЭтикеткиДляПечати Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Печатная форма недоступна.';uk='Друкована форма недоступна.'"));
	КонецЕсли;
	
	Если СтруктураВозврата.ЕстьЭтикеткиДляПечати И СтруктураВозврата.ЕстьШаблонЭтикетки Тогда
		
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			"Обработка.ПечатьЭтикетокИЦенников",
			"ЭтикеткаУпаковочныеЛисты",
			ОписаниеКоманды.ОбъектыПечати,
			Неопределено,
			Новый Структура(
				"АдресВХранилище, ШаблонЭтикетки, КоличествоЭкземпляров",
				СтруктураВозврата.АдресВХранилище, СтруктураВозврата.ШаблонЭтикетки, 1));
		
	КонецЕсли;
		
КонецФункции

Функция ПечатьЭтикетокУпаковочныхЛистовНаПринтер(ОписаниеКоманды) Экспорт
	
	СтруктураВозврата = УправлениеПечатьюУТВызовСервера.ДанныеДляПечатиЭтикетокУпаковочныеЛисты(ОписаниеКоманды.ОбъектыПечати);
		
	Если Не СтруктураВозврата.ЕстьШаблонЭтикетки Тогда
		ТекстСообщения = НСтр("ru='Не настроен общий шаблон этикетки упаковочного листа. Обратитесь к администратору.';uk='Не настроєний загальний шаблон етикетки пакувального листа. Зверніться до адміністратора.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Если НЕ СтруктураВозврата.ЕстьЭтикеткиДляПечати Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Печатная форма недоступна.';uk='Друкована форма недоступна.'"));
	КонецЕсли;
	
	Если СтруктураВозврата.ЕстьЭтикеткиДляПечати И СтруктураВозврата.ЕстьШаблонЭтикетки Тогда
		
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(
			"Обработка.ПечатьЭтикетокИЦенников",
			"ЭтикеткаУпаковочныеЛисты",
			ОписаниеКоманды.ОбъектыПечати,
			Новый Структура(
				"АдресВХранилище, ШаблонЭтикетки, КоличествоЭкземпляров",
				СтруктураВозврата.АдресВХранилище, СтруктураВозврата.ШаблонЭтикетки, 1));
		
	КонецЕсли;
		
КонецФункции

#КонецОбласти

#Область КомплектыДокументов

Функция ПечатьКомплектаДокументов(ОписаниеКоманды) Экспорт
	
	ЗапрашиватьПодтверждение = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить("ОбщиеНастройкиПользователя", "ЗапрашиватьПодтверждениеПриПечатиКомплектаДокументов");
	Если ЗапрашиватьПодтверждение <> Неопределено Тогда
		ЗапрашиватьПодтверждениеПриПечатиКомплектаДокументов = ЗапрашиватьПодтверждение;
	Иначе
		ЗапрашиватьПодтверждениеПриПечатиКомплектаДокументов = Истина;
	КонецЕсли; 
	
	Если ЗапрашиватьПодтверждениеПриПечатиКомплектаДокументов Тогда
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить("КодВозвратаДиалога.Да",	НСтр("ru='Распечатать';uk='Роздрукувати'"));
		Кнопки.Добавить("КодВозвратаДиалога.Нет",	НСтр("ru='Не печатать';uk='Не друкувати'"));
		
		Параметры = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
		Параметры.КнопкаПоУмолчанию = КодВозвратаДиалога.Да;
		Параметры.Заголовок = НСтр("ru='Печать комплекта документов';uk='Друк комплекта документів'");
		Параметры.КнопкаТаймаута = КодВозвратаДиалога.Нет;
		Параметры.БольшеНеЗадаватьЭтотВопрос = ЗапрашиватьПодтверждениеПриПечатиКомплектаДокументов;
		
		СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(
			Новый ОписаниеОповещения("ПечатьКомплектаДокументовЗавершение", ЭтотОбъект, Новый Структура("ОписаниеКоманды", ОписаниеКоманды)),
			НСтр("ru='Распечатать комплект документов на принтере?';uk='Роздрукувати комплект документів на принтері?'"), 
			Кнопки,
			Параметры);
		Возврат Ложь;
		
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(
		НСтр("ru='Документы отправлены на печать';uk='Документи відправлені на друк'"),
		,
		,
		БиблиотекаКартинок.Информация32);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(
		"РегистрСведений.НастройкиПечатиОбъектов",
		"КомплектДокументов",
		ОписаниеКоманды.ОбъектыПечати,
		Новый Структура);
	
КонецФункции

// Служебная процедура.
Процедура ПечатьКомплектаДокументовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ОписаниеКоманды = ДополнительныеПараметры.ОписаниеКоманды;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить("ОбщиеНастройкиПользователя", "ЗапрашиватьПодтверждениеПриПечатиКомплектаДокументов", Не Результат.БольшеНеЗадаватьЭтотВопрос);
	
	Если Результат.Значение <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(
		НСтр("ru='Документы отправлены на печать';uk='Документи відправлені на друк'"),
		,
		,
		БиблиотекаКартинок.Информация32);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(
		"РегистрСведений.НастройкиПечатиОбъектов",
		"КомплектДокументов",
		ОписаниеКоманды.ОбъектыПечати,
		Новый Структура);
	
КонецПроцедуры

Функция ПечатьКомплектаДокументовСНастройкой(ОписаниеКоманды) Экспорт
	
	ОткрытьФорму(
		"РегистрСведений.НастройкиПечатиОбъектов.Форма.НастройкаПечатиКомплекта",
		Новый Структура("Объекты", ОписаниеКоманды.ОбъектыПечати));
	
КонецФункции

#КонецОбласти

#Область М21

Функция ПечатьМ21(ОписаниеКоманды) Экспорт
	                    	
	ОчиститьСообщения();
	// не поддерживаем множественную печать
	Если ОписаниеКоманды.ОбъектыПечати.Количество() > 1 Тогда
		ТекстСообщения = НСтр("ru='Не поддерживается групповое формирование печатной формы ""М-21"" через список документов ""Пересчеты товаров"".';uk='Не підтримується групове формування друкованої форми ""М-21"" через список документів ""Перерахунки товарів"".'");	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь;
	КонецЕсли;
	
	ПараметрыФормирования = УправлениеПечатьюУТВызовСервера.ПолучитьПараметрыФормирования(ОписаниеКоманды.ОбъектыПечати);
	ПараметрыФормирования.Вставить("Идентификатор", ОписаниеКоманды.Идентификатор);
	
	ОткрытьФорму("Документ.ИнвентаризационнаяОпись.Форма.ФормаПечати",
		ПараметрыФормирования, ОписаниеКоманды.Форма,,ВариантОткрытияОкна.ОтдельноеОкно,,,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		
КонецФункции

#КонецОбласти


#Область ДокументАктвыполненныхработ

Функция ПечатьАктВыполненныхРаботMicrosoftWord(ОписаниеКоманды) Экспорт
	КодЯзыка = Локализация.ПолучитьЯзыкФормированияПечатныхФорм();
	Возврат ПечатьАктMicrosoftWord(ОписаниеКоманды, КодЯзыка);
КонецФункции // ПечатьАктВыполненныхРаботMicrosoftWord 

Функция ПечатьАктВыполненныхРаботMicrosoftWordАльтернативныйЯзык(ОписаниеКоманды) Экспорт
	КодЯзыка = ?(Локализация.ПолучитьЯзыкФормированияПечатныхФорм() = "uk", "ru", "uk");
	Если НЕ УправлениеПечатьюКлиент.ПроверитьДоступностьЯзыкаДляПечати(КодЯзыка) Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат ПечатьАктMicrosoftWord(ОписаниеКоманды, КодЯзыка);
КонецФункции // ПечатьАктВыполненныхРаботMicrosoftWord 

Функция ПечатьАктMicrosoftWord(ОписаниеКоманды, КодЯзыка) Экспорт
	
	Состояние(НСтр("ru='Выполняется формирование печатных форм';uk='Виконується формування друкованих форм'"));
	
	ПараметрыВывода = Новый Структура;
	ПараметрыВывода.Вставить("КодЯзыкаДляМногоязычныхПечатныхФорм",	КодЯзыка);	
	
	ИмяМакета = "ПФ_DOC_Акт";
	ТипМакета = "doc";
	
	МакетИДанныеОбъекта = УправлениеПечатьюВызовСервера.МакетыИДанныеОбъектовДляПечати(
		"Обработка.ПечатьАктОбОказанииУслуг",
		ИмяМакета,
		ОписаниеКоманды.ОбъектыПечати,
		ПараметрыВывода
	);
		
	ДвоичныеДанныеМакетов = МакетИДанныеОбъекта.Макеты.ДвоичныеДанныеМакетов;
	
	Секции = МакетИДанныеОбъекта.Макеты.ОписаниеСекций;
	
	Для Каждого ДокументСсылка Из ОписаниеКоманды.ОбъектыПечати Цикл
		
		ДанныеОбъекта = МакетИДанныеОбъекта.Данные[ДокументСсылка][ИмяМакета];
		
		Попытка
			
			ПечатнаяФорма = УправлениеПечатьюКлиент.ИнициализироватьПечатнуюФорму(ТипМакета);
			Макет = УправлениеПечатьюКлиент.ИнициализироватьМакетОфисногоДокумента(
				ДвоичныеДанныеМакетов[ИмяМакета], ТипМакета);
				
			
				
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].Шапка);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
		
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].Заголовок);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ДополнительнаяИнформацияШапки) Тогда
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].ДополнительнаяИнформацияШапки);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			КонецЕсли;
			
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].ТекстАктаНачало);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
					
			ОбластьСтроки = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].ДопИнформация);
			УправлениеПечатьюКлиент.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма, ОбластьСтроки, ДанныеОбъекта.ДопИнформация, Ложь);
			
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].ТекстАктаКонец);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			ЕстьНДС       = ДанныеОбъекта.УчитыватьНДС;
			ЕстьСкидки    = ДанныеОбъекта.ЕстьСкидки;
			ПоказыватьНДС = ДанныеОбъекта.ПоказыватьНДС;
			
			СуффиксОбласти = ?(ЕстьСкидки, "СоСкидкой", "") + ?(ЕстьНДС И ПоказыватьНДС, "СНДС", "");
			
			ОбластьШапки = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета]["ШапкаТаблицы" + СуффиксОбласти]);
			
			УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, ОбластьШапки, Ложь);
			УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
			
			ОбластьСтроки = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета]["Строка" + СуффиксОбласти]);
			УправлениеПечатьюКлиент.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма, ОбластьСтроки, ДанныеОбъекта.Услуги, Истина);
			
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].Итого);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			Если ЕстьНДС Тогда
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].ИтогоНДС);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				Если Не ДанныеОбъекта.ЦенаВключаетНДС Тогда
					Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].ИтогоСНДС);
					УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				КонецЕсли;
			КонецЕсли;
			
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].СуммаПрописью);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ДополнительнаяИнформация) Тогда
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].ДополнительнаяИнформация);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			КонецЕсли;
			
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].Подвал);
			УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.МестоСоставления) Тогда
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].МестоСоставления);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			КонецЕсли;
			
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].Подписи);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			УправлениеПечатьюКлиент.ПоказатьДокумент(ПечатнаяФорма);
			
		Исключение
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма);
			УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
			
			Возврат Ложь;
			
		КонецПопытки;
		
		УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма, Ложь);
		УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
		
	КонецЦикла;
	
	Состояние(НСтр("ru='Формирование печатных форм завершено';uk='Формування друкованих форм завершено'"));
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ДокументВзаимозачетзадолженности

Функция ПечатьАктаВзаимозачетаЗадолженностиMicrosoftWord(ОписаниеКоманды) Экспорт
	
	Состояние(НСтр("ru='Выполняется формирование печатных форм';uk='Виконується формування друкованих форм'"));
	
	ИмяМакета = "ПФ_DOC_АктВзаимозачета";
	ИмяМакета = "ПФ_DOC_АктВзаимозачета_uk";
	ТипМакета = "doc";
	
	МакетИДанныеОбъекта = УправлениеПечатьюВызовСервера.МакетыИДанныеОбъектовДляПечати("Документ.ВзаимозачетЗадолженности",
		ИмяМакета,
		ОписаниеКоманды.ОбъектыПечати);
		
	ДвоичныеДанныеМакетов = МакетИДанныеОбъекта.Макеты.ДвоичныеДанныеМакетов;
	
	Секции = МакетИДанныеОбъекта.Макеты.ОписаниеСекций;
	
	Для Каждого ДокументСсылка Из ОписаниеКоманды.ОбъектыПечати Цикл
		
		ДанныеОбъекта = МакетИДанныеОбъекта.Данные[ДокументСсылка][ИмяМакета];
		
		Если Не ДанныеОбъекта.Взаимозачет Тогда
			
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не требуется печатать акт взаимозачета для документа %1';uk='Не потрібно друкувати акт взаємозаліку для документа %1'"),
				ДокументСсылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				ДокументСсылка);
			
		Иначе
		
			Попытка
					
				ПечатнаяФорма = УправлениеПечатьюКлиент.ИнициализироватьПечатнуюФорму(ТипМакета);
				Макет = УправлениеПечатьюКлиент.ИнициализироватьМакетОфисногоДокумента(
					ДвоичныеДанныеМакетов[ИмяМакета], ТипМакета);
					
				// Вывод колонтитулов документа.
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета]["ВерхнийКолонтитул"]);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета]["НижнийКолонтитул"]);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
				// Выводим заголовок документа.	
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].Заголовок);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Истина);
				
				// Выводим данные дебиторской задолженности;
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ДебиторскаяЗадолженность);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Истина);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ШапкаТаблицы);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].СтрокаТаблицы);
				УправлениеПечатьюКлиент.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма,Область, ДанныеОбъекта.ДебиторскаяЗадолженность, Ложь);
				
				// Выводим данные кредиторской задолженности.
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].КредиторскаяЗадолженность);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Истина);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ШапкаТаблицы);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].СтрокаТаблицы);
				УправлениеПечатьюКлиент.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма,Область, ДанныеОбъекта.КредиторскаяЗадолженность, Ложь);
				
				// Выводим подвал документа.
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].Подвал);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Истина);

				УправлениеПечатьюКлиент.ПоказатьДокумент(ПечатнаяФорма);
				
			Исключение
			
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма);
				УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
						
				Возврат Ложь;
				
			КонецПопытки;

			УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма, Ложь);
			УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Состояние(НСтр("ru='Формирование печатных форм завершено';uk='Формування друкованих форм завершено'"));
	
КонецФункции

Функция ПечатьАктаПереуступкиДолгаMicrosoftWord(ОписаниеКоманды) Экспорт
	
	Состояние(НСтр("ru='Выполняется формирование печатных форм';uk='Виконується формування друкованих форм'"));
	
	ИмяМакета = "ПФ_DOC_АктПереуступкиДолга_uk";
	ТипМакета = "doc";
	
	МакетИДанныеОбъекта = УправлениеПечатьюВызовСервера.МакетыИДанныеОбъектовДляПечати("Документ.ВзаимозачетЗадолженности",
		ИмяМакета,
		ОписаниеКоманды.ОбъектыПечати);
		
	ДвоичныеДанныеМакетов = МакетИДанныеОбъекта.Макеты.ДвоичныеДанныеМакетов;
	
	Секции = МакетИДанныеОбъекта.Макеты.ОписаниеСекций;
	
	Для Каждого ДокументСсылка Из ОписаниеКоманды.ОбъектыПечати Цикл
		
		ДанныеОбъекта = МакетИДанныеОбъекта.Данные[ДокументСсылка][ИмяМакета];
		
		Если ДанныеОбъекта.Взаимозачет или ДанныеОбъекта.ВзаимозачетМеждуОрганизациями Тогда
			
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не требуется печатать акт переуступки долга для документа %1';uk='Не потрібно друкувати акт переуступки боргу для документа %1'"),
				ДокументСсылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				ДокументСсылка);
			
		Иначе

			Попытка
					
				ПечатнаяФорма = УправлениеПечатьюКлиент.ИнициализироватьПечатнуюФорму(ТипМакета);
				Макет = УправлениеПечатьюКлиент.ИнициализироватьМакетОфисногоДокумента(
					ДвоичныеДанныеМакетов[ИмяМакета], ТипМакета);
					
				// Вывод колонтитулов документа.
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета]["ВерхнийКолонтитул"]);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета]["НижнийКолонтитул"]);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
				// Выводим заголовок документа.	
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].Заголовок);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Истина);
				
				// Выводим данные дебиторской задолженности;
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ДебиторскаяЗадолженность);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Истина);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ШапкаТаблицы);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].СтрокаТаблицы);
				УправлениеПечатьюКлиент.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма,Область, ДанныеОбъекта.ДебиторскаяЗадолженность, Ложь);
				
				// Выводим данные кредиторской задолженности.
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].КредиторскаяЗадолженность);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Истина);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ШапкаТаблицы);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].СтрокаТаблицы);
				УправлениеПечатьюКлиент.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма,Область, ДанныеОбъекта.КредиторскаяЗадолженность, Ложь);
				
				// Выводим подвал документа.
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].Итог);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Истина);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].Подвал);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Истина);
				
				УправлениеПечатьюКлиент.ПоказатьДокумент(ПечатнаяФорма);
				
			Исключение
			
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма);
				УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
						
				Возврат Ложь;
				
			КонецПопытки;

			УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма, Ложь);
			УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Состояние(НСтр("ru='Формирование печатных форм завершено';uk='Формування друкованих форм завершено'"));
	
КонецФункции

Функция ПечатьАктаПереуступкиДолгаМеждуОрганизациямиMicrosoftWord(ОписаниеКоманды) Экспорт
	
	Состояние(НСтр("ru='Выполняется формирование печатных форм';uk='Виконується формування друкованих форм'"));
	
	ИмяМакета = "ПФ_DOC_АктПереуступкиДолгаМеждуОрганизациями_uk";
	ТипМакета = "doc";
	
	МакетИДанныеОбъекта = УправлениеПечатьюВызовСервера.МакетыИДанныеОбъектовДляПечати("Документ.ВзаимозачетЗадолженности",
		ИмяМакета,
		ОписаниеКоманды.ОбъектыПечати);
		
	ДвоичныеДанныеМакетов = МакетИДанныеОбъекта.Макеты.ДвоичныеДанныеМакетов;
	
	Секции = МакетИДанныеОбъекта.Макеты.ОписаниеСекций;
	
	Для Каждого ДокументСсылка Из ОписаниеКоманды.ОбъектыПечати Цикл
		
		ДанныеОбъекта = МакетИДанныеОбъекта.Данные[ДокументСсылка][ИмяМакета];
		
		Если не ДанныеОбъекта.ВзаимозачетМеждуОрганизациями Тогда
			
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не требуется печатать акт переуступки долга между организациями для документа %1';uk='Не потрібно друкувати акт переуступки боргу між організаціями для документа %1'"),
				ДокументСсылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				ДокументСсылка);
			
		Иначе

			Попытка
					
				ПечатнаяФорма = УправлениеПечатьюКлиент.ИнициализироватьПечатнуюФорму(ТипМакета);
				Макет = УправлениеПечатьюКлиент.ИнициализироватьМакетОфисногоДокумента(
					ДвоичныеДанныеМакетов[ИмяМакета], ТипМакета);
					
				// Вывод колонтитулов документа.
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета]["ВерхнийКолонтитул"]);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета]["НижнийКолонтитул"]);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
				// Выводим заголовок документа.	
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].Заголовок);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Истина);
				
				// Выводим данные дебиторской задолженности;
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ДебиторскаяЗадолженность);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Истина);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ШапкаТаблицы);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].СтрокаТаблицы);
				УправлениеПечатьюКлиент.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма,Область, ДанныеОбъекта.ДебиторскаяЗадолженность, Ложь);
				
				// Выводим данные кредиторской задолженности.
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].КредиторскаяЗадолженность);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Истина);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ШапкаТаблицы);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].СтрокаТаблицы);
				УправлениеПечатьюКлиент.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма,Область, ДанныеОбъекта.КредиторскаяЗадолженность, Ложь);
				
				// Выводим подвал документа.
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].Итог);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Истина);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].Подвал);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Истина);
				
				УправлениеПечатьюКлиент.ПоказатьДокумент(ПечатнаяФорма);
				
			Исключение
			
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма);
				УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
						
				Возврат Ложь;
				
			КонецПопытки;

			УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма, Ложь);
			УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Состояние(НСтр("ru='Формирование печатных форм завершено';uk='Формування друкованих форм завершено'"));
	
КонецФункции

#КонецОбласти

#Область ДокументЗаданиеторговомупредставителю

Функция НастройкаПечатиБланковЗаданияТорговомуПредставителю(ОписаниеКоманды) Экспорт
	
	ОткрытьФорму(
		"Документ.ЗаданиеТорговомуПредставителю.Форма.НастройкаПечатиБланковЗадания",
		,
		ОписаниеКоманды.Форма,
		ОписаниеКоманды.Форма.УникальныйИдентификатор);
	
КонецФункции

#КонецОбласти

#Область ДокументКоммерческоепредложениеклиенту

Функция ПечатьКоммерческоеПредложениеMicrosoftWord(ОписаниеКоманды) Экспорт
	
	Состояние(НСтр("ru='Выполняется формирование печатных форм';uk='Виконується формування друкованих форм'"));
	
	КодЯзыка = Локализация.ПолучитьЯзыкФормированияПечатныхФорм();
	Если Найти(ОписаниеКоманды.Идентификатор,"АльтернативныйЯзык") Тогда
	    КодЯзыка = ?(КодЯзыка = "uk", "ru", "uk");
	КонецЕсли; 
	ПараметрыВывода = Новый Структура("КодЯзыкаДляМногоязычныхПечатныхФорм", КодЯзыка);	
	
	ИмяМакета = "ПФ_DOC_КоммерческоеПредложение";
	ТипМакета = "doc";
	
	МакетИДанныеОбъекта = УправлениеПечатьюВызовСервера.МакетыИДанныеОбъектовДляПечати("Документ.КоммерческоеПредложениеКлиенту",
		ИмяМакета,
		ОписаниеКоманды.ОбъектыПечати,
		ПараметрыВывода
	);
		
	ДвоичныеДанныеМакетов = МакетИДанныеОбъекта.Макеты.ДвоичныеДанныеМакетов;
	
	Секции = МакетИДанныеОбъекта.Макеты.ОписаниеСекций;
	
	Для Каждого ДокументСсылка Из ОписаниеКоманды.ОбъектыПечати Цикл
		
		ДанныеОбъекта = МакетИДанныеОбъекта.Данные[ДокументСсылка][ИмяМакета];
		
		Попытка
			
			ПечатнаяФорма = УправлениеПечатьюКлиент.ИнициализироватьПечатнуюФорму(ТипМакета);
			Макет = УправлениеПечатьюКлиент.ИнициализироватьМакетОфисногоДокумента(
				ДвоичныеДанныеМакетов[ИмяМакета], ТипМакета);
			
			// Вывод колонтитулов документа.
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета]["ВерхнийКолонтитул"]);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
		
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета]["НижнийКолонтитул"]);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].Заголовок);
			УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
			УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.КонтактноеЛицо) Тогда
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].КонтактноеЛицо);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
			КонецЕсли;
			
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].Предложение);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			Если ДанныеОбъекта.ЕстьСкидки Тогда
				ПостфиксСтрок = "";
			Иначе
				ПостфиксСтрок = "БезСкидки";
			КонецЕсли;
			
			ОбластьСтрокаТаблицыТоварыСтандарт    = Секции[ИмяМакета]["СтрокаТаблицаТовары" + ПостфиксСтрок];
			ОбластьШапкаТаблицыТовары             = Секции[ИмяМакета]["ШапкаТаблицаТовары"  + ПостфиксСтрок];
			
			Если ДанныеОбъекта.ИспользоватьНаборы Тогда
				ОбластьСтрокаТаблицыТоварыНабор              = Секции[ИмяМакета]["СтрокаТаблицаТовары"  + ПостфиксСтрок + "Набор"];
				ОбластьСтрокаТаблицыТоварыКомплектующие      = Секции[ИмяМакета]["СтрокаТаблицаТовары"  + ПостфиксСтрок + "Комплектующие"];
				ОбластьСтрокаТаблицыТоварыКомплектующиеКонец = Секции[ИмяМакета]["СтрокаТаблицаТовары"  + ПостфиксСтрок + "КомплектующиеКонец"];
			КонецЕсли;
			
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,ОбластьШапкаТаблицыТовары);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			Индекс = 0;
			ВсегоСтрок = ДанныеОбъекта.Товары.Количество();
			Для Каждого СтрокаТЧ Из ДанныеОбъекта.Товары Цикл
				
				Индекс = Индекс + 1;
				
				Если НаборыКлиентСервер.ИспользоватьОбластьНабор(СтрокаТЧ, ДанныеОбъекта.ИспользоватьНаборы) Тогда
					ОбластьСтрокаТаблицыТовары = ОбластьСтрокаТаблицыТоварыНабор;
				ИначеЕсли НаборыКлиентСервер.ИспользоватьОбластьКомплектующие(СтрокаТЧ, ДанныеОбъекта.ИспользоватьНаборы) И Индекс < ВсегоСтрок Тогда
					ОбластьСтрокаТаблицыТовары = ОбластьСтрокаТаблицыТоварыКомплектующие;
				ИначеЕсли НаборыКлиентСервер.ИспользоватьОбластьКомплектующие(СтрокаТЧ, ДанныеОбъекта.ИспользоватьНаборы) Тогда
					ОбластьСтрокаТаблицыТовары = ОбластьСтрокаТаблицыТоварыКомплектующиеКонец;
				Иначе
					ОбластьСтрокаТаблицыТовары = ОбластьСтрокаТаблицыТоварыСтандарт;
				КонецЕсли;
				
				Масс = Новый Массив;
				Масс.Добавить(СтрокаТЧ);
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, ОбластьСтрокаТаблицыТовары);
				УправлениеПечатьюКлиент.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма, Область, Масс, Ложь);
				
			КонецЦикла;
			
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].ИтоговаяСтрока);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.СрокДействия) Тогда
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].СрокДействия);
				УправлениеПечатьюКлиент.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
				УправлениеПечатьюКлиент.ЗаполнитьПараметры(ПечатнаяФорма, ДанныеОбъекта);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ГрафикОплаты)
				ИЛИ ЗначениеЗаполнено(ДанныеОбъекта.ФормаОплаты)
				ИЛИ ЗначениеЗаполнено(ДанныеОбъекта.Склад)
				ИЛИ ЗначениеЗаполнено(ДанныеОбъекта.ДополнительнаяИнформация) Тогда
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].ЗаголовокДопИнформации);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ГрафикОплаты) Тогда
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ГрафикОплаты);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ФормаОплаты) Тогда
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ФормаОплаты);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.Склад) Тогда
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].Склад);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ДополнительнаяИнформация) Тогда
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ДополнительнаяИнформация);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
			КонецЕсли;
			
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].Подвал);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ТелефонОрганизации) Тогда
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ТелефонОрганизации);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.АдресЭлектроннойПочтыМенеджера) Тогда
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].АдресЭлектроннойПочтыМенеджера);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
			КонецЕсли;
			
			УправлениеПечатьюКлиент.ПоказатьДокумент(ПечатнаяФорма);
			
		Исключение
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма);
			УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
			
			Возврат Ложь;
			
		КонецПопытки;
		
		УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма, Ложь);
		УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
		
	КонецЦикла;
	
	Состояние(НСтр("ru='Формирование печатных форм завершено';uk='Формування друкованих форм завершено'"));
	
КонецФункции

Функция ПечатьКоммерческоеПредложениеOpenOfficeOrgWriter(ОписаниеКоманды) Экспорт
	
	Состояние(НСтр("ru='Выполняется формирование печатных форм';uk='Виконується формування друкованих форм'"));
	
	КодЯзыка = Локализация.ПолучитьЯзыкФормированияПечатныхФорм();
	Если Найти(ОписаниеКоманды.Идентификатор,"АльтернативныйЯзык") Тогда
	    КодЯзыка = ?(КодЯзыка = "uk", "ru", "uk");
	КонецЕсли; 
	ПараметрыВывода = Новый Структура("КодЯзыкаДляМногоязычныхПечатныхФорм", КодЯзыка);	
	
	ИмяМакета = "ПФ_ODT_КоммерческоеПредложение";
	ТипМакета = "odt";
	
	МакетИДанныеОбъекта = УправлениеПечатьюВызовСервера.МакетыИДанныеОбъектовДляПечати("Документ.КоммерческоеПредложениеКлиенту",
		ИмяМакета,
		ОписаниеКоманды.ОбъектыПечати,
		ПараметрыВывода
	);
		
	ДвоичныеДанныеМакетов = МакетИДанныеОбъекта.Макеты.ДвоичныеДанныеМакетов;
	
	Секции = МакетИДанныеОбъекта.Макеты.ОписаниеСекций;
	
	Для Каждого ДокументСсылка Из ОписаниеКоманды.ОбъектыПечати Цикл
		
		ДанныеОбъекта = МакетИДанныеОбъекта.Данные[ДокументСсылка][ИмяМакета];
		
		Попытка
			
			ПечатнаяФорма = УправлениеПечатьюКлиент.ИнициализироватьПечатнуюФорму(ТипМакета);
			Макет = УправлениеПечатьюКлиент.ИнициализироватьМакетОфисногоДокумента(
				ДвоичныеДанныеМакетов[ИмяМакета], ТипМакета);
				
			// Вывод колонтитулов документа.
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета]["ВерхнийКолонтитул"]);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
		
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета]["НижнийКолонтитул"]);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].Заголовок);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.КонтактноеЛицо) Тогда
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].КонтактноеЛицо);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			КонецЕсли;
			
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].Предложение);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			Если ДанныеОбъекта.ЕстьСкидки Тогда
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].ШапкаТаблицаТовары);
			Иначе
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].ШапкаТаблицаТоварыБезСкидки);
			КонецЕсли;
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			Если ДанныеОбъекта.ЕстьСкидки Тогда
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].СтрокаТаблицаТовары);
			Иначе
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].СтрокаТаблицаТоварыБезСкидки);
			КонецЕсли;
			УправлениеПечатьюКлиент.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма,Область, ДанныеОбъекта.Товары, Ложь);
			
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].ИтоговаяСтрока);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.СрокДействия) Тогда
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].СрокДействия);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ГрафикОплаты)
				Или ЗначениеЗаполнено(ДанныеОбъекта.ФормаОплаты)
				Или ЗначениеЗаполнено(ДанныеОбъекта.Склад)
				Или ЗначениеЗаполнено(ДанныеОбъекта.ДополнительнаяИнформация) Тогда
			
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет,Секции[ИмяМакета].ЗаголовокДопИнформации);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ГрафикОплаты) Тогда
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ГрафикОплаты);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ФормаОплаты) Тогда
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ФормаОплаты);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.Склад) Тогда
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].Склад);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ДополнительнаяИнформация) Тогда
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ДополнительнаяИнформация);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
			КонецЕсли;
			
			Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].Подвал);
			УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.ТелефонОрганизации) Тогда
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].ТелефонОрганизации);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеОбъекта.АдресЭлектроннойПочтыМенеджера) Тогда
				
				Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Секции[ИмяМакета].АдресЭлектроннойПочтыМенеджера);
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
				
			КонецЕсли;
			
			УправлениеПечатьюКлиент.ПоказатьДокумент(ПечатнаяФорма);
			
		Исключение
		
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма);
			УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
				
			Возврат Ложь;
			
		КонецПопытки;

		УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма, Ложь);
		УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
		
	КонецЦикла;
	
	Состояние(НСтр("ru='Формирование печатных форм завершено';uk='Формування друкованих форм завершено'"));
	
КонецФункции

#КонецОбласти

#Область ДокументУстановкаценноменклатуры

Функция ПечатьПереоценкаВРознице(ОписаниеКоманды, АдресДанныеДляПечатиВоВременномХранилище = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура("АдресДанныеДляПечатиВоВременномХранилище, ОписаниеКоманды", 
		АдресДанныеДляПечатиВоВременномХранилище, ОписаниеКоманды);
	
	ОткрытьФорму(
		"Документ.УстановкаЦенНоменклатуры.Форма.ФормаВыбораРозничногоМагазинаИОрганизацииДляПереоценки",
		,
		,
		,
		,
		,
		Новый ОписаниеОповещения(
			"ПечатьПереоценкаВРозницеЗавершение", 
			ЭтотОбъект, 
			ДополнительныеПараметры), 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецФункции

Процедура ПечатьПереоценкаВРозницеЗавершение(ПараметрыПечати, ДополнительныеПараметры) Экспорт
	
	АдресДанныеДляПечатиВоВременномХранилище = ДополнительныеПараметры.АдресДанныеДляПечатиВоВременномХранилище;
	ОписаниеКоманды = ДополнительныеПараметры.ОписаниеКоманды;
	
	Если ПараметрыПечати <> Неопределено Тогда
		
		ПараметрыПечати.Вставить("АдресДанныеДляПечатиВоВременномХранилище", АдресДанныеДляПечатиВоВременномХранилище);
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Документ.УстановкаЦенНоменклатуры",
		"ПереоценкаТоваровВРознице",
		ОписаниеКоманды.ОбъектыПечати,
		ОписаниеКоманды.Форма,
		ПараметрыПечати);
		
	КонецЕсли;

КонецПроцедуры

Функция ПечатьУстановкаЦенНоменклатуры(ОписаниеКоманды, АдресДанныеДляПечатиВоВременномХранилище = Неопределено) Экспорт
	
	ПараметрыФормыНастройки = Новый Структура(
		"МассивДокументов, УникальныйИдентификатор, АдресДанныеДляПечатиВоВременномХранилище",
		ОписаниеКоманды.ОбъектыПечати,
		ОписаниеКоманды.Форма.УникальныйИдентификатор,
		АдресДанныеДляПечатиВоВременномХранилище);
	
	ПараметрыПечати = Неопределено;

	
	ОткрытьФорму(
		"Документ.УстановкаЦенНоменклатуры.Форма.ФормаНастройкиПечатнойФормыУстановкиЦенНоменклатуры",
		ПараметрыФормыНастройки,
		,
		,
		,
		,
		Новый ОписаниеОповещения("ПечатьУстановкаЦенНоменклатурыЗавершение", ЭтотОбъект, Новый Структура("ОписаниеКоманды", ОписаниеКоманды)), 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецФункции

Процедура ПечатьУстановкаЦенНоменклатурыЗавершение(ПараметрыПечати, ДополнительныеПараметры) Экспорт
	
	ОписаниеКоманды = ДополнительныеПараметры.ОписаниеКоманды;
	
	Если ПараметрыПечати <> Неопределено Тогда
		
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Документ.УстановкаЦенНоменклатуры",
		"УстановкаЦенНоменклатуры",
		ОписаниеКоманды.ОбъектыПечати,
		Неопределено,
		ПараметрыПечати);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СправочникВариантыГрафиковКредитовИДепозитов

Функция ПечатьВариантаГрафикаОплатыДоговоровКредитовИДепозитов(ОписаниеКоманды) Экспорт
	
	ПараметрыФормы = Новый Структура("КлючВарианта, Отбор, СформироватьПриОткрытии, ВидимостьКомандВариантовОтчетов",
		"ГрафикОплатПоМесяцамКонтекст",
		Новый Структура("ВариантГрафика", ОписаниеКоманды.ОбъектыПечати[ОписаниеКоманды.ОбъектыПечати.Количество()-1]),
		Истина,
		Ложь);
	
	ОткрытьФорму("Отчет.ГрафикОплатНачисленийКредитовИДепозитов.Форма", 
		ПараметрыФормы, 
		ОписаниеКоманды.Форма);
	
КонецФункции

#КонецОбласти

#Область ТТН

Функция ПечатьТТН(ОписаниеКоманды) Экспорт
	
	МассивОбъектов = ОписаниеКоманды.ОбъектыПечати;
	ОбъектыПечати = ОписаниеКоманды.ОбъектыПечати;
	ТипДокументов = ТипЗнч(МассивОбъектов[0]);
	
	КоличествоРаспоряженийБезТранспортныхНакладных = 0;
	Если ТипДокументов = Тип("ДокументСсылка.ЗаданиеНаПеревозку") Тогда
		КоличествоРаспоряженийБезТранспортныхНакладных 
			= УправлениеПечатьюУТВызовСервера.ПроверитьНаличиеТранспортныхНакладныхДляРаспоряженийИзЗаданийНаПеревозку(МассивОбъектов);		
	ИначеЕсли ТипДокументов <> Тип("ДокументСсылка.ТранспортнаяНакладная") Тогда
		СтруктураВозврата = УправлениеПечатьюУТВызовСервера.ПроверитьНаличиеТранспортныхНакладныхДляРаспоряжений(МассивОбъектов);
		ОбъектыПечати = СтруктураВозврата.МассивОбъектов;
		КоличествоРаспоряженийБезТранспортныхНакладных = СтруктураВозврата.КоличествоРаспоряженийБезТранспортныхНакладных; 				
	КонецЕсли;
	
	Если КоличествоРаспоряженийБезТранспортныхНакладных <> 0 Тогда
		СписокЗначенийКнопок = Новый СписокЗначений;
		СписокЗначенийКнопок.Добавить(КодВозвратаДиалога.ОК, НСтр("ru='Создать и вывести на печать';uk='Створити і вивести на друк'"));
		СписокЗначенийКнопок.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru='Отмена';uk='Відмінити'"));
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ТипДокументов", ТипДокументов);
		ДополнительныеПараметры.Вставить("МассивОбъектов", МассивОбъектов);
		ДополнительныеПараметры.Вставить("ОписаниеКоманды", ОписаниеКоманды);
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("НеСозданыТранспортныеНакладныеЗавершение", УправлениеПечатьюУТКлиент, ДополнительныеПараметры);
		
		ПоказатьВопрос(ОповещениеОЗавершении,
						НСтр("ru='Для некоторых распоряжений не созданы транспортные накладные.';uk='Для деяких розпоряджень не створені транспортні накладні.'"), 
						СписокЗначенийКнопок);
		
	Иначе
				
		Если ОписаниеКоманды.Идентификатор = "ТТН" Тогда
			МенеджерПечати = "Обработка.ПечатьТТН";
			ИменаМакетов = "ТТН";	
		КонецЕсли;
		
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
					МенеджерПечати,
					ИменаМакетов,
					ОбъектыПечати,
					ОписаниеКоманды.Форма);
					
	КонецЕсли;	
	
КонецФункции

Процедура НеСозданыТранспортныеНакладныеЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	
	ТипДокументов	= ДополнительныеПараметры.ТипДокументов;
	МассивОбъектов	= ДополнительныеПараметры.МассивОбъектов;
	ОписаниеКоманды = ДополнительныеПараметры.ОписаниеКоманды;
		
	Если КодВозврата = КодВозвратаДиалога.ОК Тогда 
		
		Если ТипДокументов = Тип("ДокументСсылка.ЗаданиеНаПеревозку") Тогда
			СозданныеТраспортныеНакладные = УправлениеПечатьюУТВызовСервера.СоздатьТранспортныеНакладныеДляЗаданийНаПеревозку(МассивОбъектов);
		Иначе
			СозданныеТраспортныеНакладные = УправлениеПечатьюУТВызовСервера.СоздатьТранспортныеНакладные(МассивОбъектов);
		КонецЕсли;
		
		Оповестить("Запись_ТранспортнаяНакладная");
		
		Если ЗначениеЗаполнено(СозданныеТраспортныеНакладные) Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ТранспортныеНакладные", СозданныеТраспортныеНакладные);
			ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьКомандуПечатиПечатьТТН", УправлениеПечатьюУТКлиент, ОписаниеКоманды);
			
			ОткрытьФорму(
				"Документ.ТранспортнаяНакладная.Форма.СозданныеТранспортныеНакладные", 
				ПараметрыФормы,,,,,
				ОписаниеОповещения);
		Иначе
			
			ТекстСообщения = НСтр("ru='Транспортные накладные не созданы.';uk='Транспортні накладні не створені.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ВыполнитьКомандуПечатиПечатьТТН(КодВозврата, ОписаниеКоманды) Экспорт 
			
	Если ОписаниеКоманды.Идентификатор = "ТТН" Тогда
		МенеджерПечати = "Обработка.ПечатьТТН";
		ИменаМакетов = "ТТН";	
	КонецЕсли;
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			МенеджерПечати,
			ИменаМакетов,
			ОписаниеКоманды.ОбъектыПечати,
			ОписаниеКоманды.Форма);
							
КонецПроцедуры

#КонецОбласти


#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Функция ВыполнитьКомандуПечати(ОписаниеКоманды) Экспорт
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ОписаниеКоманды.МенеджерПечати, ОписаниеКоманды.Идентификатор, ОписаниеКоманды.ОбъектыПечати,
		ОписаниеКоманды.Форма, ПолучитьЗаголовокПечатнойФормы(ОписаниеКоманды.ОбъектыПечати));
	
КонецФункции
	
Функция ПолучитьЗаголовокПечатнойФормы(ПараметрКоманды) Экспорт 
	
	Если Тип(ПараметрКоманды) = Тип("Массив") И ПараметрКоманды.Количество() = 1 Тогда 
		Возврат Новый Структура("ЗаголовокФормы", ПараметрКоманды[0]);
	Иначе
		Возврат Новый Структура;
	КонецЕсли;
	
КонецФункции
#КонецОбласти

#Область ЭДО

Функция ОткрытьПечатнуюФормуПредварительногоПросмотраЭДО(ОписаниеКоманды) Экспорт
	
	Результат = Истина;
	
	МассивОбъектов = ОписаниеКоманды.ОбъектыПечати;
	
	Если МассивОбъектов.Количество() = 0  Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Не РегламентированнаяОтчетностьКлиент.ПодключитьМенеджерЗвит1С() Тогда
		Возврат Результат;
	КонецЕсли;
	
	ИмяОтчета 		= "РегламентированныйОтчетПервичныеДокументыЗвит1С";
	ЭтоВнешнийОтчет = РегламентированнаяОтчетность.ЭтоВнешнийОтчет(ИмяОтчета);
	
	Для каждого Ссылка Из МассивОбъектов Цикл	
		
		ИмяФормыПросмотра = глМенеджерЗвит1С.ОпределитьИмяФормыВыгрузки(Ссылка);
		
		// откроем отчет и загрузим в него данные XML
		Попытка
			ФормаПросмотра = ПолучитьФорму(?(ЭтоВнешнийОтчет, "Внешний", "") + "Отчет." + ИмяОтчета + ".Форма." + ИмяФормыПросмотра);
		Исключение
			ТекстОшибки = НСтр("ru='Не удалось открыть для просмотра требуемую форму отчета ';uk='Не вдалося відкрити для перегляду необхідну форму звіту '")+ИмяОтчета;
			ПоказатьПредупреждение(,ТекстОшибки);
			Возврат Результат;	
		КонецПопытки;
		
		ТабДокумент = ФормаПросмотра.ЗаполнитьИРаспечатать(Ссылка);
		ТабДокумент.Защита 			= Истина;
		ТабДокумент.ТолькоПросмотр 	= Ложь;
		ТабДокумент.ОтображатьСетку 	 = Ложь;
		ТабДокумент.ОтображатьЗаголовки  = Ложь;
		ТабДокумент.Показать("" + Ссылка + НСтр("ru=' (для выгрузки в FREDO ДокМен)';uk= ' (для вивантаження до FREDO ДокМен)'"));
		
	КонецЦикла;
	
КонецФункции

#КонецОбласти

