&НаКлиенте
Перем ВыполняетсяЗакрытие;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ПрочитатьДиапазонДопустимыхЦен();

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Не ВыполняетсяЗакрытие и Модифицированность Тогда
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект), НСтр("ru='Данные изменены. Сохранить?';uk='Дані змінені. Зберегти?'"),РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ОтветНаВопрос = РезультатВопроса;
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		ВыполняетсяЗакрытие = Истина;
		ЗаписатьДиапазонДопустимыхЦен();
		Закрыть();
	ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
		ВыполняетсяЗакрытие = Истина;
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрытьВыполнить()
	
	ЗаписатьДиапазонДопустимыхЦен();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьВыполнить()
	
	ЗаписатьДиапазонДопустимыхЦен();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура ЗаписатьДиапазонДопустимыхЦен()
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Константы.ВидМаксимальноДопустимыхЦенЗакупки КАК ВидМаксимальноДопустимыхЦенЗакупки,
		|	Константы.ВидМаксимальноДопустимыхЦенПродажи КАК ВидМаксимальноДопустимыхЦенПродажи,
		|	Константы.ВидМинимальноДопустимыхЦенПродажи  КАК ВидМинимальноДопустимыхЦенПродажи
		|ИЗ
		|	Константы КАК Константы
		|");
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если ВидМинимальноДопустимыхЦенПродажи <> Выборка.ВидМинимальноДопустимыхЦенПродажи Тогда
			Константы.ВидМинимальноДопустимыхЦенПродажи.Установить(ВидМинимальноДопустимыхЦенПродажи);
		КонецЕсли;
			
		Если ВидМаксимальноДопустимыхЦенПродажи <> Выборка.ВидМаксимальноДопустимыхЦенПродажи Тогда
			Константы.ВидМаксимальноДопустимыхЦенПродажи.Установить(ВидМаксимальноДопустимыхЦенПродажи);
		КонецЕсли;
		
		Если ВидМаксимальноДопустимыхЦенЗакупки <> Выборка.ВидМаксимальноДопустимыхЦенЗакупки Тогда
			Константы.ВидМаксимальноДопустимыхЦенЗакупки.Установить(ВидМаксимальноДопустимыхЦенЗакупки);
		КонецЕсли;
			
	КонецЕсли;
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДиапазонДопустимыхЦен()
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Константы.ВидМаксимальноДопустимыхЦенЗакупки КАК ВидМаксимальноДопустимыхЦенЗакупки,
		|	Константы.ВидМаксимальноДопустимыхЦенПродажи КАК ВидМаксимальноДопустимыхЦенПродажи,
		|	Константы.ВидМинимальноДопустимыхЦенПродажи  КАК ВидМинимальноДопустимыхЦенПродажи
		|ИЗ
		|	Константы КАК Константы
		|");
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ВидМинимальноДопустимыхЦенПродажи  = Выборка.ВидМинимальноДопустимыхЦенПродажи;
		ВидМаксимальноДопустимыхЦенПродажи = Выборка.ВидМаксимальноДопустимыхЦенПродажи;
		ВидМаксимальноДопустимыхЦенЗакупки = Выборка.ВидМаксимальноДопустимыхЦенЗакупки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

ВыполняетсяЗакрытие = Ложь;