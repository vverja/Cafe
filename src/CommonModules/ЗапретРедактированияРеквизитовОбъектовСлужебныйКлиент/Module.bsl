////////////////////////////////////////////////////////////////////////////////
// Подсистема "Запрет редактирования реквизитов объектов".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура РазрешитьРедактированиеРеквизитовОбъектаПослеПредупреждения(ОбработкаПродолжения) Экспорт
	
	Если ОбработкаПродолжения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОбработкаПродолжения, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура РазрешитьРедактированиеРеквизитовОбъектаПослеПроверкиСсылок(Результат, Параметры) Экспорт
	
	Если Результат Тогда
		ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьРазрешенностьРедактированияРеквизитов(
			Параметры.Форма, Параметры.ЗаблокированныеРеквизиты);
		
		ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(Параметры.Форма);
	КонецЕсли;
	
	Если Параметры.ОбработкаПродолжения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьСсылкиНаОбъектПослеПодтвержденияПроверки(Ответ, Параметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения, Ложь);
		Возврат;
	КонецЕсли;
		
	Если Параметры.МассивСсылок.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения, Истина);
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияВызовСервера.ЕстьСсылкиНаОбъект(Параметры.МассивСсылок) Тогда
		
		Если Параметры.МассивСсылок.Количество() = 1 Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Элемент ""%1"" уже используется в других местах в программе.
                           |Не рекомендуется разрешать редактирование из-за риска рассогласования данных.'
                           |;uk='Елемент ""%1"" уже використовується в інших місцях у програмі.
                           |Не рекомендується дозволяти редагування через ризик неузгодженості даних.'"),
				Параметры.МассивСсылок[0]);
		Иначе
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Выбранные элементы (%1) уже используются в других местах в программе.
                           |Не рекомендуется разрешать редактирование из-за риска рассогласования данных.'
                           |;uk='Обрані елементи (%1) уже використовуються в інших місцях програми.
                           |Не рекомендується дозволяти редагування через ризик неузгодженості даних.'"),
				Параметры.МассивСсылок.Количество());
		КонецЕсли;
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Разрешить редактирование';uk='Дозволити редагування'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Отмена';uk='Відмінити'"));
		ПоказатьВопрос(
			Новый ОписаниеОповещения(
				"ПроверитьСсылкиНаОбъектПослеПодтвержденияРедактирования", ЭтотОбъект, Параметры),
			ТекстСообщения, Кнопки, , КодВозвратаДиалога.Нет, Параметры.ЗаголовокДиалога);
	Иначе
		Если Параметры.МассивСсылок.Количество() = 1 Тогда
			ПоказатьОповещениеПользователя(НСтр("ru='Редактирование реквизитов разрешено';uk='Редагування реквізитів дозволене'"),
				ПолучитьНавигационнуюСсылку(Параметры.МассивСсылок[0]), Параметры.МассивСсылок[0]);
		Иначе
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Разрешено редактирование реквизитов объектов (%1)';uk='Дозволено редагування реквізитів об''єктів (%1)'"),
				Параметры.МассивСсылок.Количество());
			
			ПоказатьОповещениеПользователя(НСтр("ru='Редактирование реквизитов разрешено';uk='Редагування реквізитів дозволене'"),,
				ТекстСообщения);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьСсылкиНаОбъектПослеПодтвержденияРедактирования(Ответ, Параметры) Экспорт
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения, Ответ = КодВозвратаДиалога.Да);
	
КонецПроцедуры

#КонецОбласти
