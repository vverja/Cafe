#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда



    
    
#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиОбновления


Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаВерсию2_1_12(Параметры) Экспорт
	
	ДопПараметрыОтметки = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДопПараметрыОтметки.ЭтоДвижения = Истина;
	ДопПараметрыОтметки.ПолноеИмяРегистра = "РегистрНакопления.ПрочиеРасходы";
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК ДанныеРегистра
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) = ТИП(Документ.ПоступлениеБезналичныхДенежныхСредств)
	|	И ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаДенежныхСредств)
	|";
	
	ДанныеКОбработке = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДанныеКОбработке, ДопПараметрыОтметки);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаВерсию2_1_12(Параметры) Экспорт
	
	Регистраторы = Новый Массив;
	Регистраторы.Добавить("Документ.ПоступлениеБезналичныхДенежныхСредств");
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
		Регистраторы,
		"РегистрНакопления.ПрочиеРасходы",
		Параметры.Очередь
    );
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаВерсию2_1_15(Параметры) Экспорт
	
	ДопПараметрыОтметки = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДопПараметрыОтметки.ЭтоДвижения = Истина;
	ДопПараметрыОтметки.ПолноеИмяРегистра = "РегистрНакопления.ПрочиеРасходы";
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК ДанныеРегистра
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) = ТИП(Документ.ПокупкаПродажаВалюты)
    |   И (ДанныеРегистра.СуммаРегл <> 0 ИЛИ ДанныеРегистра.СуммаРеглБезНДС <> 0 ИЛИ ДанныеРегистра.СуммаБезНДС <> 0 ИЛИ ДанныеРегистра.НалоговоеНазначение <> ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка))
    |	И &УправлениеТорговлей
	|";
    
    УправлениеТорговлей = ПолучитьФункциональнуюОпцию("УправлениеТорговлей");
    Запрос.УстановитьПараметр("УправлениеТорговлей", УправлениеТорговлей);
    
	ДанныеКОбработке = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДанныеКОбработке, ДопПараметрыОтметки);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаВерсию2_1_15(Параметры) Экспорт
	
	Регистраторы = Новый Массив;
	Регистраторы.Добавить("Документ.ПокупкаПродажаВалюты");
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
		Регистраторы,
		"РегистрНакопления.ПрочиеРасходы",
		Параметры.Очередь
    );
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#КонецЕсли