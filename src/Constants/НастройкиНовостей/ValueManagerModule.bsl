////////////////////////////////////////////////////////////////////////////////
// Константа.НастройкиНовостей: Модуль менеджера.
//
////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПередЗаписью(Отказ)

	Если ОбработкаНовостейВызовСервера.ВестиПодробныйЖурналРегистрации() = Истина Тогда
		ЭтотОбъект.ДополнительныеСвойства.Вставить("ЗначениеПередЗаписью", Константы.НастройкиНовостей.Получить());
	КонецЕсли;

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	ТипСтруктура         = Тип("Структура");
	ТипХранилищеЗначения = Тип("ХранилищеЗначения");

	// Получение функциональных опций "Новости_РазрешенаРаботаСНовостями"
	//  и "Новости_РазрешенаРаботаСНовостямиЧерезИнтернет" осуществляется через
	//  общий модуль "ОбработкаНовостейПовтИсп", поэтому после установки
	//  константы необходимо сбросить кэш.
	ОбновитьПовторноИспользуемыеЗначения();

	Если ОбработкаНовостейВызовСервера.ВестиПодробныйЖурналРегистрации() = Истина Тогда
		// Запись в журнал регистрации
		ТекстСообщения = НСтр("ru='Записана константа НастройкиНовостей
            |Предыдущее значение:
            |%ПредыдущееЗначение%
            |
            |Новое значение:
            |%НовоеЗначение%'
            |;uk='Записана константа НастройкиНовостей
            |Попереднє значення:
            |%ПредыдущееЗначение%
            |
            |Нове значення:
            |%НовоеЗначение%'");

		лкПредыдущееЗначение_Строка = "Неопределено";
		Если ЭтотОбъект.ДополнительныеСвойства.Свойство("ЗначениеПередЗаписью") Тогда
			лкСтароеЗначение = ЭтотОбъект.ДополнительныеСвойства.ЗначениеПередЗаписью;
			Если ТипЗнч(лкСтароеЗначение) = ТипХранилищеЗначения Тогда
				Если ТипЗнч(лкСтароеЗначение.Получить()) = ТипСтруктура Тогда
					лкПредыдущееЗначение_Строка = ОбработкаНовостейКлиентСервер.ПолучитьСтроковоеПредставление(
						лкСтароеЗначение.Получить(),
						": ",
						Символы.ПС);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		лкНовоеЗначение_Строка = "Неопределено";
		Если ТипЗнч(ЭтотОбъект.Значение) = ТипХранилищеЗначения Тогда
			Если ТипЗнч(ЭтотОбъект.Значение.Получить()) = ТипСтруктура Тогда
				лкНовоеЗначение_Строка  = ОбработкаНовостейКлиентСервер.ПолучитьСтроковоеПредставление(
					ЭтотОбъект.Значение.Получить(),
					": ",
					Символы.ПС);
			КонецЕсли;
		КонецЕсли;

		#Если ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда
			ОбъектМетаданных = Неопределено;
		#Иначе
			ОбъектМетаданных = Метаданные.Константы.НастройкиНовостей;
		#КонецЕсли

		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПредыдущееЗначение%", лкПредыдущееЗначение_Строка);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НовоеЗначение%", лкНовоеЗначение_Строка);
		// Запись в журнал регистрации
		ОбработкаНовостейВызовСервера.ЗаписатьСообщениеВЖурналРегистрации(
			НСтр("ru='БИП:Новости.Изменение данных';uk='БІП:Новини.Зміна даних'"), // ИмяСобытия
			НСтр("ru='Новости. Изменение данных. Константы. НастройкиНовостей';uk='Новини. Зміна даних. Константи. НастройкиНовостей'"), // ИдентификаторШага
			"Информация", // УровеньЖурналаРегистрации.*
			ОбъектМетаданных, // ОбъектМетаданных
			, // Данные
			ТекстСообщения); // Комментарий

	КонецЕсли;

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
