
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ИсправитьОбеспечение = Параметры.Исправить;

	Элементы.СнятьПреждевременноеОбеспечение.Доступность = Параметры.СнятьРезервы;
	СнятьПреждевременноеОбеспечение = Параметры.СнятьРезервы;
	СнятьОбеспечениеСоСклада = СнятьПреждевременноеОбеспечение И Параметры.СнятьРезервыСоСклада;
	СнятьПреждевременноеОбеспечениеИзменение();
	Элементы.УскоритьОтгрузкуЗаказа.Доступность = Параметры.Ускорить;
	УскоритьОтгрузкуЗаказа = Параметры.Ускорить;
	ОтгружатьЧастями = Параметры.ОтгружатьЧастями;
	Элементы.НадписьОтгружатьЧастями.Видимость = Параметры.ОтгружатьЧастями;
	УскоритьОтгрузкуЗаказаИзменение();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СнятьПреждевременноеОбеспечениеПриИзменении(Элемент)
	СнятьПреждевременноеОбеспечениеИзменение();
КонецПроцедуры

&НаКлиенте
Процедура УскоритьОтгрузкуЗаказаПриИзменении(Элемент)
	УскоритьОтгрузкуЗаказаИзменение();
КонецПроцедуры

&НаСервере
Процедура СнятьПреждевременноеОбеспечениеИзменение()

	Элементы.ГруппаСнятьОбеспечениеСоСклада.Доступность = СнятьПреждевременноеОбеспечение И Параметры.СнятьРезервыСоСклада;
	Если Не СнятьПреждевременноеОбеспечение Тогда
		СнятьОбеспечениеСоСклада = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УскоритьОтгрузкуЗаказаИзменение()

	Элементы.ГруппаОтгружатьЧастями.Доступность = УскоритьОтгрузкуЗаказа И Параметры.ЕстьОтгружатьЧастями И Не Параметры.ОтгружатьЧастями;
	Если Не УскоритьОтгрузкуЗаказа И Элементы.ГруппаОтгружатьЧастями.Доступность Тогда
		ОтгружатьЧастями = Ложь;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьДействия(Команда)

	ЗначениеВыбора = ОбеспечениеКлиентСервер.ДействияСЗаказом();
	
	ЗначениеВыбора.Исправить            = ИсправитьОбеспечение;
	ЗначениеВыбора.СнятьРезервы         = СнятьПреждевременноеОбеспечение;
	ЗначениеВыбора.СнятьРезервыСоСклада = СнятьОбеспечениеСоСклада;
	ЗначениеВыбора.Ускорить             = УскоритьОтгрузкуЗаказа;
	ЗначениеВыбора.ОтгружатьЧастями     = ?(Параметры.ЕстьОтгружатьЧастями И ОтгружатьЧастями, Истина, Неопределено);
	ОповеститьОВыборе(ЗначениеВыбора);

КонецПроцедуры

#КонецОбласти

