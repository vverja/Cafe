#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы


Функция ТекстОбработчиковОбновления() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расходы.Регистратор       КАК Ссылка,
	|	Расходы.МоментВремени     КАК МоментВремени,
	|	""РегистрНакопления""     КАК ТипМетаданных,
	|	""ПартииПрочихРасходов""  КАК ИмяМетаданных
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК Расходы
	|ГДЕ
	|	Расходы.МоментВремени < &МоментВремени
	|	И Расходы.СтатьяРасходов.ВариантРаспределенияРасходов
	|		= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И Расходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расходы.Регистратор       КАК Ссылка,
	|	Расходы.МоментВремени     КАК МоментВремени,
	|	""РегистрНакопления""     КАК ТипМетаданных,
	|	""ПрочиеРасходы""         КАК ИмяМетаданных
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК Расходы
	|ГДЕ
	|	Расходы.МоментВремени < &МоментВремени
	|	И Расходы.СтатьяРасходов.ВариантРаспределенияРасходов
	|		= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И Расходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции


Процедура ИсправитьВводОстатковТЗР_ДанныеДляОбновления(Параметры) Экспорт
    
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ВводОстатков КАК Документ
	|	ПО
	|		ДанныеРегистра.Регистратор = Документ.Ссылка
    |
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ВводОстатков.ПрочиеРасходы КАК ДанныеДокументаРасходы
	|	ПО
	|		ДанныеДокументаРасходы.Ссылка = Документ.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		ДанныеДокументаРасходы.СтатьяРасходов = СтатьиРасходов.Ссылка
	|
	|ГДЕ
	|	ДанныеРегистра.Регистратор ССЫЛКА Документ.ВводОстатков
    |   И СтатьиРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|");
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
        Параметры, 
        Регистраторы, 
        "РегистрНакопления.ПартииПрочихРасходов"
    );
    
КонецПроцедуры

Процедура ИсправитьВводОстатковТЗР(Параметры) Экспорт
	
	Регистраторы = Новый Массив;
	Регистраторы.Добавить("Документ.ВводОстатков");
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
		Регистраторы,
		"РегистрНакопления.ПартииПрочихРасходов",
		Параметры.Очередь);
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#КонецЕсли
