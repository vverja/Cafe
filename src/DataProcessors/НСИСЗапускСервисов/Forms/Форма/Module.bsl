
&НаСервере
Функция ВыполнитьЗагрузку()
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаименованиеФоновогоЗадания = НСтр("ru='Обновление данных';uk='Оновлення даних'");
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("ВыводитьПротокол",  ВыводитьПротокол);
	ПараметрыЗагрузки.Вставить("ВФоне",  Истина);
	ПараметрыЗагрузки.Вставить("ЗагружатьНормативныеВеличины",  ЗагружатьНормативныеВеличины);
	ПараметрыЗагрузки.Вставить("ЗагружатьКлассификаторы",  ЗагружатьКлассификаторы);
	ПараметрыЗагрузки.Вставить("ЗагружатьКурсыВалют",  ЗагружатьКурсыВалют);
	ПараметрыЗагрузки.Вставить("ЗагружатьНовости",  ЗагружатьНовости);
	ПараметрыЗагрузки.Вставить("ЗагружатьКалендарь",  ЗагружатьКалендарь);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("Обработки.НСИСЗапускСервисов.ЗапуститьСервисы", ПараметрыЗагрузки, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ПриЗавершенииЗагрузки(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.ГруппаВыполнение.Видимость = Ложь;
	Элементы.ФормаКоманднаяПанель.Доступность = Истина;
	
	ОбщегоназначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Загрузка завершена, протокол сохранен в журнале регистрации';uk='Завантаження завершено, протокол збережено в журналі реєстрації'"));
	
КонецПроцедуры

&НаКлиенте
Процедура Запустить(Команда)
	
	ОчиститьСообщения();
	
	Элементы.ГруппаВыполнение.Видимость = Истина;
	Элементы.ФормаКоманднаяПанель.Доступность = Ложь;

	ОперацияЗагрузки = ВыполнитьЗагрузку();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииЗагрузки", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ОперацияЗагрузки, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Процедура ЗапуститьИнтерактивноНаСервере()
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("ВыводитьПротокол",  ВыводитьПротокол);
	ПараметрыЗагрузки.Вставить("ВФоне",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьНормативныеВеличины",  ЗагружатьНормативныеВеличины);
	ПараметрыЗагрузки.Вставить("ЗагружатьКлассификаторы",  ЗагружатьКлассификаторы);
	ПараметрыЗагрузки.Вставить("ЗагружатьКурсыВалют",  ЗагружатьКурсыВалют);
	ПараметрыЗагрузки.Вставить("ЗагружатьНовости",  ЗагружатьНовости);
	ПараметрыЗагрузки.Вставить("ЗагружатьКалендарь",  ЗагружатьКалендарь);
	
	Обработки.НСИСЗапускСервисов.ЗапуститьСервисы(ПараметрыЗагрузки, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьИнтерактивно(Команда)
	
	ОчиститьСообщения();
	ЗапуститьИнтерактивноНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьДоступНаСервере()
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("ВыводитьПротокол",  Истина);
	ПараметрыЗагрузки.Вставить("ВФоне",  Ложь);
	
	Обработки.НСИСЗапускСервисов.ПроверитьДоступ(ПараметрыЗагрузки);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьДоступ(Команда)
	ПроверитьДоступНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройки()
	
	Настройки = НСИССервер.ПолучитьНастройкиЗагрузок();
	
	ЗагружатьНормативныеВеличины = Настройки.ЗагружатьНормативныеВеличины;
	ЗагружатьКлассификаторы = Настройки.ЗагружатьКлассификаторы;
	ЗагружатьКурсыВалют = Настройки.ЗагружатьКурсыВалют;
	ЗагружатьНовости = Настройки.ЗагружатьНовости;
	ЗагружатьКалендарь = Настройки.ЗагружатьКалендарь;
	ВыводитьПротокол = Истина;
	
	
КонецПроцедуры	

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗагрузитьНастройки();
	Элементы.ГруппаВыполнение.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьНастройки(Команда)
	
	ФормаНастройки = ОткрытьФорму("Обработка.НСИСНастройка.Форма.Форма");	

	
КонецПроцедуры



