
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НачальныйПризнакВыполнения = Объект.Выполнена;
	ЗадачаОбъект = РеквизитФормыВЗначение("Объект");
	ЗаданиеОбъект = ЗадачаОбъект.БизнесПроцесс.ПолучитьОбъект();
	ЗначениеВРеквизитФормы(ЗаданиеОбъект, "Задание");

	ИспользоватьДатуИВремяВСрокахЗадач = ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач");
	Элементы.СрокИсполненияВремя.Видимость = ИспользоватьДатуИВремяВСрокахЗадач;
	Элементы.СрокНачалаИсполненияВремя.Видимость = ИспользоватьДатуИВремяВСрокахЗадач;
	Элементы.ДатаИсполнения.Формат = ?(ИспользоватьДатуИВремяВСрокахЗадач, "ДЛФ=DT", "ДЛФ=D");

	БизнесПроцессыИЗадачиСервер.ФормаЗадачиПриСозданииНаСервере(
		ЭтаФорма, Объект, Элементы.ГруппаСостояние, Элементы.ДатаИсполнения);

	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаказКлиента.Ссылка
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|ГДЕ
		|	(НЕ ЗаказКлиента.ПометкаУдаления)
		|	И ЗаказКлиента.Сделка = &Сделка");
	Запрос.УстановитьПараметр("Сделка", Задание.Предмет);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Заказ = Выборка.Ссылка;
		Элементы.ЗаказКлиента.Заголовок = Заказ;
	Иначе
		Элементы.ЗаказКлиента.Заголовок = НСтр("ru='Создать заказ';uk='Створити замовлення'");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Перем ПараметрСделка;
	
	Если ИмяСобытия = "Запись_ЗаказКлиента"
		И Параметр.Свойство("Сделка", ПараметрСделка) Тогда
		
		Если ПараметрСделка = Задание.Предмет Тогда
			Заказ = Источник;
			Элементы.ЗаказКлиента.Заголовок = Источник;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполненоВыполнить()

	БизнесПроцессыИЗадачиКлиент.ЗаписатьИЗакрытьВыполнить(ЭтаФорма,Истина,Новый Структура("Сделка",Объект.Предмет));

КонецПроцедуры

&НаКлиенте
Процедура Заказ(Команда)

	Если ЗначениеЗаполнено(Заказ) Тогда
		ПоказатьЗначение(Неопределено, Заказ);
	Иначе
		ОткрытьФорму(
			"Документ.ЗаказКлиента.ФормаОбъекта",
			Новый Структура("Основание", КоммерческоеПредложениеПоСделке(Задание.Предмет)),,,,, Неопределено, РежимОткрытияОкнаФормы.Независимый);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрытьВыполнить()
	
	БизнесПроцессыИЗадачиКлиент.ЗаписатьИЗакрытьВыполнить(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция КоммерческоеПредложениеПоСделке(Сделка)

	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КоммерческоеПредложениеКлиенту.Ссылка
		|ИЗ
		|	Документ.КоммерческоеПредложениеКлиенту КАК КоммерческоеПредложениеКлиенту
		|ГДЕ
		|	(НЕ КоммерческоеПредложениеКлиенту.ПометкаУдаления)
		|	И КоммерческоеПредложениеКлиенту.Сделка = &Сделка
		|
		|УПОРЯДОЧИТЬ ПО
		|	КоммерческоеПредложениеКлиенту.Дата УБЫВ");
	Запрос.УстановитьПараметр("Сделка", Сделка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;

КонецФункции

#КонецОбласти
