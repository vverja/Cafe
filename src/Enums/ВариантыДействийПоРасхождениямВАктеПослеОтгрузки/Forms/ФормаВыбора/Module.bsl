
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РасхожденияСервер.УправлениеСтраницамиВыборДействия(ЭтотОбъект, Параметры);
	
	ИспользоватьЗаявкиНаВозврат = ПолучитьФункциональнуюОпцию("ИспользоватьЗаявкиНаВозвратТоваровОтКлиентов");
	ИспользоватьЗаказыКлиентов = ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыКлиентов");
	ИспользоватьЗаказыНаПеремещение = ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыНаПеремещение");
	КоличествоУпаковокРасхождения = Параметры.КоличествоУпаковокРасхождения;
	ГрупповоеИзменение = Параметры.ГрупповоеИзменение;
	
	Если Не ГрупповоеИзменение Тогда
		Если КоличествоУпаковокРасхождения >= 0 Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы.Излишки;
			ДействиеИзлишки = Параметры.ВыбранноеДействие;
		Иначе
			Элементы.Страницы.ТекущаяСтраница = Элементы.Недостачи;
			ДействиеНедостачи = Параметры.ВыбранноеДействие;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ГрупповоеИзменение Тогда
		Если КоличествоУпаковокРасхождения >= 0 Тогда
			Элементы.Недостачи.Видимость = Ложь;
			Заголовок = НСтр("ru='Как отработать излишек';uk='Як відпрацювати надлишок'");
		Иначе
			Элементы.Излишки.Видимость = Ложь;
			Заголовок = НСтр("ru='Как отработать недостачу';uk='Як відпрацювати нестачу'");
		КонецЕсли;
	Иначе
		Заголовок = НСтр("ru='Как отработать расхождения';uk='Як відпрацювати розбіжності'");
	КонецЕслИ;
	
	Элементы.КартинкаПояснения.Видимость = Параметры.ПоказыватьПояснение;
	Элементы.Пояснение.Видимость = Параметры.ПоказыватьПояснение;
	
	СформироватьЗаголовки(
		Параметры.ТипАкта,
		Параметры.КоличествоУпаковокРасхождения,
		Параметры.СпособОтраженияРасхождений,
		Параметры.ГрупповоеИзменение,
		Параметры.СтрокаПоЗаказу);
	
КонецПроцедуры
	
&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Не ВыполняетсяЗакрытие И Модифицированность Тогда
		
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект),
		               НСтр("ru='Выполненные изменения будут утеряны. Все равно закрыть?';uk='Виконані зміни будуть втрачені. Все одно закрити?'"),
		               РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ВыполняетсяЗакрытие = Истина;
	
	Если ГрупповоеИзменение Тогда
		ПараметрыЗакрытия = Новый Структура();
		ПараметрыЗакрытия.Вставить("ДействиеИзлишки", ДействиеИзлишки);
		ПараметрыЗакрытия.Вставить("ДействиеНедостачи", ДействиеНедостачи);
		Закрыть(ПараметрыЗакрытия);
	ИначеЕсли КоличествоУпаковокРасхождения > 0 Тогда
		Закрыть(ДействиеИзлишки);
	ИначеЕсли КоличествоУпаковокРасхождения < 0 Тогда
		Закрыть(ДействиеНедостачи);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ВыполняетсяЗакрытие = Истина;
	Закрыть(Неопределено);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьЗаголовки(ТипАкта, КоличествоУпаковокРасхождения, СпособОтраженияРасхождений, ГрупповоеИзменение, СтрокаПоЗаказу)
	
	Если ТипАкта = "Реализация" Тогда
		
		// Недостачи
		Элементы.ДекорацияНедостачиСогласованы.Заголовок   = НСтр("ru='Недостачи при реализации согласованы с клиентом';uk='Нестачі при реалізації погоджені з клієнтом'");
		Элементы.ДекорацияНедостачиНеСогласованы.Заголовок = НСтр("ru='Недостачи при реализации не согласованы с клиентом';uk='Нестачі при реалізації не погоджені з клієнтом'");
		
		Если СпособОтраженияРасхождений = Перечисления.СпособыОтраженияРасхожденийАктПриемкиКлиента.ИсправлениеПервичныхДокументов Тогда
			Если СтрокаПоЗаказу Или (ГрупповоеИзменение И ИспользоватьЗаказыКлиентов) Тогда
				Элементы.ДопоставкаНеТребуетсяДекорация.Заголовок = НСтр("ru='Реализация уменьшается на недопоставленный товар с последующей отменой недопоставленных строк заказа';uk='Реалізація зменшується на недопоставлений товар з подальшою відміною недопоставлених рядків замовлення'");
			Иначе
				Элементы.ДопоставкаНеТребуетсяДекорация.Заголовок = НСтр("ru='Реализация уменьшается на недопоставленный товар';uk='Реалізація зменшується на недопоставлений товар'");
			КонецЕсли;
			Элементы.ТребуетсяДопоставкаДекорация.Заголовок = НСтр("ru='Реализация уменьшается на недопоставленный товар с последующим оформлением новой реализации недопоставленного товара';uk='Реалізація зменшується на недопоставлений товар з подальшим оформленням нової реалізації недопоставленого товару'");
		ИначеЕсли СпособОтраженияРасхождений = Перечисления.СпособыОтраженияРасхожденийАктПриемкиКлиента.ОформлениеКорректировкиКакИсправлениеПервичныхДокументов Тогда
			Элементы.ДопоставкаНеТребуетсяДекорация.Заголовок = НСтр("ru='На недопоставленный товар оформляется корректировка реализации (исправление ошибок)';uk='На недопоставлений товар оформлюється коригування реалізації (виправлення помилок)'");
			Элементы.ТребуетсяДопоставкаДекорация.Заголовок = НСтр("ru='На недопоставленный товар оформляется корректировка реализации (исправление ошибок) с последующим оформлением реализации недопоставленного товара';uk='На недопоставлений товар оформлюється коригування реалізації (виправлення помилок) з подальшим оформленням реалізації недопоставленого товару'");
		ИначеЕсли СпособОтраженияРасхождений = Перечисления.СпособыОтраженияРасхожденийАктПриемкиКлиента.ОформлениеКорректировокКакНовыеПервичныеДокументы Тогда
			Элементы.ДопоставкаНеТребуетсяДекорация.Заголовок = НСтр("ru='На недопоставленный товар оформляется корректировка реализации (возврат недопоставленного товара)';uk='На недопоставлений товар оформлюється коригування реалізації (повернення недопоставленого товару)'");
			Элементы.ТребуетсяДопоставкаДекорация.Заголовок = НСтр("ru='На недопоставленный товар оформляется корректировка реализации (возврат недопоставленного товара) с последующим оформлением реализации на допоставку';uk='На недопоставлений товар оформлюється коригування реалізації (повернення недопоставленого товару) з подальшим оформленням реалізації на довезення'");
		КонецЕсли;
		
		Элементы.НедостачаНеПризнанаДекорация.Заголовок = НСтр("ru='Недопоставленный товар не отгружается со склада, документы не переоформляются';uk='Недопоставлений товар не відвантажується зі складу, документи не переоформлюються'");
		Элементы.ОтгрузитьСейчасДекорация.Заголовок = НСтр("ru='Недопоставленный товар отгружается со склада, документы не переоформляются';uk='Недопоставлений товар відвантажується зі складу, документи не переоформлюються'");
		
		// Излишки
		
		Элементы.ДекорацияИзлишкиСогласованы.Заголовок   = НСтр("ru='Излишки при реализации согласованы с клиентом';uk='Надлишки при реалізації погоджені з клієнтом'");
		Элементы.ДекорацияИзлишкиНеСогласованы.Заголовок = НСтр("ru='Излишки при реализации не согласованы с клиентом';uk='Надлишки при реалізації не погоджені з клієнтом'");
		
		Если СпособОтраженияРасхождений = Перечисления.СпособыОтраженияРасхожденийАктПриемкиКлиента.ИсправлениеПервичныхДокументов Тогда
			Элементы.ПокупкаПерепоставленногоДекорация.Заголовок = НСтр("ru='Реализация увеличивается на перепоставленный товар';uk='Реалізація збільшується на перепоставлений товар'");
			Если ИспользоватьЗаявкиНаВозврат Тогда
				Элементы.ВозвратПерепоставленногоДекорация.Заголовок = НСтр("ru='Реализация увеличивается на перепоставленный товар с последующим оформлением заявки на возврат перепоставленного товара';uk='Реалізація збільшується на перепоставлений товар з подальшим оформленням заявки на повернення перепоставленого товару'");
			Иначе
				Элементы.ВозвратПерепоставленногоДекорация.Заголовок = НСтр("ru='Реализация увеличивается на перепоставленный товар с последующим оформлением возврата перепоставленного товара';uk='Реалізація збільшується на перепоставлений товар з подальшим оформленням повернення перепоставленого товару'");
			КонецЕсли;
		ИначеЕсли СпособОтраженияРасхождений = Перечисления.СпособыОтраженияРасхожденийАктПриемкиКлиента.ОформлениеКорректировкиКакИсправлениеПервичныхДокументов Тогда
			Элементы.ПокупкаПерепоставленногоДекорация.Заголовок = НСтр("ru='На перепоставленный товар оформляется корректировка реализации (исправление ошибок)';uk='На перепоставлений товар оформлюється коригування реалізації (виправлення помилок)'");
			Если ИспользоватьЗаявкиНаВозврат Тогда
				Элементы.ВозвратПерепоставленногоДекорация.Заголовок = НСтр("ru='На перепоставленный товар оформляется корректировка реализации (исправление ошибок) с последующим оформлением заявки на возврат перепоставленного товара';uk='На перепоставлений товар оформлюється коригування реалізації (виправлення помилок) з подальшим оформленням заявки на повернення перепоставленого товару'");
			Иначе
				Элементы.ВозвратПерепоставленногоДекорация.Заголовок = НСтр("ru='На перепоставленный товар оформляется корректировка реализации (исправление ошибок) с последующим оформлением возврата перепоставленного товара';uk='На перепоставлений товар оформлюється коригування реалізації (виправлення помилок) з подальшим оформленням повернення перепоставленого товару'");
			КонецЕсли;
		ИначеЕсли СпособОтраженияРасхождений = Перечисления.СпособыОтраженияРасхожденийАктПриемкиКлиента.ОформлениеКорректировокКакНовыеПервичныеДокументы Тогда
			Элементы.ПокупкаПерепоставленногоДекорация.Заголовок = НСтр("ru='На перепоставленный товар оформляется корректировка реализации (реализация перепоставленного)';uk='На перепоставлений товар оформлюється коригування реалізації (реалізація перепоставленого)'");
			Если ИспользоватьЗаявкиНаВозврат Тогда
				Элементы.ВозвратПерепоставленногоДекорация.Заголовок = НСтр("ru='На перепоставленный товар оформляется корректировка реализации (реализация перепоставленного товара) с последующим оформлением заявки на возврат перепоставленного товара';uk='На перепоставлений товар оформлюється коригування реалізації (реалізація перепоставленого товару) з подальшим оформленням заявки на повернення перепоставленого товару'");
			Иначе
				Элементы.ВозвратПерепоставленногоДекорация.Заголовок = НСтр("ru='На перепоставленный товар оформляется корректировка реализации (реализация перепоставленного товара) с последующим оформлением возврата перепоставленного товара';uk='На перепоставлений товар оформлюється коригування реалізації (реалізація перепоставленого товару) з подальшим оформленням повернення перепоставленого товару'");
			КонецЕсли;
		КонецЕсли;
		
		Элементы.ПерепоставленноеДаритсяДекорация.Заголовок = НСтр("ru='Перепоставленный товар не возвращается на склад, документы не переоформляются';uk='Перепоставлений товар не повертається на склад, документи не переоформлюються'");
		Элементы.ОприходоватьСейчасДекорация.Заголовок = НСтр("ru='Перепоставленный товар возвращается на склад, документы не переоформляются';uk='Перепоставлений товар повертається на склад, документи не переоформлюються'");
		
	ИначеЕсли ТипАкта = "Возврат" Тогда
		
		// Недостачи
		Элементы.ДекорацияНедостачиСогласованы.Заголовок   = НСтр("ru='Недостачи при возврате согласованы с поставщиком';uk='Нестачі при поверненні погоджені з постачальником'");
		Элементы.ДекорацияНедостачиНеСогласованы.Заголовок = НСтр("ru='Недостачи при возврате не согласованы с поставщиком';uk='Нестачі при поверненні не погоджені з постачальником'");
		
		Элементы.ДопоставкаНеТребуетсяДекорация.Заголовок = НСтр("ru='Возврат товаров поставщику уменьшается на недопоставленный товар';uk='Повернення товарів постачальнику зменшується на недопоставлений товар'");
		Элементы.ТребуетсяДопоставкаДекорация.Заголовок = НСтр("ru='Возврат товаров поставщику уменьшается на недопоставленный товар с последующим оформлением нового возврата недопоставленного товара';uk='Повернення товарів постачальнику зменшується на недопоставлений товар з подальшим оформленням нового повернення недопоставленого товару'");
		Элементы.НедостачаНеПризнанаДекорация.Заголовок = НСтр("ru='Недопоставленный товар не отгружается со склада, документы не переоформляются';uk='Недопоставлений товар не відвантажується зі складу, документи не переоформлюються'");
		Элементы.ОтгрузитьСейчасДекорация.Заголовок = НСтр("ru='Недопоставленный товар отгружается со склада, документы не переоформляются';uk='Недопоставлений товар відвантажується зі складу, документи не переоформлюються'");
		
		// Излишки
		Элементы.ДекорацияИзлишкиСогласованы.Заголовок   = НСтр("ru='Излишки при возврате согласованы с поставщиком';uk='Надлишки при поверненні погоджені з постачальником'");
		Элементы.ДекорацияИзлишкиНеСогласованы.Заголовок = НСтр("ru='Излишки при возврате не согласованы с поставщиком';uk='Надлишки при поверненні не погоджені з постачальником'");
		
		Элементы.ПокупкаПерепоставленногоДекорация.Заголовок = НСтр("ru='Возврат товаров поставщику увеличивается на перепоставленный товар';uk='Повернення товарів постачальнику збільшується на перепоставлений товар'");
		Элементы.ВозвратПерепоставленногоДекорация.Заголовок = НСтр("ru='Возврат товаров поставщику увеличивается на перепоставленный товар с последующим оформлением поступления перепоставленного товара';uk='Повернення товарів постачальнику збільшується на перепоставлений товар з подальшим оформленням надходження перепоставленого товару'");
		Элементы.ПерепоставленноеДаритсяДекорация.Заголовок = НСтр("ru='Перепоставленный товар не возвращается на склад, документы не переоформляются';uk='Перепоставлений товар не повертається на склад, документи не переоформлюються'");
		Элементы.ОприходоватьСейчасДекорация.Заголовок = НСтр("ru='Перепоставленный товар возвращается на склад, документы не переоформляются';uk='Перепоставлений товар повертається на склад, документи не переоформлюються'");
		
	ИначеЕсли ТипАкта = "Перемещение" Тогда
		
		// Недостачи
		Элементы.ДекорацияНедостачиСогласованы.Заголовок   = НСтр("ru='Недостачи при поступлении согласованы с отправителем';uk='Нестачі при надходженні погоджені з відправником'");
		Элементы.ДекорацияНедостачиНеСогласованы.Заголовок = НСтр("ru='Недостачи при поступлении не согласованы с отправителем';uk='Нестачі при надходженні не погоджені з відправником'");
		
		Если СтрокаПоЗаказу Или (ГрупповоеИзменение И ИспользоватьЗаказыНаПеремещение) Тогда
			Элементы.ДопоставкаНеТребуетсяДекорация.Заголовок = НСтр("ru='Перемещение товаров уменьшается на недопоставленный товар с последующей отменой недопоставленных строк заказа';uk='Переміщення товарів зменшується на недопоставлений товар з подальшою відміною недопоставлених рядків замовлення'");
		Иначе
			Элементы.ДопоставкаНеТребуетсяДекорация.Заголовок = НСтр("ru='Перемещение товаров уменьшается на недопоставленный товар';uk='Переміщення товарів зменшується на недопоставлений товар'");
		КонецЕсли;
		Элементы.ТребуетсяДопоставкаДекорация.Заголовок = НСтр("ru='Перемещение товаров уменьшается на недопоставленный товар с последующим оформлением нового перемещения недопоставленного товара';uk='Переміщення товарів зменшується на недопоставлений товар з подальшим оформленням нового переміщення недопоставленого товару'");
		Элементы.НедостачаНеПризнанаДекорация.Заголовок = НСтр("ru='Недопоставленный товар не отгружается со склада-отправителя, документы не переоформляются';uk='Недопоставлений товар не відвантажується зі складу-відправника, документи не переоформлюються'");
		Элементы.ОтгрузитьСейчасДекорация.Заголовок = НСтр("ru='Недопоставленный товар отгружается со склада-отправителя, документы не переоформляются';uk='Недопоставлений товар відвантажується зі складу-відправника, документи не переоформлюються'");
		
		// Излишки
		Элементы.ДекорацияИзлишкиСогласованы.Заголовок   = НСтр("ru='Излишки при поступлении согласованы с отправителем';uk='Надлишки при надходженні погоджені з відправником'");
		Элементы.ДекорацияИзлишкиНеСогласованы.Заголовок = НСтр("ru='Излишки при поступлении не согласованы с отправителем';uk='Надлишки при надходженні не погоджені з відправником'");
		
		Элементы.ПокупкаПерепоставленногоДекорация.Заголовок = НСтр("ru='Перемещение товаров увеличивается на перепоставленный товар';uk='Переміщення товарів збільшується на перепоставлений товар'");
		Элементы.ВозвратПерепоставленногоДекорация.Заголовок = НСтр("ru='Перемещение товаров увеличивается на перепоставленный товар с последующим оформлением обратного перемещения перепоставленного товара';uk='Переміщення товарів збільшується на перепоставлений товар з подальшим оформленням зворотного переміщення перепоставленого товару'");
		Элементы.ПерепоставленноеДаритсяДекорация.Заголовок = НСтр("ru='Перепоставленный товар не возвращается на склад-отправитель, документы не переоформляются';uk='Перепоставлений товар не повертається на склад-відправник, документи не переоформлюються'");
		Элементы.ОприходоватьСейчасДекорация.Заголовок = НСтр("ru='Перепоставленный товар возвращается на склад-отправитель, документы не переоформляются';uk='Перепоставлений товар повертається на склад-відправник, документи не переоформлюються'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ВыполняетсяЗакрытие = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

