#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ВидКлассификатораСтарый = ВидКлассификатора;
	
	ОбновитьМакет();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоКлассификатора

&НаКлиенте
Процедура ОбходДереваВверх(ТекущиеДанные)

	Родитель = ТекущиеДанные.ПолучитьРодителя();
	Если Родитель <> Неопределено Тогда // Верхний уровень
		
		ДочерниеСтроки = Родитель.ПолучитьЭлементы();
		КоличествоВыбранных = 0;
		ОбщееКоличество = 0;
		Для каждого Элемент Из ДочерниеСтроки Цикл
			Если Элемент.Выбран = 2 Тогда
				КоличествоВыбранных = КоличествоВыбранных + 0.5;
			ИначеЕсли Элемент.Выбран = 1 Тогда
				КоличествоВыбранных = КоличествоВыбранных + 1;
			КонецЕсли;
			ОбщееКоличество = ОбщееКоличество + 1;
		КонецЦикла;
		
		Если ОбщееКоличество = КоличествоВыбранных Тогда
			Родитель.Выбран = 1;
		ИначеЕсли КоличествоВыбранных = 0 Тогда
			Родитель.Выбран = 0;
		Иначе
			Родитель.Выбран = 2;
		КонецЕсли;
		
		ОбходДереваВверх(Родитель);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбходДереваВниз(ТекущиеДанные)
	
	ДочерниеСтроки = ТекущиеДанные.ПолучитьЭлементы();
	Для каждого Элемент Из ДочерниеСтроки Цикл
		Элемент.Выбран = ТекущиеДанные.Выбран;
		ОбходДереваВниз(Элемент);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранПриИзменении(ТекущиеДанные)
	
	Если ТекущиеДанные.Выбран = 2 Тогда
		ТекущиеДанные.Выбран = 0;
	КонецЕсли;
	
	ОбходДереваВверх(ТекущиеДанные);
	ОбходДереваВниз(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКлассификатораВыбранПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоКлассификатора.ТекущиеДанные;
	ВыбранПриИзменении(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидКлассификатораПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ВидКлассификатораСтарый) Тогда
		
		ТекстВопроса = НСтр("ru='Текущее выделение будет потеряно. Продолжить?';uk='Поточне виділення буде втрачено. Продовжити?'");
		
		Оповещение = Новый ОписаниеОповещения("ВидКлассификатораПриИзмененииПродолжить", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, НСтр("ru='Изменение классификатора';uk='Зміна класифікатора'"));
		
	Иначе
		
		ВидКлассификатораПриИзмененииНаСервере();
		
		ВидКлассификатораСтарый = ВидКлассификатора;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидКлассификатораПриИзмененииПродолжить(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ВидКлассификатораПриИзмененииНаСервере();
		
		ВидКлассификатораСтарый = ВидКлассификатора;
		
	Иначе
		
		ВидКлассификатора = ВидКлассификатораСтарый;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВидКлассификатораПриИзмененииНаСервере()
	
	ОбновитьМакет();
	
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ОбработатьРезультатыПодбораНаСервере();
	ОповеститьОбИзменении(Тип("СправочникСсылка.КлассификаторУКТВЭД"));	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоКлассификатораВыбран.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоКлассификатора.Код");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоКлассификатора.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоКлассификатора.Уровень");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 1;
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(WindowsШрифты.DefaultGUIFont, , , Истина, Ложь, Ложь, Ложь, ));

	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоКлассификатора.Имя);
	
	ГруппаОтбора1 = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;

	ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоКлассификатора.Существует");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоКлассификатора.Код");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.DarkSlateBlue);

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементов()
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НадписьПустойВидКлассификатора", "Видимость", НЕ ЗначениеЗаполнено(ВидКлассификатора));
	
КонецПроцедуры

#Область Прочее

&НаСервере
Процедура ОбновитьМакет()
	
	Если НЕ ЗначениеЗаполнено(ВидКлассификатора) Тогда
		Возврат;
	ИначеЕсли ВидКлассификатора = Перечисления.ВидыКодовДляНалоговойНакладной.КодУслуги Тогда
		ИмяКлассификатора = "КлассификаторДКПП";
	Иначе
		ИмяКлассификатора = "КлассификаторУКТВЭД";
	КонецЕсли;
	
	Макет = НСИССервер.ПолучитьМакет(ИмяКлассификатора, Справочники.КлассификаторУКТВЭД.ПолучитьМакет(ИмяКлассификатора));
	КлассификаторXML = Макет.ПолучитьТекст();
	
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	Дерево = РеквизитФормыВЗначение("ДеревоКлассификатора");
	
	Дерево.Строки.Очистить();
	
	Для Каждого КлассификаторЗапись Из КлассификаторТаблица Цикл
		
		Если КлассификаторЗапись.Level = "1" Тогда
			СтрокаУровень1 = Дерево.Строки.Добавить();
			НоваяСтрока = СтрокаУровень1;
		Иначе
			НоваяСтрока = СтрокаУровень1.Строки.Добавить();		
		КонецЕсли;
		
		НоваяСтрока.Уровень      = КлассификаторЗапись.Level; // Для оформления формы
		НоваяСтрока.Код          = КлассификаторЗапись.Code;
		НоваяСтрока.Наименование = КлассификаторЗапись.Name;
		Если ИмяКлассификатора = "КлассификаторУКТВЭД" Тогда
			НоваяСтрока.ВыводитьПриПечатиЧека = КлассификаторЗапись.Print = "1";	
		КонецЕсли; 

	КонецЦикла;
	
	Соответствие = Новый Соответствие;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КлассификаторУКТВЭД.Код КАК Код
	|ИЗ
	|	Справочник.КлассификаторУКТВЭД КАК КлассификаторУКТВЭД
	|ГДЕ
	|	КлассификаторУКТВЭД.Вид = &ВидКлассификатора";
	Запрос.УстановитьПараметр("ВидКлассификатора", ВидКлассификатора);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Соответствие.Вставить(СокрЛП(Выборка.Код), СокрЛП(Выборка.Код));
	КонецЦикла;
	
	Для каждого СтрокаУровень1 Из Дерево.Строки Цикл
		Для каждого СтрокаУровень2 Из СтрокаУровень1.Строки Цикл
			Если Соответствие.Получить(СтрокаУровень2.Код) <> Неопределено Тогда
				СтрокаУровень2.Существует = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоКлассификатора");
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатыПодбораНаСервере()
	
	Если НЕ ЗначениеЗаполнено(ВидКлассификатора) Тогда
		Возврат;
	КонецЕсли;
	
	МассивВыбранныхСтрок = Новый Массив;
	МассивКодов = Новый Массив;
	
	Дерево = РеквизитФормыВЗначение("ДеревоКлассификатора");
	Для каждого СтрокаУровень1 Из Дерево.Строки Цикл
		Если СтрокаУровень1.Выбран Тогда
			Для каждого СтрокаУровень2 Из СтрокаУровень1.Строки Цикл
				Если СтрокаУровень2.Выбран Тогда
					Если СтрокаУровень2.Выбран И Не ПустаяСтрока(СтрокаУровень2.Код)Тогда
					
						МассивВыбранныхСтрок.Добавить(СтрокаУровень2);
						МассивКодов.Добавить(СтрокаУровень2.Код);
						
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КлассификаторУКТВЭД.Ссылка КАК Ссылка,
	|	КлассификаторУКТВЭД.Вид КАК Вид,
	|	КлассификаторУКТВЭД.Код КАК Код
	|ИЗ
	|	Справочник.КлассификаторУКТВЭД КАК КлассификаторУКТВЭД
	|ГДЕ
	|	КлассификаторУКТВЭД.Код В(&МассивКодов)";
	
	Запрос.УстановитьПараметр("МассивКодов", МассивКодов);
	
	ТаблицаКлассификатор = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаКлассификатор Из ТаблицаКлассификатор Цикл
		СтрокаКлассификатор.Код = СокрЛП(СтрокаКлассификатор.Код);
	КонецЦикла;
	ТаблицаКлассификатор.Индексы.Добавить("Код, Вид");
	
	ПараметрыПоиска = Новый Структура("Код, Вид");
	ПараметрыПоиска.Вид = ВидКлассификатора;
	
	НачатьТранзакцию();
	
	Для каждого СтрокаДерева Из МассивВыбранныхСтрок Цикл
		
		ПараметрыПоиска.Код = СтрокаДерева.Код;
		НайденныеЭлементы = ТаблицаКлассификатор.НайтиСтроки(ПараметрыПоиска);
		
		Если НайденныеЭлементы.Количество() > 0 Тогда
			СправочникОбъект = НайденныеЭлементы[0].Ссылка.ПолучитьОбъект();
		Иначе
			СправочникОбъект = Справочники.КлассификаторУКТВЭД.СоздатьЭлемент();
		КонецЕсли;
		
		СправочникОбъект.Код                     = СтрокаДерева.Код;
		СправочникОбъект.Вид                     = ВидКлассификатора;
		СправочникОбъект.НаименованиеПолное      = СобратьПолноеНаименование(Дерево, СтрокаДерева);
		
		Если ВидКлассификатора = Перечисления.ВидыКодовДляНалоговойНакладной.КодУслуги Тогда
			
			СправочникОбъект.Наименование            = СокрЛП(СправочникОбъект.Код) + "/"+ СтрокаДерева.Наименование;
			
		ИначеЕсли ВидКлассификатора = Перечисления.ВидыКодовДляНалоговойНакладной.КодТовараИмпортированного Тогда
			
			СправочникОбъект.Наименование            = СокрЛП(СправочникОбъект.Код) + " X" + "/"+ СтрЗаменить(СправочникОбъект.НаименованиеПолное, Символы.ПС, "");
			
		Иначе
			
			СправочникОбъект.Наименование            = СокрЛП(СправочникОбъект.Код) + "/"+ СтрЗаменить(СправочникОбъект.НаименованиеПолное, Символы.ПС, "");
			
		КонецЕсли;
		
		СправочникОбъект.ВыводитьПриПечатиЧека       = СтрокаДерева.ВыводитьПриПечатиЧека;
		
		СправочникОбъект.Записать();
	
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

&НаСервере
Функция СобратьПолноеНаименование(Дерево, СтрокаДерева)

	СтруктураОписания = ЗаполнитьСтруктуруТекстУровень(СтрокаДерева.Наименование);
	
	ТекУровень = СтруктураОписания.Уровень;
	Если ТекУровень = 0 Тогда
		
		Возврат СтруктураОписания.Текст;
		
	Иначе
		
		КоллекцияСтрокРодителя = СтрокаДерева.Родитель.Строки;		
		ИндексСтроки = КоллекцияСтрокРодителя.Индекс(СтрокаДерева);
		
		Пока ИндексСтроки > 0 Цикл
			
			ИндексСтроки = ИндексСтроки - 1;
			
			ВерхнийУровень = ЗаполнитьСтруктуруТекстУровень(КоллекцияСтрокРодителя[ИндексСтроки].Наименование);
			Если ВерхнийУровень.Уровень >= текУровень Тогда
				Продолжить;
			КонецЕсли;			
			
			Если ВерхнийУровень.Уровень >= 0 Тогда
				
				ТекстВерхнегоУровня     = ВерхнийУровень.Текст + Символы.ПС;
				СтруктураОписания.Текст = ТекстВерхнегоУровня + СтруктураОписания.Текст;
				
			КонецЕсли;
			
			текУровень = ТекУровень - 1;
			
			Если ВерхнийУровень.Уровень = 0 Тогда
				Возврат СтруктураОписания.Текст;
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат СтруктураОписания.Текст;
		
	КонецЕсли;
	
КонецФункции 

&НаСервере
Функция ЗаполнитьСтруктуруТекстУровень(СтрокаНаименование)
	
	Возврат Новый Структура("Текст, Уровень", СокрЛ(СтрокаНаименование), УровеньКлассификатора(СтрокаНаименование));
	
КонецФункции

&НаСервере
Функция УровеньКлассификатора(СтрокаНаименование)
	
	Уровень = 0;
	
	ДлинаСтроки = СтрДлина(СтрокаНаименование);
	Для Сч = 1 По ДлинаСтроки Цикл
		
		ТекСимв = Сред(СтрокаНаименование, Сч, 1);
		
		Если ТекСимв = "-" Тогда
			Уровень = Уровень + 1;
		ИначеЕсли  ТекСимв  = " " Тогда
			Продолжить;	
		Иначе
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Уровень;
	
КонецФункции

#КонецОбласти

#КонецОбласти
