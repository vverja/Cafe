#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Подготавливает необходимые данные для формирования пакета ЭД
//
// Параметры:
// МассивСсылокНаОбъект - Массив - объекты для формирования ЭД
// АдресХранилища - Строка - адрес хранилища в котором находятся подготовленные данные
//
Процедура ПодготовитьДанныеДляЗаполненияДокументов(Параметры, АдресХранилища) Экспорт
	
	ТаблицаЭД = Новый ТаблицаЗначений;
	ТаблицаЭД.Колонки.Добавить("ПолноеИмяФайла");
	ТаблицаЭД.Колонки.Добавить("НаименованиеФайла");
	ТаблицаЭД.Колонки.Добавить("НаправлениеЭД");
	ТаблицаЭД.Колонки.Добавить("Контрагент");
	ТаблицаЭД.Колонки.Добавить("УникальныйИдентификатор");
	ТаблицаЭД.Колонки.Добавить("ВладелецЭД");
	ТаблицаЭД.Колонки.Добавить("АдресХранилища");
	ТаблицаЭД.Колонки.Добавить("ДвоичныеДанныеПакета");
	ТаблицаЭД.Колонки.Добавить("ПолноеИмяДопФайла");
	ТаблицаЭД.Колонки.Добавить("ИдентификаторДопФайла");
	
	// Дополнительные свойства для 1С:Бизнес-сеть.
	ТаблицаЭД.Колонки.Добавить("ВидЭД");
	ТаблицаЭД.Колонки.Добавить("Сумма");
	ТаблицаЭД.Колонки.Добавить("АдресХранилищаПредставления");
	ТаблицаЭД.Колонки.Добавить("ДвоичныеДанныеПредставления");
	
	МассивСсылокНаОбъект = Параметры.МассивСсылокНаОбъект;
	
	НастройкиОбъектов = Новый Соответствие;
	Для Каждого СсылкаНаОбъект Из МассивСсылокНаОбъект Цикл
		НастройкиОбмена = ОбменСКонтрагентамиСлужебный.ЗаполнитьПараметрыЭДПоИсточнику(СсылкаНаОбъект);
		
		НастройкиОбмена.Вставить("ИдентификаторОрганизации", ОбменСКонтрагентамиПереопределяемый.ПолучитьИДКонтрагента(
		НастройкиОбмена.Организация, "Организация"));
		НастройкиОбмена.Вставить("ИдентификаторКонтрагента", ОбменСКонтрагентамиПереопределяемый.ПолучитьИДКонтрагента(
		НастройкиОбмена.Контрагент, "Контрагент"));
		
		НастройкиОбмена.Вставить("ПрофильНастроекЭДО", Новый Структура("СпособОбменаЭД", Перечисления.СпособыОбменаЭД.БыстрыйОбмен));
		НастройкиОбмена.Вставить("СоглашениеЭД", "");
		НастройкиОбмена.Вставить("ВерсияФормата", ОбменСКонтрагентамиПовтИсп.ВерсияСхемыCML2());
		
		НастройкиОбъектов.Вставить(СсылкаНаОбъект, НастройкиОбмена);
		Если НастройкиОбмена.ВидЭД = Перечисления.ВидыЭД.АктИсполнитель
			ИЛИ НастройкиОбмена.ВидЭД = Перечисления.ВидыЭД.АктЗаказчик Тогда
			НастройкиОбмена.Вставить("ВерсияРегламентаЭДО", Перечисления.ВерсииРегламентаОбмена1С.Версия20);
		КонецЕсли;
	КонецЦикла;
	
	МассивСтруктурОбмена = ОбменСКонтрагентамиСлужебный.СформироватьХМЛФайлыДокументов(МассивСсылокНаОбъект,
		НастройкиОбъектов);
		
	Для Каждого СтруктураОбмена Из МассивСтруктурОбмена Цикл
		ПолноеИмяФайла = СтруктураОбмена.ПолноеИмяФайла;
		Если НЕ ЗначениеЗаполнено(ПолноеИмяФайла) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаЭД.Добавить();
		НоваяСтрока.ПолноеИмяФайла = ПолноеИмяФайла;
		СтруктураОбмена.Свойство("ПолноеИмяДопФайла", НоваяСтрока.ПолноеИмяДопФайла);
		СтруктураОбмена.Свойство("ИдентификаторДопФайла", НоваяСтрока.ИдентификаторДопФайла);
		
		НаименованиеФайла = БыстрыйОбменИмяСохраняемогоФайла(СтруктураОбмена.СтруктураЭД.ВладелецЭД);
		НоваяСтрока.НаименованиеФайла = НаименованиеФайла;
		НоваяСтрока.НаправлениеЭД = СтруктураОбмена.СтруктураЭД.НаправлениеЭД;
		НоваяСтрока.Контрагент = СтруктураОбмена.СтруктураЭД.Контрагент;
		НоваяСтрока.УникальныйИдентификатор = СтруктураОбмена.СтруктураЭД.ВладелецЭД.УникальныйИдентификатор();
		НоваяСтрока.ВладелецЭД = СтруктураОбмена.СтруктураЭД.ВладелецЭД;
		
		СтруктураОбмена.Вставить("ДвоичныеДанныеПредставления");
		
		Если ЗначениеЗаполнено(СтруктураОбмена.ДвоичныеДанныеПредставления) Тогда
			НоваяСтрока.ДвоичныеДанныеПредставления = СтруктураОбмена.ДвоичныеДанныеПредставления;
		КонецЕсли;
		НоваяСтрока.ВидЭД = СтруктураОбмена.ВидЭД;
		СтруктураОбмена.СтруктураЭД.Свойство("СуммаДокумента", НоваяСтрока.Сумма);
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТаблицаЭД) Тогда
		ПоместитьВоВременноеХранилище(ТаблицаЭД, АдресХранилища);
	Иначе
		
		АдресХранилища = "";
		
		ОтправкаЧерезБС = Параметры.ОтправкаЧерезБС;
		
		Если ОтправкаЧерезБС Тогда
			ШаблонОшибки = НСтр("ru='Произошла ошибка при отправке документа %1.';uk='Виникла помилка при відправленні документа %1.'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, СсылкаНаОбъект);
		Иначе
			ТекстОшибки = НСтр("ru='Произошла ошибка при формировании пакета однократной сделки.
            |Подробности см. Журнал Регистрации'
            |;uk='Виникла помилка при формуванні пакета одноразової угоди.
            |Подробиці див. Журнал Реєстрації'")
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция БыстрыйОбменИмяСохраняемогоФайла(ВладелецЭД)
	
	НаименованиеФайла = "";
	ОбменСКонтрагентамиПереопределяемый.ЗадатьИмяСохраняемогоФайлаПриБыстромОбмене(ВладелецЭД, НаименованиеФайла);
	Если ЗначениеЗаполнено(ВладелецЭД) И НЕ ЗначениеЗаполнено(НаименованиеФайла) Тогда
		
		Если ТипЗнч(ВладелецЭД) = Тип("СправочникСсылка.Организации") Тогда
			
			СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВладелецЭД, "Наименование");
			ШаблонФайла = НСтр("ru='%1';uk='%1'");
			НаименованиеФайла = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонФайла, СтруктураРеквизитов.Наименование);
			
		Иначе
			
			СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВладелецЭД, "Номер, Дата");		
			ШаблонФайла = НСтр("ru='%1 № %2 от %3';uk='%1 № %2 від %3'");
			НаименованиеФайла = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонФайла, Строка(ТипЗнч(ВладелецЭД)),
															СтруктураРеквизитов.Номер, Формат(СтруктураРеквизитов.Дата, "ДЛФ=Д"));
		КонецЕсли;
		
		НаименованиеФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(НаименованиеФайла);
		
	КонецЕсли;
	
	Возврат НаименованиеФайла;
	
КонецФункции

#КонецОбласти

#КонецЕсли