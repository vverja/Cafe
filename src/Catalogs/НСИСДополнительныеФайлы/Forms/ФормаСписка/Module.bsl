
&НаСервере
Процедура ПоказатьДоступныеФайлы()
	
	ТаблицаФайлов.Очистить();
	
	СписокФайлов = НСИССервер.ПрочитатьДокументИзБазы("СписокДополнительныхФайлов");
	Если СписокФайлов = Неопределено ИЛИ ТипЗнч(СписокФайлов) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаДоступных = ТаблицаФайлов.Выгрузить();
	Для Каждого Файл из СписокФайлов Цикл
		НС = ТаблицаДоступных.Добавить();
		ЗаполнитьЗначенияСвойств(НС, Файл)
	КонецЦикла;	
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ТаблицаДоступных", ТаблицаДоступных);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	НСИСДополнительныеФайлы.Идентификатор КАК ИмяФайла,
	               |	МАКСИМУМ(НСИСДополнительныеФайлы.Версия) КАК Версия
				   |ПОМЕСТИТЬ ВТДоступныеВерсии
				   |ИЗ
	               |	Справочник.НСИСДополнительныеФайлы КАК НСИСДополнительныеФайлы
				   |СГРУППИРОВАТЬ ПО
				   |	НСИСДополнительныеФайлы.Идентификатор
				   |;
				   |
				   |ВЫБРАТЬ
	               |	ТаблицаДоступных.ИмяФайла,
	               |	ТаблицаДоступных.Версия,
				   |	ТаблицаДоступных.Описание,
				   |	ТаблицаДоступных.ПодробноеОписание
				   |ПОМЕСТИТЬ ВТТаблицаДоступных
				   |ИЗ
	               |	&ТаблицаДоступных КАК ТаблицаДоступных
				   |;
				   |
				   |ВЫБРАТЬ
				   |	ИСТИНА КАК Отметка,
				   |	ТаблицаДоступных.ИмяФайла,
	               |	ТаблицаДоступных.Версия,
				   |	ТаблицаДоступных.Описание,
				   |	ТаблицаДоступных.ПодробноеОписание
				   |ИЗ
	               |	ВТТаблицаДоступных КАК ТаблицаДоступных
				   |    ЛЕВОЕ СОЕДИНЕНИЕ
				   |    ВТДоступныеВерсии КАК ДоступныеВерсии
				   |    ПО ТаблицаДоступных.ИмяФайла = ДоступныеВерсии.ИмяФайла
				   |ГДЕ
				   |    ЕСТЬNULL(ДоступныеВерсии.Версия,0) < ТаблицаДоступных.Версия
				   |";
	ТаблицаФайлов.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры	

&НаСервере
Функция ОбновитьСписокНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаименованиеФоновогоЗадания = НСтр("ru='Обновление данных';uk='Оновлення даних'");
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("ВыводитьПротокол",  Истина);
	ПараметрыЗагрузки.Вставить("ВФоне",  Истина);
	ПараметрыЗагрузки.Вставить("ЗагружатьНормативныеВеличины",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьКлассификаторы",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьКурсыВалют",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьНовости",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьКалендарь",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьСписокФайлов",  Истина);
		
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("Обработки.НСИСЗапускСервисов.ЗапуститьСервисы", ПараметрыЗагрузки, ПараметрыВыполнения);

КонецФункции

&НаСервере
Функция ЗагрузитьНаСервере(ФайлыКЗагрузке)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаименованиеФоновогоЗадания = НСтр("ru='Обновление данных';uk='Оновлення даних'");
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("ВыводитьПротокол",  Истина);
	ПараметрыЗагрузки.Вставить("ВФоне",  Истина);
	ПараметрыЗагрузки.Вставить("ЗагружатьНормативныеВеличины",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьКлассификаторы",  Истина);
	ПараметрыЗагрузки.Вставить("ЗагружатьКурсыВалют",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьНовости",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьКалендарь",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьСписокФайлов",  Ложь);
	ПараметрыЗагрузки.Вставить("СписокФайлов",  ФайлыКЗагрузке);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("Обработки.НСИСЗапускСервисов.ЗапуститьСервисы", ПараметрыЗагрузки, ПараметрыВыполнения);

КонецФункции

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	ОчиститьСообщения();
	
	Элементы.ГруппаВыполнение.Видимость = Истина;
	Элементы.ГруппаКомандЗагрузки.Доступность = Ложь;
	Элементы.НадписьВыполняетсяЗагрузка.Заголовок = НСтр("ru='Обновление списка файлов...';uk='Оновлення списку файлів ...'"); 
	
	ОперацияЗагрузки = ОбновитьСписокНаСервере();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииОбновления", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ОперацияЗагрузки, ОписаниеОповещения, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииОбновления(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.ГруппаВыполнение.Видимость = Ложь;
	Элементы.ГруппаКомандЗагрузки.Доступность = Истина;
	
	ПоказатьДоступныеФайлы();
	Элементы.Список.Обновить();

	Если ТаблицаФайлов.Количество() > 0 Тогда
		Элементы.ТаблицаФайлов.Видимость = Истина;
		Элементы.ТаблицаФайловЗагрузить.Видимость = Истина;
	Иначе
		Элементы.ТаблицаФайлов.Видимость = Ложь;
		Элементы.ТаблицаФайловЗагрузить.Видимость = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	
	ФайлыКЗагрузке = Новый Массив();
	Для Каждого Файл из ТаблицаФайлов Цикл
		Если Файл.Отметка Тогда
			ФайлыКЗагрузке.Добавить(Файл.ИмяФайла);
		КонецЕсли;
	КонецЦикла;
	Если ФайлыКЗагрузке.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	Элементы.ГруппаВыполнение.Видимость = Истина;
	Элементы.ГруппаКомандЗагрузки.Доступность = Ложь;
	Элементы.НадписьВыполняетсяЗагрузка.Заголовок = НСтр("ru='Выполняется загрузка...';uk='Виконується завантаження...'");

	ОперацияЗагрузки = ЗагрузитьНаСервере(ФайлыКЗагрузке);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииОбновления", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ОперацияЗагрузки, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказатьДоступныеФайлы();
	Обновить = Параметры.Свойство("Обновить");
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ТаблицаФайлов.Количество() > 0 Тогда
		Элементы.ТаблицаФайлов.Видимость = Истина;
		Элементы.ТаблицаФайловЗагрузить.Видимость = Истина;
	Иначе
		Элементы.ТаблицаФайлов.Видимость = Ложь;
		Элементы.ТаблицаФайловЗагрузить.Видимость = Ложь;
	КонецЕсли;
	
	Если Обновить Тогда
		ОбновитьСписок(Неопределено);
	КонецЕсли;	
	
КонецПроцедуры


&НаСервере
Процедура УстановитьНаСервере()
	
	Результат = Справочники.НСИСДополнительныеФайлы.УстановитьДополнительныйФайл(Элементы.Список.ТекущаяСтрока);
	Если Результат = Истина Тогда
		ФайлОбъект = Элементы.Список.ТекущаяСтрока.ПолучитьОбъект();
		ФайлОбъект.Устанавливался = Истина;
		ФайлОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура Установить(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(,НСтр("ru='Выберите файл';uk='Виберіть файл'"));
		Возврат;
	КонецЕсли;
	Если НЕ Элементы.Список.ТекущиеДанные.ДанныеЗагружены Тогда
		ПоказатьПредупреждение(,НСтр("ru='Файл не содержит данные';uk='Файл не містить дані'"));
		Возврат;
	КонецЕсли;	
	УстановитьНаСервере();
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьНаСервере()
	
	АдресДанных = ПоместитьВоВременноеХранилище(Элементы.Список.ТекущаяСтрока.ПолучитьОбъект().Файл.Получить(), Новый УникальныйИдентификатор)
	
КонецПроцедуры


&НаКлиенте
Процедура Выгрузить(Команда)
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(,НСтр("ru='Выберите файл';uk='Виберіть файл'"));
		Возврат;
	КонецЕсли;
	Если НЕ Элементы.Список.ТекущиеДанные.ДанныеЗагружены Тогда
		ПоказатьПредупреждение(,НСтр("ru='Файл не содержит данные';uk='Файл не містить дані'"));
		Возврат;
	КонецЕсли;
	
	ПоместитьНаСервере();

	ПолноеИмяФайлаВыбор = "";
	#Если ВебКлиент Тогда
		
		ПолучитьФайл(АдресДанных,Элементы.Список.ТекущиеДанные.Идентификатор+"."+Элементы.Список.ТекущиеДанные.Тип, Истина);
		
	#Иначе
		
		// инициализируем диалог выбора файлов
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		Диалог.ПроверятьСуществованиеФайла = Истина;
		Диалог.Расширение = Элементы.Список.ТекущиеДанные.Тип;
		Диалог.Фильтр = "(*."+Элементы.Список.ТекущиеДанные.Тип+")|*."+Элементы.Список.ТекущиеДанные.Тип;
		
		Диалог.ПолноеИмяФайла = Элементы.Список.ТекущиеДанные.Идентификатор+"."+Элементы.Список.ТекущиеДанные.Тип;
		
		// показываем диалог
		Если НЕ Диалог.Выбрать() Тогда
			Возврат;
		Иначе
			ПолноеИмяФайлаВыбор = Диалог.ПолноеИмяФайла;
		КонецЕсли;
		
		ПолучитьИзВременногоХранилища(АдресДанных).Записать(ПолноеИмяФайлаВыбор);
		
	#КонецЕсли
КонецПроцедуры

