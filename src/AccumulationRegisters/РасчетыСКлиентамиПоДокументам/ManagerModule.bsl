#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы


Процедура ЗаменитьАналитикуУчетаПоПартнерамОчиститьЗаказыДанныеДляОбновления(Параметры) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Расчеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО Расчеты.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|			И (Аналитика.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК Договоры
	|		ПО (Договоры.Ссылка = 
	|				ВЫБОР КОГДА Расчеты.ЗаказКлиента ССЫЛКА Справочник.ДоговорыКонтрагентов
	|					ТОГДА Расчеты.ЗаказКлиента
	|				ИНАЧЕ Расчеты.ЗаказКлиента.Договор
	|			КОНЕЦ)
	|ГДЕ
	|	(НЕ (Аналитика.КлючАналитики ЕСТЬ NULL) И НЕ (Договоры.Ссылка ЕСТЬ NULL))
	|	ИЛИ ТИПЗНАЧЕНИЯ(Расчеты.РасчетныйДокумент) = ТИП(Документ.ЗаказКлиента)
	|	ИЛИ Расчеты.ДатаПлатежа <> Расчеты.УдалитьДатаПлатежа
	|");
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоДвижения", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", "РегистрНакопления.РасчетыСКлиентамиПоДокументам");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
        Параметры, 
        Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"), 
        ДополнительныеПараметры
    );
    
КонецПроцедуры

// Обработчик обновления BAS УТ 3.2.2
// Производит замену измерения "АналитикаУчетаПоПартнерам" с учетом договора в измерении "Заказ",
// удаляет движения, где расчетным документом является заказ поставщику.
Процедура ЗаменитьАналитикуУчетаПоПартнерамОчиститьЗаказы(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.РасчетыСКлиентамиПоДокументам";
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АналитикаРегистра.Партнер                            КАК Партнер,
	|	АналитикаРегистра.Организация                        КАК Организация,
	|	АналитикаРегистра.Контрагент                         КАК Контрагент,
	|	АналитикаРегистра.НаправлениеДеятельности            КАК НаправлениеДеятельности,
	|	ВЫБОР КОГДА Расчеты.ЗаказКлиента ССЫЛКА Справочник.ДоговорыКонтрагентов
	|		ТОГДА Расчеты.ЗаказКлиента
	|	ИНАЧЕ
	|		ЕСТЬNULL(Расчеты.ЗаказКлиента.Договор, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|	КОНЕЦ                                                КАК Договор
	|ИЗ
	|	&ВТДляОбработкиСсылка КАК СписокРегистраторов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Расчеты
	|		ПО Расчеты.Регистратор = СписокРегистраторов.Регистратор
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаРегистра
	|		ПО Расчеты.АналитикаУчетаПоПартнерам = АналитикаРегистра.КлючАналитики
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК НоваяАналитика
	|		ПО АналитикаРегистра.Партнер = НоваяАналитика.Партнер
	|			И АналитикаРегистра.Организация = НоваяАналитика.Организация
	|			И АналитикаРегистра.Контрагент = НоваяАналитика.Контрагент
	|			И АналитикаРегистра.НаправлениеДеятельности = НоваяАналитика.НаправлениеДеятельности
	|			И (НоваяАналитика.Договор =
	|				ВЫБОР КОГДА Расчеты.ЗаказКлиента ССЫЛКА Справочник.ДоговорыКонтрагентов
	|					ТОГДА Расчеты.ЗаказКлиента
	|				ИНАЧЕ ЕСТЬNULL(Расчеты.ЗаказКлиента.Договор, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|			КОНЕЦ)
	|ГДЕ
	|	НоваяАналитика.КлючАналитики ЕСТЬ NULL
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокРегистраторов.Регистратор КАК Ссылка
	|ИЗ
	|	&ВТДляОбработкиСсылка КАК СписокРегистраторов";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
    
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуРегистраторовРегистраДляОбработки(
	     Параметры.Очередь,
	     Неопределено,
	     ПолноеИмяРегистра,
	     МенеджерВременныхТаблиц
    );
	
	Если НЕ Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
    КонецЕсли;
    
	Если НЕ Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли; 
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВТДляОбработкиСсылка", Результат.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Результат = Запрос.ВыполнитьПакет();
	
	КоличествоЗапросов = Результат.ВГраница();
	
	АналитикиКСозданию = Результат[КоличествоЗапросов-1].Выгрузить();
	Для Каждого СтрокаАналитики Из АналитикиКСозданию Цикл
	    НоваяАналитика = РегистрыСведений.АналитикаУчетаПоПартнерам.СоздатьКлючАналитики(СтрокаАналитики);
    КонецЦикла;
    
    ЗапросДанных = Новый Запрос("
    |ВЫБРАТЬ
    |	Расчеты.Период                    КАК Период,
    |	Расчеты.ВидДвижения               КАК ВидДвижения,
    |	НоваяАналитика.КлючАналитики      КАК АналитикаУчетаПоПартнерам,
    |	Расчеты.ЗаказКлиента              КАК ЗаказКлиента,
    |	Расчеты.РасчетныйДокумент         КАК РасчетныйДокумент,
    |	Расчеты.Валюта                    КАК Валюта,
    |	Расчеты.УдалитьДатаПлатежа        КАК УдалитьДатаПлатежа,
    |
    |	0                                 КАК УдалитьКОтгрузке,
    |	0                                 КАК УдалитьКОплате,
    |	0                                 КАК КВозврату,
    |	Расчеты.Долг                      КАК Долг,
    |	Расчеты.ДолгУпр                   КАК ДолгУпр,
    |	Расчеты.ДолгРегл                  КАК ДолгРегл,
    |	Расчеты.Предоплата                КАК Предоплата,
    |	Расчеты.ПредоплатаУпр             КАК ПредоплатаУпр,
    |	Расчеты.ПредоплатаРегл            КАК ПредоплатаРегл,
    |	Расчеты.ЗалогЗаТару               КАК ЗалогЗаТару,
    |	Расчеты.ЗалогЗаТаруРегл           КАК ЗалогЗаТаруРегл,
    |
    |	Расчеты.ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
    |	Расчеты.УдалитьДатаПлатежа        КАК ДатаПлатежа,
    |	Расчеты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств
    |ИЗ
    |	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Расчеты
    |
    |	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаРегистра
    |	ПО Расчеты.АналитикаУчетаПоПартнерам = АналитикаРегистра.КлючАналитики
    |
    |	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК НоваяАналитика
    |	ПО АналитикаРегистра.Партнер = НоваяАналитика.Партнер
    |		И АналитикаРегистра.Организация = НоваяАналитика.Организация
    |		И АналитикаРегистра.Контрагент = НоваяАналитика.Контрагент
    |		И (НоваяАналитика.Договор =
    |				ВЫБОР КОГДА Расчеты.ЗаказКлиента ССЫЛКА Справочник.ДоговорыКонтрагентов
    |					ТОГДА Расчеты.ЗаказКлиента
    |					ИНАЧЕ ЕСТЬNULL(Расчеты.ЗаказКлиента.Договор, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
    |			КОНЕЦ)
    |ГДЕ
    |	Расчеты.Регистратор = &Расчеты
    |	И (ТИПЗНАЧЕНИЯ(Расчеты.РасчетныйДокумент) <> ТИП(Документ.ЗаказКлиента)
    |		ИЛИ Расчеты.РасчетныйДокумент = НЕОПРЕДЕЛЕНО)
    |");
	
	Выборка = Результат[КоличествоЗапросов].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра + ".НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Ссылка);
            
			Блокировка.Заблокировать();
		
			ЗапросДанных.УстановитьПараметр("Расчеты", Выборка.Ссылка);
			
			Набор = РегистрыНакопления.РасчетыСКлиентамиПоДокументам.СоздатьНаборЗаписей();
			Набор.Отбор.Регистратор.Установить(Выборка.Ссылка);
			Набор.Загрузить(ЗапросДанных.Выполнить().Выгрузить());
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
			
			ЗафиксироватьТранзакцию();
		
		Исключение
		
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru='Не удалось дополнить аналитику по партнерам договором при обработке документа: %Ссылка% по причине: %Причина%';uk='Не вдалося доповнити аналітику за партнерами договором при обробці документа: %Ссылка% по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
                ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
                УровеньЖурналаРегистрации.Предупреждение,
				Выборка.Ссылка.Метаданные(), 
                Выборка.Ссылка, 
                ТекстСообщения
            );
            
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
        Параметры.Очередь, 
        ПолноеИмяРегистра
    );
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#КонецЕсли