
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	УстановитьДоступностьКомандПечати(Элементы);
	УстановитьДоступностьКомандПорядка(Элементы);

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСтруктураЦелейСписок

&НаКлиенте
Процедура СтруктураЦелейСписокПриАктивизацииСтроки(Элемент)
	
	ОбновитьСодержимоеФормыПриаАктивизацииЦели();
	
	УстановитьДоступностьКомандПечати(Элементы);
	УстановитьДоступностьКомандПорядка(Элементы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВариантыАнализаСписок

&НаКлиенте
Процедура ВариантыАнализаСписокПриАктивизацииСтроки(Элемент)
	
	УстановитьДоступностьКомандПечати(Элементы);
	УстановитьДоступностьКомандПорядка(Элементы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область ПроцедурыПодсистемыНастройкаПорядкаЭлементов

&НаКлиенте
Процедура ПереместитьЭлементВверх(Команда)
	
	НастройкаПорядкаЭлементовКлиент.ПереместитьЭлементВверхВыполнить(ВариантыАнализаСписок, Элементы.ВариантыАнализаСписок);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьЭлементВниз(Команда)
	
	НастройкаПорядкаЭлементовКлиент.ПереместитьЭлементВнизВыполнить(ВариантыАнализаСписок, Элементы.ВариантыАнализаСписок);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьЭлементЦелиВверх(Команда)
	
	НастройкаПорядкаЭлементовКлиент.ПереместитьЭлементВверхВыполнить(СтруктураЦелейСписок, Элементы.СтруктураЦелейСписок);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьЭлементЦелиВниз(Команда)
	
	НастройкаПорядкаЭлементовКлиент.ПереместитьЭлементВнизВыполнить(СтруктураЦелейСписок, Элементы.СтруктураЦелейСписок);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВариантыАнализаСписок.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользоватьВариантыАнализа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.FormBackColor);
	Элемент.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);

КонецПроцедуры

&НаКлиенте 
Функция НайтиПолеОтбора(Элементы, ИмяОтбора)
	
	ИскомоеПоле = Неопределено;
	
	ПолеОтбора = Новый ПолеКомпоновкиДанных(ИмяОтбора);
	
	Для каждого ОтборПоле из Элементы Цикл
		
		Если ТипЗнч(ОтборПоле) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") тогда
			ИскомоеПоле = НайтиПолеОтбора(ОтборПоле.Элементы, ИмяОтбора);
		Иначе
			Если ОтборПоле.Использование и ОтборПоле.ЛевоеЗначение = ПолеОтбора тогда
				
				ИскомоеПоле = ОтборПоле;
				
				Прервать;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ИскомоеПоле;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьСодержимоеФормыПриаАктивизацииЦели()
	
	Если ТипЗнч(Элементы.СтруктураЦелейСписок.ТекущаяСтрока) = Тип("СправочникСсылка.СтруктураЦелей") Тогда
		ТекущаяЦель = Элементы.СтруктураЦелейСписок.ТекущаяСтрока;
		ИспользоватьВариантыАнализа = ЦельИзмеримая(ТекущаяЦель);
		
	Иначе
		ТекущаяЦель = Неопределено;
		ИспользоватьВариантыАнализа = Ложь;
		
	КонецЕсли;
	
	ИскомоеПолеОтбора = НайтиПолеОтбора(ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(ВариантыАнализаСписок).Элементы, "Владелец");
	
	Если ИскомоеПолеОтбора = Неопределено Тогда
		ОтборПоЦели = ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(ВариантыАнализаСписок).Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборПоЦели.Использование = Истина;
		ОтборПоЦели.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Владелец");
		ОтборПоЦели.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборПоЦели.ПравоеЗначение = ТекущаяЦель;
		ОтборПоЦели.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		
	Иначе
		ИскомоеПолеОтбора.Использование = Истина;
		ИскомоеПолеОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Владелец");
		ИскомоеПолеОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ИскомоеПолеОтбора.ПравоеЗначение = ТекущаяЦель;
		ИскомоеПолеОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		
	КонецЕсли;
	
	Элементы.ВариантыАнализаСписокСоздать.Доступность = ИспользоватьВариантыАнализа;
	Элементы.ВариантыАнализаСписокСоздатьКонтекстноеМеню.Доступность = ИспользоватьВариантыАнализа;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура УстановитьДоступностьКомандПечати(Элементы)
	
	Если Элементы.ВариантыАнализаСписок.ТекущаяСтрока = Неопределено Тогда
		Элементы.ВариантыАнализаСписокОтчетМониторЦелевыхПоказателейПечатнаяФорма.Доступность = Ложь;
		Элементы.ВариантыАнализаСписокОтчетМониторЦелевыхПоказателейПечатнаяФормаКонтекст.Доступность = Ложь;
	Иначе
		Элементы.ВариантыАнализаСписокОтчетМониторЦелевыхПоказателейПечатнаяФорма.Доступность = Истина;
		Элементы.ВариантыАнализаСписокОтчетМониторЦелевыхПоказателейПечатнаяФормаКонтекст.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура УстановитьДоступностьКомандПорядка(Элементы)
	
	Если Элементы.ВариантыАнализаСписок.ТекущаяСтрока = Неопределено Тогда
		Элементы.ВариантыАнализаСписокКоманднаяПанель.ПодчиненныеЭлементы.ГруппаИзменениеПорядка.ПодчиненныеЭлементы.ПереместитьЭлементВверх.Доступность = Ложь;
		Элементы.ВариантыАнализаСписокКоманднаяПанель.ПодчиненныеЭлементы.ГруппаИзменениеПорядка.ПодчиненныеЭлементы.ПереместитьЭлементВниз.Доступность = Ложь;
		Элементы.ВариантыАнализаСписокКонтекстноеМеню.ПодчиненныеЭлементы.ГруппаИзменениеПорядкаКонтекстноеМеню.ПодчиненныеЭлементы.ПереместитьЭлементВверхКонтекстноеМеню.Доступность = Ложь;
		Элементы.ВариантыАнализаСписокКонтекстноеМеню.ПодчиненныеЭлементы.ГруппаИзменениеПорядкаКонтекстноеМеню.ПодчиненныеЭлементы.ПереместитьЭлементВнизКонтекстноеМеню.Доступность = Ложь;
		
	Иначе
		Элементы.ВариантыАнализаСписокКоманднаяПанель.ПодчиненныеЭлементы.ГруппаИзменениеПорядка.ПодчиненныеЭлементы.ПереместитьЭлементВверх.Доступность = Истина;
		Элементы.ВариантыАнализаСписокКоманднаяПанель.ПодчиненныеЭлементы.ГруппаИзменениеПорядка.ПодчиненныеЭлементы.ПереместитьЭлементВниз.Доступность = Истина;
		Элементы.ВариантыАнализаСписокКонтекстноеМеню.ПодчиненныеЭлементы.ГруппаИзменениеПорядкаКонтекстноеМеню.ПодчиненныеЭлементы.ПереместитьЭлементВверхКонтекстноеМеню.Доступность = Истина;
		Элементы.ВариантыАнализаСписокКонтекстноеМеню.ПодчиненныеЭлементы.ГруппаИзменениеПорядкаКонтекстноеМеню.ПодчиненныеЭлементы.ПереместитьЭлементВнизКонтекстноеМеню.Доступность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере 
Функция ЦельИзмеримая(Цель)
	
	ЦельИзмеримая = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Цель", Цель);
	Запрос.Текст = "ВЫБРАТЬ
	|	СтруктураЦелей.ЦельИзмеримая
	|ИЗ
	|	Справочник.СтруктураЦелей КАК СтруктураЦелей
	|ГДЕ
	|	СтруктураЦелей.Ссылка = &Цель";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		ЦельИзмеримая = Выборка.ЦельИзмеримая;
		
	КонецЕсли;
	
	Возврат ЦельИзмеримая;
	
КонецФункции

#КонецОбласти
