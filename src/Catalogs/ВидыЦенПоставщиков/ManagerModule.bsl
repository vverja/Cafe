#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получает реквизиты объекта, которые необходимо блокировать от изменения
//
// Возвращаемое значение:
//	Массив - блокируемые реквизиты объекта
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт

	Результат = Новый Массив;
	Результат.Добавить("Владелец");
	Результат.Добавить("Валюта");
	Результат.Добавить("ЦенаВключаетНДС");
	Результат.Добавить("ДоступноДляЗакупки");
	Результат.Добавить("ДоступноДляПродажиКлиентам");

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы


// Обработчик обновления BAS УТ 3.2.1
// Заполняет поля Валюта и ЦенаВключаетНДС в справочнике ВидыЦенПоставщиков
Процедура ЗаполнитьДанныеВидовЦенПоставщиковИзУсловийЦенПоставщиков() Экспорт
	
	НачатьТранзакцию();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыЦенПоставщиков.Ссылка                                                                    КАК ВидЦеныПоставщика,
	|	ВЫРАЗИТЬ(ВидыЦенПоставщиков.Владелец КАК Справочник.СоглашенияСПоставщиками).Партнер         КАК Партнер,
	|	ВЫРАЗИТЬ(ВидыЦенПоставщиков.Владелец КАК Справочник.СоглашенияСПоставщиками).Валюта          КАК Валюта,
	|	ВЫРАЗИТЬ(ВидыЦенПоставщиков.Владелец КАК Справочник.СоглашенияСПоставщиками).ЦенаВключаетНДС КАК ЦенаВключаетНДС
	|ИЗ
	|	Справочник.ВидыЦенПоставщиков КАК ВидыЦенПоставщиков
	|ГДЕ
	|	ВидыЦенПоставщиков.Владелец ССЫЛКА Справочник.СоглашенияСПоставщиками";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Объект = Выборка.ВидЦеныПоставщика.ПолучитьОбъект();
		Объект.Валюта          = Выборка.Валюта;
		Объект.ЦенаВключаетНДС = Выборка.ЦенаВключаетНДС;
		Объект.Наименование    = Объект.Наименование + " (" + Объект.Владелец + ")";
		Объект.Владелец        = Выборка.Партнер;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Объект);
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#КонецЕсли