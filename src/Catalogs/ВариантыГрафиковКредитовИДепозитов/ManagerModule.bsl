#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ЕстьГрафик(СсылкаОбъекта,ТипГрафика = "Оплаты") Экспорт
	
	ВариантГрафика = ТекущийВариантГрафика(СсылкаОбъекта);
	Если ВариантГрафика.Пустая() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Отбор = Новый Структура("ВариантГрафика", ВариантГрафика);
	Если ТипГрафика = "Транши" Тогда
		ЗаписиРегистра = РегистрыСведений.ГрафикТраншейКредитовИДепозитов.СрезПервых(,Отбор);
	ИначеЕсли ТипГрафика = "Оплаты" Тогда
		ЗаписиРегистра = РегистрыСведений.ГрафикОплатКредитовИДепозитов.СрезПоследних(,Отбор);
	ИначеЕсли ТипГрафика = "Начисления" Тогда
		ЗаписиРегистра = РегистрыСведений.ГрафикНачисленийКредитовИДепозитов.СрезПоследних(,Отбор);
	КонецЕсли;
	
	Возврат ЗаписиРегистра.Количество() > 0;
	
КонецФункции

Функция ИтогиГрафика(СсылкаОбъекта) Экспорт
	
	ИтогиГрафика = Новый Структура("ДатаПервогоТранша,ДатаПоследнегоПлатежа,СуммаТраншей,СуммаОплаты,СуммаПроцентов,СуммаКомиссии,НачисленияПроцентов,НачисленияКомиссии");
	
	ВариантГрафика = ТекущийВариантГрафика(СсылкаОбъекта);
	Если ВариантГрафика.Пустая() Тогда
		Возврат ИтогиГрафика;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ГрафикТраншейСрезПервых.ВариантГрафика,
	|	ГрафикТраншейСрезПервых.Период КАК ДатаПервогоТранша
	|ИЗ
	|	РегистрСведений.ГрафикТраншейКредитовИДепозитов.СрезПервых КАК ГрафикТраншейСрезПервых
	|ГДЕ
	|	ГрафикТраншейСрезПервых.ВариантГрафика = &ВариантГрафика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплатСрезПоследних.ВариантГрафика,
	|	ГрафикОплатСрезПоследних.Период КАК ДатаПоследнегоПлатежа
	|ИЗ
	|	РегистрСведений.ГрафикОплатКредитовИДепозитов.СрезПоследних КАК ГрафикОплатСрезПоследних
	|ГДЕ
	|	ГрафикОплатСрезПоследних.ВариантГрафика = &ВариантГрафика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикТраншейКредитовИДепозитов.ВариантГрафика,
	|	СУММА(ГрафикТраншейКредитовИДепозитов.Сумма) КАК СуммаТраншей
	|ИЗ
	|	РегистрСведений.ГрафикТраншейКредитовИДепозитов КАК ГрафикТраншейКредитовИДепозитов
	|ГДЕ
	|	ГрафикТраншейКредитовИДепозитов.ВариантГрафика = &ВариантГрафика
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрафикТраншейКредитовИДепозитов.ВариантГрафика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикОплатКредитовИДепозитов.ВариантГрафика,
	|	СУММА(ГрафикОплатКредитовИДепозитов.Сумма) КАК СуммаОплаты,
	|	СУММА(ГрафикОплатКредитовИДепозитов.Проценты) КАК СуммаПроцентов,
	|	СУММА(ГрафикОплатКредитовИДепозитов.Комиссия) КАК СуммаКомиссии
	|ИЗ
	|	РегистрСведений.ГрафикОплатКредитовИДепозитов КАК ГрафикОплатКредитовИДепозитов
	|ГДЕ
	|	ГрафикОплатКредитовИДепозитов.ВариантГрафика = &ВариантГрафика
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрафикОплатКредитовИДепозитов.ВариантГрафика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикНачисленийКредитовИДепозитов.ВариантГрафика,
	|	СУММА(ГрафикНачисленийКредитовИДепозитов.Проценты) КАК НачисленияПроцентов,
	|	СУММА(ГрафикНачисленийКредитовИДепозитов.Комиссия) КАК НачисленияКомиссии
	|ИЗ
	|	РегистрСведений.ГрафикНачисленийКредитовИДепозитов КАК ГрафикНачисленийКредитовИДепозитов
	|ГДЕ
	|	ГрафикНачисленийКредитовИДепозитов.ВариантГрафика = &ВариантГрафика
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрафикНачисленийКредитовИДепозитов.ВариантГрафика";
	
	Запрос.УстановитьПараметр("ВариантГрафика", ВариантГрафика);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Для Каждого Результат Из МассивРезультатов Цикл
		
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ИтогиГрафика,Выборка);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ИтогиГрафика;
	
КонецФункции

Процедура ОбновитьДанныеГрафиков(Объект) Экспорт
	
	ЗаполнитьЗначенияСвойств(Объект,ИтогиГрафика(Объект.Ссылка));
	ПересчитатьСроки(Объект);
	
КонецПроцедуры

Процедура ПересчитатьСроки(Объект, ИмяРеквизита = "ДатаПоследнегоПлатежа") Экспорт
	
	Если (ИмяРеквизита = "ДатаПервогоТранша" ИЛИ ИмяРеквизита = "ДатаПоследнегоПлатежа")
		И ЗначениеЗаполнено(Объект.ДатаПервогоТранша) 
		И ЗначениеЗаполнено(Объект.ДатаПоследнегоПлатежа) Тогда
		
		Объект.СрокМес = ОбщегоНазначенияУТ.РазностьДат(Объект.ДатаПервогоТранша,Объект.ДатаПоследнегоПлатежа, Перечисления.Периодичность.Месяц);
		Объект.СрокДней = ОбщегоНазначенияУТ.РазностьДат(Объект.ДатаПервогоТранша,Объект.ДатаПоследнегоПлатежа, Перечисления.Периодичность.День);
		
	ИначеЕсли ИмяРеквизита = "СрокМес" И ЗначениеЗаполнено(Объект.ДатаПервогоТранша) Тогда
		
		Объект.ДатаПоследнегоПлатежа = ДобавитьМесяц(Объект.ДатаПервогоТранша,Объект.СрокМес);
		Объект.СрокДней = ОбщегоНазначенияУТ.РазностьДат(Объект.ДатаПервогоТранша,Объект.ДатаПоследнегоПлатежа, Перечисления.Периодичность.День);
		
	ИначеЕсли ИмяРеквизита = "СрокДней"  И ЗначениеЗаполнено(Объект.ДатаПервогоТранша) Тогда
		
		Объект.ДатаПоследнегоПлатежа = Объект.ДатаПервогоТранша + Объект.СрокДней * 60*60*24;
		Объект.СрокМес = ОбщегоНазначенияУТ.РазностьДат(Объект.ДатаПервогоТранша,Объект.ДатаПоследнегоПлатежа, Перечисления.Периодичность.Месяц);
		
	ИначеЕсли ИмяРеквизита = "СрокМес" И ЗначениеЗаполнено(Объект.ДатаПоследнегоПлатежа) Тогда
		
		Объект.ДатаПервогоТранша = ДобавитьМесяц(Объект.ДатаПоследнегоПлатежа, -Объект.СрокМес);
		Объект.СрокДней = ОбщегоНазначенияУТ.РазностьДат(Объект.ДатаПервогоТранша,Объект.ДатаПоследнегоПлатежа, Перечисления.Периодичность.День);
		
	ИначеЕсли ИмяРеквизита = "СрокДней"  И ЗначениеЗаполнено(Объект.ДатаПоследнегоПлатежа) Тогда
		
		Объект.ДатаПервогоТранша = Объект.ДатаПоследнегоПлатежа - Объект.СрокДней * 60*60*24;
		Объект.СрокМес = ОбщегоНазначенияУТ.РазностьДат(Объект.ДатаПервогоТранша,Объект.ДатаПоследнегоПлатежа, Перечисления.Периодичность.Месяц);
		
	Иначе
		
		Объект.СрокМес = 0;
		Объект.СрокДней = 0;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ТекущийВариантГрафика(Договор) Экспорт
	
	Результат = ПустаяСсылка();
	
	Если НЕ ЗначениеЗаполнено(Договор) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ТипЗнч(Договор) = Тип("СправочникСсылка.ВариантыГрафиковКредитовИДепозитов") Тогда
		 Возврат Договор;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВариантыГрафиковКредитовИДепозитов.Ссылка
	|ИЗ
	|	Справочник.ВариантыГрафиковКредитовИДепозитов КАК ВариантыГрафиковКредитовИДепозитов
	|ГДЕ
	|	ВариантыГрафиковКредитовИДепозитов.Владелец = &Владелец
	|	И НЕ ВариантыГрафиковКредитовИДепозитов.ПометкаУдаления
	|	И ВариантыГрафиковКредитовИДепозитов.Используется";
	
	Запрос.УстановитьПараметр("Владелец",Договор);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Если ПравоДоступа("Чтение", Метаданные.Справочники.ДоговорыКредитовИДепозитов) Тогда
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор  = "ГрафикОплатНачисленийКредитовИДепозитов";
		КомандаПечати.Представление  = НСтр("ru='Печать';uk='Друк'");
		КомандаПечати.Обработчик     = "УправлениеПечатьюУТКлиент.ПечатьВариантаГрафикаОплатыДоговоровКредитовИДепозитов";
		КомандаПечати.МенеджерПечати = "";
		КомандаПечати.Порядок        = 1;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Отчеты

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуПланФактныйАнализДоговора(КомандыОтчетов);

	КомандаОтчет = ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСравнениеПроизвольныхГрафиков(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		КомандаОтчет.СписокФорм = "ФормаЭлемента,ФормаСписка";
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

