#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрПериодОтчета = НастройкаПараметра("ПериодОтчета");
	Если ПараметрПериодОтчета <> Неопределено Тогда
		Если НЕ ПараметрПериодОтчета.Использование Тогда
			Параметр = НастройкаПараметра("НачалоПериода");
			Параметр.Значение = Дата("00010101");
			Параметр.Использование = Истина;
			Параметр = НастройкаПараметра("КонецПериода");
			Параметр.Значение = Дата("29991231");
			Параметр.Использование = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ВалютаРеглУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	ВалютаУпрУчета = Константы.ВалютаУправленческогоУчета.Получить();
	
	Параметр = НастройкаПараметра("ВалютаРегл");
	Если Параметр <> Неопределено Тогда
		Параметр.Значение = ВалютаРеглУчета;
		Параметр.Использование = Истина;
	КонецЕсли;
	
	Параметр = НастройкаПараметра("ВалютаУпр");
	Если Параметр <> Неопределено Тогда
		Параметр.Значение = ВалютаУпрУчета;
		Параметр.Использование = Истина;
	КонецЕсли;
	
	ИспользоватьУчетПрочихДоходовРасходов = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов");
	
	Параметр = НастройкаПараметра("ИспользоватьУчетПрочихДоходовРасходов");
	Если Параметр <> Неопределено Тогда
		Параметр.Значение = ИспользоватьУчетПрочихДоходовРасходов;
		Параметр.Использование = Истина;
	КонецЕсли;
	
	ВалютыСовпадают = ВалютаРеглУчета = ВалютаУпрУчета;
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	Для Каждого  ЭлементВыбора Из НастройкиОтчета.Выбор.Элементы Цикл

		Если Не ТипЗнч(ЭлементВыбора) = Тип("ВыбранноеПолеКомпоновкиДанных") Тогда
			Если СтрНайти(ЭлементВыбора.Заголовок,"упр") > 0 Тогда
				ЭлементВыбора.Заголовок = НСтр("ru='Валюта упр.';uk='Валюта упр.'") + " (" + ВалютаУпрУчета + ")";
				ЭлементВыбора.Использование = НЕ ВалютыСовпадают;
			ИначеЕсли СтрНайти(ЭлементВыбора.Заголовок,"регл") > 0 Тогда
				ЭлементВыбора.Заголовок = НСтр("ru='Валюта регл.';uk='Валюта регл.'") + " (" + ВалютаРеглУчета + ")";
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);

	// Создадим и инициализируем процессор компоновки
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);

	// Создадим и инициализируем процессор вывода результата
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);

	// Обозначим начало вывода
	ПроцессорВывода.НачатьВывод();
	ТаблицаЗафиксирована = Ложь;

	ДокументРезультат.ФиксацияСверху = 0;
	// Основной цикл вывода отчета
	Пока Истина Цикл
		// Получим следующий элемент результата компоновки
		ЭлементРезультата = ПроцессорКомпоновки.Следующий();

		Если ЭлементРезультата = Неопределено Тогда
			// Следующий элемент не получен - заканчиваем цикл вывода
			Прервать;
		Иначе
			// Зафиксируем шапку
			Если  Не ТаблицаЗафиксирована 
				  И ЭлементРезультата.ЗначенияПараметров.Количество() > 0 
				  И ТипЗнч(КомпоновщикНастроек.Настройки.Структура[0]) <> Тип("ДиаграммаКомпоновкиДанных") Тогда

				ТаблицаЗафиксирована = Истина;
				ДокументРезультат.ФиксацияСверху = ДокументРезультат.ВысотаТаблицы;

			КонецЕсли;
			// Элемент получен - выведем его при помощи процессора вывода
			ПроцессорВывода.ВывестиЭлемент(ЭлементРезультата);
		КонецЕсли;
	КонецЦикла;

	ПроцессорВывода.ЗакончитьВывод();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НастройкаПараметра(ИмяПараметра)

	ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	Если ПараметрДанных <> Неопределено Тогда
		ПараметрПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
		Если ПараметрПользовательскойНастройки <> Неопределено Тогда
			Возврат ПараметрПользовательскойНастройки;
		Иначе
			Возврат ПараметрДанных;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

#КонецОбласти

#КонецЕсли