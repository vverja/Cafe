#Область ПрограммныйИнтерфейс

// Возвращает стандартные настройки формирования монитора целевых покзателей
//
// Возвращаемое значение:
//	Структура - стандартные настройки:
//		* ВариантГруппировкиПоказателей - группировка показателей по категориям целей
//		* КоличествоКолонокМонитораПоказателей - вывод в 3 колонки
//		* РазмерДиаграмм - крупный (2), высота примерно равна ширине
//		* ВариантОтображенияДеталей - только текущее значение
//		* ПериодАвтообновления - автообновление каждые 15 минут
//
Функция СтандартныеНастройки() Экспорт
	
	НастройкиПоУмолчанию = Новый Структура;
	
	// порядок вывода показателей
	НастройкиПоУмолчанию.Вставить("ВариантГруппировкиПоказателей", "ПоКатегориямЦелей");
	НастройкиПоУмолчанию.Вставить("КоличествоКолонокМонитораПоказателей", 3);
	
	// Внешний вид показателей
	НастройкиПоУмолчанию.Вставить("РазмерДиаграмм", 1);
	НастройкиПоУмолчанию.Вставить("ПроизвольныйМасштаб", 100);
	НастройкиПоУмолчанию.Вставить("ВариантОтображенияДеталей", "ПоказыватьТолькоТекущееЗначение");
	
	// Обновление данных
	НастройкиПоУмолчанию.Вставить("ПериодАвтообновления", 15);
	
	Возврат НастройкиПоУмолчанию;
	
КонецФункции

// Возвращает названия перечисления Периодичность в разных склонениях
//
// Параметры
//	ЧислоПериодов - Число - количество периодов, определяет склонение
//	Периодичность - ссылка на значение перечисления "Периодичность"
//
// Возвращаемое значение:
//  ПериодПрописью - Строка - название периода в правильном склонении
//
Функция ПериодПрописью(ЧислоПериодов, Периодичность) Экспорт
	ПериодПрописью = "";
	ФорматнаяСтрока = НСтр("ru='L = ru_RU';uk='L = uk_UA'");
	ПараметрыПредметаИсчиления = "";
	
	Если Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.День") Тогда
 		ПараметрыПредметаИсчиления = НСтр("ru='день, дня, дней, м';uk='день, дня, днів, м'");
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчиления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='день';uk='день'")) Тогда
			ПериодПрописью = НСтр("ru='день';uk='день'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='дня';uk='дня'")) Тогда
			ПериодПрописью = НСтр("ru='дня';uk='дня'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='дней';uk='днів'")) Тогда
			ПериодПрописью = НСтр("ru='дней';uk='днів'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Неделя") Тогда
		ПараметрыПредметаИсчиления = НСтр("ru='неделя, недели, недель, ж';uk='тиждень, тижня, тижнів, м'");		
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчиления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='неделя';uk='тиждень'")) Тогда
			ПериодПрописью = НСтр("ru='неделя';uk='тиждень'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='недели';uk='тижня'")) Тогда
			ПериодПрописью = НСтр("ru='недели';uk='тижня'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='недель';uk='тижнів'")) Тогда
			ПериодПрописью = НСтр("ru='недель';uk='тижнів'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Декада") Тогда
		ПараметрыПредметаИсчиления = НСтр("ru='декада, декады, декад, ж';uk='декада, декади, декад, ж'");		
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчиления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='декада';uk='декада'")) Тогда
			ПериодПрописью = НСтр("ru='декада';uk='декада'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='декады';uk='декади'")) Тогда
			ПериодПрописью = НСтр("ru='декады';uk='декади'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='декад';uk='декад'")) Тогда
			ПериодПрописью = НСтр("ru='декад';uk='декад'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц") Тогда
		ПараметрыПредметаИсчиления = НСтр("ru='месяц, месяца, месяцев, м';uk='місяць, місяця, місяців, м'");		
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчиления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='месяцев';uk='місяців'")) Тогда
			ПериодПрописью = НСтр("ru='месяцев';uk='місяців'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='месяца';uk='місяця'")) Тогда
			ПериодПрописью = НСтр("ru='месяца';uk='місяця'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='месяц';uk='місяць'")) Тогда
			ПериодПрописью = НСтр("ru='месяц';uk='місяць'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал") Тогда
		ПараметрыПредметаИсчиления = НСтр("ru='квартал, квартала, кварталов, м';uk='квартал, кварталу, кварталів, м'");		
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчиления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='кварталов';uk='кварталів'")) Тогда
			ПериодПрописью = НСтр("ru='кварталов';uk='кварталів'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='квартала';uk='кварталу'")) Тогда
			ПериодПрописью = НСтр("ru='квартала';uk='кварталу'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='квартал';uk='квартал'")) Тогда
			ПериодПрописью = НСтр("ru='квартал';uk='квартал'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Полугодие") Тогда
		ПараметрыПредметаИсчиления = НСтр("ru='полугодие, полугодия, полугодий, с';uk='півріччя, півріччя, півріч, з'");		
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчиления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='полугодие';uk='півріччя'")) Тогда
			ПериодПрописью = НСтр("ru='полугодие';uk='півріччя'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='полугодия';uk='півріччя'")) Тогда
			ПериодПрописью = НСтр("ru='полугодия';uk='півріччя'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='полугодий';uk='піврічь'")) Тогда
			ПериодПрописью = НСтр("ru='полугодий';uk='піврічь'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
		ПараметрыПредметаИсчиления = НСтр("ru='год, года, лет, м';uk='рік, року, років, м'");		
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчиления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='год';uk='рік'")) Тогда
			ПериодПрописью = НСтр("ru='год';uk='рік'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='года';uk='року'")) Тогда
			ПериодПрописью = НСтр("ru='года';uk='року'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='лет';uk='років'")) Тогда
			ПериодПрописью = НСтр("ru='лет';uk='років'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПериодПрописью;
КонецФункции

// Возвращает структуру настроек для формирования состава монитора
//
// Возвращаемое значение:
//	Структура
//
Функция ИнициализацияПараметровСоставаМонитора() Экспорт
	Возврат Новый Структура("РежимАнализаВзаимосвязанных,
		|ДемонстрационныйРежим,
		|ОтборВариантовАнализа, 
		|ПринудительноОбновитьДанные,
		|СтруктураНастроек, 
		|ОтборЗонаВнимания, 
		|ОтборСтатус, 
		|ПериодичностьКонтроля, 
		|УчитыватьВариантыОтображения,
		// Параметры анализа взаимосвязанных
		|ВидПоиска, 
		|СвойстваВариантаАнализа,
		|АдресаДинамическихПараметров");
КонецФункции

#КонецОбласти
