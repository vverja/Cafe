
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не ФормироватьРезервОтпусковБУ Тогда
		МассивНепроверяемыхРеквизитов.Добавить("МетодНачисленияРезерваОтпусков");
	КонецЕсли;
	
		МассивНепроверяемыхРеквизитов.Добавить("НормативОтчисленийВРезервОтпусков");
		МассивНепроверяемыхРеквизитов.Добавить("ПредельнаяВеличинаОтчисленийВРезервОтпусков");
		МассивНепроверяемыхРеквизитов.Добавить("ИспользуемыеКлассыСчетовРасходов");

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	// Запись настроек в регистр УчетнаяПолитикаОрганизаций
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УчетнаяПолитикаОрганизаций.Организация КАК Организация,
	|	УчетнаяПолитикаОрганизаций.Организация.ЮрФизЛицо КАК ЮрФизЛицо,
	|	УчетнаяПолитикаОрганизаций.ИННПлательщикаНДС КАК ИННПлательщикаНДС,
	|	УчетнаяПолитикаОрганизаций.НомерСвидетельстваПлательщикаНДС КАК НомерСвидетельстваПлательщикаНДС,
	|	УчетнаяПолитикаОрганизаций.Период КАК Период
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних КАК УчетнаяПолитикаОрганизаций
	|ГДЕ
	|	УчетнаяПолитикаОрганизаций.УчетнаяПолитика = &УчетнаяПолитика";
	
	Запрос.УстановитьПараметр("УчетнаяПолитика", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
        
        
		Набор = РегистрыСведений.УчетнаяПолитикаОрганизаций.СоздатьНаборЗаписей();
		Набор.Отбор.Организация.Установить(Выборка.Организация);
		Набор.Отбор.Период.Установить(Выборка.Период);
		
		Набор.Прочитать();
		
		Набор.Записать(Истина);
	КонецЦикла;
    
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

