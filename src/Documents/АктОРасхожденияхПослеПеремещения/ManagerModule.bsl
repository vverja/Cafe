#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов


//Имена реквизитов, от значений которых зависят параметры указания серий
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	
	ИменаРеквизитов = "СкладОтправитель,СкладПолучатель,Дата";
	
	Возврат ИменаРеквизитов;
	
КонецФункции

//Возвращает параметры указания серий для товаров, указанных в документе
//
//	Параметры
//			Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий
//	Возвращаемое значение
//			Тип Структура
//				Состав полей задается в функции ОбработкаТабличнойЧастиКлиентСервер.ПараметрыУказанияСерий
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.АктОРасхожденияхПослеПеремещения";
	
	ПараметрыСерийСкладаОтправитель = СкладыСервер.ИспользованиеСерийНаСкладе(Объект.СкладОтправитель, Ложь);
	ПараметрыСерийСкладаПолучатель  = СкладыСервер.ИспользованиеСерийНаСкладе(Объект.СкладПолучатель, Ложь);
	
	ИспользоватьОрдернуюСхемуПолучатель  = СкладыСервер.ИспользоватьОрдернуюСхемуПриПоступлении(Объект.СкладПолучатель, Объект.Дата, Ложь);
	ИспользоватьОрдернуюСхемуОтправитель = СкладыСервер.ИспользоватьОрдернуюСхемуПриОтгрузке(Объект.СкладОтправитель, Объект.Дата);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры = ПараметрыСерийСкладаОтправитель.ИспользоватьСерииНоменклатуры
														Или (ПараметрыСерийСкладаПолучатель.ИспользоватьСерииНоменклатуры
														И Не ИспользоватьОрдернуюСхемуПолучатель)
														Или ПараметрыСерийСкладаПолучатель.УчитыватьСебестоимостьПоСериям;
	
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ПараметрыСерийСкладаОтправитель.УчитыватьСебестоимостьПоСериям
														Или ПараметрыСерийСкладаПолучатель.УчитыватьСебестоимостьПоСериям;
														
	ПараметрыУказанияСерий.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ОтгрузкаПоПеремещению);
	ПараметрыУказанияСерий.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ПриемкаПоПеремещению);
	
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Назначение");
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("НазначениеОтправителя");
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("ЗаполненоПоОснованию");
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("ДокументОснование");
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Действие");
	
	ПараметрыУказанияСерий.ЭтоНакладная = Истина;
	
	ПараметрыУказанияСерий.ИмяПоляСклад = "СкладОтправитель";
	
	ПараметрыУказанияСерий.ИмяПоляСкладОтправитель = "СкладОтправитель";
	ПараметрыУказанияСерий.ИмяПоляСкладПолучатель  = "СкладПолучатель";
	
	ПараметрыУказанияСерий.ПланированиеОтгрузки = Ложь;
	ПараметрыУказанияСерий.ПланированиеОтбора   = Ложь;
	ПараметрыУказанияСерий.ФактОтбора           = Истина;
	
	ПараметрыУказанияСерий.РегистрироватьСерии = Истина;
	ПараметрыУказанияСерий.Дата = Объект.Дата;
	
	ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Добавить("СтатусУказанияСерий");
	ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Добавить("СтатусУказанияСерийОтправитель");
	ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Добавить("СтатусУказанияСерийПолучатель");
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания серий
//	Параметры:
//		ПараметрыУказанияСерий - Структура - состав полей задается в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий
//	Возвращаемое значение:
//		Строка - текст запроса
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	Товары.НазначениеОтправителя,
	|	Товары.Серия,
	|	Товары.Количество,
	|	Товары.СтатусУказанияСерий,
	|	Товары.СтатусУказанияСерийОтправитель,
	|	Товары.СтатусУказанияСерийПолучатель,
	|	Товары.НомерСтроки,
	|	Товары.Действие,
	|	Товары.ДокументОснование,
	|	Товары.ЗаполненоПоОснованию
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	Товары.НазначениеОтправителя,
	|	СУММА(Товары.Количество) КАК Количество,
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры КАК ВидНоменклатуры,
	|	Товары.Действие,
	|	Товары.ДокументОснование,
	|	Товары.ЗаполненоПоОснованию
	|ПОМЕСТИТЬ ТоварыДляЗапроса
	|ИЗ
	|	Товары КАК Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	Товары.НазначениеОтправителя,
	|	Товары.Серия,
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры,
	|	Товары.Действие,
	|	Товары.ДокументОснование,
	|	Товары.ЗаполненоПоОснованию
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура,
	|	Серии.Характеристика,
	|	Серии.Назначение,
	|	Серии.НазначениеОтправителя,
	|	Серии.Количество,
	|	Серии.Действие,
	|	Серии.ДокументОснование,
	|	Серии.ЗаполненоПоОснованию
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	&Серии КАК Серии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура,
	|	Серии.Характеристика,
	|	Серии.Назначение,
	|	Серии.НазначениеОтправителя,
	|	СУММА(Серии.Количество) КАК Количество,
	|	Серии.Действие,
	|	Серии.ДокументОснование,
	|	Серии.ЗаполненоПоОснованию
	|ПОМЕСТИТЬ СерииДляЗапроса
	|ИЗ
	|	Серии КАК Серии
	|
	|СГРУППИРОВАТЬ ПО
	|	Серии.Номенклатура,
	|	Серии.Характеристика,
	|	Серии.Назначение,
	|	Серии.НазначениеОтправителя,
	|	Серии.Действие,
	|	Серии.ДокументОснование,
	|	Серии.ЗаполненоПоОснованию
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерийОтправитель.ПолитикаУчетаСерий ЕСТЬ NULL 
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерийОтправитель.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|			ТОГДА ВЫБОР
	|					КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА 14
	|					ИНАЧЕ 13
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерийОтправитель.ПолитикаУчетаСерий.УказыватьПриПланированииОтгрузки
	|			ТОГДА ВЫБОР
	|					КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА 10
	|					ИНАЧЕ 9
	|				КОНЕЦ
	|		КОГДА СкладОтправитель.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				И &Дата >= СкладОтправитель.ДатаНачалаОрдернойСхемыПриОтгрузке
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерийОтправитель.ПолитикаУчетаСерий.УказыватьПриПланированииОтбора
	|			ТОГДА ВЫБОР
	|					КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|								И ТоварыДляЗапроса.Количество > 0
	|							ИЛИ Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА ВЫБОР
	|								КОГДА ПолитикиУчетаСерийОтправитель.ПолитикаУчетаСерий.УчетСерийПоFEFO
	|									ТОГДА 6
	|								ИНАЧЕ 8
	|							КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ПолитикиУчетаСерийОтправитель.ПолитикаУчетаСерий.УчетСерийПоFEFO
	|								ТОГДА 5
	|							ИНАЧЕ 7
	|						КОНЕЦ
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерийОтправитель.ПолитикаУчетаСерий.УказыватьПоФактуОтбора
	|				И ПолитикиУчетаСерийОтправитель.ПолитикаУчетаСерий.УказыватьПриОтгрузкеПоПеремещению
	|				И &ФактОтбора
	|			ТОГДА ВЫБОР
	|					КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|								И ТоварыДляЗапроса.Количество > 0
	|							ИЛИ Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА ВЫБОР
	|								КОГДА ПолитикиУчетаСерийОтправитель.ПолитикаУчетаСерий.УчитыватьОстаткиСерий
	|									ТОГДА 4
	|								ИНАЧЕ 2
	|							КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ПолитикиУчетаСерийОтправитель.ПолитикаУчетаСерий.УчитыватьОстаткиСерий
	|								ТОГДА 3
	|							ИНАЧЕ 1
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтатусУказанияСерийОтправитель,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерийПолучатель.ПолитикаУчетаСерий ЕСТЬ NULL 
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерийПолучатель.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|			ТОГДА ВЫБОР
	|					КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА 14
	|					ИНАЧЕ 13
	|				КОНЕЦ
	|		КОГДА СкладПолучатель.ИспользоватьОрдернуюСхемуПриПоступлении
	|				И &Дата >= СкладПолучатель.ДатаНачалаОрдернойСхемыПриПоступлении
	|				И &ПриемкаПоПеремещению
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерийПолучатель.ПолитикаУчетаСерий.УказыватьПриПланированииОтгрузки
	|			ТОГДА ВЫБОР
	|					КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА 10
	|					ИНАЧЕ 9
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерийПолучатель.ПолитикаУчетаСерий.УказыватьПриПланированииОтбора
	|			ТОГДА ВЫБОР
	|					КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|								И ТоварыДляЗапроса.Количество > 0
	|							ИЛИ Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА 8
	|					ИНАЧЕ 7
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерийПолучатель.ПолитикаУчетаСерий.УказыватьПриПриемке
	|				И ПолитикиУчетаСерийПолучатель.ПолитикаУчетаСерий.УказыватьПриПриемкеПоПеремещению
	|			ТОГДА ВЫБОР
	|					КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|								И ТоварыДляЗапроса.Количество > 0
	|							ИЛИ Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА ВЫБОР
	|								КОГДА ПолитикиУчетаСерийПолучатель.ПолитикаУчетаСерий.УчитыватьОстаткиСерий
	|									ТОГДА 4
	|								ИНАЧЕ 2
	|							КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ПолитикиУчетаСерийПолучатель.ПолитикаУчетаСерий.УчитыватьОстаткиСерий
	|								ТОГДА 3
	|							ИНАЧЕ 1
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтатусУказанияСерийПолучатель,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерийСтарый,
	|	Товары.СтатусУказанияСерийОтправитель КАК СтатусУказанияСерийОтправительСтарый,
	|	Товары.СтатусУказанияСерийПолучатель КАК СтатусУказанияСерийПолучательСтарый
	|ПОМЕСТИТЬ Статусы
	|ИЗ
	|	ТоварыДляЗапроса КАК ТоварыДляЗапроса
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Товары КАК Товары
	|		ПО ТоварыДляЗапроса.Номенклатура = Товары.Номенклатура
	|			И ТоварыДляЗапроса.Характеристика = Товары.Характеристика
	|			И ТоварыДляЗапроса.Назначение = Товары.Назначение
	|			И ТоварыДляЗапроса.НазначениеОтправителя = Товары.НазначениеОтправителя
	|			И ТоварыДляЗапроса.Действие = Товары.Действие
	|			И ТоварыДляЗапроса.ДокументОснование = Товары.ДокументОснование
	|			И ТоварыДляЗапроса.ЗаполненоПоОснованию = Товары.ЗаполненоПоОснованию
	|		ЛЕВОЕ СОЕДИНЕНИЕ СерииДляЗапроса КАК СерииДляЗапроса
	|		ПО ТоварыДляЗапроса.Номенклатура = СерииДляЗапроса.Номенклатура
	|			И ТоварыДляЗапроса.Характеристика = СерииДляЗапроса.Характеристика
	|			И ТоварыДляЗапроса.Назначение = СерииДляЗапроса.Назначение
	|			И ТоварыДляЗапроса.НазначениеОтправителя = СерииДляЗапроса.НазначениеОтправителя
	|			И ТоварыДляЗапроса.Действие = СерииДляЗапроса.Действие
	|			И ТоварыДляЗапроса.ДокументОснование = СерииДляЗапроса.ДокументОснование
	|			И ТоварыДляЗапроса.ЗаполненоПоОснованию = СерииДляЗапроса.ЗаполненоПоОснованию
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерийОтправитель
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СкладОтправитель
	|			ПО (СкладОтправитель.Ссылка = &СкладОтправитель)
	|				И (ПолитикиУчетаСерийОтправитель.Склад = &СкладОтправитель)
	|		ПО ТоварыДляЗапроса.ВидНоменклатуры = ПолитикиУчетаСерийОтправитель.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерийПолучатель
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СкладПолучатель
	|			ПО (СкладПолучатель.Ссылка = &СкладПолучатель)
	|				И (ПолитикиУчетаСерийПолучатель.Склад = &СкладПолучатель)
	|		ПО ТоварыДляЗапроса.ВидНоменклатуры = ПолитикиУчетаСерийПолучатель.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Статусы.НомерСтроки,
	|	Статусы.СтатусУказанияСерийОтправитель,
	|	ВЫБОР
	|		КОГДА &ПриемкаПоПеремещению
	|			ТОГДА Статусы.СтатусУказанияСерийПолучатель
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтатусУказанияСерийПолучатель,
	|	ВЫБОР
	|		КОГДА Статусы.СтатусУказанияСерийПолучатель В (13, 14, 9, 10)
	|				И Статусы.СтатусУказанияСерийОтправитель <> 0
	|			ТОГДА Статусы.СтатусУказанияСерийПолучатель
	|		КОГДА Статусы.СтатусУказанияСерийОтправитель В (5, 6)
	|			ТОГДА Статусы.СтатусУказанияСерийОтправитель
	|		КОГДА Статусы.СтатусУказанияСерийОтправитель > ВЫБОР
	|				КОГДА &ПриемкаПоПеремещению
	|					ТОГДА Статусы.СтатусУказанияСерийПолучатель
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|			ТОГДА Статусы.СтатусУказанияСерийОтправитель
	|		ИНАЧЕ ВЫБОР
	|				КОГДА &ПриемкаПоПеремещению
	|					ТОГДА Статусы.СтатусУказанияСерийПолучатель
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ИЗ
	|	Статусы КАК Статусы
	|ГДЕ
	|	(Статусы.СтатусУказанияСерийОтправитель <> Статусы.СтатусУказанияСерийОтправительСтарый
	|			ИЛИ ВЫБОР
	|				КОГДА &ПриемкаПоПеремещению
	|					ТОГДА Статусы.СтатусУказанияСерийПолучатель
	|				ИНАЧЕ 0
	|			КОНЕЦ <> Статусы.СтатусУказанияСерийПолучательСтарый
	|			ИЛИ ВЫБОР
	|				КОГДА Статусы.СтатусУказанияСерийПолучатель В (13, 14, 9, 10)
	|						И Статусы.СтатусУказанияСерийОтправитель <> 0
	|					ТОГДА Статусы.СтатусУказанияСерийПолучатель
	|				КОГДА Статусы.СтатусУказанияСерийОтправитель В (5, 6)
	|					ТОГДА Статусы.СтатусУказанияСерийОтправитель
	|				КОГДА Статусы.СтатусУказанияСерийОтправитель > ВЫБОР
	|						КОГДА &ПриемкаПоПеремещению
	|							ТОГДА Статусы.СтатусУказанияСерийПолучатель
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|					ТОГДА Статусы.СтатусУказанияСерийОтправитель
	|				ИНАЧЕ ВЫБОР
	|						КОГДА &ПриемкаПоПеремещению
	|							ТОГДА Статусы.СтатусУказанияСерийПолучатель
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			КОНЕЦ <> Статусы.СтатусУказанияСерийСтарый)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
	ВводНаОснованииПереопределяемый.ДобавитьКомандыСоздатьНаОснованииПисмаПоШаблону(КомандыСоздатьНаОсновании);
	
	ВводНаОснованииПереопределяемый.ДобавитьКомандуСоздатьНаОснованииБизнесПроцессЗадание(КомандыСоздатьНаОсновании);
	
	Документы.ПоручениеЭкспедитору.ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании);
	
	Документы.РасходныйОрдерНаТовары.ДобавитьКомандуСоздатьНаОснованииРасходныйОрдерНаТовары(КомандыСоздатьНаОсновании);
	
КонецПроцедуры

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	 
	Если ПравоДоступа("Добавление", Метаданные.Документы.АктОРасхожденияхПослеПеремещения) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.АктОРасхожденияхПослеПеремещения.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.АктОРасхожденияхПослеПеремещения);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьАктыРасхожденийПослеПеремещения";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);
	
	Возврат; //В дальнейшем будет добавлен код команд
	
КонецПроцедуры

#Область Заполнение

Функция ДополнитьСтрокиТаблицыПерезаполненияПоЗаказу(ТаблицаНовыхСтрок) Экспорт
	
	// Заказ на перемещение
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НовыеСтроки.Заказ,
	|	НовыеСтроки.ДокументОснование,
	|	НовыеСтроки.Распоряжение,
	|	НовыеСтроки.Номенклатура,
	|	НовыеСтроки.Характеристика,
	|	НовыеСтроки.Серия,
	|	НовыеСтроки.Назначение,
	|	НовыеСтроки.НазначениеОтправителя,
	|	НовыеСтроки.Количество,
	|	НовыеСтроки.КодСтроки
	|ПОМЕСТИТЬ втНовыеСтроки
	|ИЗ
	|	&ТаблицаНовыеСтроки КАК НовыеСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказНаПеремещениеТовары.НомерСтроки,
	|	ИСТИНА КАК ДанныеПоЗаказу,
	|	ЗаказНаПеремещениеТовары.Упаковка,
	|	НовыеСтроки.Серия,
	|	НовыеСтроки.Количество,
	|	НовыеСтроки.Номенклатура,
	|	НовыеСтроки.КодСтроки,
	|	НовыеСтроки.Характеристика,
	|	НовыеСтроки.Назначение,
	|	ВЫБОР
	|		КОГДА ЗаказНаПеремещениеТовары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно)
	|				И НовыеСтроки.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			ТОГДА НовыеСтроки.Назначение
	|		КОГДА ЗаказНаПеремещениеТовары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно)
	|			ТОГДА ЗаказНаПеремещениеТовары.Ссылка.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ КАК НазначениеОтправителя,
	|	ЗаказНаПеремещениеТовары.Ссылка КАК Заказ,
	|	НовыеСтроки.ДокументОснование КАК ДокументОснование,
	|	НовыеСтроки.Распоряжение КАК Распоряжение
	|ПОМЕСТИТЬ ВтДанныеПоЗаказуНакладной
	|ИЗ
	|	Документ.ЗаказНаПеремещение.Товары КАК ЗаказНаПеремещениеТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНовыеСтроки КАК НовыеСтроки
	|		ПО ЗаказНаПеремещениеТовары.Номенклатура = НовыеСтроки.Номенклатура
	|			И ЗаказНаПеремещениеТовары.Характеристика = НовыеСтроки.Характеристика
	|			И ЗаказНаПеремещениеТовары.Ссылка = НовыеСтроки.Заказ
	|			И ЗаказНаПеремещениеТовары.КодСтроки = НовыеСтроки.КодСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПеремещениеТоваровТовары.НомерСтроки,
	|	ЛОЖЬ,
	|	ПеремещениеТоваровТовары.Упаковка,
	|	НовыеСтроки.Серия,
	|	НовыеСтроки.Количество,
	|	НовыеСтроки.Номенклатура,
	|	НовыеСтроки.КодСтроки,
	|	НовыеСтроки.Характеристика,
	|	НовыеСтроки.Назначение,
	|	НовыеСтроки.НазначениеОтправителя,
	|	ПеремещениеТоваровТовары.ЗаказНаПеремещение,
	|	НовыеСтроки.ДокументОснование,
	|	НовыеСтроки.Распоряжение
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНовыеСтроки КАК НовыеСтроки
	|		ПО ПеремещениеТоваровТовары.Номенклатура = НовыеСтроки.Номенклатура
	|			И ПеремещениеТоваровТовары.Характеристика = НовыеСтроки.Характеристика
	|			И ПеремещениеТоваровТовары.ЗаказНаПеремещение = НовыеСтроки.Заказ
	|			И (ПеремещениеТоваровТовары.Ссылка.Проведен)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(ТаблицаПоЗаказу.НомерСтроки, ТаблицаПоНакладной.НомерСтроки)
	|		, 0) КАК НомерСтроки,
	|	НовыеСтроки.Заказ,
	|	НовыеСтроки.ДокументОснование,
	|	НовыеСтроки.Распоряжение,
	|	НовыеСтроки.Номенклатура,
	|	НовыеСтроки.Характеристика,
	|	НовыеСтроки.Серия,
	|	НовыеСтроки.Назначение,
	|	ЕСТЬNULL(ТаблицаПоЗаказу.НазначениеОтправителя, НовыеСтроки.НазначениеОтправителя) КАК НазначениеОтправителя,
	|	НовыеСтроки.Количество,
	|	НовыеСтроки.КодСтроки,
	|	ЕСТЬNULL(ТаблицаПоЗаказу.Упаковка, ТаблицаПоНакладной.Упаковка) КАК Упаковка
	|ИЗ
	|	втНовыеСтроки КАК НовыеСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДанныеПоЗаказуНакладной КАК ТаблицаПоЗаказу
	|		ПО (ТаблицаПоЗаказу.Заказ = НовыеСтроки.Заказ)
	|			И (ТаблицаПоЗаказу.Номенклатура = НовыеСтроки.Номенклатура)
	|			И (ТаблицаПоЗаказу.Характеристика = НовыеСтроки.Характеристика)
	|			И (ТаблицаПоЗаказу.КодСтроки = НовыеСтроки.КодСтроки)
	|			И (ТаблицаПоЗаказу.ДанныеПоЗаказу)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДанныеПоЗаказуНакладной КАК ТаблицаПоНакладной
	|		ПО (ТаблицаПоНакладной.Заказ = НовыеСтроки.Заказ)
	|			И (ТаблицаПоНакладной.Номенклатура = НовыеСтроки.Номенклатура)
	|			И (ТаблицаПоНакладной.Характеристика = НовыеСтроки.Характеристика)
	|			И (ТаблицаПоНакладной.КодСтроки = НовыеСтроки.КодСтроки)
	|			И (НЕ ТаблицаПоНакладной.ДанныеПоЗаказу)";
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("ТаблицаНовыеСтроки", ТаблицаНовыхСтрок);
	
	ТаблицаНайденныеДопРеквизиты = Запрос.Выполнить().Выгрузить();
	
	ИтоговаяТаблица = ТаблицаНайденныеДопРеквизиты.СкопироватьКолонки();
	
	Отбор = Новый Структура("Распоряжение, КодСтроки, Номенклатура, Характеристика, Серия, Назначение, НазначениеОтправителя");
	
	// Слияние доп. реквизитов и строк, для которых не были найдены данные
	Для Каждого Строка Из ТаблицаНовыхСтрок Цикл
		НоваяСтрока = ИтоговаяТаблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		ЗаполнитьЗначенияСвойств(Отбор, Строка);
		
		НайденныеСтроки = ТаблицаНайденныеДопРеквизиты.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденныеСтроки[0],, "Количество");
		КонецЕсли;
	КонецЦикла;
	
	ИтоговаяТаблица.Сортировать("НомерСтроки");
	
	Возврат ИтоговаяТаблица;
	
КонецФункции

// Процедура пересчитывает поле КоличествоУпаковок и другие зависимые поля
//
// Параметры:
//  Товары				 - ДанныеФормыКоллекция - Табличная часть "Товары"
//  ПараметрыЗаполнения	 - Структура - 
//
Процедура ОбновитьЗависимыеРеквизитыТабличнойЧасти(Товары, ПараметрыЗаполнения) Экспорт
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
	СтруктураДействий.Вставить("ПересчитатьРасхождения");
	
	Для Каждого Строка Из Товары Цикл
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(Строка, СтруктураДействий, Неопределено);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает таблицу Товары
//
// Параметры:
//  Накладная	 - Ссылка - Ссылка на накладную, табличную часть Товары которой необходимо вернуть
// 
// Возвращаемое значение:
//   - ТаблицаЗначений
//
Функция ДанныеТаблицыТоварыДокумента(Накладная, ОрдернаяСхемаПриОтгрузке, ОрдернаяСхемаПриПриемке) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.Ссылка,
	|	&ОрдернаяСхемаПриОтгрузке КАК ОрдернаяСхемаПриОтгрузке,
	|	&ОрдернаяСхемаПриПриемке КАК ОрдернаяСхемаПриПриемке,
	|	Таблица.НомерСтроки,
	|	Таблица.ТекстовоеОписание,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Назначение,
	|	Таблица.Упаковка,
	|	Таблица.Количество,
	|	Таблица.Заказ,
	|	Таблица.КодСтроки,
	|	Таблица.Серия,
	|	Таблица.СтатусУказанияСерий,
	|	Таблица.ДокументОснование,
	|	Таблица.ЗаполненоПоОснованию,
	|	Таблица.КоличествоПоДокументу,
	|	Таблица.Действие,
	|	Таблица.КомментарийПолучателя,
	|	Таблица.КомментарийМенеджера,
	|	Таблица.СтатусУказанияСерийОтправитель,
	|	Таблица.СтатусУказанияСерийПолучатель,
	|	Таблица.НазначениеОтправителя,
	|	ВЫБОР
	|		КОГДА Таблица.Заказ = ЗНАЧЕНИЕ(Документ.ЗаказНаПеремещение.ПустаяСсылка)
	|			ТОГДА Таблица.ДокументОснование
	|		ИНАЧЕ Таблица.Заказ
	|	КОНЕЦ КАК Распоряжение,
	|	ВЫБОР
	|		КОГДА &ОрдернаяСхемаПриОтгрузке
	|				И &ОрдернаяСхемаПриПриемке
	|				И Таблица.СтатусУказанияСерийОтправитель В (2, 4, 6, 8)
	|				И Таблица.СтатусУказанияСерийПолучатель В (0)
	|			ТОГДА ""А0""
	|		КОГДА &ОрдернаяСхемаПриПриемке
	|				И Таблица.СтатусУказанияСерийОтправитель В (0)
	|				И Таблица.СтатусУказанияСерийПолучатель В (0)
	|			ТОГДА ""А0""
	|		КОГДА НЕ &ОрдернаяСхемаПриОтгрузке
	|				И &ОрдернаяСхемаПриПриемке
	|				И Таблица.СтатусУказанияСерийОтправитель В (2, 4, 6, 8)
	|			ТОГДА ""А4""
	|		КОГДА &ОрдернаяСхемаПриПриемке
	|				И Таблица.СтатусУказанияСерийОтправитель В (2, 4, 6, 8)
	|				И Таблица.СтатусУказанияСерийПолучатель В (0, 14)
	|			ТОГДА ""А1""
	|		КОГДА &ОрдернаяСхемаПриПриемке
	|				И Таблица.СтатусУказанияСерийОтправитель В (10, 14)
	|				И Таблица.СтатусУказанияСерийПолучатель В (0, 14)
	|			ТОГДА ""А2""
	|		КОГДА &ОрдернаяСхемаПриПриемке
	|				И Таблица.СтатусУказанияСерийОтправитель В (0)
	|				И Таблица.СтатусУказанияСерийПолучатель В (14)
	|			ТОГДА ""А2""
	|		ИНАЧЕ ""А3""
	|	КОНЕЦ КАК СпособСоединения
	|ПОМЕСТИТЬ ВтДанныеДокумента
	|ИЗ
	|	Документ.АктОРасхожденияхПослеПеремещения.Товары КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &Накладная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Ссылка,
	|	Таблица.ОрдернаяСхемаПриОтгрузке,
	|	Таблица.ОрдернаяСхемаПриПриемке,
	|	Таблица.НомерСтроки,
	|	Таблица.ТекстовоеОписание,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.Назначение,
	|	Таблица.Упаковка,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаСерии.Номенклатура ЕСТЬ NULL 
	|			ТОГДА ТаблицаСерии.Количество
	|		ИНАЧЕ Таблица.Количество
	|	КОНЕЦ КАК Количество,
	|	Таблица.Заказ,
	|	Таблица.КодСтроки,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаСерии.Номенклатура ЕСТЬ NULL 
	|			ТОГДА ТаблицаСерии.Серия
	|		ИНАЧЕ Таблица.Серия
	|	КОНЕЦ КАК Серия,
	|	Таблица.СтатусУказанияСерий,
	|	Таблица.ДокументОснование,
	|	Таблица.ЗаполненоПоОснованию,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаСерии.Номенклатура ЕСТЬ NULL 
	|			ТОГДА ТаблицаСерии.КоличествоПоДокументу
	|		ИНАЧЕ Таблица.КоличествоПоДокументу
	|	КОНЕЦ КАК КоличествоПоДокументу,
	|	Таблица.Действие,
	|	Таблица.КомментарийПолучателя,
	|	Таблица.КомментарийМенеджера,
	|	Таблица.СтатусУказанияСерийОтправитель,
	|	Таблица.СтатусУказанияСерийПолучатель,
	|	Таблица.НазначениеОтправителя,
	|	Таблица.Распоряжение,
	|	Таблица.СпособСоединения КАК СпособСоединения
	|ИЗ
	|	ВтДанныеДокумента КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктОРасхожденияхПослеПеремещения.Серии КАК ТаблицаСерии
	|		ПО (Таблица.СпособСоединения = ""А4"")
	|			И Таблица.Ссылка = ТаблицаСерии.Ссылка
	|			И Таблица.Номенклатура = ТаблицаСерии.Номенклатура
	|			И Таблица.Характеристика = ТаблицаСерии.Характеристика
	|			И Таблица.Назначение = ТаблицаСерии.Назначение
	|			И Таблица.НазначениеОтправителя = ТаблицаСерии.НазначениеОтправителя
	|			И Таблица.Действие = ТаблицаСерии.Действие
	|			И Таблица.ЗаполненоПоОснованию = ТаблицаСерии.ЗаполненоПоОснованию
	|			И Таблица.ДокументОснование = ТаблицаСерии.ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	СпособСоединения";
	
	Запрос.УстановитьПараметр("Накладная", Накладная);
	Запрос.УстановитьПараметр("ОрдернаяСхемаПриОтгрузке", ОрдернаяСхемаПриОтгрузке);
	Запрос.УстановитьПараметр("ОрдернаяСхемаПриПриемке", ОрдернаяСхемаПриПриемке);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру необходимую для дальнейшего использования при заполнении документа по заказу или ордерам
// 
// Возвращаемое значение:
//   - Структура
//
Функция ПараметрыЗаполненияДокумента() Экспорт
	
	ПараметрыЗаполнения = Новый Структура();
	ПараметрыЗаполнения.Вставить("Менеджер", Документы.АктОРасхожденияхПослеПеремещения);
	ПараметрыЗаполнения.Вставить("ИмяРегистраЗаказ", "ЗаказыНаПеремещение");
	ПараметрыЗаполнения.Вставить("МенеджерРегистраЗаказ", РегистрыНакопления.ЗаказыНаПеремещение);
	ПараметрыЗаполнения.Вставить("ИмяПоляЗаказ", "Заказ");
	ПараметрыЗаполнения.Вставить("Дата", КонецДня(ТекущаяДатаСеанса()));
	ПараметрыЗаполнения.Вставить("КлючевыеПоля", "Распоряжение, Номенклатура, Характеристика, Серия, Назначение");
	ПараметрыЗаполнения.Вставить("ДополнительныеПоляТаблицыДокумента", "Упаковка, КодСтроки, СтатусУказанияСерий, 
																		|Заказ, НазначениеОтправителя");
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

// Возвращает текст запроса необходимый для заполнения документа на основании документа Перемещение товаров
//
Функция ТекстЗапросаПоОснованиюПеремещения() Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НЕ ПеремещениеТоваров.Проведен КАК ЕстьОшибкиПроведен,
	|	ВЫБОР
	|		КОГДА ПеремещениеТоваров.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПеремещенийТоваров.Отгружено)
	|				ИЛИ ПеремещениеТоваров.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПеремещенийТоваров.Принято)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьОшибкиСтатус,
	|	ПеремещениеТоваров.Ссылка КАК ДокументОснование,
	|	ПеремещениеТоваров.Статус КАК СтатусДокумента,
	|	ПеремещениеТоваров.Организация,
	|	ПеремещениеТоваров.ОрганизацияПолучатель,
	|	ПеремещениеТоваров.Ответственный КАК Менеджер,
	|	ПеремещениеТоваров.СкладОтправитель,
	|	ПеремещениеТоваров.СкладПолучатель,
	|	ПеремещениеТоваров.ХозяйственнаяОперация
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|ГДЕ
	|	ПеремещениеТоваров.Ссылка В (&Основания)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПеремещениеТоваровТовары.Номенклатура,
	|	ПеремещениеТоваровТовары.Характеристика,
	|	ПеремещениеТоваровТовары.Серия,
	|	ПеремещениеТоваровТовары.Назначение,
	|	ПеремещениеТоваровТовары.НазначениеОтправителя,
	|	ПеремещениеТоваровТовары.Упаковка,
	|	ПеремещениеТоваровТовары.КоличествоУпаковок,
	|	ПеремещениеТоваровТовары.Количество,
	|	ПеремещениеТоваровТовары.СтатусУказанияСерий,
	|	ПеремещениеТоваровТовары.Ссылка КАК ДокументОснование,
	|	ПеремещениеТоваровТовары.ЗаказНаПеремещение КАК Заказ,
	|	ПеремещениеТоваровТовары.КодСтроки КАК КодСтроки,
	|	ИСТИНА КАК ЗаполненоПоОснованию,
	|	ПеремещениеТоваровТовары.КоличествоУпаковок КАК КоличествоУпаковокПоДокументу,
	|	ПеремещениеТоваровТовары.Количество КАК КоличествоПоДокументу
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
	|ГДЕ
	|	ПеремещениеТоваровТовары.Ссылка В (&Основания)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПеремещениеТоваровСерии.Серия,
	|	ИСТИНА КАК ЗаполненоПоОснованию,
	|	ПеремещениеТоваровСерии.Количество,
	|	ПеремещениеТоваровСерии.Количество КАК КоличествоПоДокументу,
	|	ПеремещениеТоваровСерии.Номенклатура,
	|	ПеремещениеТоваровСерии.Характеристика,
	|	ПеремещениеТоваровСерии.Назначение,
	|	ПеремещениеТоваровСерии.НазначениеОтправителя,
	|	ПеремещениеТоваровСерии.Ссылка КАК ДокументОснование
	|ИЗ
	|	Документ.ПеремещениеТоваров.Серии КАК ПеремещениеТоваровСерии
	|ГДЕ
	|	ПеремещениеТоваровСерии.Ссылка В (&Основания)";
	
	Возврат ТекстЗапроса;

КонецФункции 

// Возвращает список полей табличной части Товары
// 
// Возвращаемое значение:
//   - Строка
//
Функция СписокПолейТЧТоварыСтрокой() Экспорт
	
	Возврат "НомерСтроки,
	|ТекстовоеОписание,
	|Номенклатура,
	|Характеристика,
	|Назначение,
	|Упаковка,
	|КоличествоУпаковок,
	|Количество,
	|Заказ,
	|КодСтроки,
	|Серия,
	|СтатусУказанияСерий,
	|ДокументОснование,
	|ЗаполненоПоОснованию,
	|КоличествоУпаковокПоДокументу,
	|КоличествоПоДокументу,
	|Действие,
	|КомментарийПолучателя,
	|КомментарийМенеджера,
	|СтатусУказанияСерийОтправитель,
	|СтатусУказанияСерийПолучатель,
	|НазначениеОтправителя";
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт

	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаГрафикОтгрузкиТоваров(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСвободныеОстатки(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаОбеспечениеЗаказов(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыКОтгрузке(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыНаСкладах(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыКОформлениюИзлишковНедостач(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыКПоступлению(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыКОформлениюПоступления(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДвижениеТоваров(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеШапки.Дата                         КАК Период,
	|	ДанныеШапки.Ссылка                       КАК Ссылка,
	|	ДанныеШапки.Организация                  КАК Организация,
	|	ДанныеШапки.Статус                       КАК Статус,
	|	ДанныеШапки.СкладОтправитель             КАК СкладОтправитель,
	|	ДанныеШапки.СкладПолучатель              КАК СкладПолучатель,
	|	ДанныеШапки.ХозяйственнаяОперация        КАК ХозяйственнаяОперация
	|
	|ИЗ
	|	Документ.АктОРасхожденияхПослеПеремещения КАК ДанныеШапки
	|ГДЕ
	|	ДанныеШапки.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",                           Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка",                           Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("Статус",                           Реквизиты.Статус);
	Запрос.УстановитьПараметр("Организация",                      Реквизиты.Организация);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",            Реквизиты.ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("СкладОтправитель",                 Реквизиты.СкладОтправитель);
	Запрос.УстановитьПараметр("СкладПолучатель",                  Реквизиты.СкладПолучатель);
	Запрос.УстановитьПараметр("ПустоеНазначение",                 Справочники.Назначения.ПустаяСсылка());
	Запрос.УстановитьПараметр("СкладскаяОперация",                Перечисления.СкладскиеОперации.ОтгрузкаПоПеремещению);
	
КонецПроцедуры

Функция ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВТТовары";

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка                                          КАК Ссылка,
	|	ТаблицаТовары.НомерСтроки                                     КАК НомерСтроки,
	|	ТаблицаТовары.КодСтроки                                       КАК КодСтроки,
	|	ТаблицаТовары.Номенклатура                                    КАК Номенклатура,
	|	ТаблицаТовары.Характеристика                                  КАК Характеристика,
	|	&СкладОтправитель                                             КАК Склад,
	|	&СкладПолучатель                                              КАК СкладПолучатель,
	|	ТаблицаТовары.НазначениеОтправителя                           КАК Назначение,
	|	ТаблицаТовары.Назначение                                      КАК НазначениеПолучателя,
	|	ВЫБОР КОГДА ЕСТЬNULL(ТаблицаТовары.НазначениеОтправителя.ДвиженияПоСкладскимРегистрам, ЛОЖЬ) ТОГДА
	|				ТаблицаТовары.НазначениеОтправителя
	|			ИНАЧЕ
	|				ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		КОНЕЦ                                                      КАК НазначениеСкладское,
	|	ВЫБОР КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ) ТОГДА
	|				ТаблицаТовары.Назначение
	|			ИНАЧЕ
	|				ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		КОНЕЦ                                                      КАК НазначениеПолучателяСкладское,
	|	ТаблицаТовары.Количество - ТаблицаТовары.КоличествоПоДокументу КАК Расхождения,
	|	ТаблицаТовары.СтатусУказанияСерий                              КАК СтатусУказанияСерий,
	|	ТаблицаТовары.Серия                                            КАК Серия,
	|	ТаблицаТовары.ДокументОснование                                КАК ДокументОснование,
	|	ТаблицаТовары.Заказ                                            КАК Заказ,
	|	ТаблицаТовары.Действие                                         КАК Действие,
	|	
	|	ВЫБОР КОГДА ТаблицаТовары.Заказ В(НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Документ.ЗаказНаПеремещение.ПустаяСсылка)) ТОГДА
	|				ТаблицаТовары.ДокументОснование
	|			ИНАЧЕ
	|				ТаблицаТовары.Заказ
	|		КОНЕЦ                                                      КАК Распоряжение,
	|	
	|	ВЫБОР КОГДА СкладОтправитель.ИспользоватьОрдернуюСхемуПриПоступлении И СкладОтправитель.ДатаНачалаОрдернойСхемыПриПоступлении <= &Период ТОГДА
	|				ИСТИНА
	|			ИНАЧЕ
	|				ЛОЖЬ
	|		КОНЕЦ         КАК ОрдернаяСхемаПриПриемке,
	|	
	|	ВЫБОР КОГДА СкладОтправитель.ИспользоватьОрдернуюСхемуПриОтгрузке И СкладОтправитель.ДатаНачалаОрдернойСхемыПриОтгрузке <= &Период ТОГДА
	|				ИСТИНА
	|			ИНАЧЕ
	|				ЛОЖЬ
	|		КОНЕЦ         КАК ОрдернаяСхемаПриОтгрузке,
	|	
	|	ВЫБОР КОГДА СкладОтправитель.ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач И СкладОтправитель.ДатаНачалаОрдернойСхемыПриОтраженииИзлишковНедостач <= &Период ТОГДА
	|				ИСТИНА
	|			ИНАЧЕ
	|				ЛОЖЬ
	|		КОНЕЦ         КАК ОрдернаяСхемаПриОтраженииИзлишковНедостач,
	|	ВЫБОР 
	|		КОГДА СкладПолучатель.ИспользоватьОрдернуюСхемуПриПоступлении И СкладПолучатель.ДатаНачалаОрдернойСхемыПриПоступлении <= &Период 
	|			ТОГДА
	|				ИСТИНА
	|			ИНАЧЕ
	|				ЛОЖЬ
	|	КОНЕЦ         КАК ОрдернаяСхемаПриПриемкеНаСкладеПриемки,
	|
	|	ВЫБОР 
	|		КОГДА СкладПолучатель.ИспользоватьОрдернуюСхемуПриОтгрузке И СкладПолучатель.ДатаНачалаОрдернойСхемыПриОтгрузке <= &Период 
	|			ТОГДА
	|				ИСТИНА
	|			ИНАЧЕ
	|				ЛОЖЬ
	|	КОНЕЦ         КАК ОрдернаяСхемаПриОтгрузкеНаСкладеПриемки,
	|	
	|	ВЫБОР 
	|		КОГДА СкладПолучатель.ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач И СкладПолучатель.ДатаНачалаОрдернойСхемыПриОтраженииИзлишковНедостач <= &Период 
	|			ТОГДА
	|				ИСТИНА
	|			ИНАЧЕ
	|				ЛОЖЬ
	|	КОНЕЦ         КАК ОрдернаяСхемаПриОтраженииИзлишковНедостачНаСкладеПриемки
	|	
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	Документ.АктОРасхожденияхПослеПеремещения.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СкладОтправитель
	|		ПО СкладОтправитель.Ссылка = &СкладОтправитель
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СкладПолучатель
	|		ПО СкладПолучатель.Ссылка = &СкладПолучатель
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.КоличествоПоДокументу <> ТаблицаТовары.Количество";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаВТТоварыИСерии(Запрос, ТекстыЗапроса)

	ИмяРегистра = "ВТТоварыИСерии";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ИСТИНА КАК ДляСкладаОтгрузки,
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.НазначениеОтправителя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ КАК Назначение,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ КАК НазначениеПолучателя,
	|	&СкладОтправитель КАК Склад,
	|	ТаблицаТовары.Количество - ТаблицаТовары.КоличествоПоДокументу КАК Расхождения,
	|	ТаблицаТовары.СтатусУказанияСерийОтправитель КАК СтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерийОтправитель В (4, 6, 8, 10, 14)
	|			ТОГДА ТаблицаТовары.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Серия,
	|	ТаблицаТовары.Действие КАК Действие,
	|	ВЫБОР
	|		КОГДА СпрСклад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				И СпрСклад.ДатаНачалаОрдернойСхемыПриОтгрузке <= &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдернаяСхемаПриОтгрузке,
	|	ВЫБОР
	|		КОГДА СпрСклад.ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач
	|				И СпрСклад.ДатаНачалаОрдернойСхемыПриОтраженииИзлишковНедостач <= &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдернаяСхемаПриОтраженииИзлишковНедостач,
	|	NULL КАК ОрдернаяСхемаПриПриемке
	|ПОМЕСТИТЬ ВТТоварыИСерии
	|ИЗ
	|	Документ.АктОРасхожденияхПослеПеремещения.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклад
	|		ПО (СпрСклад.Ссылка = &СкладОтправитель)
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.КоличествоПоДокументу <> ТаблицаТовары.Количество
	|	И (ТаблицаТовары.СтатусУказанияСерийОтправитель В (0, 2, 10, 14)
	|			ИЛИ ТаблицаТовары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЛОЖЬ КАК ДляСкладаОтгрузки,
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.НомерСтроки,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.НазначениеОтправителя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ,
	|	&СкладПолучатель,
	|	ТаблицаТовары.Количество - ТаблицаТовары.КоличествоПоДокументу,
	|	ТаблицаТовары.СтатусУказанияСерийПолучатель,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерийПолучатель В (4, 6, 8, 10, 14)
	|			ТОГДА ТаблицаТовары.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаТовары.Действие,
	|	ВЫБОР
	|		КОГДА СпрСклад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				И СпрСклад.ДатаНачалаОрдернойСхемыПриОтгрузке <= &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдернаяСхемаПриОтгрузке,
	|	ВЫБОР
	|		КОГДА СпрСклад.ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач
	|				И СпрСклад.ДатаНачалаОрдернойСхемыПриОтраженииИзлишковНедостач <= &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдернаяСхемаПриОтраженииИзлишковНедостач,
	|	ВЫБОР
	|		КОГДА СпрСклад.ИспользоватьОрдернуюСхемуПриПоступлении
	|				И СпрСклад.ДатаНачалаОрдернойСхемыПриПоступлении <= &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдернаяСхемаПриПриемке
	|ИЗ
	|	Документ.АктОРасхожденияхПослеПеремещения.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклад
	|		ПО (СпрСклад.Ссылка = &СкладПолучатель)
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.КоличествоПоДокументу <> ТаблицаТовары.Количество
	|	И (ТаблицаТовары.СтатусУказанияСерийПолучатель В (0, 2, 10, 14)
	|			ИЛИ ТаблицаТовары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ИСТИНА) КАК ДляСкладаОтгрузки,
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.НомерСтроки,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.НазначениеОтправителя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ,
	|	&СкладОтправитель,
	|	МАКСИМУМ(ТаблицаТовары.Количество - ТаблицаТовары.КоличествоПоДокументу),
	|	МАКСИМУМ(ТаблицаТоварыДокумента.СтатусУказанияСерийОтправитель),
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.Действие,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА СпрСклад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|					И СпрСклад.ДатаНачалаОрдернойСхемыПриОтгрузке <= &Период
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ОрдернаяСхемаПриОтгрузке,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА СпрСклад.ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач
	|					И СпрСклад.ДатаНачалаОрдернойСхемыПриОтраженииИзлишковНедостач <= &Период
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ОрдернаяСхемаПриОтраженииИзлишковНедостач,
	|	NULL КАК ОрдернаяСхемаПриПриемке
	|ИЗ
	|	Документ.АктОРасхожденияхПослеПеремещения.Серии КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклад
	|		ПО (СпрСклад.Ссылка = &СкладОтправитель)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктОРасхожденияхПослеПеремещения.Товары КАК ТаблицаТоварыДокумента
	|		ПО ТаблицаТовары.Ссылка = ТаблицаТоварыДокумента.Ссылка
	|			И ТаблицаТовары.Номенклатура = ТаблицаТоварыДокумента.Номенклатура
	|			И ТаблицаТовары.Характеристика = ТаблицаТоварыДокумента.Характеристика
	|			И ТаблицаТовары.Назначение = ТаблицаТоварыДокумента.Назначение
	|			И ТаблицаТовары.ДокументОснование = ТаблицаТоварыДокумента.ДокументОснование
	|			И ТаблицаТовары.ЗаполненоПоОснованию = ТаблицаТоварыДокумента.ЗаполненоПоОснованию
	|			И ТаблицаТовары.Действие = ТаблицаТоварыДокумента.Действие
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.КоличествоПоДокументу <> ТаблицаТовары.Количество
	|	И ТаблицаТоварыДокумента.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Назначение,
	|	ТаблицаТовары.НазначениеОтправителя,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.НомерСтроки,
	|	ТаблицаТовары.Действие,
	|	ТаблицаТовары.Ссылка,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.НазначениеОтправителя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(ТаблицаТоварыДокумента.СтатусУказанияСерийОтправитель) В (4, 6, 8)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЛОЖЬ) КАК ДляСкладаОтгрузки,
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.НомерСтроки,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.НазначениеОтправителя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ,
	|	&СкладПолучатель,
	|	МАКСИМУМ(ТаблицаТовары.Количество - ТаблицаТовары.КоличествоПоДокументу),
	|	МАКСИМУМ(ТаблицаТоварыДокумента.СтатусУказанияСерийПолучатель),
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.Действие,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА СпрСклад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|					И СпрСклад.ДатаНачалаОрдернойСхемыПриОтгрузке <= &Период
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ОрдернаяСхемаПриОтгрузке,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА СпрСклад.ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач
	|					И СпрСклад.ДатаНачалаОрдернойСхемыПриОтраженииИзлишковНедостач <= &Период
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ОрдернаяСхемаПриОтраженииИзлишковНедостач,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА СпрСклад.ИспользоватьОрдернуюСхемуПриПоступлении
	|					И СпрСклад.ДатаНачалаОрдернойСхемыПриПоступлении <= &Период
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ОрдернаяСхемаПриПриемке
	|ИЗ
	|	Документ.АктОРасхожденияхПослеПеремещения.Серии КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклад
	|		ПО (СпрСклад.Ссылка = &СкладПолучатель)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктОРасхожденияхПослеПеремещения.Товары КАК ТаблицаТоварыДокумента
	|		ПО ТаблицаТовары.Ссылка = ТаблицаТоварыДокумента.Ссылка
	|			И ТаблицаТовары.Номенклатура = ТаблицаТоварыДокумента.Номенклатура
	|			И ТаблицаТовары.Характеристика = ТаблицаТоварыДокумента.Характеристика
	|			И ТаблицаТовары.Назначение = ТаблицаТоварыДокумента.Назначение
	|			И ТаблицаТовары.ДокументОснование = ТаблицаТоварыДокумента.ДокументОснование
	|			И ТаблицаТовары.ЗаполненоПоОснованию = ТаблицаТоварыДокумента.ЗаполненоПоОснованию
	|			И ТаблицаТовары.Действие = ТаблицаТоварыДокумента.Действие
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.КоличествоПоДокументу <> ТаблицаТовары.Количество
	|	И ТаблицаТоварыДокумента.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Назначение,
	|	ТаблицаТовары.НазначениеОтправителя,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.НомерСтроки,
	|	ТаблицаТовары.Действие,
	|	ТаблицаТовары.Ссылка,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.НазначениеОтправителя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(ТаблицаТоварыДокумента.СтатусУказанияСерийПолучатель) В (4, 6, 8)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаТоварыКОтгрузке(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыКОтгрузке";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВТТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
		/////////////////////////////////////////
		// Недостача при приемке (Расхождения < 0).
		//
		// Компенсация расходного ордера (оптимистично считая, что товар недогрузили)
		// для до-отгрузки из свободного остатка или для отмены заказа(если доспоставка не требуется).
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Распоряжение             КАК ДокументОтгрузки,
		|	
		|	&СкладПолучатель                       КАК Получатель,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	
		|	ТаблицаТовары.НазначениеПолучателяСкладское КАК Назначение,
		|	ТаблицаТовары.Серия                    КАК Серия,
		|	
		|	ТаблицаТовары.Расхождения              КАК КОтгрузке
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.ОрдернаяСхемаПриОтгрузке
		|	
		|	И ТаблицаТовары.Действие В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОтгрузитьСейчас),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется))
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|
		/////////////////////////////////////////
		// Излишек при приемке (Расхождения > 0).
		//
		// Компенсация увеличения накладной на ордерном на отгрузку складе (оптимистично считая, что отгрузили лишнее).
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Распоряжение             КАК ДокументОтгрузки,
		|	
		|	&СкладПолучатель                       КАК Получатель,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	ТаблицаТовары.НазначениеПолучателяСкладское КАК Назначение,
		|	ТаблицаТовары.Серия                    КАК Серия,
		|	
		|	ТаблицаТовары.Расхождения              КАК КОтгрузке
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.ОрдернаяСхемаПриОтгрузке
		|
		|	И ТаблицаТовары.Действие В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Излишек при приемке (Расхождения > 0). Вернуть без оформления (Оприходовать сейчас)
		//
		// Формируем задание на отгрузку
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
		|	&Период                                КАК Период,
		|	
		|	&Ссылка						           КАК ДокументОтгрузки,
		|	
		|	ТаблицаТовары.Склад                    КАК Получатель,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.СкладПолучатель          КАК Склад,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ТаблицаТовары.Серия                    КАК Серия,
		|	
		|	ТаблицаТовары.Расхождения              КАК КОтгрузке
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОприходоватьСейчас)
		|		
		|ОБЪЕДИНИТЬ ВСЕ
		|	
		// Закрываем к отгрузке если не ведутся ордера на отгрузку
		| ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
		|	&Период                                КАК Период,
		|	
		|	&Ссылка						           КАК ДокументОтгрузки,
		|	
		|	ТаблицаТовары.Склад                    КАК Получатель,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.СкладПолучатель          КАК Склад,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ТаблицаТовары.Серия                    КАК Серия,
		|	
		|	ТаблицаТовары.Расхождения              КАК КОтгрузке
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|
		|	И НЕ ТаблицаТовары.ОрдернаяСхемаПриПриемкеНаСкладеПриемки
		|
		|	И ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОприходоватьСейчас)";
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСвободныеОстатки(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СвободныеОстатки";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВТТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
		/////////////////////////////////////////
		// Недостача при приемке (Расхождения < 0).
		//
		// Компенсация обнаруженного пересчетом товаров на складе излишка (оптимистично считая, что товар недогрузили)
		// для до-отгрузки из свободного остатка или для отмены заказа(если доспоставка не требуется).
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	
		|	-ТаблицаТовары.Расхождения              КАК ВНаличии,
		|	ВЫБОР КОГДА ТаблицаТовары.НазначениеСкладское <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) ТОГДА
		|				-ТаблицаТовары.Расхождения
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ                              КАК ВРезервеПодЗаказ
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостач
		|	И ТаблицаТовары.Действие В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОтгрузитьСейчас),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется))
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|	
		/////////////////////////////////////////
		// Недостача при приемке (Расхождения < 0).
		//
		//Компенсация корректирвоки поступления по складу при ордерной схеме приемки
		//Оптимистично считаем, что приходным ордером отразили факт недостачи на складе
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.СкладПолучатель          КАК Склад,
		|	
		|	ТаблицаТовары.Расхождения              КАК ВНаличии,
		|	ВЫБОР 
		|		КОГДА ТаблицаТовары.НазначениеПолучателяСкладское <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) 
		|			ТОГДА
		|				ТаблицаТовары.Расхождения
		|			ИНАЧЕ
		|				0
		|	КОНЕЦ                              	   КАК ВРезервеПодЗаказ
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.ОрдернаяСхемаПриПриемкеНаСкладеПриемки
		|	И НЕ ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостачНаСкладеПриемки
		|	И ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		/////////////////////////////////////////
		// Излишек при приемке (Расхождения > 0).
		//
		// Обнаружение недостачи на складе (оптимистично считая, что отгрузили лишнее).
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	
		|	ТаблицаТовары.Расхождения              КАК ВНаличии,
		|	ВЫБОР КОГДА ТаблицаТовары.НазначениеСкладское <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) ТОГДА
		|				ТаблицаТовары.Расхождения
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ                              КАК ВРезервеПодЗаказ
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И НЕ ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостач
		|	И ТаблицаТовары.Действие В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного))
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|	
		// Излишек при приемке (Расхождения > 0).
		//
		// Закрытие свободных остатков при возврате перепоставленного товара
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.СкладПолучатель          КАК Склад,
		|	
		|	-ТаблицаТовары.Расхождения             КАК ВНаличии,
		|	ВЫБОР 
		|		КОГДА ТаблицаТовары.НазначениеПолучателяСкладское <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) 
		|			ТОГДА
		|			-ТаблицаТовары.Расхождения
		|		ИНАЧЕ
		|				0
		|	КОНЕЦ                              	   КАК ВРезервеПодЗаказ
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.ОрдернаяСхемаПриПриемкеНаСкладеПриемки
		|	И (ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится)
		|			И НЕ ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостачНаСкладеПриемки)
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|	
		// Компенсация увеличения накладной на неордерном на отгрузку складе (оптимистично считая, что отгрузили лишнее).
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	
		|	-ТаблицаТовары.Расхождения             КАК ВНаличии,
		|	ВЫБОР КОГДА ТаблицаТовары.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) ТОГДА
		|			-ТаблицаТовары.Расхождения
		|		КОНЕЦ                              КАК ВРезервеПодЗаказ
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.КодСтроки = 0
		|	И ТаблицаТовары.Действие В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного))";
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОбеспечениеЗаказов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОбеспечениеЗаказов";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВТТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
		/////////////////////////////////////////
		// Недостача при приемке (Расхождения < 0).
		//
		//Формирование потребности для до-отгрузки из свободного остатка
		//или для отмены заказа(если доспоставка не требуется) в предварительном статусе.
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	
		|	ТаблицаТовары.НазначениеСкладское      КАК Назначение,
		|	-ТаблицаТовары.Расхождения             КАК Потребность,
		|	-ТаблицаТовары.Расхождения             КАК КЗаказу,
		|	0                                      КАК НаличиеПодЗаказ
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Согласовано)
		|
		|	И ТаблицаТовары.НазначениеСкладское <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	
		|	И ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостач
		|	И ТаблицаТовары.Действие В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОтгрузитьСейчас),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Компенсация обнаруженного пересчетом товаров на складе излишка (оптимистично считая, что товар недогрузили)
		// для до-отгрузки из свободного остатка или для отмены заказа(если доспоставка не требуется).
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	
		|	ТаблицаТовары.НазначениеСкладское      КАК Назначение,
		|	0                                      КАК Потребность,
		|	ТаблицаТовары.Расхождения             КАК КЗаказу,
		|	-ТаблицаТовары.Расхождения              КАК НаличиеПодЗаказ
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.НазначениеСкладское <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	
		|	И ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостач
		|	И ТаблицаТовары.Действие В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОтгрузитьСейчас),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|	
		// Недостача при приемке (Расхождения < 0).
		//
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.СкладПолучатель          КАК Склад,
		|	
		|	ТаблицаТовары.НазначениеПолучателя     КАК Назначение,
		|	0                                      КАК Потребность,
		|	ВЫБОР КОГДА ТаблицаТовары.КодСтроки = 0 Тогда
		|		-ТаблицаТовары.Расхождения
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ                                  КАК КЗаказу,
		|	0                                      КАК НаличиеПодЗаказ
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.НазначениеПолучателя <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	И ТаблицаТовары.ОрдернаяСхемаПриПриемкеНаСкладеПриемки
		|	И НЕ ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостачНаСкладеПриемки
		|	И ТаблицаТовары.Действие =
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		/////////////////////////////////////////
		// Излишек при приемке (Расхождения > 0).
		//
		// Обнаруженние недостачи на складе (оптимистично считая, что отгрузили лишнее).
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	
		|	ТаблицаТовары.НазначениеСкладское      КАК Назначение,
		|	0                                      КАК Потребность,
		|	-ТаблицаТовары.Расхождения             КАК КЗаказу,
		|	ТаблицаТовары.Расхождения              КАК НаличиеПодЗаказ
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.НазначениеСкладское <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	
		|	И НЕ ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостач
		|	И ТаблицаТовары.Действие В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Компенсация увеличения накладной на неордерном на отгрузку складе (оптимистично считая, что отгрузили лишнее).
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	
		|	ТаблицаТовары.Назначение               КАК Назначение,
		|	0                                      КАК Потребность,
		|	ТаблицаТовары.Расхождения              КАК КЗаказу,
		|	-ТаблицаТовары.Расхождения             КАК НаличиеПодЗаказ
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	
		|	И ТаблицаТовары.КодСтроки = 0
		|	И ТаблицаТовары.Действие В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного))
		// Излишек при приемке (Расхождения > 0)
		// 
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.СкладПолучатель          КАК Склад,
		|	
		|	ТаблицаТовары.НазначениеПолучателя     КАК Назначение,
		|	0                                      КАК Потребность,
		|	ВЫБОР КОГДА ТаблицаТовары.КодСтроки = 0 Тогда
		|		ТаблицаТовары.Расхождения
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ                                  КАК КЗаказу,
		|	-ТаблицаТовары.Расхождения             КАК НаличиеПодЗаказ
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.НазначениеПолучателя <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	И ТаблицаТовары.ОрдернаяСхемаПриПриемкеНаСкладеПриемки
		|	И НЕ ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостачНаСкладеПриемки
		|	И ТаблицаТовары.Действие =
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится)";
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаГрафикОтгрузкиТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикОтгрузкиТоваров";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВТТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
		//Формирование потребности для до-отгрузки из свободного остатка
		//или для отмены заказа(если доспоставка не требуется) в предварительном статусе.
		"ВЫБРАТЬ
		|	ТаблицаТовары.НомерСтроки            КАК НомерСтроки,
		|	&Период                              КАК Период,
		|	&Период                              КАК ДатаОтгрузки,
		|	
		|	ТаблицаТовары.Номенклатура           КАК Номенклатура,
		|	ТаблицаТовары.Характеристика         КАК Характеристика,
		|	ТаблицаТовары.Склад                  КАК Склад,
		|	ТаблицаТовары.Назначение             КАК Назначение,
		|	
		|	ВЫБОР КОГДА ТаблицаТовары.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) ТОГДА
		|			-ТаблицаТовары.Расхождения
		|		КОНЕЦ                            КАК КоличествоПодЗаказ,
		|	
		|	ВЫБОР КОГДА ТаблицаТовары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) ТОГДА
		|			-ТаблицаТовары.Расхождения
		|		КОНЕЦ                            КАК КоличествоНеобеспечено
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Согласовано)
		|	
		|	И ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостач
		|	И ТаблицаТовары.Действие В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОтгрузитьСейчас),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется))";
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыНаСкладах(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыНаСкладах";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВТТоварыИСерии", ТекстыЗапроса) Тогда
		ТекстЗапросаВТТоварыИСерии(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
		/////////////////////////////////////////
		// Недостача при приемке (Расхождения < 0).
		//
		// Компенсация обнаруженного пересчетом товаров на складе излишка (оптимистично считая, что товар недогрузили)
		// для до-отгрузки из свободного остатка или для отмены заказа(если доспоставка не требуется).
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	ТаблицаТовары.Назначение               КАК Назначение,
		|	ТаблицаТовары.Серия                    КАК Серия,
		|
		|	-ТаблицаТовары.Расхождения              КАК ВНаличии,
		|	ЛОЖЬ                                   КАК КонтролироватьОстатки
		|ИЗ
		|	ВТТоварыИСерии КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И НЕ ТаблицаТовары.ОрдернаяСхемаПриОтгрузке
		|	И ТаблицаТовары.Действие В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОтгрузитьСейчас),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется))
		|	И ТаблицаТовары.ДляСкладаОтгрузки
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	ТаблицаТовары.Назначение               КАК Назначение,
		|	ТаблицаТовары.Серия                    КАК Серия,
		|
		|	-ТаблицаТовары.Расхождения              КАК ВНаличии,
		|	ЛОЖЬ                                   КАК КонтролироватьОстатки
		|ИЗ
		|	ВТТоварыИСерии КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И НЕ ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостач
		|	И ТаблицаТовары.Действие В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОтгрузитьСейчас),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется))
		|	И ТаблицаТовары.ДляСкладаОтгрузки
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Склад-получатель
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	ТаблицаТовары.НазначениеПолучателя     КАК Назначение,
		|	ТаблицаТовары.Серия                    КАК Серия,
		|
		|	ТаблицаТовары.Расхождения              КАК ВНаличии,
		|	ЛОЖЬ                                   КАК КонтролироватьОстатки
		|ИЗ
		|	ВТТоварыИСерии КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.ОрдернаяСхемаПриПриемке
		|	И НЕ ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостач
		|	И ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана)
		|	И НЕ ТаблицаТовары.ДляСкладаОтгрузки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		/////////////////////////////////////////
		// Излишек при приемке (Расхождения > 0).
		//
		// Обнаружение недостачи на складе (оптимистично считая, что отгрузили лишнее).
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	ТаблицаТовары.Назначение               КАК Назначение,
		|	ТаблицаТовары.Серия                    КАК Серия,
		|	
		|	ТаблицаТовары.Расхождения             КАК ВНаличии,
		|	ЛОЖЬ                                   КАК КонтролироватьОстатки
		|ИЗ
		|	ВТТоварыИСерии КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И НЕ ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостач
		|	
		|	И ТаблицаТовары.Действие В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного))
		|	И ТаблицаТовары.ДляСкладаОтгрузки
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Компенсация увеличения накладной на неордерном на отгрузку складе (оптимистично считая, что отгрузили лишнее).
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	ТаблицаТовары.Назначение               КАК Назначение,
		|	ТаблицаТовары.Серия                    КАК Серия,
		|	
		|	-ТаблицаТовары.Расхождения             КАК ВНаличии,
		|	ЛОЖЬ                                   КАК КонтролироватьОстатки
		|ИЗ
		|	ВТТоварыИСерии КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И НЕ ТаблицаТовары.ОрдернаяСхемаПриОтгрузке
		|	
		|	И ТаблицаТовары.Действие В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного))
		|	И ТаблицаТовары.ДляСкладаОтгрузки
		|
		// Склад-получатель
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	ТаблицаТовары.НазначениеПолучателя     КАК Назначение,
		|	ТаблицаТовары.Серия                    КАК Серия,
		|	
		|	-ТаблицаТовары.Расхождения             КАК ВНаличии,
		|	ЛОЖЬ                                   КАК КонтролироватьОстатки
		|ИЗ
		|	ВТТоварыИСерии КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.ОрдернаяСхемаПриПриемке 
		|	И НЕ ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостач
		|	И (ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится))
		|	И НЕ ТаблицаТовары.ДляСкладаОтгрузки";
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыКОформлениюИзлишковНедостач(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыКОформлениюИзлишковНедостач";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВТТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
		// Сторно отражения излишка на складе (Расхождения < 0)
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	ТаблицаТовары.НазначениеСкладское      КАК Назначение,
		|
		|	ВЫБОР КОГДА ТаблицаТовары.СтатусУказанияСерий = 14 ТОГДА
		|				ТаблицаТовары.Серия
		|			ИНАЧЕ
		|				ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|		КОНЕЦ                              КАК Серия,
		|	
		|	ТаблицаТовары.Расхождения              КАК КОформлениюАктов
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостач
		|	
		|	И ТаблицаТовары.Действие В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОтгрузитьСейчас),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.СкладПолучатель          КАК Склад,
		|	ТаблицаТовары.НазначениеПолучателяСкладское КАК Назначение,
		|
		|	ВЫБОР 
		|		КОГДА ТаблицаТовары.СтатусУказанияСерий = 14 
		|			ТОГДА
		|				ТаблицаТовары.Серия
		|			ИНАЧЕ
		|				ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|	КОНЕЦ	                               КАК Серия,
		|	
		|	-ТаблицаТовары.Расхождения             КАК КОформлениюАктов
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостачНаСкладеПриемки
		|	И (ТаблицаТовары.ОрдернаяСхемаПриПриемкеНаСкладеПриемки
		|		 И ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана))
		|
		| ОБЪЕДИНИТЬ ВСЕ
		|	
		// Оприходование излишков (Расхождения > 0)
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.Склад                    КАК Склад,
		|	ТаблицаТовары.НазначениеСкладское      КАК Назначение,
		|
		|	ВЫБОР КОГДА ТаблицаТовары.СтатусУказанияСерий = 14 ТОГДА
		|				ТаблицаТовары.Серия
		|			ИНАЧЕ
		|				ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|		КОНЕЦ                              КАК Серия,
		|	
		|	-ТаблицаТовары.Расхождения             КАК КОформлениюАктов
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостач
		|	
		|	И ТаблицаТовары.Действие В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного))
		|
		| ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Период                                КАК Период,
		|	
		|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
		|	ТаблицаТовары.Характеристика           КАК Характеристика,
		|	ТаблицаТовары.СкладПолучатель          КАК Склад,
		|	ТаблицаТовары.НазначениеПолучателяСкладское КАК Назначение,
		|
		|	ВЫБОР 
		|		КОГДА ТаблицаТовары.СтатусУказанияСерий = 14 
		|			ТОГДА
		|				ТаблицаТовары.Серия
		|			ИНАЧЕ
		|				ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|	КОНЕЦ	                               КАК Серия,
		|	
		|	ТаблицаТовары.Расхождения             КАК КОформлениюАктов
		|ИЗ
		|	ВТТовары КАК ТаблицаТовары
		|ГДЕ
		|	&Статус В(
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
		|	
		|	И ТаблицаТовары.ОрдернаяСхемаПриОтраженииИзлишковНедостачНаСкладеПриемки
		|	И (ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится)
		|			И ТаблицаТовары.ОрдернаяСхемаПриПриемкеНаСкладеПриемки)";
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаВТСерии(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВТСерии";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаСерии.Ссылка КАК Ссылка,
	|	ТаблицаСерии.Номенклатура КАК Номенклатура,
	|	ТаблицаСерии.Характеристика КАК Характеристика,
	|	ТаблицаСерии.Серия КАК Серия,
	|	&СкладОтправитель КАК Склад,
	|	&СкладПолучатель КАК СкладПолучатель,
	|	ТаблицаСерии.Действие КАК Действие,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаСерии.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаСерии.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ КАК Назначение,
	|	&СкладскаяОперация КАК СкладскаяОперация,
	|	ВЫБОР
	|		КОГДА СпрСклад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				И СпрСклад.ДатаНачалаОрдернойСхемыПриОтгрузке <= &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдернаяСхемаПриОтгрузке,
	|	ВЫБОР
	|		КОГДА СпрСклад.ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач
	|				И СпрСклад.ДатаНачалаОрдернойСхемыПриОтраженииИзлишковНедостач <= &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдернаяСхемаПриОтраженииИзлишковНедостач,
	|	ВЫБОР
	|		КОГДА СкладПолучатель.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				И СкладПолучатель.ДатаНачалаОрдернойСхемыПриОтгрузке <= &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдернаяСхемаПриОтгрузкеНаСкладеПриемки,
	|	ВЫБОР
	|		КОГДА СкладПолучатель.ИспользоватьОрдернуюСхемуПриПоступлении
	|				И СкладПолучатель.ДатаНачалаОрдернойСхемыПриПоступлении <= &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдернаяСхемаПриПриемкеНаСкладеПриемки,
	|	ВЫБОР
	|		КОГДА СкладПолучатель.ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач
	|				И СкладПолучатель.ДатаНачалаОрдернойСхемыПриОтраженииИзлишковНедостач <= &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдернаяСхемаПриОтраженииИзлишковНедостачНаСкладеПриемки,
	|	ВЫБОР
	|		КОГДА НЕ СпрСклад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				ИЛИ СпрСклад.ДатаНачалаОрдернойСхемыПриОтгрузке > &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоСкладскоеДвижение,
	|	ВЫБОР
	|		КОГДА НЕ СкладПолучатель.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				ИЛИ СкладПолучатель.ДатаНачалаОрдернойСхемыПриОтгрузке > &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоСкладскоеДвижениеНаСкладеПриемки,
	|	ТаблицаСерии.Количество - ТаблицаСерии.КоличествоПоДокументу КАК Расхождения
	|ПОМЕСТИТЬ ВТСерии
	|ИЗ
	|	Документ.АктОРасхожденияхПослеПеремещения.Серии КАК ТаблицаСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклад
	|		ПО (СпрСклад.Ссылка = &СкладОтправитель)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СкладПолучатель
	|		ПО (СкладПолучатель.Ссылка = &СкладПолучатель)
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И ТаблицаСерии.Количество <> ТаблицаСерии.КоличествоПоДокументу
	|	И НЕ ТаблицаСерии.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана)
	|	И НЕ ТаблицаСерии.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится)
	|	И &Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению), ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаСерии.Ссылка КАК Ссылка,
	|	ТаблицаСерии.Номенклатура,
	|	ТаблицаСерии.Характеристика,
	|	ТаблицаСерии.Серия,
	|	&СкладОтправитель,
	|	&СкладПолучатель,
	|	ТаблицаСерии.Действие,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаСерии.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаСерии.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ,
	|	&СкладскаяОперация,
	|	ВЫБОР
	|		КОГДА СпрСклад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				И СпрСклад.ДатаНачалаОрдернойСхемыПриОтгрузке <= &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СпрСклад.ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач
	|				И СпрСклад.ДатаНачалаОрдернойСхемыПриОтраженииИзлишковНедостач <= &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СкладПолучатель.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				И СкладПолучатель.ДатаНачалаОрдернойСхемыПриОтгрузке <= &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдернаяСхемаПриОтгрузкеНаСкладеПриемки,
	|	ВЫБОР
	|		КОГДА СкладПолучатель.ИспользоватьОрдернуюСхемуПриПоступлении
	|				И СкладПолучатель.ДатаНачалаОрдернойСхемыПриПоступлении <= &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдернаяСхемаПриПриемкеНаСкладеПриемки,
	|	ВЫБОР
	|		КОГДА СкладПолучатель.ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач
	|				И СкладПолучатель.ДатаНачалаОрдернойСхемыПриОтраженииИзлишковНедостач <= &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдернаяСхемаПриОтраженииИзлишковНедостачНаСкладеПриемки,
	|	ВЫБОР
	|		КОГДА НЕ СпрСклад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				ИЛИ СпрСклад.ДатаНачалаОрдернойСхемыПриОтгрузке > &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ СкладПолучатель.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				ИЛИ СкладПолучатель.ДатаНачалаОрдернойСхемыПриОтгрузке > &Период
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоСкладскоеДвижениеНаСкладеПриемки,
	|	ТаблицаСерии.Количество - ТаблицаСерии.КоличествоПоДокументу
	|ИЗ
	|	Документ.АктОРасхожденияхПослеПеремещения.Товары КАК ТаблицаСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклад
	|		ПО (СпрСклад.Ссылка = &СкладОтправитель)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СкладПолучатель
	|		ПО (СкладПолучатель.Ссылка = &СкладПолучатель)
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И ТаблицаСерии.КоличествоПоДокументу <> ТаблицаСерии.Количество
	|	И ТаблицаСерии.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	И НЕ ТаблицаСерии.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана)
	|	И НЕ ТаблицаСерии.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится)
	|	И &Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению), ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияСерийТоваров";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВТСерии", ТекстыЗапроса) Тогда
		ТекстЗапросаВТСерии(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	// Списание недостачи (Расхождения < 0)
	"ВЫБРАТЬ
	|	&Период                                КАК Период,
	|	&Ссылка                                КАК Документ,
	|	
	|	ТаблицаСерии.Номенклатура             КАК Номенклатура,
	|	ТаблицаСерии.Характеристика           КАК Характеристика,
	|	ТаблицаСерии.Склад                    КАК Отправитель,
	|	&СкладПолучатель                       КАК Получатель,
	|	ТаблицаСерии.Назначение               КАК Назначение,
	|	ТаблицаСерии.Серия                    КАК Серия,
	|	ТаблицаСерии.ЭтоСкладскоеДвижение     КАК ЭтоСкладскоеДвижение,
	|
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ОтражениеНедостач)
	|	                                       КАК СкладскаяОперация,
	|	
	|	ТаблицаСерии.Расхождения              КАК Количество
	|ИЗ
	|	ВТСерии КАК ТаблицаСерии
	|ГДЕ
	|	ТаблицаСерии.ОрдернаяСхемаПриОтраженииИзлишковНедостач
	|	
	|	И(ТаблицаСерии.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОтгрузитьСейчас)
	|	
	|		ИЛИ ТаблицаСерии.Действие В(
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка),
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется)))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Оприходование излишков (Расхождения > 0)
	|ВЫБРАТЬ
	|	&Период                                КАК Период,
	|	&Ссылка                                КАК Документ,
	|	
	|	ТаблицаСерии.Номенклатура             КАК Номенклатура,
	|	ТаблицаСерии.Характеристика           КАК Характеристика,
	|	ТаблицаСерии.Склад                    КАК Отправитель,
	|	&СкладПолучатель                       КАК Получатель,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	                                       КАК Назначение,
	|	ТаблицаСерии.Серия                    КАК Серия,
	|	ТаблицаСерии.ЭтоСкладскоеДвижение     КАК ЭтоСкладскоеДвижение,
	|
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ОтражениеИзлишков)
	|	                                       КАК СкладскаяОперация,
	|	
	|	ТаблицаСерии.Расхождения              КАК Количество
	|ИЗ
	|	ВТСерии КАК ТаблицаСерии
	|ГДЕ
	|	ТаблицаСерии.ОрдернаяСхемаПриОтраженииИзлишковНедостач
	|	
	|	И(ТаблицаСерии.Действие В(
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного),
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного)))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// До-отгрузка при недостаче на неордерном складе (Расхождения < 0)
	|ВЫБРАТЬ
	|	&Период                                КАК Период,
	|	&Ссылка                                КАК Документ,
	|
	|	ТаблицаСерии.Номенклатура             КАК Номенклатура,
	|	ТаблицаСерии.Характеристика           КАК Характеристика,
	|	ТаблицаСерии.Склад                    КАК Отправитель,
	|	&СкладПолучатель                       КАК Получатель,
	|	ТаблицаСерии.Назначение               КАК Назначение,
	|	ТаблицаСерии.Серия                    КАК Серия,
	|	ТаблицаСерии.ЭтоСкладскоеДвижение     КАК ЭтоСкладскоеДвижение,
	|	&СкладскаяОперация                     КАК СкладскаяОперация,
	|
	|	ТаблицаСерии.Расхождения              КАК Количество
	|ИЗ
	|	ВТСерии КАК ТаблицаСерии
	|ГДЕ
	|	НЕ ТаблицаСерии.ОрдернаяСхемаПриОтгрузке
	|	И ТаблицаСерии.ОрдернаяСхемаПриОтраженииИзлишковНедостач
	|	
	|	И(ТаблицаСерии.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОтгрузитьСейчас)
	|	
	|		ИЛИ ТаблицаСерии.Действие В(
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка),
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется)))
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Отражение излишка на ордерном на отгрузку складе при неордерной схеме отражения излишков, при недостаче у клиента (Расхождения < 0)
	|ВЫБРАТЬ
	|	&Период                                КАК Период,
	|	&Ссылка                                КАК Документ,
	|
	|	ТаблицаСерии.Номенклатура             КАК Номенклатура,
	|	ТаблицаСерии.Характеристика           КАК Характеристика,
	|	ТаблицаСерии.Склад                    КАК Отправитель,
	|	&СкладПолучатель                       КАК Получатель,
	|	ТаблицаСерии.Назначение               КАК Назначение,
	|	ТаблицаСерии.Серия                    КАК Серия,
	|	ТаблицаСерии.ЭтоСкладскоеДвижение     КАК ЭтоСкладскоеДвижение,
	|	&СкладскаяОперация                     КАК СкладскаяОперация,
	|	
	|	-ТаблицаСерии.Расхождения             КАК Количество
	|ИЗ
	|	ВТСерии КАК ТаблицаСерии
	|ГДЕ
	|	ТаблицаСерии.ОрдернаяСхемаПриОтгрузке
	|	И НЕ ТаблицаСерии.ОрдернаяСхемаПриОтраженииИзлишковНедостач
	|	
	|	И(ТаблицаСерии.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОтгрузитьСейчас)
	|	
	|		ИЛИ ТаблицаСерии.Действие В(
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка),
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется)))
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Компенсация накладной сверх-заказа оформляющий отгрузку излишка на неордерном складе (Расхождения > 0)
	|ВЫБРАТЬ
	|	&Период                                КАК Период,
	|	&Ссылка                                КАК Документ,
	|	
	|	ТаблицаСерии.Номенклатура             КАК Номенклатура,
	|	ТаблицаСерии.Характеристика           КАК Характеристика,
	|	ТаблицаСерии.Склад                    КАК Отправитель,
	|	&СкладПолучатель                       КАК Получатель,
	|	ТаблицаСерии.Назначение               КАК Назначение,
	|	ТаблицаСерии.Серия                    КАК Серия,
	|	ТаблицаСерии.ЭтоСкладскоеДвижение     КАК ЭтоСкладскоеДвижение,
	|	&СкладскаяОперация                     КАК СкладскаяОперация,
	|	
	|	-ТаблицаСерии.Расхождения             КАК Количество
	|ИЗ
	|	ВТСерии КАК ТаблицаСерии
	|ГДЕ
	|	НЕ ТаблицаСерии.ОрдернаяСхемаПриОтгрузке
	|	И ТаблицаСерии.ОрдернаяСхемаПриОтраженииИзлишковНедостач
	|	
	|	И ТаблицаСерии.Действие В(
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного))
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Отражение недостачи на ордерном на отгрузку складе при неордерной схеме отражения излишков, при излишке у клиента (Расхождения > 0)
	|ВЫБРАТЬ
	|	&Период                                КАК Период,
	|	&Ссылка                                КАК Документ,
	|	
	|	ТаблицаСерии.Номенклатура             КАК Номенклатура,
	|	ТаблицаСерии.Характеристика           КАК Характеристика,
	|	ТаблицаСерии.Склад                    КАК Отправитель,
	|	&СкладПолучатель                       КАК Получатель,
	|	ТаблицаСерии.Назначение               КАК Назначение,
	|	ТаблицаСерии.Серия                    КАК Серия,
	|	ТаблицаСерии.ЭтоСкладскоеДвижение     КАК ЭтоСкладскоеДвижение,
	|	&СкладскаяОперация                     КАК СкладскаяОперация,
	|	
	|	ТаблицаСерии.Расхождения              КАК Количество
	|ИЗ
	|	ВТСерии КАК ТаблицаСерии
	|ГДЕ
	|	ТаблицаСерии.ОрдернаяСхемаПриОтгрузке
	|	И НЕ ТаблицаСерии.ОрдернаяСхемаПриОтраженииИзлишковНедостач
	|	
	|	И ТаблицаСерии.Действие В(
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                КАК Период,
	|	&Ссылка                                КАК Документ,
	|	ТаблицаСерии.Номенклатура             КАК Номенклатура,
	|	ТаблицаСерии.Характеристика           КАК Характеристика,
	|	ТаблицаСерии.Склад                    КАК Отправитель,
	|	ТаблицаСерии.СкладПолучатель          КАК Получатель,
	|	ТаблицаСерии.Назначение               КАК Назначение,
	|	ТаблицаСерии.Серия                    КАК Серия,
	|	ТаблицаСерии.ЭтоСкладскоеДвижениеНаСкладеПриемки КАК ЭтоСкладскоеДвижение,
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ОтражениеНедостач) КАК СкладскаяОперация,
	|	ТаблицаСерии.Расхождения              КАК Количество
	|ИЗ
	|	ВТСерии КАК ТаблицаСерии
	|ГДЕ
	|	
	|	ТаблицаСерии.ОрдернаяСхемаПриПриемкеНаСкладеПриемки
	|	И НЕ ТаблицаСерии.ОрдернаяСхемаПриОтраженииИзлишковНедостачНаСкладеПриемки
	|	И ТаблицаСерии.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	/////////////////////////////////////////
	// Излишек при приемке (Расхождения > 0).
	//
	//Компенсация корректирвоки поступления по складу при ордерной схеме приемки
	//Оптимистично считаем, что приходным ордером уже отражен факт излишка
	|ВЫБРАТЬ
	|	&Период                                КАК Период,
	|	&Ссылка                                КАК Документ,
	|	ТаблицаСерии.Номенклатура             КАК Номенклатура,
	|	ТаблицаСерии.Характеристика           КАК Характеристика,
	|	ТаблицаСерии.Склад                    КАК Отправитель,
	|	ТаблицаСерии.СкладПолучатель          КАК Получатель,
	|	ТаблицаСерии.Назначение               КАК Назначение,
	|	ТаблицаСерии.Серия                    КАК Серия,
	|	ТаблицаСерии.ЭтоСкладскоеДвижениеНаСкладеПриемки     КАК ЭтоСкладскоеДвижение,
	|	&СкладскаяОперация КАК СкладскаяОперация,
	|	ТаблицаСерии.Расхождения              КАК Количество
	|ИЗ
	|	ВТСерии КАК ТаблицаСерии
	|ГДЕ
	|	&Статус В(
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
	|	
	|	И ТаблицаСерии.ОрдернаяСхемаПриПриемкеНаСкладеПриемки
	|	И НЕ ТаблицаСерии.ОрдернаяСхемаПриОтраженииИзлишковНедостачНаСкладеПриемки
	|	И ТаблицаСерии.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится)
	|
	| ОБЪЕДИНИТЬ ВСЕ
	|
	/////////////////////////////////////////
	// Недостача при приемке (Расхождения < 0).
	//
	// Компесация списания недостачи, оптимистично считая, что недостача уже отражена в приходном ордере
	|ВЫБРАТЬ
	|	&Период                                КАК Период,
	|	&Ссылка                                КАК Документ,
	|	ТаблицаСерии.Номенклатура             КАК Номенклатура,
	|	ТаблицаСерии.Характеристика           КАК Характеристика,
	|	ТаблицаСерии.Склад                    КАК Отправитель,
	|	ТаблицаСерии.СкладПолучатель          КАК Получатель,
	|	ТаблицаСерии.Назначение               КАК Назначение,
	|	ТаблицаСерии.Серия                    КАК Серия,
	|	ТаблицаСерии.ЭтоСкладскоеДвижениеНаСкладеПриемки     КАК ЭтоСкладскоеДвижение,
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ОтражениеНедостач) КАК СкладскаяОперация,
	|	ТаблицаСерии.Расхождения              КАК Количество
	|ИЗ
	|	ВТСерии КАК ТаблицаСерии
	|ГДЕ
	|	ТаблицаСерии.ОрдернаяСхемаПриОтраженииИзлишковНедостачНаСкладеПриемки
	|	И ТаблицаСерии.ОрдернаяСхемаПриПриемкеНаСкладеПриемки
	|	И ТаблицаСерии.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыКПоступлению(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыКПоступлению";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВТТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	/////////////////////////////////////////
	// Недостача при приемке (Расхождения < 0).
	// 
	// Для ордерной схемы при приемке
	// Компенсация Приходного ордера (оптимистично считая, что товар недоприняли)
	// при корректировке.
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
	|	&Период                                КАК Период,
	|	
	|	ТаблицаТовары.Распоряжение             КАК ДокументПоступления,
	|	
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	ТаблицаТовары.СкладПолучатель          КАК Склад,
	|	
	|	ТаблицаТовары.НазначениеСкладское      КАК Назначение,
	|	
	|	ТаблицаТовары.Расхождения              КАК КПоступлению
	|ИЗ
	|	ВТТовары КАК ТаблицаТовары
	|ГДЕ
	|	&Статус В(
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
	|	И ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана)
	|	И ТаблицаТовары.ОрдернаяСхемаПриПриемкеНаСкладеПриемки
	|		
	|ОБЪЕДИНИТЬ ВСЕ
	|
	/////////////////////////////////////////
	// Излишек при приемке (Расхождения > 0).
	//
	// Компенсация заказа/накладной на ордерном складе (оптимистично считая, что приняли лишнее).
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
	|	&Период                                КАК Период,
	|	
	|	ТаблицаТовары.Распоряжение             КАК ДокументПоступления,
	|	
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	ТаблицаТовары.СкладПолучатель          КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|	
	|	ТаблицаТовары.Расхождения              КАК КПоступлению
	|ИЗ
	|	ВТТовары КАК ТаблицаТовары
	|ГДЕ
	|	&Статус В(
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
	| 	И ТаблицаТовары.ОрдернаяСхемаПриПриемкеНаСкладеПриемки
	|	И ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОприходоватьСейчас)
	|		
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Компенсация заказа/накладной (оптимистично считая, что приняли лишнее).
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
	|	&Период                                КАК Период,
	|	
	|	ТаблицаТовары.Распоряжение             КАК ДокументПоступления,
	|	
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	ТаблицаТовары.СкладПолучатель          КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|	
	|	ТаблицаТовары.Расхождения              КАК КПоступлению
	|ИЗ
	|	ВТТовары КАК ТаблицаТовары
	|ГДЕ
	|	&Статус В(
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
	|	
	|	И ТаблицаТовары.ОрдернаяСхемаПриПриемкеНаСкладеПриемки
	|
	|	И ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится)";
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыКОформлениюПоступления(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыКОформлениюПоступления";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВТТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	/////////////////////////////////////////
	// Недостача при приемке (Расхождения < 0).
	//
	// Компенсация Приходного ордера (оптимистично считая, что товар недоприняли) при корректировке.
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
	|	&Период                                КАК Период,
	|	
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	ТаблицаТовары.СкладПолучатель          КАК Склад,
	|	ТаблицаТовары.НазначениеСкладское      КАК Назначение,
	|	ТаблицаТовары.Распоряжение             КАК ДокументПоступления,
	|
	|	ТаблицаТовары.Серия					   КАК Серия,
	|	
	|	ТаблицаТовары.Расхождения              КАК КОформлению
	|ИЗ
	|	ВТТовары КАК ТаблицаТовары
	|ГДЕ
	|	&Статус В(
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
	|	
	|	И ТаблицаТовары.ОрдернаяСхемаПриПриемкеНаСкладеПриемки
	|	
	|	И ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	/////////////////////////////////////////
	// Излишек при приемке (Расхождения > 0).
	//
	// Закрытие свободных остатков при возврате перепоставленного товара
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
	|	&Период                                КАК Период,
	|	
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	ТаблицаТовары.СкладПолучатель          КАК Склад,
	|	ТаблицаТовары.НазначениеСкладское      КАК Назначение,
	|	ТаблицаТовары.Распоряжение             КАК ДокументПоступления,
	|
	|	ТаблицаТовары.Серия 				   КАК Серия,
	|	
	|	-ТаблицаТовары.Расхождения             КАК КОформлению
	|ИЗ
	|	ВТТовары КАК ТаблицаТовары
	|ГДЕ
	|	&Статус В(
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
	|	И ТаблицаТовары.ОрдернаяСхемаПриПриемкеНаСкладеПриемки
	|	И (ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОприходоватьСейчас)
	|		ИЛИ ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится))";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
	Возврат ТекстЗапроса;	
КонецФункции

Функция ТекстЗапросаТаблицаДвижениеТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвижениеТоваров";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВТТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "
	/////////////////////////////////////////
	// Недостача при приемке (Расхождения < 0).
	//
	// Компенсация расходного ордера/накладной
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ТаблицаТовары.Распоряжение,
	|	ТаблицаТовары.СкладПолучатель КАК Склад,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.НазначениеПолучателя КАК Назначение,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.НазначениеПолучателя = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.Расхождения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПланируемоеПоступление,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.НазначениеПолучателя <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.Расхождения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПланируемоеПоступлениеПодЗаказ
	|ИЗ
	|	ВТТовары КАК ТаблицаТовары
	|ГДЕ
	|	&Статус В(
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.КВыполнению),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыАктаОРасхождениях.Отработано))
	|	И ТаблицаТовары.КодСтроки <> 0
	|	И ТаблицаТовары.ОрдернаяСхемаПриПриемкеНаСкладеПриемки
	|	И ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

// Формирует запрос проверки при смене статуса списка документов
//
// Параметры:
//	МассивДокументов - Массив - Массив ссылок на документы, которые надо проверять
//	НовыйСтатус - Строка - Имя нового статуса
//	ДополнительныеПараметры - Структура - Структура дополнительных параметров
//
// Возвращаемое значение:
//	Запрос - Запрос проверки перед сменой статуса
//
Функция СформироватьЗапросПроверкиПриСменеСтатуса(МассивДокументов, НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	ЗначениеНовогоСтатуса = Перечисления.СтатусыАктаОРасхождениях[НовыйСтатус];
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокументов.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Ссылка) КАК Представление,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Статус) КАК ПредставлениеТекущегоСтатуса,
	|	ПРЕДСТАВЛЕНИЕ(&Статус) КАК ПредставлениеНовогоСтатуса,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Статус = &Статус
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтатусСовпадает,
	|	ТаблицаДокументов.Проведен КАК Проведен,
	|	ТаблицаДокументов.ПометкаУдаления КАК ПометкаУдаления,
	|	ИСТИНА КАК ЗаписьПроведением
	|ИЗ
	|	Документ.АктОРасхожденияхПослеПеремещения КАК ТаблицаДокументов
	|ГДЕ
	|	ТаблицаДокументов.Ссылка В (&МассивДокументов)
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Статус", ЗначениеНовогоСтатуса);
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	Возврат Запрос;
	
КонецФункции

// Возвращает результат проверки при смене статуса документа
//
// Параметры:
//	ВыборкаПроверки - ВыборкаИзРезультатаЗапроса - Текущая строка выборки
//	НовыйСтатус - Перечисление - Новый статус
//	ДополнительныеПараметры - Структура - Структура дополнительных параметров
//
// Возвращаемое значение:
//	Булево - Истина, в случае успешного завершения проверки
//
Функция ПроверкаПередСменойСтатуса(ВыборкаПроверки, НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений();
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие();
	ПолноеИмяДокумента = "Документ.АктОРасхожденияхПослеПеремещения";
	
	Если ИмяРегистра = "СвободныеОстатки" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаСвободныеОстатки(Запрос, ТекстыЗапроса, ИмяРегистра);
		ТекстыЗапросаВременныхТаблиц.Вставить("ВТТовары", ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса));
		СинонимТаблицыДокумента = "ТаблицаТовары";
	
	ИначеЕсли ИмяРегистра = "ОбеспечениеЗаказов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаОбеспечениеЗаказов(Запрос, ТекстыЗапроса, ИмяРегистра);
		ТекстыЗапросаВременныхТаблиц.Вставить("ВТТовары", ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса));
		СинонимТаблицыДокумента = "ТаблицаТовары";
	
	ИначеЕсли ИмяРегистра = "ТоварыКОформлениюИзлишковНедостач" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаТоварыКОформлениюИзлишковНедостач(Запрос, ТекстыЗапроса, ИмяРегистра);
		ТекстыЗапросаВременныхТаблиц.Вставить("ВТТовары", ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса));
		СинонимТаблицыДокумента = "ТаблицаТовары";
	
	ИначеЕсли ИмяРегистра = "ТоварыНаСкладах" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаТоварыНаСкладах(Запрос, ТекстыЗапроса, ИмяРегистра);
		ТекстыЗапросаВременныхТаблиц.Вставить("ВТТоварыИСерии", ТекстЗапросаВТТоварыИСерии(Запрос, ТекстыЗапроса));
		СинонимТаблицыДокумента = "ТаблицаТовары";
		
	ИначеЕсли ИмяРегистра = "ТоварыКОтгрузке" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаТоварыКОтгрузке(Запрос, ТекстыЗапроса, ИмяРегистра);
		ТекстыЗапросаВременныхТаблиц.Вставить("ВТТовары", ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса));
		СинонимТаблицыДокумента = "ТаблицаТовары";
		
	ИначеЕсли ИмяРегистра = "ДвиженияСерийТоваров" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, ИмяРегистра);
		ТекстыЗапросаВременныхТаблиц.Вставить("ВТСерии", ТекстЗапросаВТСерии(Запрос, ТекстыЗапроса));
		СинонимТаблицыДокумента = "ТаблицаСерии";
		
	ИначеЕсли ИмяРегистра = "ДвижениеТоваров" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДвижениеТоваров(Запрос, ТекстыЗапроса, ИмяРегистра);
		ТекстыЗапросаВременныхТаблиц.Вставить("ВТТовары", ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса));
		СинонимТаблицыДокумента = "ТаблицаТовары";
		
	ИначеЕсли ИмяРегистра = "ТоварыКПоступлению" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаТоварыКПоступлению(Запрос, ТекстыЗапроса, ИмяРегистра);
		ТекстыЗапросаВременныхТаблиц.Вставить("ВТТовары", ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса));
		СинонимТаблицыДокумента = "ТаблицаТовары";
		
	ИначеЕсли ИмяРегистра = "ТоварыКОформлениюПоступления" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаТоварыКОформлениюПоступления(Запрос, ТекстыЗапроса, ИмяРегистра);
		ТекстыЗапросаВременныхТаблиц.Вставить("ВТТовары", ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса));
		СинонимТаблицыДокумента = "ТаблицаТовары";
		
	Иначе
		ТекстИсключения = НСтр("ru='В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';uk='У документі %ПолноеИмяДокумента% не реалізована адаптація тексту запиту формування рухів по регістру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("СкладскаяОперация", 
												"ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ОтражениеИзлишков)");
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(ТекстЗапроса,
																								ПолноеИмяДокумента,
																								СинонимТаблицыДокумента,
																								ПереопределениеРасчетаПараметров,
																								ТекстыЗапросаВременныхТаблиц);
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Прочее

Функция ТекстЗапросаДокументыОснованияЗаказНаПеремещение() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ПеремещениеТоваровТовары.Ссылка КАК ДокументОснование,
	|	ПеремещениеТоваровТовары.ЗаказНаПеремещение КАК Заказ,
	|	НЕОПРЕДЕЛЕНО КАК Склад
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
	|ГДЕ
	|	ПеремещениеТоваровТовары.Ссылка В(&МассивОснований)
	|ИТОГИ ПО
	|	ДокументОснование";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

КонецПроцедуры

#КонецОбласти

#КонецЕсли
