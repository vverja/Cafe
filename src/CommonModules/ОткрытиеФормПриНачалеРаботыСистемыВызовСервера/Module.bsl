
#Область СлужебныеПроцедурыИФункции

// Серверный обработчик события ОбработкаПолученияДанныхВыбора перечисления ФормыОткрываемыеПриНачалеРаботыСистемы.
//
Процедура ФормыОткрываемыеПриНачалеРаботыСистемыОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("БазоваяВерсия") Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытиеФормПриНачалеРаботыСистемы.ПолучитьМассивИспользуемыхФорм(ДанныеВыбора, Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает имя формы, которую необходимо открыть для выполнения настройки программы при первом запуске.
//
Функция ФормаНачальнойНастройкиПрограммы() Экспорт
	
	// Установим служебные константы до выполнения всех обработчиков обновления
	Если ОбновлениеИнформационнойБазы.НеобходимоОбновлениеИнформационнойБазы() Тогда
		ОбновлениеИнформационнойБазыУТ.УстановитьСлужебныеКонстанты();
	КонецЕсли;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	// Вернем имя формы помощника первого запуска
	УстановитьПривилегированныйРежим(Истина);
	
	ИмяФормы = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Количество
	|ИЗ
	|	РегистрСведений.ВерсииПодсистем КАК ВерсииПодсистем
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1
	|ИЗ
	|	РегистрСведений.УдалитьВерсииПодсистем КАК УдалитьВерсииПодсистем";
	
	Если Запрос.Выполнить().Пустой() Тогда
		
		Если СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации() Тогда
			ИмяФормы = "";
		ИначеЕсли ОбщегоНазначенияУТ.ИДКонфигурации() = "BASERP" Тогда
			ИмяФормы = "Обработка.ПомощникЗаполненияНастроекИСправочников.Форма.Форма"; // первый запуск BAS ERP
		ИначеЕсли ОбщегоНазначенияУТ.ИДКонфигурации() = "BASКУП" Тогда
			ИмяФормы = ""; // первый запуск BAS КУП
		Иначе
			ИмяФормы = "ОбщаяФорма.ВыборВариантаНастройкиПрограммы"; // первый запуск BAS УТ3
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИмяФормы;
	
КонецФункции

#КонецОбласти
