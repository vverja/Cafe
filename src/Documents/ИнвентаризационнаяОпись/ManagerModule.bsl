#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт



КонецПроцедуры

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт

	 
	Если ПравоДоступа("Добавление", Метаданные.Документы.ИнвентаризационнаяОпись) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.ИнвентаризационнаяОпись.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.ИнвентаризационнаяОпись);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

	Возврат; //В дальнейшем будет добавлен код команд

КонецПроцедуры


//Возвращает список организаций, по которым печатают инвентаризационные описи
//	Параметры:
//		МассивДокументов - Массив - массви сслылок на инвентаризационные описи
//
//	Возвращаемые значения:
//		СписокЗначений - список организаций
//
Функция СписокОрганизацийИзДокументов(МассивДокументов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ИнвентаризационнаяОпись.Организация
	|ИЗ
	|	Документ.ИнвентаризационнаяОпись КАК ИнвентаризационнаяОпись
	|ГДЕ
	|	ИнвентаризационнаяОпись.Ссылка В (&МассивДокументов)";
	
	МассивОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");
	
	СписокОрганизаций = Новый СписокЗначений;
	СписокОрганизаций.ЗагрузитьЗначения(МассивОрганизаций);
	
	Возврат СписокОрганизаций;
	
КонецФункции

// Возвращает текст запроса для записи данных в регистр сведений ДанныеВнутреннихДокументов
//
//	Возвращаемое значение:
//		Строка - текст запроса
//
Функция ТекстЗапросаДанныеВнутреннихДокументов() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Номер,
	|	Документ.Дата,
	|	Документ.Организация,
	|	Документ.Склад,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	Документ.Ответственный,
	|	Документ.Комментарий,
	|	"""" КАК Дополнительно,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
	|	"""" КАК Статус,
	|	Документ.Проведен,
	|	Документ.ПометкаУдаления
	|ИЗ
	|	Документ.ИнвентаризационнаяОпись КАК Документ
	|ГДЕ
	|	Документ.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Если ПравоДоступа("Изменение", Метаданные.Документы.ИнвентаризационнаяОпись) Тогда
		
		
 		// Инвентаризационная опись (М-21)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьМ21";
		КомандаПечати.Идентификатор = "М21";
		КомандаПечати.Представление = НСтр("ru='Инвентаризационная опись (М-21)';uk='Інвентаризаційний опис (М-21)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		
	КонецЕсли;

КонецПроцедуры

// Функция получает данные для формирования печатной формы ИНВ21
//
Функция ПолучитьДанныеДляПечатнойФормыМ21(ПараметрыПечати, ДокументОснование) Экспорт
		
	УстановитьПривилегированныйРежим(Истина);
	
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
		
	ЗапросПредварительныхДанных = Новый Запрос;
	ЗапросПредварительныхДанных.Текст = 
	"ВЫБРАТЬ
	|	ИнвентаризационнаяОпись.Дата КАК Дата,
	|	ИнвентаризационнаяОпись.ДатаНачала,
	|	ИнвентаризационнаяОпись.ДатаОкончания,
	|	ИнвентаризационнаяОпись.ВидЦены,
	|	ИнвентаризационнаяОпись.Склад,
	|	ИнвентаризационнаяОпись.ИсточникИнформацииОЦенахДляПечати,
	|	ИнвентаризационнаяОпись.Организация,
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.ПредварительныйРасчет КАК ПредварительныйРасчет
	|ИЗ
	|	Документ.ИнвентаризационнаяОпись КАК ИнвентаризационнаяОпись
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		ПО (РасчетСебестоимостиТоваровОрганизации.Организация = ИнвентаризационнаяОпись.Организация)
	|			И (РасчетСебестоимостиТоваровОрганизации.Ссылка.Проведен)
	|			И (РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ИнвентаризационнаяОпись.ДатаНачала, МЕСЯЦ) И КОНЕЦПЕРИОДА(ИнвентаризационнаяОпись.ДатаОкончания, МЕСЯЦ))
	|ГДЕ
	|	ИнвентаризационнаяОпись.Ссылка = &ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата УБЫВ";
	
	ЗапросПредварительныхДанных.УстановитьПараметр("ДокументОснование", ДокументОснование);	
 	ПредварительныеДанныеРезультат = ЗапросПредварительныхДанных.Выполнить().Выбрать();
		
	Если НЕ ПредварительныеДанныеРезультат.Следующий() Тогда
		ТекстСообщения = НСтр("ru='Печать %Документ% не выполнена: в указанный инвентаризационный период не найдены пересчеты товаров.';uk='Друк %Документ% не виконаний: у зазначений інвентаризаційний період не знайдені перерахунки товарів.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", Строка(ДокументОснование));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,ДокументОснование);
		Возврат Неопределено
	КонецЕсли;
	
	ПредварительныйРасчет 			  = ПредварительныеДанныеРезультат.ПредварительныйРасчет;
	ИсточникИнформацииОЦенахДляПечати = ПредварительныеДанныеРезультат.ИсточникИнформацииОЦенахДляПечати;
	ДатаНачала 						  = ПредварительныеДанныеРезультат.ДатаНачала;
	ДатаОкончания 					  = КонецДня(ПредварительныеДанныеРезультат.ДатаОкончания);
	Склад							  = ПредварительныеДанныеРезультат.Склад;	
	Организация 					  = ПредварительныеДанныеРезультат.Организация;
	ВидЦены 						  = ПредварительныеДанныеРезультат.ВидЦены;
				
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", 	   ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", 	   ДатаОкончания);
	Запрос.УстановитьПараметр("Склад", 			   Склад);
	Запрос.УстановитьПараметр("ДатаОстатков", 	   ДатаОкончания+1);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("Организация", 	   Организация);
	Запрос.УстановитьПараметр("ВидЦены", 		   ВидЦены);
	
	УчитыватьСебестоимостьТоваровПоВидамЗапасов = ПолучитьФункциональнуюОпцию("УчитыватьСебестоимостьТоваровПоВидамЗапасов");
	Запрос.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоВидамЗапасов", УчитыватьСебестоимостьТоваровПоВидамЗапасов);
	
	
	Если ИсточникИнформацииОЦенахДляПечати = Перечисления.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости Тогда
		
		Если ПредварительныйРасчет = Null Тогда
			
			ТекстСообщения = НСтр("ru='Не удалось получить цены по себестоимости для документа %Документ%: с %ПериодС% по %ПериодПо% не произведен расчет себестоимости.';uk='Не вдалося отримати ціни за собівартістю для документа %Документ%: з %ПериодС% по %ПериодПо% не проведений розрахунок собівартості.'");
			
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", ДокументОснование);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПериодС%", Формат(НачалоМесяца(ДатаНачала),"ДЛФ=DD"));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПериодПо%", Формат(КонецМесяца(ДатаОкончания),"ДЛФ=DD"));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
			Возврат Неопределено;
			
		КонецЕсли;
		
		Если ПредварительныйРасчет Тогда
			
			ТекстСообщения = НСтр("ru='При печати документа %Документ% использовались данные предварительного расчета себестоимости.';uk='При друку документа %Документ% використовувалися дані попереднього розрахунку собівартості.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", ДокументОснование);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
			ТекстЗапроса =
			"ВЫБРАТЬ 
			|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
			|	&Организация КАК Организация
			|ПОМЕСТИТЬ ТаблицаНоменклатуры
			|ИЗ
			|	Документ.ПересчетТоваров.Товары КАК ПересчетТоваровТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
			|		ПО ПересчетТоваровТовары.Номенклатура = Аналитика.Номенклатура
			|			И ПересчетТоваровТовары.Характеристика = Аналитика.Характеристика
			|			И (ПересчетТоваровТовары.Серия = Аналитика.Серия
			|				ИЛИ НЕ ПересчетТоваровТовары.СтатусУказанияСерий = 14)
			|			И ПересчетТоваровТовары.Ссылка.Склад = Аналитика.Склад
			|ГДЕ
			|	ПересчетТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ПересчетТоваровТовары.Ссылка.Склад = &Склад
			|	И ПересчетТоваровТовары.Ссылка.Проведен
			|	И ПересчетТоваровТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПересчетовТоваров.Выполнено)
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ 
			|	СписаниеНедостачТоваровТовары.АналитикаУчетаНоменклатуры,
			|	&Организация
			|ИЗ
			|	Документ.СписаниеНедостачТоваров.Товары КАК СписаниеНедостачТоваровТовары
			|ГДЕ
			|	СписаниеНедостачТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И СписаниеНедостачТоваровТовары.Ссылка.Склад = &Склад
			|	И СписаниеНедостачТоваровТовары.Ссылка.Проведен
			|	И СписаниеНедостачТоваровТовары.Ссылка.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ 
			|	ОприходованиеИзлишковТоваровТовары.АналитикаУчетаНоменклатуры,
			|	&Организация
			|ИЗ
			|	Документ.ОприходованиеИзлишковТоваров.Товары КАК ОприходованиеИзлишковТоваровТовары
			|ГДЕ
			|	ОприходованиеИзлишковТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Склад = &Склад
			|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Проведен
			|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ 
			|	ПересортицаТоваровТовары.АналитикаУчетаНоменклатуры,
			|	&Организация
			|ИЗ
			|	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
			|ГДЕ
			|	ПересортицаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ПересортицаТоваровТовары.Ссылка.Склад = &Склад
			|	И ПересортицаТоваровТовары.Ссылка.Проведен
			|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТоварыОрганизацийОстатки.ВидЗапасов,
			|	ТоварыОрганизацийОстатки.КоличествоОстаток
			|ПОМЕСТИТЬ ТоварыОрганизацийОстатки
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций.Остатки(&ДатаОстатков, Организация = &Организация,
			|			АналитикаУчетаНоменклатуры В
			|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|						ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры
			|					ИЗ
			|						ТаблицаНоменклатуры)
			|   ) КАК ТоварыОрганизацийОстатки
			|ГДЕ
			|	(&УчитыватьСебестоимостьТоваровПоВидамЗапасов
			|				И ТоварыОрганизацийОстатки.ВидЗапасов.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Материал))
			|			ИЛИ НЕ &УчитыватьСебестоимостьТоваровПоВидамЗапасов)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТоварыОрганизацийОстатки.ВидЗапасов КАК ВидЗапасов,
			|	ТоварыОрганизацийОстатки.КоличествоОстаток КАК КоличествоФактическое
			|ПОМЕСТИТЬ ТоварыИнвентаризации
			|ИЗ
			|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыОрганизацийОстатки КАК ТоварыОрганизацийОстатки
			|		ПО ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры = ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварыИнвентаризации.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТоварыИнвентаризации.ВидЗапасов КАК ВидЗапасов,
			|	&Организация КАК Организация,
			|	СУММА(ТоварыИнвентаризации.КоличествоФактическое) КАК КоличествоФактическое
			|ПОМЕСТИТЬ ТаблицаИнвентаризации
			|ИЗ
			|	ТоварыИнвентаризации КАК ТоварыИнвентаризации
			|
			|СГРУППИРОВАТЬ ПО
			|	ТоварыИнвентаризации.АналитикаУчетаНоменклатуры,
			|	ТоварыИнвентаризации.ВидЗапасов
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).НаименованиеСокращенное КАК ОрганизацияПредставление,
			|	ИнвентаризационнаяОпись.Склад.Наименование КАК ПредставлениеСклада,
			|	ИнвентаризационнаяОпись.ПредседательКомиссии КАК ПредседательКомиссии,
			|	ИнвентаризационнаяОпись.ЧленКомиссии1 КАК ЧленКомиссии1,
			|	ИнвентаризационнаяОпись.ЧленКомиссии2 КАК ЧленКомиссии2,
			|	ИнвентаризационнаяОпись.ЧленКомиссии3 КАК ЧленКомиссии3,
			|	ИнвентаризационнаяОпись.ДолжностьПредседателяКомиссии КАК ДолжностьПредседателяКомиссии,
			|	ИнвентаризационнаяОпись.ДолжностьЧленаКомиссии1 КАК ДолжностьЧленаКомиссии1,
			|	ИнвентаризационнаяОпись.ДолжностьЧленаКомиссии2 КАК ДолжностьЧленаКомиссии2,
			|	ИнвентаризационнаяОпись.ДолжностьЧленаКомиссии3 КАК ДолжностьЧленаКомиссии3,
			|	ИнвентаризационнаяОпись.Склад.ТекущийОтветственный КАК Кладовщик,
			|	ИнвентаризационнаяОпись.Склад.ТекущаяДолжностьОтветственного КАК ДолжностьКладовщика,
			|	ИнвентаризационнаяОпись.Дата КАК ОснованиеДата,
			|	ИнвентаризационнаяОпись.Номер КАК ОснованиеНомер,
			|	ИнвентаризационнаяОпись.Номер КАК НомерДокумента,
			|	ИнвентаризационнаяОпись.Дата КАК ДатаДокумента,
			|	ИнвентаризационнаяОпись.Ссылка КАК Ссылка
			|ИЗ
			|	Документ.ИнвентаризационнаяОпись КАК ИнвентаризационнаяОпись
			|ГДЕ
			|	ИнвентаризационнаяОпись.Ссылка = &ДокументОснование
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЦеныНоменклатуры.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ЦеныНоменклатуры.ВидЗапасов КАК ВидЗапасов,
			|	ЦеныНоменклатуры.Организация КАК Организация,
			|	ЦеныНоменклатуры.СтоимостьРегл
			|ПОМЕСТИТЬ ЦеныНоменклатуры
			|ИЗ
			|	РегистрСведений.СтоимостьТоваров.СрезПоследних(
			|			КОНЕЦПЕРИОДА(&ДатаОстатков, ДЕНЬ),
			|			(АналитикаУчетаНоменклатуры, ВидЗапасов, РазделУчета, Организация) В
			|				(ВЫБРАТЬ
			|					Таблица.АналитикаУчетаНоменклатуры,
			|					ВЫБОР
			|						КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
			|							ТОГДА Таблица.ВидЗапасов
			|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
			|					КОНЕЦ,
			|					ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
			|					Таблица.Организация
			|				ИЗ
			|					ТаблицаИнвентаризации КАК Таблица)) КАК ЦеныНоменклатуры
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
			|	СУММА(ТаблицаИнвентаризации.КоличествоФактическое) КАК КоличествоФактическое,
			|	ЕСТЬNULL(ЦеныНоменклатуры.СтоимостьРегл, 0) КАК Цена,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.Код КАК ТоварКод,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование
			|ИЗ
			|	ТаблицаИнвентаризации КАК ТаблицаИнвентаризации
			|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныНоменклатуры КАК ЦеныНоменклатуры
			|		ПО ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры = ЦеныНоменклатуры.АналитикаУчетаНоменклатуры
			|			И (ТаблицаИнвентаризации.ВидЗапасов = ЦеныНоменклатуры.ВидЗапасов
			|					И &УчитыватьСебестоимостьТоваровПоВидамЗапасов
			|				ИЛИ НЕ &УчитыватьСебестоимостьТоваровПоВидамЗапасов)
			|			И (ЦеныНоменклатуры.Организация = &Организация)
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура,
			|	ЕСТЬNULL(ЦеныНоменклатуры.СтоимостьРегл, 0),
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика
			|
			|УПОРЯДОЧИТЬ ПО
			|	ТоварНаименование";
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Номенклатура.Код", "Номенклатура." + КолонкаКодов);
			
			Запрос.Текст = ТекстЗапроса;
			
			Результаты = Запрос.ВыполнитьПакет();
			РезультатПоШапке = Результаты[4];
			РезультатПоТабличнойЧасти = Результаты[6];
			РезультатКурсВалюты = Неопределено;
			
		Иначе
			// фактический расчет
						
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
			|	&Организация КАК Организация
			|ПОМЕСТИТЬ ТаблицаНоменклатуры
			|ИЗ
			|	Документ.ПересчетТоваров.Товары КАК ПересчетТоваровТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
			|		ПО ПересчетТоваровТовары.Номенклатура = Аналитика.Номенклатура
			|			И ПересчетТоваровТовары.Характеристика = Аналитика.Характеристика
			|			И (ПересчетТоваровТовары.Серия = Аналитика.Серия
			|				ИЛИ НЕ ПересчетТоваровТовары.СтатусУказанияСерий = 14)
			|			И ПересчетТоваровТовары.Ссылка.Склад = Аналитика.Склад
			|ГДЕ
			|	ПересчетТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ПересчетТоваровТовары.Ссылка.Склад = &Склад
			|	И ПересчетТоваровТовары.Ссылка.Проведен
			|	И ПересчетТоваровТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПересчетовТоваров.Выполнено)
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ
			|	СписаниеНедостачТоваровТовары.АналитикаУчетаНоменклатуры,
			|	&Организация
			|ИЗ
			|	Документ.СписаниеНедостачТоваров.Товары КАК СписаниеНедостачТоваровТовары
			|ГДЕ
			|	СписаниеНедостачТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И СписаниеНедостачТоваровТовары.Ссылка.Склад = &Склад
			|	И СписаниеНедостачТоваровТовары.Ссылка.Проведен
			|	И СписаниеНедостачТоваровТовары.Ссылка.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ
			|	ОприходованиеИзлишковТоваровТовары.АналитикаУчетаНоменклатуры,
			|	&Организация
			|ИЗ
			|	Документ.ОприходованиеИзлишковТоваров.Товары КАК ОприходованиеИзлишковТоваровТовары
			|ГДЕ
			|	ОприходованиеИзлишковТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Склад = &Склад
			|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Проведен
			|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Организация = &Организация
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ
			|	ПересортицаТоваровТовары.АналитикаУчетаНоменклатуры,
			|	&Организация
			|ИЗ
			|	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
			|ГДЕ
			|	ПересортицаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ПересортицаТоваровТовары.Ссылка.Склад = &Склад
			|	И ПересортицаТоваровТовары.Ссылка.Проведен
			|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТоварыОрганизацийОстатки.ВидЗапасов,
			|	ТоварыОрганизацийОстатки.КоличествоОстаток
			|ПОМЕСТИТЬ ТоварыОрганизацийОстатки
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций.Остатки(&ДатаОстатков, Организация = &Организация) КАК ТоварыОрганизацийОстатки
			|ГДЕ
			|	(&УчитыватьСебестоимостьТоваровПоВидамЗапасов
			|				И ТоварыОрганизацийОстатки.ВидЗапасов.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Материал))
			|			ИЛИ НЕ &УчитыватьСебестоимостьТоваровПоВидамЗапасов)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
 			|ВЫБРАТЬ
			|	ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТоварыОрганизацийОстатки.ВидЗапасов КАК ВидЗапасов,
			|	ТоварыОрганизацийОстатки.КоличествоОстаток КАК КоличествоФактическое
			|ПОМЕСТИТЬ ТоварыИнвентаризации
			|ИЗ
			|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыОрганизацийОстатки КАК ТоварыОрганизацийОстатки
			|		ПО ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры = ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТоварыКОформлениюОтчетовКомитенту.ВидЗапасов КАК ВидЗапасов,
			|	ТоварыКОформлениюОтчетовКомитенту.КорВидЗапасов КАК НовыйВидЗапасов,
			|	ТоварыКОформлениюОтчетовКомитенту.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
			|ПОМЕСТИТЬ ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров
			|ИЗ
			|	ТоварыИнвентаризации КАК ТоварыИнвентаризации
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту КАК ТоварыКОформлениюОтчетовКомитенту
			|		ПО (ТоварыКОформлениюОтчетовКомитенту.ВидЗапасов = ТоварыИнвентаризации.ВидЗапасов)
			|			И (ТоварыКОформлениюОтчетовКомитенту.АналитикаУчетаНоменклатуры.Номенклатура = ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура)
			|			И (ТоварыКОформлениюОтчетовКомитенту.АналитикаУчетаНоменклатуры.Характеристика = ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика)
			|			И ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Серия = ТоварыКОформлениюОтчетовКомитенту.АналитикаУчетаНоменклатуры.Серия
			|ГДЕ
			|	ТоварыИнвентаризации.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТОвар)
			|	И ТоварыКОформлениюОтчетовКомитенту.КорВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.ВидЗапасов ЕСТЬ NULL 
			|			ТОГДА ТоварыИнвентаризации.АналитикаУчетаНоменклатуры
			|		ИНАЧЕ ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.АналитикаУчетаНоменклатуры
			|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
			|	ВЫБОР
			|		КОГДА ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.ВидЗапасов ЕСТЬ NULL 
			|			ТОГДА ТоварыИнвентаризации.ВидЗапасов
			|		ИНАЧЕ ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.НовыйВидЗапасов
			|	КОНЕЦ КАК ВидЗапасов,
			|	&Организация КАК Организация,
			|	СУММА(ТоварыИнвентаризации.КоличествоФактическое) КАК КоличествоФактическое
			|ПОМЕСТИТЬ ТаблицаИнвентаризации
			|ИЗ
			|	ТоварыИнвентаризации КАК ТоварыИнвентаризации
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров КАК ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров
			|		ПО (ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.ВидЗапасов = ТоварыИнвентаризации.ВидЗапасов)
			|			И (ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.АналитикаУчетаНоменклатуры.Номенклатура = ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура)
			|			И (ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.АналитикаУчетаНоменклатуры.Характеристика = ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика)
			|			И (ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.АналитикаУчетаНоменклатуры.Серия = ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Серия)
			|
			|СГРУППИРОВАТЬ ПО
			|	ВЫБОР
			|		КОГДА ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.ВидЗапасов ЕСТЬ NULL 
			|			ТОГДА ТоварыИнвентаризации.АналитикаУчетаНоменклатуры
			|		ИНАЧЕ ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.АналитикаУчетаНоменклатуры
			|	КОНЕЦ,
			|	ВЫБОР
			|		КОГДА ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.ВидЗапасов ЕСТЬ NULL 
			|			ТОГДА ТоварыИнвентаризации.ВидЗапасов
			|		ИНАЧЕ ВидыЗапасовИАналитикиУчетаНоменклатурыКомиссионныхТоваров.НовыйВидЗапасов
			|	КОНЕЦ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СебестоимостьТоваровОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	СебестоимостьТоваровОстатки.ВидЗапасов КАК ВидЗапасов,
			|	СебестоимостьТоваровОстатки.Организация КАК Организация,
			|	ВЫБОР
			|		КОГДА СебестоимостьТоваровОстатки.КоличествоОстаток = 0
			|			ТОГДА 0
			|		ИНАЧЕ СебестоимостьТоваровОстатки.СтоимостьРеглОстаток / СебестоимостьТоваровОстатки.КоличествоОстаток
			|	КОНЕЦ КАК Цена
			|ПОМЕСТИТЬ СебестоимостьТоваровОстатки
			|ИЗ
			|	РегистрНакопления.СебестоимостьТоваров.Остатки(
			|			&ДатаОстатков,
			|			(АналитикаУчетаНоменклатуры, ВидЗапасов, Организация) В
			|				(ВЫБРАТЬ
			|					Таблица.АналитикаУчетаНоменклатуры,
			|					ВЫБОР
			|						КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
			|							ТОГДА Таблица.ВидЗапасов
			|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
			|					КОНЕЦ,
			|					Таблица.Организация
			|				ИЗ
			|					ТаблицаИнвентаризации КАК Таблица)) КАК СебестоимостьТоваровОстатки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СтоимостьТоваровСрезПоследних.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	СтоимостьТоваровСрезПоследних.ВидЗапасов КАК ВидЗапасов,
			|	СтоимостьТоваровСрезПоследних.Организация КАК Организация,
			|	СтоимостьТоваровСрезПоследних.СтоимостьРегл КАК Цена
			|ПОМЕСТИТЬ СтоимостьТоваровСрезПоследних
			|ИЗ
			|	РегистрСведений.СтоимостьТоваров.СрезПоследних(
			|			&ДатаОстатков,
			|			(АналитикаУчетаНоменклатуры, ВидЗапасов, РазделУчета, Организация) В
			|				(ВЫБРАТЬ
			|					Таблица.АналитикаУчетаНоменклатуры,
			|					ВЫБОР
			|						КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
			|							ТОГДА Таблица.ВидЗапасов
			|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
			|					КОНЕЦ,
			|					ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
			|					Таблица.Организация
			|				ИЗ
			|					ТаблицаИнвентаризации КАК Таблица)) КАК СтоимостьТоваровСрезПоследних
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТаблицаИнвентаризации.ВидЗапасов КАК ВидЗапасов,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(СебестоимостьТоваровОстатки.Цена, 0) = 0
			|			ТОГДА ЕСТЬNULL(СтоимостьТоваровСрезПоследних.Цена, 0)
			|		ИНАЧЕ СебестоимостьТоваровОстатки.Цена
			|	КОНЕЦ КАК Цена
			|ПОМЕСТИТЬ ТаблицаЦен
			|ИЗ
			|	ТаблицаИнвентаризации КАК ТаблицаИнвентаризации
			|		ЛЕВОЕ СОЕДИНЕНИЕ СебестоимостьТоваровОстатки КАК СебестоимостьТоваровОстатки
			|		ПО ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры = СебестоимостьТоваровОстатки.АналитикаУчетаНоменклатуры
			|			И (ТаблицаИнвентаризации.ВидЗапасов = СебестоимостьТоваровОстатки.ВидЗапасов
			|					И &УчитыватьСебестоимостьТоваровПоВидамЗапасов
			|				ИЛИ НЕ &УчитыватьСебестоимостьТоваровПоВидамЗапасов)
			|			И (СебестоимостьТоваровОстатки.Организация = &Организация)
			|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьТоваровСрезПоследних КАК СтоимостьТоваровСрезПоследних
			|		ПО ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры = СтоимостьТоваровСрезПоследних.АналитикаУчетаНоменклатуры
			|			И (ТаблицаИнвентаризации.ВидЗапасов = СтоимостьТоваровСрезПоследних.ВидЗапасов
			|					И &УчитыватьСебестоимостьТоваровПоВидамЗапасов
			|				ИЛИ НЕ &УчитыватьСебестоимостьТоваровПоВидамЗапасов)
			|			И (СтоимостьТоваровСрезПоследних.Организация = &Организация)
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(СебестоимостьТоваровОстатки.Цена, 0) = 0
			|			ТОГДА ЕСТЬNULL(СтоимостьТоваровСрезПоследних.Цена, 0)
			|		ИНАЧЕ СебестоимостьТоваровОстатки.Цена
			|	КОНЕЦ,
			|	ТаблицаИнвентаризации.ВидЗапасов
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).НаименованиеСокращенное КАК ОрганизацияПредставление,
			|	ИнвентаризационнаяОпись.Склад.ТекущийОтветственный КАК Кладовщик,
			|	ИнвентаризационнаяОпись.Склад.ТекущаяДолжностьОтветственного КАК ДолжностьКладовщика,
			|	ИнвентаризационнаяОпись.Склад.Наименование КАК ПредставлениеСклада,
			|	ИнвентаризационнаяОпись.ПредседательКомиссии КАК ПредседательКомиссии,
			|	ИнвентаризационнаяОпись.ЧленКомиссии1 КАК ЧленКомиссии1,
			|	ИнвентаризационнаяОпись.ЧленКомиссии2 КАК ЧленКомиссии2,
			|	ИнвентаризационнаяОпись.ЧленКомиссии3 КАК ЧленКомиссии3,
			|	ИнвентаризационнаяОпись.ДолжностьПредседателяКомиссии КАК ДолжностьПредседателяКомиссии,
			|	ИнвентаризационнаяОпись.ДолжностьЧленаКомиссии1 КАК ДолжностьЧленаКомиссии1,
			|	ИнвентаризационнаяОпись.ДолжностьЧленаКомиссии2 КАК ДолжностьЧленаКомиссии2,
			|	ИнвентаризационнаяОпись.ДолжностьЧленаКомиссии3 КАК ДолжностьЧленаКомиссии3,
			|	ИнвентаризационнаяОпись.Дата КАК ОснованиеДата,
			|	ИнвентаризационнаяОпись.Номер КАК ОснованиеНомер,
			|	ИнвентаризационнаяОпись.Номер КАК НомерДокумента,
			|	ИнвентаризационнаяОпись.Дата КАК ДатаДокумента,
			|	ИнвентаризационнаяОпись.Ссылка КАК Ссылка
			|ИЗ
			|	Документ.ИнвентаризационнаяОпись КАК ИнвентаризационнаяОпись
			|ГДЕ
			|	ИнвентаризационнаяОпись.Ссылка = &ДокументОснование
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
			|	СУММА(ТаблицаИнвентаризации.КоличествоФактическое) КАК КоличествоФактическое,
			|	ТаблицаЦен.Цена КАК Цена,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура.Код КАК ТоварКод
			|ИЗ
			|	ТаблицаИнвентаризации КАК ТаблицаИнвентаризации
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЦен КАК ТаблицаЦен
			|		ПО ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры = ТаблицаЦен.АналитикаУчетаНоменклатуры
			|			И ТаблицаИнвентаризации.ВидЗапасов = ТаблицаЦен.ВидЗапасов
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура,
			|	ТаблицаЦен.Цена,
			|	ТаблицаИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика
			|
			|УПОРЯДОЧИТЬ ПО
			|	ТоварНаименование";
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Номенклатура.Код", "Номенклатура." + КолонкаКодов);
			
			Запрос.Текст = ТекстЗапроса;
			
			Результаты = Запрос.ВыполнитьПакет();
			РезультатПоШапке = Результаты[8];
			РезультатПоТабличнойЧасти = Результаты[9];
			РезультатКурсВалюты = Неопределено;
			
 		КонецЕсли;		
		
	ИначеЕсли ИсточникИнформацииОЦенахДляПечати = Перечисления.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен Тогда 
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
		|	&Организация КАК Организация
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ
		|	Документ.ПересчетТоваров.Товары КАК ПересчетТоваровТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
		|		ПО ПересчетТоваровТовары.Номенклатура = Аналитика.Номенклатура
		|			И ПересчетТоваровТовары.Характеристика = Аналитика.Характеристика
		|			И (ПересчетТоваровТовары.Серия = Аналитика.Серия
		|				ИЛИ НЕ ПересчетТоваровТовары.СтатусУказанияСерий = 14)
		|			И ПересчетТоваровТовары.Ссылка.Склад = Аналитика.Склад
		|ГДЕ
		|	ПересчетТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ПересчетТоваровТовары.Ссылка.Склад = &Склад
		|	И ПересчетТоваровТовары.Ссылка.Проведен
		|	И ПересчетТоваровТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПересчетовТоваров.Выполнено)
		|
		|ОБЪЕДИНИТЬ 
		|
		|ВЫБРАТЬ
		|	СписаниеНедостачТоваровТовары.АналитикаУчетаНоменклатуры,
		|	&Организация
		|ИЗ
		|	Документ.СписаниеНедостачТоваров.Товары КАК СписаниеНедостачТоваровТовары
		|ГДЕ
		|	СписаниеНедостачТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И СписаниеНедостачТоваровТовары.Ссылка.Склад = &Склад
		|	И СписаниеНедостачТоваровТовары.Ссылка.Проведен
		|	И СписаниеНедостачТоваровТовары.Ссылка.Организация = &Организация
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ОприходованиеИзлишковТоваровТовары.АналитикаУчетаНоменклатуры,
		|	&Организация
		|ИЗ
		|	Документ.ОприходованиеИзлишковТоваров.Товары КАК ОприходованиеИзлишковТоваровТовары
		|ГДЕ
		|	ОприходованиеИзлишковТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Склад = &Склад
		|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Проведен
		|	И ОприходованиеИзлишковТоваровТовары.Ссылка.Организация = &Организация
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПересортицаТоваровТовары.АналитикаУчетаНоменклатуры,
		|	&Организация
		|ИЗ
		|	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
		|ГДЕ
		|	ПересортицаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ПересортицаТоваровТовары.Ссылка.Склад = &Склад
		|	И ПересортицаТоваровТовары.Ссылка.Проведен
		|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ТоварыОрганизацийОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ ТоварыОрганизацийОстатки
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций.Остатки(&ДатаОстатков, Организация = &Организация) КАК ТоварыОрганизацийОстатки
		|ГДЕ
		|	(&УчитыватьСебестоимостьТоваровПоВидамЗапасов
		|				И ТоварыОрганизацийОстатки.ВидЗапасов.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Материал))
		|			ИЛИ НЕ &УчитыватьСебестоимостьТоваровПоВидамЗапасов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		//|ВЫБРАТЬ
		//|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		//|	-ТоварыОрганизаций.Количество КАК КоличествоОтклонение,
		//|	0 КАК КоличествоФактическое
		//|ПОМЕСТИТЬ ТоварыИнвентаризации
		//|ИЗ
		//|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		//|ГДЕ
		//|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.СписаниеНедостачТоваров
		//|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.СписаниеНедостачТоваров).Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		//|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.СписаниеНедостачТоваров).Склад = &Склад
		//|	И ТоварыОрганизаций.Организация = &Организация
		//|
		//|ОБЪЕДИНИТЬ ВСЕ
		//|
		//|ВЫБРАТЬ
		//|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
		//|	ТоварыОрганизаций.Количество,
		//|	0
		//|ИЗ
		//|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		//|ГДЕ
		//|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ОприходованиеИзлишковТоваров
		//|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ОприходованиеИзлишковТоваров).Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		//|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ОприходованиеИзлишковТоваров).Склад = &Склад
		//|	И ТоварыОрганизаций.Организация = &Организация
		//|
		//|ОБЪЕДИНИТЬ ВСЕ
		//|
		//|ВЫБРАТЬ
		//|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
		//|	ВЫБОР
		//|		КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		//|			ТОГДА ТоварыОрганизаций.Количество
		//|		ИНАЧЕ -ТоварыОрганизаций.Количество
		//|	КОНЕЦ,
		//|	0
		//|ИЗ
		//|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		//|ГДЕ
		//|	ТоварыОрганизаций.Регистратор ССЫЛКА Документ.ПересортицаТоваров
		//|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПересортицаТоваров).Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		//|	И ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ПересортицаТоваров).Склад = &Склад
		//|	И ТоварыОрганизаций.Организация = &Организация
		//|
		//|ОБЪЕДИНИТЬ ВСЕ
		//|
		//|ВЫБРАТЬ
		//|	ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры,
		//|	0,
		//|	ТоварыОрганизацийОстатки.КоличествоОстаток
		|ВЫБРАТЬ
		|	ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ТоварыОрганизацийОстатки.КоличествоОстаток КАК КоличествоФактическое
		|ПОМЕСТИТЬ ТоварыИнвентаризации
		|ИЗ
		|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыОрганизацийОстатки КАК ТоварыОрганизацийОстатки
		|		ПО ТаблицаНоменклатуры.АналитикаУчетаНоменклатуры = ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	СУММА(ТоварыИнвентаризации.КоличествоФактическое) КАК КоличествоФактическое
		|ПОМЕСТИТЬ ТаблицаИнвентаризации
		|ИЗ
		|	ТоварыИнвентаризации КАК ТоварыИнвентаризации
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Номенклатура,
		|	ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Характеристика,
		|	ТоварыИнвентаризации.АналитикаУчетаНоменклатуры.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).НаименованиеСокращенное КАК ОрганизацияПредставление,
		|	ИнвентаризационнаяОпись.Склад.Наименование КАК ПредставлениеСклада,
		|	ИнвентаризационнаяОпись.ПредседательКомиссии КАК ПредседательКомиссии,
		|	ИнвентаризационнаяОпись.ЧленКомиссии1 КАК ЧленКомиссии1,
		|	ИнвентаризационнаяОпись.ЧленКомиссии2 КАК ЧленКомиссии2,
		|	ИнвентаризационнаяОпись.ЧленКомиссии3 КАК ЧленКомиссии3,
		|	ИнвентаризационнаяОпись.ДолжностьПредседателяКомиссии КАК ДолжностьПредседателяКомиссии,
		|	ИнвентаризационнаяОпись.ДолжностьЧленаКомиссии1 КАК ДолжностьЧленаКомиссии1,
		|	ИнвентаризационнаяОпись.ДолжностьЧленаКомиссии2 КАК ДолжностьЧленаКомиссии2,
		|	ИнвентаризационнаяОпись.ДолжностьЧленаКомиссии3 КАК ДолжностьЧленаКомиссии3,
 		|	ИнвентаризационнаяОпись.Склад.ТекущийОтветственный КАК Кладовщик,
		|	ИнвентаризационнаяОпись.Склад.ТекущаяДолжностьОтветственного КАК ДолжностьКладовщика,
		|	ИнвентаризационнаяОпись.Дата КАК ОснованиеДата,
		|	ИнвентаризационнаяОпись.Номер КАК ОснованиеНомер,
		|	ИнвентаризационнаяОпись.Номер КАК НомерДокумента,
		|	ИнвентаризационнаяОпись.Дата КАК ДатаДокумента,
		|	ИнвентаризационнаяОпись.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ИнвентаризационнаяОпись КАК ИнвентаризационнаяОпись
		|ГДЕ
		|	ИнвентаризационнаяОпись.Ссылка = &ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
		|	ЦеныНоменклатуры.Характеристика КАК Характеристика,
		|	ЦеныНоменклатуры.Упаковка,
		|	ЦеныНоменклатуры.Цена
		|ПОМЕСТИТЬ ЦеныНоменклатуры
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|			КОНЕЦПЕРИОДА(&ДатаОстатков, ДЕНЬ),
		|			(ВидЦены, Номенклатура, Характеристика) В
		|				(ВЫБРАТЬ
		|					&ВидЦены,
		|					Таблица.Номенклатура,
		|					Таблица.Характеристика
		|				ИЗ
		|					ТаблицаИнвентаризации КАК Таблица)) КАК ЦеныНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИнвентаризации.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
		|	ТаблицаИнвентаризации.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
		|	ТаблицаИнвентаризации.КоличествоФактическое,
		|	ВЫБОР
		|		КОГДА НЕ &ТекстЗапросаКоэффициентУпаковки ЕСТЬ NULL 
		|				И &ТекстЗапросаКоэффициентУпаковки <> 0
		|			ТОГДА ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) / &ТекстЗапросаКоэффициентУпаковки
		|		ИНАЧЕ ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
		|	КОНЕЦ КАК Цена,
		|	ТаблицаИнвентаризации.Номенклатура.Код КАК ТоварКод,
		|	ТаблицаИнвентаризации.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование
		|ИЗ
		|	ТаблицаИнвентаризации КАК ТаблицаИнвентаризации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО ТаблицаИнвентаризации.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|			И ТаблицаИнвентаризации.Характеристика = ЦеныНоменклатуры.Характеристика
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТоварНаименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность КАК КоэффициентПересчета
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОстатков, Валюта = &ВидЦены) КАК КурсыВалютСрезПоследних";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"&ТекстЗапросаКоэффициентУпаковки",
				Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
				"ЦеныНоменклатуры.Упаковка",
				"ЦеныНоменклатуры.Номенклатура"));
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Номенклатура.Код", "Номенклатура." + КолонкаКодов);
		
		Запрос.Текст = ТекстЗапроса;
		
		Результаты = Запрос.ВыполнитьПакет();
		РезультатПоШапке = Результаты[4];
		РезультатПоТабличнойЧасти = Результаты[6];
		РезультатКурсВалюты = Результаты[7];
		
	КонецЕсли;
		
	Возврат Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти, РезультатКурсВалюты",
		РезультатПоШапке,
		РезультатПоТабличнойЧасти,
		РезультатКурсВалюты);
	
КонецФункции

//

#КонецОбласти

#КонецОбласти

#КонецЕсли
