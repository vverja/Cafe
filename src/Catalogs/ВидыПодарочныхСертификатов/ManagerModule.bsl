#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получает реквизиты объекта, которые необходимо блокировать от изменения
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Массив - блокируемые реквизиты объекта
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт

	Результат = Новый Массив;
	
	Результат.Добавить("Номинал");
	Результат.Добавить("Валюта");
	Результат.Добавить("ТипКарты");
	Результат.Добавить("ПериодДействия");
	Результат.Добавить("КоличествоПериодовДействия");
	Результат.Добавить("СегментНоменклатуры;ИспользоватьСегментНоменклатуры");
	Результат.Добавить("ЧастичнаяОплата;ЧастичнаяОплатаПереключатель");
	Результат.Добавить("ШаблоныКодовПодарочныхСертификатов");
	Результат.Добавить("СчетУчета");
	Результат.Добавить("СтатьяДоходов");
	Результат.Добавить("АналитикаДоходов");
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы


// Обработчик обновления BAS УТ 3.2.2
// Заполняет новые реквизиты СтатьяДоходов и АналитикаДоходов
Процедура ЗаполнитьСтатьюАналитикуДоходов() Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Строки.ПодарочныйСертификат.Владелец КАК Вид,
	|	МАКСИМУМ(ДанныеДокумента.Дата) КАК Дата
	|ПОМЕСТИТЬ втСрезПоследних
	|ИЗ
	|	Документ.АннулированиеПодарочныхСертификатов КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АннулированиеПодарочныхСертификатов.ПодарочныеСертификаты КАК Строки
	|		ПО ДанныеДокумента.Ссылка = Строки.Ссылка
	|			И (ДанныеДокумента.Проведен)
	|			И (Строки.ПодарочныйСертификат.Владелец.СтатьяДоходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	Строки.ПодарочныйСертификат.Владелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СрезПоследних.Вид КАК ВидСертификата,
	|	ДанныеДокумента.УдалитьСтатьяДоходов КАК СтатьяДоходов,
	|	ДанныеДокумента.УдалитьАналитикаДоходов КАК АналитикаДоходов
	|ИЗ
	|	Документ.АннулированиеПодарочныхСертификатов КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСрезПоследних КАК СрезПоследних
	|		ПО ДанныеДокумента.Дата = СрезПоследних.Дата
	|			И (ДанныеДокумента.Проведен)
	|");
	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ОбъектВида = Выборка.ВидСертификата.ПолучитьОбъект();
		ЗаполнитьЗначенияСвойств(ОбъектВида, Выборка);
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОбъектВида);
		
	КонецЦикла;
	
КонецПроцедуры



#КонецОбласти

#КонецЕсли