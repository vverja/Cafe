#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура заполняет массивы реквизитов, зависимых от хозяйственной операции документа.
//
// Параметры:
//	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Выбранная хозяйственная операция
//	МассивВсехРеквизитов - Массив - Массив всех имен реквизитов, зависимых от хозяйственной операции
//	МассивРеквизитовОперации - Массив - Массив имен реквизитов, используемых в выбранной хозяйственной операции
//
Процедура ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(ХозяйственнаяОперация, МассивВсехРеквизитов, МассивРеквизитовОперации) Экспорт
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("Организация");
	МассивВсехРеквизитов.Добавить("ХозяйственнаяОперация");
	МассивВсехРеквизитов.Добавить("Банк");
	МассивВсехРеквизитов.Добавить("Валюта");
	МассивВсехРеквизитов.Добавить("СуммаГривневая");
	МассивВсехРеквизитов.Добавить("СуммаВалютная");
	МассивВсехРеквизитов.Добавить("Курс");
	МассивВсехРеквизитов.Добавить("Комиссионные");
	МассивВсехРеквизитов.Добавить("СчетВалютный");
	МассивВсехРеквизитов.Добавить("СчетГривневый");
	МассивВсехРеквизитов.Добавить("СчетВозврата");
	МассивВсехРеквизитов.Добавить("СчетБанка");
	МассивВсехРеквизитов.Добавить("Сотрудник");
	МассивВсехРеквизитов.Добавить("Основание");
	
	МассивРеквизитовОперации = Новый Массив;
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПокупкаВалюты Тогда
		МассивРеквизитовОперации.Добавить("Организация");
		МассивРеквизитовОперации.Добавить("ХозяйственнаяОперация");
		МассивРеквизитовОперации.Добавить("Банк");
		МассивРеквизитовОперации.Добавить("Валюта");
		МассивРеквизитовОперации.Добавить("СуммаГривневая");
		МассивРеквизитовОперации.Добавить("СуммаВалютная");
		МассивРеквизитовОперации.Добавить("Курс");
		МассивРеквизитовОперации.Добавить("Комиссионные");
		МассивРеквизитовОперации.Добавить("СчетВалютный");
		МассивРеквизитовОперации.Добавить("СчетГривневый");
		МассивРеквизитовОперации.Добавить("СчетВозврата");
		МассивРеквизитовОперации.Добавить("СчетБанка");
		МассивРеквизитовОперации.Добавить("Сотрудник");
		МассивРеквизитовОперации.Добавить("Основание");
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПродажаВалюты Тогда
		МассивРеквизитовОперации.Добавить("Организация");
		МассивРеквизитовОперации.Добавить("ХозяйственнаяОперация");
		МассивРеквизитовОперации.Добавить("Банк");
		МассивРеквизитовОперации.Добавить("Валюта");
		МассивРеквизитовОперации.Добавить("СуммаГривневая");
		МассивРеквизитовОперации.Добавить("СуммаВалютная");
		МассивРеквизитовОперации.Добавить("Курс");
		МассивРеквизитовОперации.Добавить("Комиссионные");
		МассивРеквизитовОперации.Добавить("СчетВалютный");
		МассивРеквизитовОперации.Добавить("СчетГривневый");
		МассивРеквизитовОперации.Добавить("СчетВозврата");
		МассивРеквизитовОперации.Добавить("СчетБанка");
		МассивРеквизитовОперации.Добавить("Сотрудник");
	КонецЕсли;

	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ЗаявкаНаПокупкуВалюты";
	КомандаПечати.Представление = НСтр("ru='Заявка на покупку-продажу валюты';uk='Заявка на купівлю-продаж валюти'");

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаявкаНаПокупкуВалюты") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ЗаявкаНаПокупкуВалюты",
			НСтр("ru='Заявка на покупку-продажу валюты';uk='Заявка на купівлю-продаж валюти'"),
			СформироватьЗаявку(МассивОбъектов, ОбъектыПечати)
		);
	КонецЕсли;
	
	ФормированиеПечатныхФорм.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);

КонецПроцедуры

Функция СформироватьЗаявку(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	КодЯзыкаПечать = "uk";
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();

	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка КАК Ссылка,
	|	БанковскиеСчета.Владелец КАК Владелец,
	|	БанковскиеСчета.НомерСчета КАК НомерСчета,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.НаименованиеБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Наименование
	|	КОНЕЦ КАК НаименованиеБанка,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.КодБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Код
	|	КОНЕЦ КАК КодБанка,
	|	БанковскиеСчета.ТекстКорреспондента КАК ТекстКорреспондента
	|
	|ПОМЕСТИТЬ БанковскиеСчетаКонтрагентов
	|ИЗ
	|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Ссылка В (
	|		ВЫБРАТЬ
	|			Документ.СчетБанка КАК БанковскийСчетКонтрагента
	|		ИЗ
	|			Документ.ЗаявкаНаПокупкуПродажуВалюты КАК Документ
	|		ГДЕ
	|			Документ.Ссылка В (&МассивДокументов)
	|		)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка КАК Ссылка,
	|	БанковскиеСчета.Владелец КАК Владелец,
	|	БанковскиеСчета.НомерСчета КАК НомерСчета,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.НаименованиеБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Наименование
	|	КОНЕЦ КАК НаименованиеБанка,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.КодБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Код
	|	КОНЕЦ КАК КодБанка
	|
	|ПОМЕСТИТЬ БанковскиеСчетаОрганизаций
	|ИЗ
	|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Ссылка В (
	|		ВЫБРАТЬ
	|			Документ.СчетГривневый КАК БанковскийСчет
	|		ИЗ
	|			Документ.ЗаявкаНаПокупкуПродажуВалюты КАК Документ
	|		ГДЕ
	|			Документ.Ссылка В (&МассивДокументов)
	|		)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка КАК Ссылка,
	|	БанковскиеСчета.Владелец КАК Владелец,
	|	БанковскиеСчета.НомерСчета КАК НомерСчета,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.НаименованиеБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Наименование
	|	КОНЕЦ КАК НаименованиеБанка,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.КодБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Код
	|	КОНЕЦ КАК КодБанка
	|
	|ПОМЕСТИТЬ ВалютныеБанковскиеСчетаОрганизаций
	|ИЗ
	|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Ссылка В (
	|		ВЫБРАТЬ
	|			Документ.СчетВалютный КАК БанковскийСчет
	|		ИЗ
	|			Документ.ЗаявкаНаПокупкуПродажуВалюты КАК Документ
	|		ГДЕ
	|			Документ.Ссылка В (&МассивДокументов)
	|		)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка КАК Ссылка,
	|	БанковскиеСчета.Владелец КАК Владелец,
	|	БанковскиеСчета.НомерСчета КАК НомерСчета,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.НаименованиеБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Наименование
	|	КОНЕЦ КАК НаименованиеБанка,
	|	ВЫБОР КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|		БанковскиеСчета.КодБанка
	|	ИНАЧЕ
	|		БанковскиеСчета.Банк.Код
	|	КОНЕЦ КАК КодБанка
	|
	|ПОМЕСТИТЬ БанковскиеСчетаВозврата
	|ИЗ
	|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Ссылка В (
	|		ВЫБРАТЬ
	|			Документ.СчетВозврата КАК БанковскийСчет
	|		ИЗ
	|			Документ.ЗаявкаНаПокупкуПродажуВалюты КАК Документ
	|		ГДЕ
	|			Документ.Ссылка В (&МассивДокументов)
	|		)
	|;	
    |
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Номер КАК Номер,
	|	Документ.Дата КАК ДатаДокумента,
	|	Документ.ХозяйственнаяОперация КАК Операция,
	|	Документ.Организация КАК Организация,
	|	Документ.Организация.Префикс КАК Префикс,
	|	Документ.Банк КАК Банк,
	|	Документ.Валюта КАК Валюта,
	|	Документ.СуммаВалютная КАК СуммаВалютная,
	|	Документ.СуммаГривневая КАК СуммаГривневая,
	|	Документ.Комиссионные КАК Комиссионные,
	|	Документ.Курс КАК Курс,
	|	Документ.Основание КАК Основание,
	|	Документ.Организация.НаименованиеПолное КАК ПлательщикНаименование,
	|	Документ.СчетГривневый.ВариантВыводаМесяца КАК ВариантВыводаМесяца,
	|	БанковскиеСчетаОрганизаций.НомерСчета КАК СчетГривневыйНомер,
	|	БанковскиеСчетаОрганизаций.НаименованиеБанка КАК СчетГривневыйБанк,
	|	БанковскиеСчетаОрганизаций.КодБанка КАК СчетГривневыйКод,
	|	ВалютныеБанковскиеСчетаОрганизаций.НомерСчета КАК СчетВалютныйНомер,
	|	ВалютныеБанковскиеСчетаОрганизаций.НаименованиеБанка КАК СчетВалютныйБанк,
	|	ВалютныеБанковскиеСчетаОрганизаций.КодБанка КАК СчетВалютныйКод,
	|	БанковскиеСчетаКонтрагентов.НомерСчета КАК СчетБанкаНомер,
	|	БанковскиеСчетаКонтрагентов.НаименованиеБанка КАК СчетБанкаБанк,
	|	БанковскиеСчетаКонтрагентов.КодБанка КАК СчетБанкаКод,
	|	БанковскиеСчетаВозврата.НомерСчета КАК СчетВозвратаНомер,
	|	БанковскиеСчетаВозврата.НаименованиеБанка КАК СчетВозвратаБанк,
	|	БанковскиеСчетаВозврата.КодБанка КАК СчетВозвратаКод
	|ИЗ
	|	Документ.ЗаявкаНаПокупкуПродажуВалюты КАК Документ
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		БанковскиеСчетаКонтрагентов КАК БанковскиеСчетаКонтрагентов
	|	ПО
	|		Документ.СчетБанка = БанковскиеСчетаКонтрагентов.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
	|	ПО
	|		Документ.СчетГривневый = БанковскиеСчетаОрганизаций.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВалютныеБанковскиеСчетаОрганизаций КАК ВалютныеБанковскиеСчетаОрганизаций
	|	ПО
	|		Документ.СчетВалютный = ВалютныеБанковскиеСчетаОрганизаций.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		БанковскиеСчетаВозврата КАК БанковскиеСчетаВозврата
	|	ПО
	|		Документ.СчетВозврата = БанковскиеСчетаВозврата.Ссылка
	|
	|ГДЕ
	|	Документ.Ссылка В (&МассивДокументов)
	|	И Документ.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номер");
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Выборка.Операция = Перечисления.ХозяйственныеОперации.ПокупкаВалюты Тогда
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗаявкаНаПокупкуПродажуВалюты.ПФ_MXL_UK_ЗаявкаНаПокупку");
		Иначе
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗаявкаНаПокупкуПродажуВалюты.ПФ_MXL_UK_ЗаявкаНаПродажу");				
		КонецЕсли;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьМакета, Выборка.Ссылка);
		ОбластьМакета.Параметры.Заполнить(Выборка);
		
		ОбластьМакета.Параметры.Адрес = ФормированиеПечатныхФорм.ОписаниеОрганизации(ФормированиеПечатныхФорм.СведенияОЮрФизЛице(Выборка.Организация, Выборка.ДатаДокумента), "ЮридическийАдрес");
		ОбластьМакета.Параметры.Телефоны = ФормированиеПечатныхФорм.ОписаниеОрганизации(ФормированиеПечатныхФорм.СведенияОЮрФизЛице(Выборка.Организация, Выборка.ДатаДокумента), "Телефоны");
		ОбластьМакета.Параметры.Сотрудник = Пользователи.ТекущийПользователь();
		
		Если Выборка.ВариантВыводаМесяца = Перечисления.ВариантыВыводаМесяцаВДатеДокумента.Прописью Тогда
			ФорматДаты = "ДФ='дд ММММ гггг'";
		Иначе
			ФорматДаты = "ДФ='дд.ММ.гггг'";
		КонецЕсли;
		ОбластьМакета.Параметры.Дата = Формат(Выборка.ДатаДокумента, ФорматДаты + ";Л=" + КодЯзыкаПечать);
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
			ТабличныйДокумент,
			НомерСтрокиНачало,
			ОбъектыПечати,
			Выборка.Ссылка
		);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

#Область Прочее

Функция ЗаявкаИспользоваласьПриПланированииПосупленияДС(Ссылка) Экспорт
	
	Возврат Ложь; 
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
