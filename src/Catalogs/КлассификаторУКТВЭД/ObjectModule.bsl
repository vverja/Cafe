#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Перем ОбновитьНоменклатуруГТД;

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	// Наименование номенклатуры ГТД формируется автоматически и код УКТВЭД
	ОбновитьНоменклатуруГТД = (Не ЭтоНовый()) И ((Ссылка.Код <> Код));

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

  	// необходимо перезаписать (перезаполнить наименования и реквизиты) в справочнике НоменклатураГТД, если изменился код справочника
	Если ОбновитьНоменклатуруГТД Тогда
		ОбновитьНоменклатуруГТД();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьНоменклатуруГТД()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураГТД.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НоменклатураГТД КАК НоменклатураГТД
	|ГДЕ
	|	НоменклатураГТД.КодУКТВЭД = &Ссылка";
				   
	Попытка
		Справочники.НоменклатураГТД.ПоискИИсправлениеНекорректныхНаименований(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	Исключение
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Не удалось обновить данные справочника ""%1"". Отображение кода УКТВЭД в наименовании этого справочника может быть не правильным!';uk='Не вдалося оновити дані довідника ""%1"". Відображення коду УКТЗЕД у найменуванні цього довідника може бути не правильним!'"), Метаданные.Справочники.НоменклатураГТД.Синоним);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, "", , );
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

ОбновитьНоменклатуруГТД = Ложь;

#КонецЕсли