#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Групповое изменение объектов.

// Возвращает реквизиты объекта, которые разрешается редактировать
// с помощью обработки группового изменения реквизитов.
//
// Возвращаемое значение:
//  Массив - список имен реквизитов объекта.
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("СпособУстановкиКурса");
	Результат.Добавить("Наценка");
	Результат.Добавить("ОсновнаяВалюта");
	Результат.Добавить("ФормулаРасчетаКурса");
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Загрузка данных из файла

// Запрещает загрузку данных в этот справочник из подсистемы "ЗагрузкаДанныхИзФайла".
// Т.к. справочник реализует свой метод обновления данных.
//
Функция ИспользоватьЗагрузкуДанныхИзФайла() Экспорт
	Возврат Ложь;
КонецФункции

// Процедура заполняет справочник валюты значениями по умолчанию.
//
Процедура ЗаполнитьВалютыПоУмолчанию() Экспорт
	
	КлассификаторXML = Справочники.Валюты.ПолучитьМакет("КлассификаторВалют").ПолучитьТекст();
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	СоздатьВалюту("980", "гривна, гривны, гривен, ж, копейка, копейки, копеек, ж, 2", "гривня, гривні, гривень, ж, копійка, копійки, копійок, ж, 2", КлассификаторТаблица);
	СоздатьВалюту("840", "доллар, доллара, долларов, м, цент, цента, центов, м, 2", "долар, долари, доларів, м, цент, центи, центів, м, 2", КлассификаторТаблица);
	СоздатьВалюту("978", "евро, евро, евро, м, евроцент, евроцента, евроцентов, м, 2", "євро, євро, євро, м, євроцент, євроценти, євроцентів, м, 2", КлассификаторТаблица);
	
КонецПроцедуры // ЗаполнитьВалютыПоУмолчанию()

// Процедура создает элемент справочника Валюты.
//
// Параметры:
//  Код - Число - Код валюты
//  ПараметрыПрописиНаРусском - строка - параметры прописи на русском языке
//  ПараметрыПрописиНаУкраинском - строка - параметры прописи на украинском языке
//	КлассификаторТаблица - Классификатор валют
//
Процедура СоздатьВалюту(Код, ПараметрыПрописиНаРусском, ПараметрыПрописиНаУкраинском, КлассификаторТаблица) Экспорт

	СтрокаТаблицы = КлассификаторТаблица.Найти(Код, "Code");
	Если СтрокаТаблицы <> Неопределено Тогда
		
		СправочникСсылка = Справочники.Валюты.НайтиПоКоду(Код);
		Если ЗначениеЗаполнено(СправочникСсылка) Тогда
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		Иначе
			СправочникОбъект = Справочники.Валюты.СоздатьЭлемент();
		КонецЕсли;
		
		СправочникОбъект.Код = Код;
		СправочникОбъект.Наименование = СтрокаТаблицы.CodeSymbol;
		СправочникОбъект.НаименованиеПолное = СтрокаТаблицы.Name;
        СправочникОбъект.ЗагружаетсяИзИнтернета = ?(ВРЕГ(СтрокаТаблицы.FinanceLoading) = "ИСТИНА", Истина, Ложь);
		Если СправочникОбъект.ЗагружаетсяИзИнтернета Тогда
			СправочникОбъект.СпособУстановкиКурса = Перечисления.СпособыУстановкиКурсаВалюты.ЗагрузкаИзИнтернета;
		Иначе
			СправочникОбъект.СпособУстановкиКурса = Перечисления.СпособыУстановкиКурсаВалюты.РучнойВвод;
		КонецЕсли;
		СправочникОбъект.ПараметрыПрописиНаРусском = ПараметрыПрописиНаРусском;
		СправочникОбъект.ПараметрыПрописиНаУкраинском = ПараметрыПрописиНаУкраинском;

		Попытка
			СправочникОбъект.Записать();
		Исключение
		КонецПопытки;

	КонецЕсли;

КонецПроцедуры // СоздатьВалюту()

#КонецОбласти

#Область ОбработчикиСобытий


Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаСписка" И Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют") Тогда
		
		Параметры.Вставить("Ключ", ДоходыИРасходыСервер.ПолучитьВалютуУправленческогоУчета());
		ВыбраннаяФорма = "ФормаЭлемента";
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
#КонецЕсли