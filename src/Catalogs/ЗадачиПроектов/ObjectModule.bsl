#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Куратор = Пользователи.ТекущийПользователь();
	Исполнитель = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Закрыта Тогда
		
		//проверить заполнение полей
		Если Куратор <> Пользователи.ТекущийПользователь() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Задача не закрыта, так как Вы не являетесь ее куратором.';uk='Задача не закрита, оскільки Ви не є його куратором.'"),
			ЭтотОбъект,
			"Куратор",
			,
			Отказ);
		КонецЕсли;	
		
		Если ОписаниеРезультата = "" Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Задача не закрыта, так как нет описания результата.';uk='Задача не закрита, оскільки немає опису результату.'"),
			ЭтотОбъект,
			"ОписаниеРезультата",, Отказ);
		КонецЕсли;	
		
		Если ДатаНачала = '00010101' Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Задача не закрыта, так не определена дата ее начала.';uk='Задача не закрита, так не визначена дата його початку.'"),
			ЭтотОбъект,
			"ДатаНачала",, Отказ);
		КонецЕсли;
		
		Если ДатаОкончания = '00010101' Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Задача не закрыта, так не определена дата ее окончания.';uk='Задача не закрита, оскільки не визначена дата його закінчення.'"),
			ЭтотОбъект,
			"ДатаОкончания",, Отказ);
		КонецЕсли;
		
		Если Стадия <> Перечисления.СтадииЗадач.Проверена Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Задача не закрыта, так как не проверена.';uk='Задача не закрита, тому що не перевірена.'"),
			ЭтотОбъект,
			"Стадия",, Отказ);
		КонецЕсли;
		
		//установить дату окончания
		Если НЕ Отказ И (ДатаОкончания < ДатаНачала) Тогда
			ДатаОкончания = ТекущаяДата();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли