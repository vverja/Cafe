
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	МассивРеквизитовОтбора = ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
	ЗаполнитьТаблицуЗначенийИзМассива(МассивРеквизитовОтбора);
	
	ТаблицаРеквизитов.Сортировать("ИмяРеквизита");

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьВПанель(Команда)
	
	ВыполнитьПодборРеквизитов();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицареквизитов

&НаКлиенте
Процедура ТаблицаРеквизитовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРеквизитовПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРеквизитовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыполнитьПодборРеквизитов();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРеквизитовВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыполнитьПодборРеквизитов();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуЗначенийИзМассива(МассивСтрок)
	
	ТаблицаРеквизитов.Очистить();
	
	Для Каждого СтрокаМассива Из МассивСтрок Цикл
		
		СтрокаТаблицыРеквизитов = ТаблицаРеквизитов.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыРеквизитов, СтрокаМассива);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПодборРеквизитов()
	
	Если Элементы.ТаблицаРеквизитов.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивДобавляемых = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока ИЗ Элементы.ТаблицаРеквизитов.ВыделенныеСтроки Цикл
		
		ПараметрыСтроки = Новый Структура("ИмяРеквизита, ЭтоДопРеквизит, Свойство, Используется, ПредставлениеРеквизита, Используется");
		
		ДанныеСтроки = Элементы.ТаблицаРеквизитов.ДанныеСтроки(ВыделеннаяСтрока);
		ДанныеСтроки.Используется = Истина;
		
		ЗаполнитьЗначенияСвойств(ПараметрыСтроки, ДанныеСтроки);
		МассивДобавляемых.Добавить(ПараметрыСтроки);
		
	КонецЦикла;
	
	Оповестить("ПодборРеквизитовВТаблицуОтбора", Новый Структура("МассивДобавляемых", МассивДобавляемых));
	
КонецПроцедуры

#КонецОбласти
