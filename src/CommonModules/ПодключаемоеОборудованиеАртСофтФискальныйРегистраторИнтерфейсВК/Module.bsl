// Программный интерфейс подключения украинских фискальных регистраторов.
// Использует в своей работе Универсальный драйвер фискальных регистраторов компании АртСофт
// Документация по работе драйвера и примеры подключения http://www.artsoft.ua/ArtSoftFiscalPrinter.php

Функция Подключить(ОбъектДрайвера, Параметры) Экспорт
	
	Перем IP, ПортIP, ТипСоединения;
	Перем Порт, Скорость, Модель;
	
	Параметры.Свойство("ТипСоединения", ТипСоединения);
	Параметры.Свойство("IP", IP);
	Параметры.Свойство("ПортIP", ПортIP);
	Параметры.Свойство("Порт", Порт);
	Параметры.Свойство("Скорость", Скорость);
	Параметры.Свойство("Модель", Модель);
	
	Результат = Истина;
	
	// Проверка параметров
	МодельНаTCPIP = (ТипСоединения=1);	
	УсловиеОшибкиПодключения = ?(МодельНаTCPIP, IP = Неопределено Или ПортIP = Неопределено, Порт = Неопределено Или Скорость = Неопределено);
	
	Если УсловиеОшибкиПодключения ИЛИ (НЕ Параметры.Свойство("ТаблицаСоответствийНалоговыхГрупп")) Тогда
		Результат = Ложь;
	Иначе
		// Начало работы с универсальным драйвером АртСофт
		ТипМодели = ?(Лев(Модель,1)="*", 100, Число(Лев(Модель,1)));
		Успешно = ОбъектДрайвера.start(ТипМодели);
		Если НЕ Успешно Тогда
			// Завершение работы с универсальным драйвером АртСофт
			ОбъектДрайвера.stop();
			Возврат Ложь;
		КонецЕсли;
		
		// Открытие СОМ-порта или TCP/IP и соединение с регистратором
		Успешно = ?(МодельНаTCPIP, ОбъектДрайвера.openPortEx(IP,ПортIP), ОбъектДрайвера.openPort(Порт,Скорость));
		Если НЕ Успешно Тогда
			ОбъектДрайвера.closePort();
			Результат = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьОшибку(ОбъектДрайвера, ОписаниеОшибки) Экспорт
	
	ОписаниеОшибки = ОбъектДрайвера.LastErrorExText;
	Возврат ОбъектДрайвера.LastError;
	
КонецФункции

Функция Отключить(ОбъектДрайвера) Экспорт
	
	// Отключение устройства.
	ОбъектДрайвера.closePort();
	ОбъектДрайвера.stop();
	
КонецФункции

Функция ОткрытьЧек(ОбъектДрайвера, ФискальныйЧек, ЧекВозврата,  АннулироватьОткрытыйЧек, НомерЧека, НомерСмены) Экспорт
	
	Результат  = Истина;

	ИмяКассира = "";
	Если ФискальныйЧек Тогда
		// Открытие фискального чека ФР
		Если НЕ ЧекВозврата Тогда
			Успешно = ОбъектДрайвера.beginFiscalReceipt(0, ИмяКассира);
		Иначе
			Успешно = ОбъектДрайвера.beginFiscalReceipt(1, ИмяКассира);
		КонецЕсли;
	Иначе
		// Открытие нефискального (чека комментариев) ФР
		Успешно = ОбъектДрайвера.beginNonFiscal();
	КонецЕсли;
	
	Если НЕ Успешно Тогда 
		Результат = Ложь;
	Иначе

		НомерСмены = ОбъектДрайвера.SmenNum;		// Получение номера смены фискального регистратора (номер Z-отчета)
		НомерЧека = ОбъектДрайвера.LastReceiptNum+1;  // Получение номера последнего фискального чека
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

Функция ЗакрытьЧек(ОбъектДрайвера, НаличнаяОплата, ОплатаКартой, ОплатаКредитом, ОплатаСертификатом, ФискальныйЧек) Экспорт
	
	Результат  = Истина;

	// Закрытие чека
	
	// Выполнение оплат только для фискального чека
	Если ФискальныйЧек Тогда
		// ОПЛАТА БЕЗНАЛИЧНЫМ ТИПОМ
		Если ОплатаКартой > 0 Тогда
			Успешно = ОбъектДрайвера.printRecTotal(ОплатаКартой, 3);
			Если НЕ Успешно Тогда 
				Результат = Ложь;
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
		
		// ОПЛАТА БЕЗНАЛИЧНЫМ ТИПОМ
		Если ОплатаКредитом > 0 Тогда
			Успешно = ОбъектДрайвера.printRecTotal(ОплатаКредитом, 1);
			Если НЕ Успешно Тогда 
				Результат = Ложь;
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
		
		// ОПЛАТА БЕЗНАЛИЧНЫМ ТИПОМ
		Если ОплатаСертификатом > 0 Тогда
			Успешно = ОбъектДрайвера.printRecTotal(ОплатаСертификатом, 2);
			Если НЕ Успешно Тогда 
				Результат = Ложь;
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
		
		// ОПЛАТА НАЛИЧНЫМ ТИПОМ
		Если НаличнаяОплата > 0 Тогда
			Успешно = ОбъектДрайвера.printRecTotal(НаличнаяОплата, 0);
			Если НЕ Успешно Тогда 
				Результат = Ложь;
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;		
	КонецЕсли;	
	
	// ЗАКРЫТИЕ ЧЕКА
	Если ФискальныйЧек Тогда
		Успешно = ОбъектДрайвера.endFiscalReceipt(); 	// Закрытие фискального чека
	Иначе
		Успешно = ОбъектДрайвера.endNonFiscal(); 		// Закрытие нефискального чека (чека комментариев)
	КонецЕсли;
	
	Если НЕ Успешно Тогда 
		Результат = Ложь;
	КонецЕсли;

	
	Возврат Результат;
	
КонецФункции
														
Функция ОтменитьЧек(ОбъектДрайвера) Экспорт
	
	Результат  = Истина;

	// Аннулировать чек
	Результат = ОбъектДрайвера.printRecVoid();

	Возврат Результат;
	
КонецФункции

Функция НапечататьОтчетБезГашения(ОбъектДрайвера) Экспорт
	
	Результат  = Истина;

	// Печать X-отчёта фискального регистратора
	Успешно = ОбъектДрайвера.printXReport();
	Если НЕ Успешно Тогда 
		Результат = Ложь;
	Иначе
		НомерСмены = ОбъектДрайвера.SmenNum;		// Получение номера смены фискального регистратора (номер Z-отчета)
		НомерЧека = ОбъектДрайвера.LastReceiptNum;  // Получение номера последнего фискального чека
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция НапечататьОтчетСГашением(ОбъектДрайвера) Экспорт
	
	Результат  = Истина;

	// Снятие Z-отчёта фискального регистратора
    Успешно = ОбъектДрайвера.printZReport();
	Если НЕ Успешно Тогда 
		Результат = Ложь;
	Иначе
		НомерСмены = ОбъектДрайвера.SmenNum;		// Получение номера смены фискального регистратора (номер Z-отчета)
		НомерЧека = ОбъектДрайвера.LastReceiptNum;  // Получение номера последнего фискального чека
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

Функция НапечататьФискСтроку(ОбъектДрайвера, Наименование, Количество, Цена, Сумма, НомерСекции, НДС, ДопРеквизиты, Параметры) Экспорт

	ТаблицаСоответствий = Новый Массив;

	Результат = Истина;
	
	СуммоваяСкидка = - Окр(Количество*Цена - Сумма, 2);
	ПроцентнаяСкидка = 0;
	НалоговаяГруппа = 0;

	ДопРеквизиты = МенеджерОборудованияВызовСервера.ПолучитьТаблицуДопРеквизитов(ДопРеквизиты);
	
	Параметры.Свойство("ТаблицаСоответствийНалоговыхГрупп", ТаблицаСоответствий);
	Для каждого Строка из ТаблицаСоответствий Цикл
		Если Строка[1] = ДопРеквизиты[0] И Строка[2] = ДопРеквизиты[1] Тогда
			НалоговаяГруппа = Строка[0];
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если ДопРеквизиты[1] = Истина Тогда
		Если ЗначениеЗаполнено(ДопРеквизиты[2]) Тогда
			
			ПечататьВВидеШтрихкодаКодПоУКТВЭД = Параметры.КодПоУКТВЭДВВидеШтрихкода;
			
			ОбъектДрайвера.setU(ДопРеквизиты[2], ПечататьВВидеШтрихкодаКодПоУКТВЭД);
			
			Если ЗначениеЗаполнено(ДопРеквизиты[3]) Тогда 
				
				ПечататьВВидеШтрихкодаШтрихкодАкцизнойМарки = Параметры.ШтрихкодАкцизнойМаркиВВидеШтрихкода;
				
				МассивАкцизныхМарок = АкцизныеМаркиКлиентПереопределяемый.РазложитьСтрокуВМассивПодстрок(ДопРеквизиты[3]);
				
				Если МассивАкцизныхМарок.Количество() = 1 Тогда
					ОбъектДрайвера.setA(МассивАкцизныхМарок[0], ПечататьВВидеШтрихкодаШтрихкодАкцизнойМарки);
				ИначеЕсли МассивАкцизныхМарок.Количество() > 1 Тогда
					Для Индекс = 0 по МассивАкцизныхМарок.ВГраница() Цикл
						ОбъектДрайвера.setA(МассивАкцизныхМарок[Индекс], ПечататьВВидеШтрихкодаШтрихкодАкцизнойМарки);
					КонецЦикла;
				КонецЕсли;

			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Печать строки чека
	Результат = ОбъектДрайвера.printRecItem(Наименование, Цена, Количество, НалоговаяГруппа, СуммоваяСкидка, ПроцентнаяСкидка);
		
	Возврат Результат;
	
КонецФункции
																										
Функция НапечататьНефискСтроку(ОбъектДрайвера, СтрокаТекста, ФискальныйЧек) Экспорт
	
	Результат = Истина;
	
	Если ФискальныйЧек Тогда
			Результат = ОбъектДрайвера.printText(СтрокаТекста);
	Иначе
			Результат = ОбъектДрайвера.printNonFiscalText(СтрокаТекста);
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

Функция НапечататьЧекВнесенияВыемки(ОбъектДрайвера, Сумма) Экспорт

	Результат  = Истина;

	// Положительная сумма - внесение суммы, отрицательная - изъятие суммы
	Результат = ОбъектДрайвера.printRecCash(Сумма);

	Возврат Результат;
	
КонецФункции

Функция НапечататьШтрихКод(ОбъектДрайвера, ТипШтрихКода2, ШтрихКод) Экспорт
	
	 
	Результат  = Истина;
	
	ТипШК = 2; // Тип штрих-кода по умолчанию CODE128
	
	Если ТипШтрихКода2 = "EAN8" Тогда
		ТипШК = 0; // 0 - EAN8 
	ИначеЕсли ТипШтрихКода2 = "EAN13" Тогда
		ТипШК = 1; // 1 - EAN13
	ИначеЕсли ТипШтрихКода2 = "CODE128" Тогда
		ТипШК = 2; // 2 - CODE128
	Иначе
		Результат  = Ложь;
		Возврат Результат;
	КонецЕсли;

	// Функция предназначена для печати штрих-кода в чеке. 
	// Типы поддерживаемых штрих-кодов зависят от моделей и версий фискальных регистраторов. 
	// ШтрихКод – Строка текста до 128 символов для печати штрих-кода на фискальном регистраторе. 
	// ТипШК – целое число, тип штрих-кода (0 - EAN8, 1 - EAN13, 2 - CODE128) 
	Результат = ОбъектДрайвера.printBarCode(ШтрихКод, ТипШК);

	Возврат Результат;
	 
КонецФункции

Функция ОткрытьДенежныйЯщик(ОбъектДрайвера) Экспорт
	Результат  = Истина;

	// Открытие денежного ящика (сейфа)
	Результат = ОбъектДрайвера.openCashDrawer();

	Возврат Результат;
	
КонецФункции

Функция ПолучитьШиринуСтроки(ОбъектДрайвера, ШиринаСтроки) Экспорт
	Возврат 34;	
КонецФункции

Функция ТестУстройства(ОбъектДрайвера, РезультатТеста, АктивированДемоРежим, Параметры) Экспорт
	
	Результат = Подключить(ОбъектДрайвера, Параметры);

	// Дополнительные действия по проверке устройства

	Если НЕ Результат Тогда
		ПолучитьОшибку(ОбъектДрайвера, РезультатТеста);
	КонецЕсли;
	
	Отключить(ОбъектДрайвера);

	Возврат Результат;
	
КонецФункции

Функция ПолучитьНомерВерсии(ОбъектДрайвера) Экспорт
	Возврат "5.3";
	
КонецФункции

Функция ПолучитьОписание(ОбъектДрайвера, Наименование, Описание, ТипОборудования, РевизияИнтерфейса, ИнтеграционнаяБиблиотека, ОсновнойДрайверУстановлен, ПолучитьURLCкачивания) Экспорт
	Наименование = "АртСофт: Универсальный драйвер фискальных регистраторов";
	Описание = "OLE-сервер ArtSoft Универсальный драйвер РРО – предназначена для работы прикладных программ с украинскими фискальными регистраторами в операционных системах MS Windows";
	ТипОборудования = "ФискальныйРегистратор";
	ИнтеграционнаяБиблиотека = "ArtSoftFiscalPrinter.dll";
	РевизияИнтерфейса = "1.0";
	ОсновнойДрайверУстановлен = Истина;
	ПолучитьURLCкачивания = "www.artsoft.ua/download/ArtSoftFiscalPrinter.rar";
КонецФункции
																			
Функция ПолучитьПараметры(ОбъектДрайвера, ТаблицаПараметров) Экспорт
	
	ТаблицаПараметров = "";
	
	Возврат Истина;
	
КонецФункции

Функция ПолучитьДополнительныеДействия(ОбъектДрайвера, ДополнительныеДействия) Экспорт
	
	ДополнительныеДействия = "";
	
	Возврат Истина;
	
КонецФункции

Функция НапечататьПериодическийОтчетПоДатам(ОбъектДрайвера, ПериодНачало, ПериодКонец, Сокращенный) Экспорт
	
	// Тип отчета	Название отчета
	// 0			отчет по дате полный
	// 1			отчет по дате краткий
	// 2			отчет по номеру полный
	// 3			отчет по номеру краткий

	Результат  = Истина;
	Если Сокращенный Тогда
		ТипОтчета = 1;
	Иначе
		ТипОтчета = 0;
	КонецЕсли;
		
	НП = Формат(ПериодНачало, "ДФ=ддММгг");
	КП = Формат(ПериодКонец, "ДФ=ддММгг");

	Результат = ОбъектДрайвера.printPeriodicReport(ТипОтчета, НП, КП);

	Возврат Результат;
	
КонецФункции

Функция НапечататьПериодическийОтчетПоНомерам(ОбъектДрайвера, НомерНачало, НомерКонец, Сокращенный) Экспорт
	
	// Тип отчета	Название отчета
	// 0			отчет по дате полный
	// 1			отчет по дате краткий
	// 2			отчет по номеру полный
	// 3			отчет по номеру краткий

	Результат  = Истина;
	
	Если Сокращенный Тогда
		ТипОтчета = 3;
	Иначе
		ТипОтчета = 2;
	КонецЕсли;
		
	Результат = ОбъектДрайвера.printPeriodicReport(ТипОтчета, НомерНачало, НомерКонец);

	Возврат Результат;
	
КонецФункции

Функция НапечататьНулевойЧек(ОбъектДрайвера) Экспорт
	
	Результат  = Истина;

	Результат = ОбъектДрайвера.printNullReceipt();

	Возврат Результат;
	
КонецФункции

Функция НапечататьОтчетОПроданныхТоварах(ОбъектДрайвера) Экспорт
	
	//	Тип отчета	Название отчета
	//	0			отчет реализованных товаров
	//	1			отчет запрограммированных товаров
	//	2			отчет по скидкам
	//	3			отчет по налоговым ставкам
	//	4			отчет по товарным группам
	//	5			отчет по отделам
	//	6			отчет по операторам
	//	7			отчет по контрольной ленте

	Результат  = Истина;

	Результат = ОбъектДрайвера.printReport(0);

	Возврат Результат;
	
КонецФункции

Функция ВывестиИнформациюНаДисплейПокупателя(ОбъектДрайвера, МассивСтрок, СуммаСтрокой) Экспорт
	
	Результат = ОбъектДрайвера.displayText(МассивСтрок[0], Число(СуммаСтрокой) );
	Если Результат Тогда
		
		Результат = ОбъектДрайвера.displayTextAt(1, МассивСтрок[0])
				  И ОбъектДрайвера.displayTextAt(2, МассивСтрок[1]);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ОчиститьДисплейПокупателя(ОбъектДрайвера) Экспорт
	
	Результат = ОбъектДрайвера.clearText();
	
	Возврат Результат;
	
КонецФункции



