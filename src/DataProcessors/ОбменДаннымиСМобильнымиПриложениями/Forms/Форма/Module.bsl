
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ТипСвязи = "TCP|IP";
	
	Если ТипПодключения = 0 Тогда
		ТипПодключения = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	 УстановитьПараметрыНастройкиОбмена(Настройки.Получить("УзелОбмена"));
	 
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПриИзмененииТипаСвязи();
	ПриИзмененииТипаПодключения();
	
	Если УзелОбмена.Пустая() ИЛИ глКомпонентаОбменаСМобильнымиПриложениями = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТипСвязиЧислом = ТипСвязиЧислом(ТипСвязи);
	
	Попытка
		глКомпонентаОбменаСМобильнымиПриложениями.УстановитьПараметрыСвязи(КодМобильногоКомпьютера, ТипСвязиЧислом, АдресСервера, Порт, ИмяСервисаIRDA);
		Элементы.ИмяМобильнойБазы.СписокВыбора.ЗагрузитьЗначения(МассивБазКлиента());
	Исключение
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипСвязиПриИзменении(Элемент)
	
	ПриИзмененииТипаСвязи();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипСвязиОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипПодключенияПриИзменении(Элемент)
	
	ПриИзмененииТипаПодключения();
	Если ТипПодключения >1 Тогда
		АдресСервера = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УзелОбменаПриИзменении(Элемент)
	
	УстановитьПараметрыНастройкиОбмена(УзелОбмена);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастройкиКомпоненты(Команда)
	
	ОткрытьФорму("ОбщаяФорма.НастройкиКомпонентыОбменаДаннымиСКПК",,,,,, Неопределено, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбменДанными(Команда)
	
	ВыполнитьОбменДаннымиСМобильнымПриложением();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокИнформационныхБаз(Команда)
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	ПроверитьВозможностьРаботы(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСостояния = НСтр("ru='Получение списка информационных баз клиента';uk='Отримання списку інформаційних баз клієнта'");
	Состояние(ТекстСостояния,,,БиблиотекаКартинок.ОбменДаннымиСМобильнымиПриложениями);
	
	Элементы.ИмяМобильнойБазы.СписокВыбора.ЗагрузитьЗначения(МассивБазКлиента());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПриИзмененииРеквизитов

&НаКлиенте
Процедура ПриИзмененииТипаСвязи()
	
	Если ТипСвязи = "TCP|IP" ИЛИ ТипСвязи = "BlueTooth" Тогда		
		Элементы.TCPIP.Видимость = Истина;
		Элементы.COMПорт.Видимость = Ложь;
		Элементы.ИнфракрасныйПорт.Видимость = Ложь;
		Элементы.Порт.Видимость = Истина;
	ИначеЕсли ТипСвязи = "COM-порт" Тогда
		Элементы.TCPIP.Видимость = Ложь;
		Элементы.COMПорт.Видимость = Истина;
		Элементы.ИнфракрасныйПорт.Видимость = Ложь;
	ИначеЕсли ТипСвязи = "Инфракрасный порт" Тогда
		Элементы.TCPIP.Видимость = Ложь;
		Элементы.COMПорт.Видимость = Ложь;
		Элементы.ИнфракрасныйПорт.Видимость = Истина;
	ИначеЕсли ТипСвязи = "ActiveSync" Тогда
		АдресСервера = "169.254.2.1";
		Элементы.TCPIP.Видимость = Истина;
		Элементы.Порт.Видимость = Истина;
		Элементы.COMПорт.Видимость = Ложь;
		Элементы.ИнфракрасныйПорт.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииТипаПодключения()
	
	Если ТипПодключения = 1 Тогда
		АдресСервера = "127.0.0.1";
		Элементы.АдресСервера.Доступность = Ложь;
	Иначе
		Элементы.АдресСервера.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура ВыполнитьОбменДаннымиСМобильнымПриложением()
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	ПроверитьВозможностьРаботы(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОбменаДанными = "";
	
	ТекстСостояния = НСтр("ru='Инициализация сеанса обмена';uk='Ініціалізація сеансу обміну'");
	Состояние(ТекстСостояния,,,БиблиотекаКартинок.ОбменДаннымиСМобильнымиПриложениями);
	
	ТипСвязиЧислом = ТипСвязиЧислом(ТипСвязи);
	глКомпонентаОбменаСМобильнымиПриложениями.УстановитьПараметрыСвязи(КодМобильногоКомпьютера, ТипСвязиЧислом, АдресСервера, Порт, ИмяСервисаIRDA);
	
	Попытка
		глКомпонентаОбменаСМобильнымиПриложениями.НачатьСеансОбмена(КодМобильногоКомпьютера, ИмяМобильнойБазы, ИмяПользователя, 
		ПарольПользователя, ПередаватьДанныеКлиенту);
	Исключение
		ПоказатьПредупреждение(Неопределено, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
		
	глКомпонентаОбменаСМобильнымиПриложениями.УстановитьРежимНачальнойИнициализации(КодМобильногоКомпьютера, НачальнаяИнициализацияИБ);
		
	// Получение приложения, предназначенного для указанного клиента
	ПриложениеСтрокой = МобильныеПриложенияВызовСервера.ПолучитьПриложение(ИмяПользователя, КодМобильногоКомпьютера, ПараметрыОбменаДанными);
	
	// Установка приложения для текущего сеанса обмена
	Попытка
		глКомпонентаОбменаСМобильнымиПриложениями.УстановитьПриложение(КодМобильногоКомпьютера, ПриложениеСтрокой);
	Исключение
		ПоказатьПредупреждение(Неопределено, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;	
	
	ТекстСостояния = НСтр("ru='Получение данных от клиента';uk='Отримання даних від клієнта'");
	Состояние(ТекстСостояния,,,БиблиотекаКартинок.ОбменДаннымиСМобильнымиПриложениями);
	
	// Получение данных от клиента
	Попытка
		ДанныеКлиента = глКомпонентаОбменаСМобильнымиПриложениями.ПолучитьДанныеКлиента(КодМобильногоКомпьютера);
	Исключение
		ПоказатьПредупреждение(Неопределено, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
			
	ТекстСостояния = НСтр("ru='Запись полученных данных';uk='Запис отриманих даних'");
	Состояние(ТекстСостояния,,,БиблиотекаКартинок.ОбменДаннымиСМобильнымиПриложениями);
	
	//Запись полученных данных в базу
	Попытка
		МобильныеПриложенияВызовСервера.ЗаписатьДанные(ИмяПользователя, КодМобильногоКомпьютера, ДанныеКлиента, ПараметрыОбменаДанными);
	Исключение
		ПоказатьПредупреждение(Неопределено, ИнформацияОбОшибке().Описание);
		Возврат;
	КонецПопытки;
	
	Если ПередаватьДанныеКлиенту Тогда
		
		ТекстСостояния = НСтр("ru='Формирование пакета обмена для клиента';uk='Формування пакету обміну для клієнта'");
		Состояние(ТекстСостояния,,,БиблиотекаКартинок.ОбменДаннымиСМобильнымиПриложениями);
		
		ДанныеДляОбмена = МобильныеПриложенияВызовСервера.ПолучитьДанные(ИмяПользователя, КодМобильногоКомпьютера, НачальнаяИнициализацияИБ, ПараметрыОбменаДанными);
		
		ТекстСостояния = НСтр("ru='Передача данных клиенту';uk='Передача даних клієнту'");
		Состояние(ТекстСостояния,,,БиблиотекаКартинок.ОбменДаннымиСМобильнымиПриложениями);
		
		Попытка
			глКомпонентаОбменаСМобильнымиПриложениями.ПередатьДанныеКлиенту(КодМобильногоКомпьютера, ДанныеДляОбмена);
		Исключение
			ПоказатьПредупреждение(Неопределено, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат;
		КонецПопытки;
		
	КонецЕсли;
	
	ТекстСостояния = НСтр("ru='Регистрация получения данных клиентом';uk='Реєстрація отримання даних клієнтом'");
	Состояние(ТекстСостояния,,,БиблиотекаКартинок.ОбменДаннымиСМобильнымиПриложениями);
	
	Попытка
		МобильныеПриложенияВызовСервера.ЗарегистрироватьПолучениеДанных(ИмяПользователя, КодМобильногоКомпьютера, ПараметрыОбменаДанными);
	Исключение
		ПоказатьПредупреждение(Неопределено, ИнформацияОбОшибке().Описание);
		Возврат;
	КонецПопытки;
	
	ТекстСостояния = НСтр("ru='Завершение сеанса обмен';uk='Завершення сеансу обмін'");
	Состояние(ТекстСостояния,,,БиблиотекаКартинок.ОбменДаннымиСМобильнымиПриложениями);
	
	Попытка
		глКомпонентаОбменаСМобильнымиПриложениями.ЗавершитьСеансОбмена(КодМобильногоКомпьютера, ИмяМобильнойБазы, ИмяПользователя);
	Исключение
		ПоказатьПредупреждение(Неопределено, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
	
	ТекстСообщения = НСтр("ru='Сеанс обмена завершен';uk='Сеанс обміну завершено'");
	ПоказатьПредупреждение(Неопределено, ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Функция ТипСвязиЧислом(ТипСвязиСтрокой)
	
	Если ТипСвязиСтрокой = "TCP|IP" Тогда
		Возврат 1;
	ИначеЕсли ТипСвязиСтрокой = "BlueTooth" Тогда
		Возврат 2;
	ИначеЕсли ТипСвязиСтрокой = "Инфракрасный порт" Тогда
	    Возврат 3;
	ИначеЕсли ТипСвязиСтрокой = "COM-порт" Тогда
	    Возврат 4;
	ИначеЕсли ТипСвязиСтрокой = "ActiveSync" Тогда
	    Возврат 5;
	КонецЕсли;
	
	Возврат 1;
	
КонецФункции	

&НаКлиенте
Функция МассивБазКлиента()
	
	МассивИменБазКлиента = Новый Массив();
	
	ТипСвязиЧислом = ТипСвязиЧислом(ТипСвязи);
	глКомпонентаОбменаСМобильнымиПриложениями.УстановитьПараметрыСвязи(КодМобильногоКомпьютера, ТипСвязиЧислом, АдресСервера, Порт, ИмяСервисаIRDA);
	
	СписокБазКлиента = глКомпонентаОбменаСМобильнымиПриложениями.ПолучитьСписокИнформационныхБазКлиента(КодМобильногоКомпьютера, ИмяПользователя);
	КоличествоСтрок = СтрЧислоСтрок(СписокБазКлиента);
	Если КоличествоСтрок>0 Тогда
		Для НомерСтроки = 1 по КоличествоСтрок Цикл
			МассивИменБазКлиента.Добавить(СтрПолучитьСтроку(СписокБазКлиента, НомерСтроки));
		КонецЦикла;
	КонецЕсли;
	
	Возврат МассивИменБазКлиента;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьВозможностьРаботы(Отказ)
	
	Если УзелОбмена.Пустая() Тогда
		ТекстСообщения = НСтр("ru='Не указаны настройки пользователя';uk='Не вказані настройки користувача'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если глКомпонентаОбменаСМобильнымиПриложениями = Неопределено Тогда
		ТекстСообщения = НСтр("ru='Не найден объект компоненты обмена данными';uk='Не знайдений об''єкт компоненти обміну даними'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыНастройкиОбмена(УзелОбмена)
	
	ИмяПользователя = СокрЛП(УзелОбмена.ИмяПользователя);
	КодМобильногоКомпьютера = СокрЛП(УзелОбмена.МобильныйКомпьютер.СерийныйНомер);
	ПарольПользователя = СокрЛП(УзелОбмена.Пароль);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
