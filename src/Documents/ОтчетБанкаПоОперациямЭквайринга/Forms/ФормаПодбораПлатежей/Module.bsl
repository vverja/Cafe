
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Организация = Параметры.Организация;
	Валюта = Параметры.Валюта;
	БанковскийСчет = Параметры.БанковскийСчет;
	Эквайер = Параметры.Эквайер;
	ПодборВходящихПлатежей = Параметры.ПодборВходящихПлатежей;
	АдресПлатежейВХранилище = Параметры.АдресПлатежейВХранилище;
	
	ЗаполнитьТаблицуПлатежей();

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент()

	ПоместитьПлатежиВХранилище();
	Закрыть(КодВозвратаДиалога.OK);
	
	Структура = Новый Структура("ПодборВходящихПлатежей, АдресПлатежейВХранилище", ПодборВходящихПлатежей, АдресПлатежейВХранилище);
	ОповеститьОВыборе(Структура);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПлатежи()

	Для Каждого СтрокаТаблицы Из ТаблицаПлатежей Цикл
		СтрокаТаблицы.Выбран = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьПлатежи()

	Для Каждого СтрокаТаблицы Из ТаблицаПлатежей Цикл
		СтрокаТаблицы.Выбран = Ложь
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВыделенныеПлатежи(Команда)
	
	МассивСтрок = Элементы.ТаблицаПлатежей.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		СтрокаТаблицы = ТаблицаПлатежей.НайтиПоИдентификатору(НомерСтроки);
		Если СтрокаТаблицы <> Неопределено Тогда
			СтрокаТаблицы.Выбран = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВыделенныеПлатежи(Команда)
	
	МассивСтрок = Элементы.ТаблицаПлатежей.ВыделенныеСтроки;
	Для Каждого НомерСтроки Из МассивСтрок Цикл
		СтрокаТаблицы = ТаблицаПлатежей.НайтиПоИдентификатору(НомерСтроки);
		Если СтрокаТаблицы <> Неопределено Тогда
			СтрокаТаблицы.Выбран = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура ПоместитьПлатежиВХранилище() 
	
	Платежи = ТаблицаПлатежей.Выгрузить(, "Выбран, ДатаПлатежа, ЭквайринговыйТерминал, КодАвторизации, НомерПлатежнойКарты, Сумма");
	МассивУдаляемыхСтрок = Новый Массив;
	Для Каждого СтрокаТаблицы Из Платежи Цикл
		Если Не СтрокаТаблицы.Выбран Тогда
			МассивУдаляемыхСтрок.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из МассивУдаляемыхСтрок Цикл
		Платежи.Удалить(СтрокаТаблицы);
	КонецЦикла;

	ПоместитьВоВременноеХранилище(Платежи, АдресПлатежейВХранилище);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуПлатежей()
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Платежи.ДатаПлатежа,
	|	Платежи.ЭквайринговыйТерминал,
	|	Платежи.НомерПлатежнойКарты,
	|	Платежи.КодАвторизации
	|ПОМЕСТИТЬ ТаблицаПлатежей
	|ИЗ
	|	&Платежи КАК Платежи
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|	
	|ВЫБРАТЬ
	|	РасчетыПоЭквайрингу.Валюта КАК Валюта,
	|	(&Знак * РасчетыПоЭквайрингу.СуммаОстаток) КАК Сумма,
	|	РасчетыПоЭквайрингу.ДатаПлатежа КАК ДатаПлатежа,
	|	РасчетыПоЭквайрингу.ЭквайринговыйТерминал КАК ЭквайринговыйТерминал,
	|	РасчетыПоЭквайрингу.НомерПлатежнойКарты КАК НомерПлатежнойКарты,
	|	МАКСИМУМ(РасчетыПоЭквайрингуДвижения.КодАвторизации) КАК КодАвторизации
	|	
	|ПОМЕСТИТЬ РасчетыПоЭквайрингу
	|ИЗ
	|	РегистрНакопления.РасчетыПоЭквайрингу.Остатки(,
	|		Организация = &Организация
	|		И Валюта = &Валюта
	|		И ТипДенежныхСредств = &ТипДенежныхСредств
	|		И ЭквайринговыйТерминал В (
	|			ВЫБРАТЬ
	|				Ссылка
	|			ИЗ
	|				Справочник.ЭквайринговыеТерминалы КАК ЭквайринговыеТерминалы
	|			ГДЕ
	|				ЭквайринговыеТерминалы.БанковскийСчет = &БанковскийСчет
	|				И ЭквайринговыеТерминалы.Эквайер = &Эквайер
	|			)
	|	) КАК РасчетыПоЭквайрингу
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.РасчетыПоЭквайрингу КАК РасчетыПоЭквайрингуДвижения
	|	ПО
	|		РасчетыПоЭквайрингу.ДатаПлатежа = РасчетыПоЭквайрингуДвижения.ДатаПлатежа
	|		И РасчетыПоЭквайрингу.ЭквайринговыйТерминал = РасчетыПоЭквайрингуДвижения.ЭквайринговыйТерминал
	|		И РасчетыПоЭквайрингу.НомерПлатежнойКарты = РасчетыПоЭквайрингуДвижения.НомерПлатежнойКарты	
	|		И РасчетыПоЭквайрингуДвижения.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|	
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоЭквайрингу.Валюта,
	|	РасчетыПоЭквайрингу.СуммаОстаток,
	|	РасчетыПоЭквайрингу.ДатаПлатежа,
	|	РасчетыПоЭквайрингу.ЭквайринговыйТерминал,
	|	РасчетыПоЭквайрингу.НомерПлатежнойКарты
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|	
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА Не ТаблицаПлатежей.ЭквайринговыйТерминал ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК Выбран,
	|
	|	РасчетыПоЭквайрингу.Валюта КАК Валюта,
	|	РасчетыПоЭквайрингу.Сумма КАК Сумма,
	|	РасчетыПоЭквайрингу.ДатаПлатежа КАК ДатаПлатежа,
	|	РасчетыПоЭквайрингу.ЭквайринговыйТерминал КАК ЭквайринговыйТерминал,
	|	РасчетыПоЭквайрингу.НомерПлатежнойКарты КАК НомерПлатежнойКарты,
	|	РасчетыПоЭквайрингу.КодАвторизации КАК КодАвторизации
	|	
	|ИЗ
	|	РасчетыПоЭквайрингу КАК РасчетыПоЭквайрингу
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТаблицаПлатежей КАК ТаблицаПлатежей
	|	ПО
	|		РасчетыПоЭквайрингу.ДатаПлатежа = ТаблицаПлатежей.ДатаПлатежа
	|		И РасчетыПоЭквайрингу.ЭквайринговыйТерминал = ТаблицаПлатежей.ЭквайринговыйТерминал
	|		И РасчетыПоЭквайрингу.НомерПлатежнойКарты = ТаблицаПлатежей.НомерПлатежнойКарты
	|		И РасчетыПоЭквайрингу.КодАвторизации = ТаблицаПлатежей.КодАвторизации
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПлатежа,
	|	НомерПлатежнойКарты
	|");
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	Запрос.УстановитьПараметр("Эквайер", Эквайер);
	Запрос.УстановитьПараметр("Валюта", Валюта);
	
	Если ПодборВходящихПлатежей Тогда
		Запрос.УстановитьПараметр("ТипДенежныхСредств", Перечисления.ТипыДенежныхСредствПоЭквайрингу.ПоступлениеПоПлатежнойКарте);
		Запрос.УстановитьПараметр("Знак", 1);
	Иначе
		Запрос.УстановитьПараметр("ТипДенежныхСредств", Перечисления.ТипыДенежныхСредствПоЭквайрингу.СписаниеПоПлатежнойКарте);
		Запрос.УстановитьПараметр("Знак", -1);
	КонецЕсли;
	
	Платежи = ПолучитьИзВременногоХранилища(АдресПлатежейВХранилище);
	Запрос.УстановитьПараметр("Платежи", Платежи);
	
	ТаблицаПлатежей.Загрузить(Запрос.Выполнить().Выгрузить());
	СуммаВсего = ТаблицаПлатежей.Итог("Сумма");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
