
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	УстановитьПараметрыДинамическогоСписка();
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("АктивПассив") Тогда
		АктивностьСтатьи = Параметры.Отбор.АктивПассив;
		Параметры.Отбор.Удалить("АктивПассив");
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"АктивПассив",
			АктивностьСтатьи,
			ВидСравненияКомпоновкиДанных.Равно,
			, // Представление
			Истина); // Использование
		Список.Параметры.УстановитьЗначениеПараметра("АктивностьСтатьи", АктивностьСтатьи);
		Список.Параметры.УстановитьЗначениеПараметра("БезОграниченияПоАктивуПассиву", Ложь);
		Если АктивностьСтатьи = Перечисления.ВидыСтатейУправленческогоБаланса.Актив Тогда
			Заголовок = НСтр("ru='Статьи активов';uk='Статті активів'");
		Иначе
			Заголовок = НСтр("ru='Статьи пассивов';uk='Статті пасивів'");
		КонецЕсли;
	Иначе
		Заголовок = НСтр("ru='Статьи активов и пассивов';uk='Статті активів і пасивів'");
		Список.Параметры.УстановитьЗначениеПараметра("АктивностьСтатьи", Перечисления.ВидыСтатейУправленческогоБаланса.Актив);
		Список.Параметры.УстановитьЗначениеПараметра("БезОграниченияПоАктивуПассиву", Истина);
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

#Область ПроцедурыПодсистемыНастройкиПорядкаЭлементов

&НаКлиенте
Процедура ПереместитьЭлементВверх(Команда)
	
	НастройкаПорядкаЭлементовКлиент.ПереместитьЭлементВверхВыполнить(Список, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьЭлементВниз(Команда)
	
	НастройкаПорядкаЭлементовКлиент.ПереместитьЭлементВнизВыполнить(Список, Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьПараметрыДинамическогоСписка()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"Ссылка",
		ПланыВидовХарактеристик.СтатьиАктивовПассивов.ЗаблокированныеСтатьиАктивовПассивов(),
		ВидСравненияКомпоновкиДанных.НеВСписке);
	
КонецПроцедуры

#КонецОбласти


