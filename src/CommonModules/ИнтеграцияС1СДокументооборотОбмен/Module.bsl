////////////////////////////////////////////////////////////////////////////////
// Подсистема "Интеграция с Документооборотом"
// Модуль ИнтеграцияС1СДокументооборотОбмен, сервер, внешнее соединение
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Последовательно отправляет сообщения очереди, потом принимает сообщения из ДО.
//
Процедура ВыполнитьОбменДанными() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюС1СДокументооборот") Тогда
		Возврат;
	КонецЕсли;
	
	// Прочтем настройки авторизации и установим их в параметры сеанса. Константы могут быть недоступны.
	УстановитьПривилегированныйРежим(Истина);
	ИмяПользователя = Константы.ИнтеграцияС1СДокументооборотИмяПользователяДляОбмена.Получить();
	Пароль = Константы.ИнтеграцияС1СДокументооборотПарольДляОбмена.Получить();
	Если Не ЗначениеЗаполнено(ИмяПользователя) Тогда // прочтем настройки из пользовательского хранилища.
		ПарольСохранен = Ложь;
		ИнтеграцияС1СДокументооборотВызовСервера.ПрочитатьНастройкиАвторизацииИзХранилищаОбщихНастроек(
			ИмяПользователя, Пароль, ПарольСохранен);
		Если Не ЗначениеЗаполнено(ИмяПользователя) Тогда
			ЗаписьЖурналаРегистрации(НСтр("ru='Интеграция с Документооборотом';uk='Інтеграція з Документообігом'"), 
				УровеньЖурналаРегистрации.Ошибка,,,
				НСтр("ru='Для пользователя регламентного задания обмена не указано имя пользователя Документооборота';uk='Для користувача регламентного завдання обміну не вказано ім''я користувача Документообігу'"));
			Возврат;
		КонецЕсли;
		Если ПарольСохранен <> Истина Тогда
			ЗаписьЖурналаРегистрации(НСтр("ru='Интеграция с Документооборотом';uk='Інтеграція з Документообігом'"), 
				УровеньЖурналаРегистрации.Ошибка,,,
				НСтр("ru='Для пользователя регламентного задания обмена не сохранен пароль Документооборота';uk='Для користувача регламентного завдання обміну не збережений пароль Документообігу'"));
			Возврат;
		КонецЕсли;
		// Перенесем настройки в константы.
		ИнтеграцияС1СДокументооборотВызовСервера.СохранитьНастройкиАвторизацииДляОбмена(ИмяПользователя, Пароль);
		ИнтеграцияС1СДокументооборотВызовСервера.УдалитьНастройкиАвторизацииИзХранилищаОбщихНастроек();
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	ИнтеграцияС1СДокументооборотВызовСервера.УстановитьНастройкиАвторизацииВПараметрыСеанса(ИмяПользователя, Пароль);
		
	// Поддержка синхронизации?
	Если Не ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP") Тогда
		// Сервис доступен, но версия младше требуемой?
		Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("") Тогда
			УзелДокументооборота = ИнтеграцияС1СДокументооборотПовтИсп.УзелДокументооборота();
			ПланыОбмена.УдалитьРегистрациюИзменений(УзелДокументооборота);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляОтправки();
	ОтправитьДанные();
	ПолучитьДанные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//Возвращает измененние объекты, которые интегрированы с Документооборотом. 
// Параметры:
//	УзелОбмена - ПланОбменаСсылка.ИнтеграцияС1СДокументооборотом - узел, для по которому нужно получить изменения
//
Функция ПолучитьМассивЗарегистрированныхДанных(УзелОбмена)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивДанных = Новый Массив;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПравилаИнтеграцииС1СДокументооборотом.Ссылка КАК ПравилоЗаполнения,
		|	ПравилаИнтеграцииС1СДокументооборотом.ТипОбъектаПотребителя КАК ТипОбъектаПотребителя,
		|	ПравилаИнтеграцииС1СДокументооборотом.ТипОбъектаДокументооборота КАК ТипОбъектаДокументооборота
		|ПОМЕСТИТЬ ПравилаЗаполнения
		|ИЗ
		|	Справочник.ПравилаИнтеграцииС1СДокументооборотом КАК ПравилаИнтеграцииС1СДокументооборотом
		|ГДЕ
		|	НЕ ПравилаИнтеграцииС1СДокументооборотом.Ссылка.ПометкаУдаления
		|	И ПравилаИнтеграцииС1СДокументооборотом.ВыгружатьИзменения";
		
	Запрос.Выполнить();
	
	//Проверим, нужно ли получить связанные объекты.
	ИнтеграцияС1СДокументооборот.ПроверитьОбновитьДанныеСвязанныхОбъектов();
	
	Для Каждого ЭлементСоставаПланаОбмена Из УзелОбмена.Метаданные().Состав Цикл
		ЗапросИзменения = Новый Запрос;
		ЗапросИзменения.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		ИмяМетаданныхЭлемента = ЭлементСоставаПланаОбмена.Метаданные.Имя;
		ПолноеИмяМетаданныхЭлемента = ЭлементСоставаПланаОбмена.Метаданные.ПолноеИмя();
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ТаблицаИзменения.Ссылка КАК Объект,
			|	ПравилаЗаполнения.ПравилоЗаполнения КАК ПравилоЗаполнения,
			|	ЕСТЬNULL(ИнтегрированныеОбъекты.ТипОбъектаДокументооборота, ПравилаЗаполнения.ТипОбъектаДокументооборота) КАК ТипОбъектаДокументооборота,
			|	ЕСТЬNULL(ИнтегрированныеОбъекты.ИдентификаторОбъектаДокументооборота, """") КАК ИдентификаторОбъектаДокументооборота
			|ИЗ
			|	%1.Изменения КАК ТаблицаИзменения
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПравилаЗаполнения КАК ПравилаЗаполнения
			|		ПО (ПравилаЗаполнения.ТипОбъектаПотребителя = ""%1"")
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИнтегрированныеОбъекты КАК ИнтегрированныеОбъекты
			|		ПО ТаблицаИзменения.Ссылка = ИнтегрированныеОбъекты.Объект
			|ГДЕ
			|	ТаблицаИзменения.Узел = &Узел
			|	И ЕСТЬNULL(ИнтегрированныеОбъекты.ТипОбъектаДокументооборота, ПравилаЗаполнения.ТипОбъектаДокументооборота) = ПравилаЗаполнения.ТипОбъектаДокументооборота
			|ИТОГИ ПО
			|	Объект";
				
		ЗапросИзменения.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстЗапроса,
			ПолноеИмяМетаданныхЭлемента);
		ЗапросИзменения.УстановитьПараметр("Узел", УзелОбмена);
		ВыборкаОбъекты = ЗапросИзменения.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаОбъекты.Следующий() Цикл
			
			ИнтегрированныйОбъект = Новый Структура("Объект", ВыборкаОбъекты.Объект);
			
			ВыборкаПравила = ВыборкаОбъекты.Выбрать();
			ВыборкаПравила.Следующий();
			
			ИнтегрированныйОбъект.Вставить("ТипОбъектаДокументооборота", 
				ВыборкаПравила.ТипОбъектаДокументооборота);
			ИнтегрированныйОбъект.Вставить("ИдентификаторОбъектаДокументооборота", 
				ВыборкаПравила.ИдентификаторОбъектаДокументооборота);
			
			Если ВыборкаПравила.Количество() = 1 Тогда
				ИнтегрированныйОбъект.Вставить("ПравилоЗаполнения", ВыборкаПравила.ПравилоЗаполнения);
			Иначе
				Правила = ИнтеграцияС1СДокументооборотВызовСервера.ПодходящиеПравила(ВыборкаОбъекты.Объект);
				Если Правила.Количество() = 0 Тогда
					ЗаписьЖурналаРегистрации(НСтр("ru='Интеграция с Документооборотом';uk='Інтеграція з Документообігом'"), 
						УровеньЖурналаРегистрации.Ошибка,,ВыборкаОбъекты.Объект,
						НСтр("ru='Не найдено подходящее правило интеграции для обновления реквизитов связанного объекта Документооборота';uk='Не знайдено відповідне правило інтеграції для оновлення реквізитів зв''язаного об''єкта Документообігу'"));
					Продолжить;
				КонецЕсли;
				ИнтегрированныйОбъект.Вставить("ПравилоЗаполнения", Правила[0].Ссылка);
			КонецЕсли;
			
			МассивДанных.Добавить(ИнтегрированныйОбъект); 
			
		КонецЦикла;
		
	КонецЦикла;
	
	МенеджерВременныхТаблиц.Закрыть();
	
	Возврат МассивДанных;
	
КонецФункции

//Готовит сообщения обмена в Документооборот и записывает их в очередь на отправку.
//
Процедура ПодготовитьДанныеДляОтправки()
	
	УзелДокументооборота = ИнтеграцияС1СДокументооборотПовтИсп.УзелДокументооборота();
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	// Выборка всех изменений для данной интегрированной системы
	ИнтегрированныеОбъекты = ПолучитьМассивЗарегистрированныхДанных(УзелДокументооборота);
	
	Если ИнтегрированныеОбъекты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяВременногоФайла, "UTF-8");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписьXML.ЗаписатьНачалоЭлемента("Message");
	
	Для Каждого ИнтегрированныйОбъект Из ИнтегрированныеОбъекты Цикл
		
		ОбъектXDTO = ИнтеграцияС1СДокументооборот.ПолучитьXDTOИзмененийИзОбъекта(ИнтегрированныйОбъект);
		
		Если ОбъектXDTO = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка
			Прокси.ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, ОбъектXDTO);
		Исключение
			Инфо = ОписаниеОшибки();
			ЗаписьЖурналаРегистрации(
				НСтр("ru='Интеграция с Документооборотом.Выгрузка XDTO в XML';uk='Інтеграція з Документообігом.Вивантаження XDTO в XML'",Метаданные.ОсновнойЯзык.КодЯзыка),
				УровеньЖурналаРегистрации.Ошибка,
				,
				ОбъектXDTO.Тип().Имя,
				ОбъектXDTO.Тип().Имя + Символы.ПС + Инфо);
		КонецПопытки;
		
	КонецЦикла;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.Закрыть();
	ДвоичныеДанныеСообщения = Новый ДвоичныеДанные(ИмяВременногоФайла);
	ДанныеСообщения = Новый ХранилищеЗначения(ДвоичныеДанныеСообщения, Новый СжатиеДанных(9));
	
	РегистрыСведений.ОчередьСообщенийВ1СДокументооборот.ДобавитьСообщение(ДанныеСообщения);
	#Если Сервер Тогда
	УдалитьФайлы(ИмяВременногоФайла);
	#КонецЕсли
	
	ПланыОбмена.УдалитьРегистрациюИзменений(УзелДокументооборота);
	
КонецПроцедуры

//Прочитывает данные из очереди на отправку и отправляет их в Документооборот.
//
Процедура ОтправитьДанные()
	
	Попытка
		
		Запрос = Новый Запрос;
		
		Запрос.Текст =  
		"ВЫБРАТЬ
		|	ОчередьСообщенийВ1СДокументооборот.МоментВремени КАК МоментВремени,
		|	ОчередьСообщенийВ1СДокументооборот.Данные,
		|	ОчередьСообщенийВ1СДокументооборот.Идентификатор
		|ИЗ
		|	РегистрСведений.ОчередьСообщенийВ1СДокументооборот КАК ОчередьСообщенийВ1СДокументооборот
		|
		|УПОРЯДОЧИТЬ ПО
		|	МоментВремени";
		
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			Возврат;
		КонецЕсли; 
		
		Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
		Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMPutChangesRequest");
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ИмяФайлаСообщенияОбмена = ПолучитьИмяВременногоФайла("xml");
			ДвоичныеДанные = Выборка.Данные.Получить();
			ДвоичныеДанные.Записать(ИмяФайлаСообщенияОбмена);
			
			ЧтениеXML = Новый ЧтениеXML;
			ЧтениеXML.ОткрытьФайл(ИмяФайлаСообщенияОбмена);
			ЧтениеXML.Прочитать();
			ЧтениеXML.Прочитать();
			
			Пока ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Цикл
				// Выполняется последовательное чтение одного объекта за другим
				ТипXDTO = Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", ЧтениеXML.Имя);
				ОбъектXDTO = Прокси.ФабрикаXDTO.ПрочитатьXML(ЧтениеXML, ТипXDTO);
				Запрос.objects.Добавить(ОбъектXDTO);
			КонецЦикла;
			
			ЧтениеXML = Неопределено;
			#Если Сервер Тогда
			УдалитьФайлы(ИмяФайлаСообщенияОбмена);
			#КонецЕсли
			
		КонецЦикла; 
		
		Если Запрос.objects.Количество() > 0 Тогда
			Результат = Прокси.execute(Запрос);
			ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
		КонецЕсли;
		
		Выборка.Сбросить();
		
		Пока Выборка.Следующий() Цикл
			
			МенеджерЗаписи = РегистрыСведений.ОчередьСообщенийВ1СДокументооборот.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
			МенеджерЗаписи.Удалить();
			
		КонецЦикла;
		
	Исключение
		Инфо = ОписаниеОшибки();
		ЗаписьЖурналаРегистрации(
			НСтр("ru='Интеграция с Документооборотом.Отправка данных';uk='Інтеграція з Документообігом.Відправка даних'",Метаданные.ОсновнойЯзык.КодЯзыка),
			УровеньЖурналаРегистрации.Ошибка,
			,
			Запрос.Тип().Имя,
			Запрос.Тип().Имя + Символы.ПС + Инфо);
		
	КонецПопытки; 
	
	
КонецПроцедуры

// Получает данные из Документооборота.
//
Процедура ПолучитьДанные()
	
	Попытка
		Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
		УзелДокументооборота = ИнтеграцияС1СДокументооборотПовтИсп.УзелДокументооборота();
		СоставПланаОбмена = Метаданные.ПланыОбмена.ИнтеграцияС1СДокументооборотом.Состав;
		
		ПрочитаныВсеСообщения = Ложь;
		
		Пока Не ПрочитаныВсеСообщения Цикл
			
			Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetChangesRequest");
			Запрос.lastMessageId = Константы.НомерПоследнегоПринятогоСообщения.Получить();
			
			Ответ = Прокси.execute(Запрос);
			ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
			
			
			Для каждого ОбъектXDTO Из Ответ.objects Цикл
				
				Если ОбъектXDTO.objectId.type = "DMInternalDocumentTemplate"
					Или ОбъектXDTO.objectId.type = "DMIncomingDocumentTemplate"
					Или ОбъектXDTO.objectId.type = "DMOutgoingDocumentTemplate" Тогда
					
					Справочники.ПравилаИнтеграцииС1СДокументооборотом.ОбновитьПравилаПоШаблону(ОбъектXDTO);
					
				КонецЕсли;
				
				ОбъектСсылка = ИнтеграцияС1СДокументооборот.СсылкаПоВнешнемуОбъекту(ОбъектXDTO.externalObject);
				Если ОбъектСсылка <> Неопределено Тогда
					Правило = Справочники.ПравилаИнтеграцииС1СДокументооборотом.ПравилаИнтеграцииОбъекта(
					ОбъектСсылка, ОбъектXDTO.objectId.type);
					Если Правило = Неопределено Тогда
						Продолжить;
					КонецЕсли;
					
					Объект = ОбъектСсылка.ПолучитьОбъект();
					Объект.ОбменДанными.Загрузка = Истина;
					
					Справочники.ПравилаИнтеграцииС1СДокументооборотом.
						ЗаполнитьОбъектПоОбъектуXDTO(Прокси, Объект, ОбъектXDTO, Правило, Истина);
						
					Объект.Записать();
					
					Если СоставПланаОбмена.Содержит(Объект.Метаданные()) Тогда
						ПланыОбмена.УдалитьРегистрациюИзменений(УзелДокументооборота, Объект.Ссылка);
					КонецЕсли;
					
				КонецЕсли;
			КонецЦикла;
			
			// С версии 1.4.8 выполняется обновление состояний согласования на стороне ИС.
			Если Ответ.Свойства().Получить("records") <> Неопределено Тогда
				
				Для каждого ОбъектXDTO Из Ответ.records Цикл
					
					ЗапросСвязанныйОбъект = Новый Запрос(
						"ВЫБРАТЬ ПЕРВЫЕ 1
						|	Объект
						|ИЗ
						|	РегистрСведений.ИнтегрированныеОбъекты
						|ГДЕ
						|	ТипОбъектаДокументооборота = &ТипОбъектаДокументооборота
						|	И ИдентификаторОбъектаДокументооборота = &ИдентификаторОбъектаДокументооборота
						|");
					Если ИнтеграцияС1СДокументооборот.ПроверитьТип(Прокси, ОбъектXDTO, 
						"DMApprovalStateRecord") Тогда
						ЗапросСвязанныйОбъект.УстановитьПараметр("ТипОбъектаДокументооборота",
							ОбъектXDTO.type);
						ЗапросСвязанныйОбъект.УстановитьПараметр("ИдентификаторОбъектаДокументооборота",
							ОбъектXDTO.id);
						Выборка = ЗапросСвязанныйОбъект.Выполнить().Выбрать();
						Если Выборка.Следующий() Тогда
							Если ОбъектXDTO.status = Неопределено Тогда // запись удалена (например, прерывание)
								ИнтеграцияС1СДокументооборотВызовСервера.ПриИзмененииСостоянияСогласования(
									Выборка.Объект,	ОбъектXDTO.id, ОбъектXDTO.type,	Неопределено, Ложь);
							Иначе
								ИдентификаторСостояния = ОбъектXDTO.status.objectId.id;
								Состояние = Перечисления.СостоянияСогласованияВДокументообороте[ИдентификаторСостояния];
								ИнтеграцияС1СДокументооборотВызовСервера.ПриИзмененииСостоянияСогласования(
									Выборка.Объект,	ОбъектXDTO.id, ОбъектXDTO.type,	Состояние,
									Ложь, ОбъектXDTO.name, ОбъектXDTO.date);
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
			НомерИсходный = Константы.НомерПоследнегоПринятогоСообщения.Получить();
			НомерПринятый = Строка(Ответ.messageId);
			Если НомерИсходный <> НомерПринятый Тогда
				Константы.НомерПоследнегоПринятогоСообщения.Установить(НомерПринятый);
			КонецЕсли;
			
			ПрочитаныВсеСообщения = (Ответ.messageId = Неопределено);
			
		КонецЦикла; 
		
	Исключение
		Инфо = ОписаниеОшибки();
		ЗаписьЖурналаРегистрации(
			НСтр("ru='Интеграция с Документооборотом.Получение данных';uk='Інтеграція з Документообігом.Отримання даних'",Метаданные.ОсновнойЯзык.КодЯзыка),
			УровеньЖурналаРегистрации.Ошибка,
			,
			Запрос.Тип().Имя,
			Запрос.Тип().Имя + Символы.ПС + Инфо);
		
	КонецПопытки; 
		
КонецПроцедуры

#КонецОбласти
