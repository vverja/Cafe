#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ОбработчикиОбновления


Процедура ИсправитьДвижения_ДанныеДляОбновления(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаявкаНаВозвратТоваровОтКлиентаВозвращаемыеТовары.Ссылка КАК ЗаявкаНаВозвратТоваровОтКлиента,
	|	ЗаявкаНаВозвратТоваровОтКлиентаВозвращаемыеТовары.Номенклатура КАК Номенклатура,
	|	ЗаявкаНаВозвратТоваровОтКлиентаВозвращаемыеТовары.Характеристика КАК Характеристика,
	|	ЗаявкаНаВозвратТоваровОтКлиентаВозвращаемыеТовары.КодСтроки КАК КодСтроки
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента.ВозвращаемыеТовары КАК ЗаявкаНаВозвратТоваровОтКлиентаВозвращаемыеТовары
	|ГДЕ
	|	ЗаявкаНаВозвратТоваровОтКлиентаВозвращаемыеТовары.Отменено
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкиНаВозврат.ЗаявкаНаВозвратТоваровОтКлиента КАК Ссылка,
	|	ЗаявкиНаВозврат.Номенклатура,
	|	ЗаявкиНаВозврат.Характеристика,
	|	ЗаявкиНаВозврат.КодСтроки
	|ПОМЕСТИТЬ ВтОстатки
	|ИЗ
	|	РегистрНакопления.ЗаявкиНаВозвратТоваровОтКлиентов.Остатки(
	|			,
	|			(ЗаявкаНаВозвратТоваровОтКлиента, Номенклатура, Характеристика, КодСтроки) В
	|				(ВЫБРАТЬ
	|					Т.ЗаявкаНаВозвратТоваровОтКлиента,
	|					Т.Номенклатура,
	|					Т.Характеристика,
	|					Т.КодСтроки
	|				ИЗ
	|					ВтТовары КАК Т)) КАК ЗаявкиНаВозврат
	|ГДЕ
	|	ЗаявкиНаВозврат.КОформлениюОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтОстатки.Ссылка КАК Ссылка
	|ИЗ
	|	ВтОстатки КАК ВтОстатки";
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
		Параметры,
		Регистраторы,
		"РегистрНакопления.ЗаявкиНаВозвратТоваровОтКлиентов");
	
КонецПроцедуры

// Обработчик обновления BAS УТ 3.2.3
// Очищает движения по РН "Заявки на возврат товаров от клиента" для отмененных строк по ресурсу КОформлению.
Процедура ИсправитьДвижения(Параметры) Экспорт
    
    Регистраторы = Новый Массив;
	Регистраторы.Добавить("Документ.ЗаявкаНаВозвратТоваровОтКлиента");
	
	ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
		Регистраторы,
		"РегистрНакопления.ЗаявкиНаВозвратТоваровОтКлиентов",
		Параметры.Очередь
    );
	
	Параметры.ОбработкаЗавершена = ОбработкаЗавершена;
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#КонецЕсли