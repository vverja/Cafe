
Процедура СформироватьОтчет(ТабДокумент, ВыводитьЗаголовок = Истина, ПечатьПолногоОтчета = Истина) Экспорт
	
	НачалоПериода = ПериодОтчета.ДатаНачала;
	КонецПериода = ПериодОтчета.ДатаОкончания;
	
	Макет = ПолучитьМакет("UK_Макет2023");

	Если ВыводитьЗаголовок Тогда
	
		ТабДокумент.Очистить();
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Организация, КонецПериода);
		ОбластьМакета.Параметры.НазваниеОрганизации = СведенияОбОрганизации.ПолноеНаименование;
		ОбластьМакета.Параметры.НалоговыйНомер = БухгалтерскийУчетПереопределяемый.ПолучитьКодОрганизации(СведенияОбОрганизации);
		ТабДокумент.Вывести(ОбластьМакета);
		ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ТабДокумент.Вывести(ОбластьМакета);
		
	КонецЕсли;
	
	ТабДокумент.ИтогиСнизу = Истина;
	ТабДокумент.ОбластьПечати = ТабДокумент.Область( , 2, , ТабДокумент.ШиринаТаблицы);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(КонецПериода));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.УстановитьПараметр("СтатьяРеализация",                Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыРеализация);
	Запрос.УстановитьПараметр("СтатьяВозвраты",                  Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыВозвраты);
	Запрос.УстановитьПараметр("СтатьяБезоплатноПолученные",      Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыСтоимостьБезоплатноПолученных);
	Запрос.УстановитьПараметр("СтатьяПросроченнаяЗадолженность", Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыПросроченнаяЗадолженность);
	СписокСтатейПревышений = Новый СписокЗначений;
	СписокСтатейПревышений.Добавить(Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыДоходыПоВидуДеятельностиНеУказанномуВСвидетельстве);
	СписокСтатейПревышений.Добавить(Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыДоходыДругойСпособРасчетов);
	СписокСтатейПревышений.Добавить(Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыДоходыПоВидуДеятельностиНеЕдиныйНалог);
	Запрос.УстановитьПараметр("СписокСтатейПревышений", СписокСтатейПревышений);
	Запрос.УстановитьПараметр("СтатьяДоходы",                    Справочники.СтатьиНалоговыхДеклараций.ЕННК_Доходы);
	Запрос.УстановитьПараметр("СтатьяРеализацияСХПродукции", 	 Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыРеализацияСХПродукции);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КурсыВалют.Период КАК Период,
	|	КурсыВалют.Валюта КАК Валюта,
	|	КурсыВалют.Курс КАК Курс,
	|	КурсыВалют.Кратность КАК Кратность
	|ПОМЕСТИТЬ ТаблицаКурсовВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют КАК КурсыВалют
	|ГДЕ
	|	КурсыВалют.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Период,
	|	КурсыВалютСрезПоследних.Валюта,
	|	КурсыВалютСрезПоследних.Курс,
	|	КурсыВалютСрезПоследних.Кратность
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(ДОБАВИТЬКДАТЕ(&НачалоПериода, ДЕНЬ, -1), ) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКурсовВалют.Валюта КАК Валюта,
	|	ТаблицаКурсовВалют.Курс КАК Курс,
	|	ТаблицаКурсовВалют.Кратность КАК Кратность,
	|	ГраницыПериода.ПериодНачало КАК ПериодНачало,
	|	ЕСТЬNULL(ГраницыПериода.ПериодКонец, &КонецПериода) КАК ПериодКонец
	|ПОМЕСТИТЬ ТаблицаКурсовВалютПериоды
	|ИЗ
	|	ТаблицаКурсовВалют КАК ТаблицаКурсовВалют
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТаблицаКурсыВалют.Валюта КАК Валюта,
	|			ТаблицаКурсыВалют.Период КАК ПериодНачало,
	|			МИНИМУМ(ТаблицаКурсыВалютКопия.Период) КАК ПериодКонец
	|		ИЗ
	|			ТаблицаКурсовВалют КАК ТаблицаКурсыВалют
	|				ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКурсовВалют КАК ТаблицаКурсыВалютКопия
	|				ПО ТаблицаКурсыВалют.Валюта = ТаблицаКурсыВалютКопия.Валюта
	|					И ТаблицаКурсыВалют.Период < ТаблицаКурсыВалютКопия.Период
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТаблицаКурсыВалют.Валюта,
	|			ТаблицаКурсыВалют.Период) КАК ГраницыПериода
	|		ПО ТаблицаКурсовВалют.Валюта = ГраницыПериода.Валюта
	|			И ТаблицаКурсовВалют.Период = ГраницыПериода.ПериодНачало
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(КнигаДоходовРасходов.Период, ДЕНЬ) КАК Период,
	|	КнигаДоходовРасходов.Регистратор КАК Регистратор,
	|	КнигаДоходовРасходов.Статья КАК Статья,
	|	КнигаДоходовРасходов.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА КнигаДоходовРасходов.Статья = &СтатьяРеализация
	|			ТОГДА КнигаДоходовРасходов.СуммаОборот * ТаблицаКурсовВалютПериоды.Курс / ТаблицаКурсовВалютПериоды.Кратность
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Статья2,
	|	ВЫБОР
	|		КОГДА КнигаДоходовРасходов.Статья = &СтатьяВозвраты
	|			ТОГДА -КнигаДоходовРасходов.СуммаОборот * ТаблицаКурсовВалютПериоды.Курс / ТаблицаКурсовВалютПериоды.Кратность
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Статья3,
	|	ВЫБОР
	|		КОГДА КнигаДоходовРасходов.Статья = &СтатьяРеализация
	|			ТОГДА КнигаДоходовРасходов.СуммаОборот * ТаблицаКурсовВалютПериоды.Курс / ТаблицаКурсовВалютПериоды.Кратность
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА КнигаДоходовРасходов.Статья = &СтатьяВозвраты
	|			ТОГДА КнигаДоходовРасходов.СуммаОборот * ТаблицаКурсовВалютПериоды.Курс / ТаблицаКурсовВалютПериоды.Кратность
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Статья4,
	|	ВЫБОР
	|		КОГДА КнигаДоходовРасходов.Статья = &СтатьяБезоплатноПолученные
	|			ТОГДА КнигаДоходовРасходов.СуммаОборот * ТаблицаКурсовВалютПериоды.Курс / ТаблицаКурсовВалютПериоды.Кратность
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Статья5,
	|	ВЫБОР
	|		КОГДА КнигаДоходовРасходов.Статья = &СтатьяПросроченнаяЗадолженность
	|			ТОГДА КнигаДоходовРасходов.СуммаОборот * ТаблицаКурсовВалютПериоды.Курс / ТаблицаКурсовВалютПериоды.Кратность
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Статья6,  
	|	ВЫБОР
	|		КОГДА КнигаДоходовРасходов.Статья = &СтатьяРеализацияСХПродукции
	|			ТОГДА КнигаДоходовРасходов.СуммаОборот * ТаблицаКурсовВалютПериоды.Курс / ТаблицаКурсовВалютПериоды.Кратность
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Статья7,  
	|	ВЫБОР
	|		КОГДА КнигаДоходовРасходов.Статья = &СтатьяРеализация
	|			ТОГДА КнигаДоходовРасходов.СуммаОборот * ТаблицаКурсовВалютПериоды.Курс / ТаблицаКурсовВалютПериоды.Кратность
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА КнигаДоходовРасходов.Статья = &СтатьяВозвраты
	|			ТОГДА КнигаДоходовРасходов.СуммаОборот * ТаблицаКурсовВалютПериоды.Курс / ТаблицаКурсовВалютПериоды.Кратность
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА КнигаДоходовРасходов.Статья = &СтатьяБезоплатноПолученные
	|			ТОГДА КнигаДоходовРасходов.СуммаОборот * ТаблицаКурсовВалютПериоды.Курс / ТаблицаКурсовВалютПериоды.Кратность
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА КнигаДоходовРасходов.Статья = &СтатьяПросроченнаяЗадолженность
	|			ТОГДА КнигаДоходовРасходов.СуммаОборот * ТаблицаКурсовВалютПериоды.Курс / ТаблицаКурсовВалютПериоды.Кратность
	|		ИНАЧЕ 0 
	|	КОНЕЦ + ВЫБОР
	|		КОГДА КнигаДоходовРасходов.Статья = &СтатьяРеализацияСХПродукции
	|			ТОГДА КнигаДоходовРасходов.СуммаОборот * ТаблицаКурсовВалютПериоды.Курс / ТаблицаКурсовВалютПериоды.Кратность
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК Статья8,
	|	ВЫБОР
	|		КОГДА КнигаДоходовРасходов.Статья В (&СписокСтатейПревышений)
	|			ТОГДА КнигаДоходовРасходов.СуммаОборот * ТаблицаКурсовВалютПериоды.Курс / ТаблицаКурсовВалютПериоды.Кратность
	|		ИНАЧЕ 0   
	|	КОНЕЦ КАК Статья10
	|ИЗ
	|	РегистрНакопления.КнигаДоходовРасходовПоЕдиномуНалогу.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			Организация = &Организация
	|				И Статья В ИЕРАРХИИ (&СтатьяДоходы)) КАК КнигаДоходовРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКурсовВалютПериоды КАК ТаблицаКурсовВалютПериоды
	|		ПО КнигаДоходовРасходов.Валюта = ТаблицаКурсовВалютПериоды.Валюта
	|			И КнигаДоходовРасходов.Период >= ТаблицаКурсовВалютПериоды.ПериодНачало
	|			И КнигаДоходовРасходов.Период < ТаблицаКурсовВалютПериоды.ПериодКонец
	|
	|УПОРЯДОЧИТЬ ПО
	|	КнигаДоходовРасходов.Период,
	|	КнигаДоходовРасходов.Регистратор
	|ИТОГИ
	|	СУММА(Статья2),
	|	СУММА(Статья3),
	|	СУММА(Статья4),
	|	СУММА(Статья5),
	|	СУММА(Статья6),
	|	СУММА(Статья7), 
	|	СУММА(Статья8), 
	|	СУММА(Статья10)
	|ПО
	|	ОБЩИЕ,
	|	Период";
	
	ОбластьСтрока    = Макет.ПолучитьОбласть("Строка");
	ОбластьДень      = Макет.ПолучитьОбласть("День");
	ОбластьИтог     = Макет.ПолучитьОбласть("Итог");
	ОбластьПодвал    = Макет.ПолучитьОбласть("Подвал");
	
	РезультатЗапроса = Запрос.Выполнить(); 
	ВыборкаДень = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период");
	
	ТабДокумент.НачатьГруппуСтрок("Итог");
	
	Пока ВыборкаДень.Следующий() Цикл
		
		ТабДокумент.НачатьГруппуСтрок("День");
		Выборка = ВыборкаДень.Выбрать(ОбходРезультатаЗапроса.Прямой);
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ТипЗаписи() <> ТипЗаписиЗапроса.ДетальнаяЗапись Тогда
				Продолжить;
			КонецЕсли;
			
			ОбластьСтрока.Параметры.Заполнить(Выборка);
			
			ВидДохода = "";
			Если Выборка.Статья = Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыДоходыПоВидуДеятельностиНеУказанномуВСвидетельстве Тогда
				ВидДохода = "пп.2 п.293.4";
			ИначеЕсли Выборка.Статья = Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыДоходыДругойСпособРасчетов Тогда
				ВидДохода = "пп.3 п.293.4";
			ИначеЕсли Выборка.Статья = Справочники.СтатьиНалоговыхДеклараций.ЕННК_ДоходыДоходыПоВидуДеятельностиНеЕдиныйНалог Тогда
				ВидДохода = "пп.4 п.293.4";
			КонецЕсли; 
			ОбластьСтрока.Параметры.Статья9 = ВидДохода;
			
			ОбластьСтрока.Параметры.ДатаНомер = Формат(ВыборкаДень.Период, "ДФ=dd.MM.yyyy");
			ТабДокумент.Вывести(ОбластьСтрока);
			
		КонецЦикла;
		
		ТабДокумент.ЗакончитьГруппуСтрок();

		ОбластьДень.Параметры.Заполнить(ВыборкаДень);
		День = ВыборкаДень.Период;
		ОбластьДень.Параметры.Итого = "Разом за " + Формат(День, "ДЛФ=D") + ":";
		ТабДокумент.Вывести(ОбластьДень);
		
	КонецЦикла;
		
	ТабДокумент.ЗакончитьГруппуСтрок();
	
	ВыборкаОбщиеИтоги = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам,"Общие");
	ВыборкаОбщиеИтоги.Следующий();
	ОбластьИтог.Параметры.Заполнить(ВыборкаОбщиеИтоги);
	ОбластьИтог.Параметры.Итого = "Разом:";
	ТабДокумент.Вывести(ОбластьИтог);
	
	Если ПечатьПолногоОтчета Тогда
		ТабДокумент.Вывести(ОбластьПодвал);
	КонецЕсли;
	
	ТабДокумент.ИмяПараметровПечати = "КнигаДоходовПоЕдиномуНалогу";
	
КонецПроцедуры