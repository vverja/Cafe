#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если СкладыСервер.ИспользоватьАдресноеХранение(Склад,Помещение,Дата) Тогда
		ПроверитьБлокировкуЯчеек(Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НоменклатураСервер.ОчиститьНеиспользуемыеСерии(ЭтотОбъект,
														НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ОрдерНаОтражениеПорчиТоваров));

	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);

	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ПроверитьОрдерныйСклад(Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);

	Документы.ОрдерНаОтражениеПорчиТоваров.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);

	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	ЗапасыСервер.ОтразитьТоварыНаСкладах(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразитьТоварыКОформлениюИзлишковНедостач(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразитьСвободныеОстатки(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразитьОбеспечениеЗаказов(ДополнительныеСвойства, Движения, Отказ);	
	
	СкладыСервер.ОтразитьТоварыВЯчейках(ДополнительныеСвойства, Движения, Отказ);
	СкладыСервер.ОтразитьДвиженияСерийТоваров(ДополнительныеСвойства, Движения, Отказ);
	
	СформироватьСписокРегистровДляКонтроля();

	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);

	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	СформироватьСписокРегистровДляКонтроля();

	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	ИнициализироватьДокумент();

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ПараметрыПроверки = ОбщегоНазначенияУТ.ПараметрыПроверкиЗаполненияКоличества();
	ПараметрыПроверки.ПроверитьВозможностьОкругления = Ложь;
	ОбщегоНазначенияУТ.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, ПараметрыПроверки);

	Если Не СкладыСервер.ИспользоватьАдресноеХранение(Склад,Помещение,Дата) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Ячейка");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Упаковка");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.УпаковкаОприходование");
	ИначеЕсли Не ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Упаковка");
		ТекстСообщения = НСтр("ru='В настройках программы не включено использование упаковок номенклатуры, 
            |поэтому нельзя оформить документ по складу с адресным хранением остатков. Обратитесь к администратору'
            |;uk='У настройках програми не включено використання упаковок номенклатури, 
            |тому не можна оформити документ по складу з адресним зберіганням залишків. Зверніться до адміністратора'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	Иначе
		НоменклатураСервер.ПроверитьЗаполнениеУпаковок(ЭтотОбъект,МассивНепроверяемыхРеквизитов,Отказ)
	КонецЕсли;
	
	Если Не СкладыСервер.ИспользоватьСкладскиеПомещения(Склад, Дата) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Помещение");
	КонецЕсли;
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверки.СуффиксДопРеквизита = "Оприходование";
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект,МассивНепроверяемыхРеквизитов,Отказ,ПараметрыПроверки);
	
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект,
												НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ОрдерНаОтражениеПорчиТоваров),
												Отказ,
												МассивНепроверяемыхРеквизитов);
	
	ТекстОшибки = НСтр("ru='Товар исходного качества совпадает с товаром другого качества';uk='Товар вихідної якості збігається з товаром іншої якості'");
	
	Для Каждого СтрТабл Из Товары Цикл
		АдресОшибки = НСтр("ru=' в строке %НомерСтроки% списка ""Товары""';uk=' у рядку %НомерСтроки% списку ""Товари""'");
		АдресОшибки =  СтрЗаменить(АдресОшибки, "%НомерСтроки%", СтрТабл.НомерСтроки);
		Если ЗначениеЗаполнено(СтрТабл.Номенклатура)
			И СтрТабл.Номенклатура = СтрТабл.НоменклатураОприходование Тогда
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрТабл.НомерСтроки, "НоменклатураОприходование");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки + АдресОшибки,ЭтотОбъект,Поле,,Отказ);
		КонецЕсли;
	КонецЦикла;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьМногооборотнуюТару") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаТоваров.Номенклатура,
		|	ТаблицаТоваров.НоменклатураОприходование,
		|	ТаблицаТоваров.НомерСтроки
		|ПОМЕСТИТЬ ТаблицаТоваров
		|ИЗ
		|	&ТаблицаТоваров КАК ТаблицаТоваров
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
		|	ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры КАК НоменклатураТипНоменклатуры,
		|	ВЫРАЗИТЬ(ТаблицаТоваров.НоменклатураОприходование КАК Справочник.Номенклатура).ТипНоменклатуры КАК НоменклатураОприходованиеТипНоменклатуры
		|ИЗ
		|	ТаблицаТоваров КАК ТаблицаТоваров
		|ГДЕ
		|	ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры <> ВЫРАЗИТЬ(ТаблицаТоваров.НоменклатураОприходование КАК Справочник.Номенклатура).ТипНоменклатуры
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";	
		
		Запрос.УстановитьПараметр("ТаблицаТоваров", Товары.Выгрузить(,"НомерСтроки,Номенклатура,НоменклатураОприходование"));
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		ТекстОшибки = НСтр("ru='В строке %НомерСтроки% списка ""Товары"" не совпадают типы номенклатуры. У номенклатуры исходхого качества тип %НоменклатураТипНоменклатуры%, другого качества - %НоменклатураОприходованиеТипНоменклатуры%.';uk='У рядку %НомерСтроки% списку ""Товари"" не співпадають типи номенклатури. У номенклатури вихідхої якості тип %НоменклатураТипНоменклатуры%, іншої якості - %НоменклатураОприходованиеТипНоменклатуры%.'");
		
		Пока Выборка.Следующий() Цикл
			СообщениеОбОшибке = СтрЗаменить(ТекстОшибки, "%НомерСтроки%", Выборка.НомерСтроки); 
			СообщениеОбОшибке = СтрЗаменить(СообщениеОбОшибке, "%НоменклатураТипНоменклатуры%", Выборка.НоменклатураТипНоменклатуры); 
			СообщениеОбОшибке = СтрЗаменить(СообщениеОбОшибке, "%НоменклатураОприходованиеТипНоменклатуры%", Выборка.НоменклатураОприходованиеТипНоменклатуры); 
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрТабл.НомерСтроки, "НоменклатураОприходование");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке,ЭтотОбъект,Поле,,Отказ);
		КонецЦикла;
		
	КонецЕсли;
	
	НоменклатураСервер.ПроверитьВидНоменклатурыОприходования(ЭтотОбъект,Отказ);
	СкладыСервер.ПроверитьОрдерностьСклада(Склад, Дата, "ПриОтраженииИзлишковНедостач", Отказ);
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РасходныйОрдерНаТовары") Тогда
		ЗаполнитьНаОсновании(ДанныеЗаполнения);
	КонецЕсли;
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

Процедура ЗаполнитьНаОсновании(Основание)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтгружаемыеТовары.Номенклатура     КАК НоменклатураБрак,
	|	ТоварыДругогоКачества.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ БракованныеТовары
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК ОтгружаемыеТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТоварыДругогоКачества КАК ТоварыДругогоКачества
	|		ПО ОтгружаемыеТовары.Номенклатура = ТоварыДругогоКачества.НоменклатураБрак
	|ГДЕ
	|	ОтгружаемыеТовары.Ссылка = &ДокументОснование
	|	И ОтгружаемыеТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Неотгружать)
	|	И ОтгружаемыеТовары.Номенклатура.Качество <> ЗНАЧЕНИЕ(Перечисление.ГрадацииКачества.Новый)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	БракованныеТовары.НоменклатураБрак КАК НоменклатураБрак,
	|	БракованныеТовары.Номенклатура     КАК Номенклатура
	|ПОМЕСТИТЬ СопоставленныеТоварыОрдера
	|ИЗ
	|	БракованныеТовары КАК БракованныеТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК ОтгружаемыеТовары
	|		ПО БракованныеТовары.Номенклатура = ОтгружаемыеТовары.Номенклатура
	|ГДЕ
	|	ОтгружаемыеТовары.Ссылка = &ДокументОснование
	|	И ОтгружаемыеТовары.Действие <> ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Неотгружать)
	|	И ОтгружаемыеТовары.Номенклатура.Качество = ЗНАЧЕНИЕ(Перечисление.ГрадацииКачества.Новый)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	КачественныеТовары.Номенклатура                 КАК Номенклатура,
	|	КОЛИЧЕСТВО(КачественныеТовары.НоменклатураБрак) КАК КоличествоСвязей
	|ПОМЕСТИТЬ ТаблицаСвязейКачественныхТоваров
	|ИЗ
	|	СопоставленныеТоварыОрдера КАК КачественныеТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	КачественныеТовары.Номенклатура
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	БракованныеТовары.НоменклатураБрак         КАК НоменклатураБрак,
	|	КОЛИЧЕСТВО(БракованныеТовары.Номенклатура) КАК КоличествоСвязей
	|ПОМЕСТИТЬ ТаблицаСвязейБракованныхТоваровОрдера
	|ИЗ
	|	СопоставленныеТоварыОрдера КАК БракованныеТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	БракованныеТовары.НоменклатураБрак
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 4
	|ВЫБРАТЬ
	|	БракованныеТовары.НоменклатураБрак         КАК НоменклатураБрак,
	|	КОЛИЧЕСТВО(БракованныеТовары.Номенклатура) КАК КоличествоСвязей
	|ПОМЕСТИТЬ ТаблицаСвязейБракованныхТоваров
	|ИЗ
	|	БракованныеТовары КАК БракованныеТовары
	|ГДЕ
	|	НЕ БракованныеТовары.НоменклатураБрак В
	|		(ВЫБРАТЬ
	|			СопоставленныеТоварыОрдера.НоменклатураБрак
	|		ИЗ
	|			СопоставленныеТоварыОрдера КАК СопоставленныеТоварыОрдера)
	|	И НЕ БракованныеТовары.Номенклатура ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	БракованныеТовары.НоменклатураБрак
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 5
	|ВЫБРАТЬ
	|	ТоварыОрдера.НоменклатураБрак КАК НоменклатураБрак,
	|	ВЫБОР
	|		КОГДА ТаблицаСвязейБракованныхТоваровОрдера.КоличествоСвязей = 1
	|				И ТаблицаСвязейКачественныхТоваров.КоличествоСвязей = 1
	|			ТОГДА ТоварыОрдера.Номенклатура
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ                         КАК Номенклатура
	|ПОМЕСТИТЬ СопоставленныеБракованныеТовары
	|ИЗ
	|	СопоставленныеТоварыОрдера КАК ТоварыОрдера
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСвязейБракованныхТоваровОрдера КАК ТаблицаСвязейБракованныхТоваровОрдера
	|		ПО ТоварыОрдера.НоменклатураБрак = ТаблицаСвязейБракованныхТоваровОрдера.НоменклатураБрак
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСвязейКачественныхТоваров КАК ТаблицаСвязейКачественныхТоваров
	|		ПО ТоварыОрдера.Номенклатура = ТаблицаСвязейКачественныхТоваров.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БракованныеТовары.НоменклатураБрак,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|ИЗ
	|	БракованныеТовары КАК БракованныеТовары
	|ГДЕ
	|	БракованныеТовары.Номенклатура ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаСвязейБракованныхТоваров.НоменклатураБрак,
	|	ВЫБОР
	|		КОГДА ТаблицаСвязейБракованныхТоваров.КоличествоСвязей = 1
	|			ТОГДА БракованныеТовары.Номенклатура
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ
	|ИЗ
	|	ТаблицаСвязейБракованныхТоваров КАК ТаблицаСвязейБракованныхТоваров
	|	ЛЕВОЕ СОЕДИНЕНИЕ БракованныеТовары КАК БракованныеТовары
	|		ПО ТаблицаСвязейБракованныхТоваров.НоменклатураБрак = БракованныеТовары.НоменклатураБрак
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 6
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	БракованныеТовары.НоменклатураБрак КАК НоменклатураБрак,
	|	ОтгружаемыеТовары.Характеристика   КАК ХарактеристикаБрак,
	|	БракованныеТовары.Номенклатура     КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА (ОтгружаемыеТовары.Номенклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры)
	|					И ОтгружаемыеТовары.Номенклатура.ВидНоменклатуры = БракованныеТовары.НоменклатураБрак.ВидНоменклатуры)
	|				ИЛИ (ОтгружаемыеТовары.Номенклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры)
	|					И ОтгружаемыеТовары.Номенклатура.ВладелецХарактеристик = БракованныеТовары.НоменклатураБрак.ВладелецХарактеристик)
	|			ТОГДА ОтгружаемыеТовары.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                              КАК Характеристика
	|ПОМЕСТИТЬ СопоставленныеБракованныеТоварыПоПараметрам
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК ОтгружаемыеТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеБракованныеТовары КАК БракованныеТовары
	|		ПО ОтгружаемыеТовары.Номенклатура = БракованныеТовары.НоменклатураБрак
	|ГДЕ
	|	ОтгружаемыеТовары.Ссылка = &ДокументОснование
	|	И ОтгружаемыеТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Неотгружать)
	|	И ОтгружаемыеТовары.Номенклатура.Качество <> ЗНАЧЕНИЕ(Перечисление.ГрадацииКачества.Новый)
	|	И ОтгружаемыеТовары.Номенклатура.ИспользованиеХарактеристик <> ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	БракованныеТовары.НоменклатураБрак,
	|	ОтгружаемыеТовары.Характеристика,
	|	БракованныеТовары.Номенклатура,
	|	ЕСТЬNULL(ХарактеристикиНоменклатуры.Ссылка, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК ОтгружаемыеТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеБракованныеТовары КАК БракованныеТовары
	|		ПО ОтгружаемыеТовары.Номенклатура = БракованныеТовары.НоменклатураБрак
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО БракованныеТовары.Номенклатура = ХарактеристикиНоменклатуры.Владелец
	|			И ВЫРАЗИТЬ(ОтгружаемыеТовары.Характеристика.Наименование КАК СТРОКА(500)) = ВЫРАЗИТЬ(ХарактеристикиНоменклатуры.Наименование КАК СТРОКА(500))
	|ГДЕ
	|	ОтгружаемыеТовары.Ссылка = &ДокументОснование
	|	И ОтгружаемыеТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Неотгружать)
	|	И ОтгружаемыеТовары.Номенклатура.Качество <> ЗНАЧЕНИЕ(Перечисление.ГрадацииКачества.Новый)
	|	И ОтгружаемыеТовары.Номенклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 7
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТоварыДругогоКачества.НоменклатураБрак КАК НоменклатураБрак,
	|	ОтгружаемыеТовары.Номенклатура         КАК Номенклатура
	|ПОМЕСТИТЬ НеотгружаемыеКачественныеТовары
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК ОтгружаемыеТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТоварыДругогоКачества КАК ТоварыДругогоКачества
	|		ПО ОтгружаемыеТовары.Номенклатура = ТоварыДругогоКачества.НоменклатураБрак
	|ГДЕ
	|	ОтгружаемыеТовары.Ссылка = &ДокументОснование
	|	И ОтгружаемыеТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Неотгружать)
	|	И ОтгружаемыеТовары.Номенклатура.Качество = ЗНАЧЕНИЕ(Перечисление.ГрадацииКачества.Новый)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 8
	|ВЫБРАТЬ
	|	КачественныеТовары.Номенклатура                 КАК Номенклатура,
	|	КОЛИЧЕСТВО(КачественныеТовары.НоменклатураБрак) КАК КоличествоСвязей
	|ПОМЕСТИТЬ ТаблицаСвязейНеотгружаемыхКачественныхТоваров
	|ИЗ
	|	НеотгружаемыеКачественныеТовары КАК КачественныеТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	КачественныеТовары.Номенклатура
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 9
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТаблицаСвязей.КоличествоСвязей = 1
	|			ТОГДА ВЫРАЗИТЬ(ТоварыОрдера.Номенклатура КАК Справочник.Номенклатура)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ                     КАК НоменклатураБрак,
	|	ТоварыОрдера.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ СопоставленныеКачественныеТовары
	|ИЗ
	|	НеотгружаемыеКачественныеТовары КАК ТоварыОрдера
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСвязейНеотгружаемыхКачественныхТоваров КАК ТаблицаСвязей
	|		ПО ТоварыОрдера.Номенклатура = ТаблицаСвязей.Номенклатура
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 10
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КачественныеТовары.НоменклатураБрак КАК НоменклатураБрак,
	|	ВЫБОР
	|		КОГДА (ОтгружаемыеТовары.Номенклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры)
	|					И ОтгружаемыеТовары.Номенклатура.ВидНоменклатуры = КачественныеТовары.НоменклатураБрак.ВидНоменклатуры)
	|				ИЛИ (ОтгружаемыеТовары.Номенклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры)
	|					И ОтгружаемыеТовары.Номенклатура.ВладелецХарактеристик = КачественныеТовары.НоменклатураБрак.ВладелецХарактеристик)
	|			ТОГДА ВЫРАЗИТЬ(ОтгружаемыеТовары.Характеристика КАК Справочник.ХарактеристикиНоменклатуры)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                               КАК ХарактеристикаБрак,
	|	ОтгружаемыеТовары.Номенклатура      КАК Номенклатура,
	|	ОтгружаемыеТовары.Характеристика    КАК Характеристика
	|ПОМЕСТИТЬ СопоставленныеКачественныеТоварыПоПараметрам
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК ОтгружаемыеТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеКачественныеТовары КАК КачественныеТовары
	|		ПО ОтгружаемыеТовары.Номенклатура = КачественныеТовары.Номенклатура
	|ГДЕ
	|	ОтгружаемыеТовары.Ссылка = &ДокументОснование
	|	И ОтгружаемыеТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Неотгружать)
	|	И ОтгружаемыеТовары.Номенклатура.Качество = ЗНАЧЕНИЕ(Перечисление.ГрадацииКачества.Новый)
	|	И ОтгружаемыеТовары.Номенклатура.ИспользованиеХарактеристик <> ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КачественныеТовары.НоменклатураБрак,
	|	ЕСТЬNULL(ХарактеристикиНоменклатуры.Ссылка, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)),
	|	ОтгружаемыеТовары.Номенклатура,
	|	ОтгружаемыеТовары.Характеристика
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК ОтгружаемыеТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеКачественныеТовары КАК КачественныеТовары
	|		ПО ОтгружаемыеТовары.Номенклатура = КачественныеТовары.Номенклатура
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО КачественныеТовары.НоменклатураБрак = ХарактеристикиНоменклатуры.Владелец
	|			И ВЫРАЗИТЬ(ОтгружаемыеТовары.Характеристика.Наименование КАК СТРОКА(500)) = ВЫРАЗИТЬ(ХарактеристикиНоменклатуры.Наименование КАК СТРОКА(500))
	|ГДЕ
	|	ОтгружаемыеТовары.Ссылка = &ДокументОснование
	|	И ОтгружаемыеТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Неотгружать)
	|	И ОтгружаемыеТовары.Номенклатура.Качество = ЗНАЧЕНИЕ(Перечисление.ГрадацииКачества.Новый)
	|	И ОтгружаемыеТовары.Номенклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 11
	|ВЫБРАТЬ
	|	БракованныеТовары.НоменклатураБрак    КАК НоменклатураОприходование,
	|	БракованныеТовары.ХарактеристикаБрак  КАК ХарактеристикаОприходование,
	|	ВЫБОР
	|		КОГДА ОтгружаемыеТовары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ                                 КАК ПодНазначение,
	|	ОтгружаемыеТовары.Серия               КАК Серия,
	|	БракованныеТовары.Номенклатура        КАК Номенклатура,
	|	БракованныеТовары.Характеристика      КАК Характеристика,
	|	ОтгружаемыеТовары.Назначение          КАК Назначение,
	|	ОтгружаемыеТовары.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	СУММА(ОтгружаемыеТовары.Количество)   КАК Количество,
	|	СУММА(ОтгружаемыеТовары.Количество)   КАК КоличествоУпаковок
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК ОтгружаемыеТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеБракованныеТоварыПоПараметрам КАК БракованныеТовары
	|		ПО ОтгружаемыеТовары.Номенклатура = БракованныеТовары.НоменклатураБрак
	|			И ОтгружаемыеТовары.Характеристика = БракованныеТовары.ХарактеристикаБрак
	|ГДЕ
	|	ОтгружаемыеТовары.Ссылка = &ДокументОснование
	|	И ОтгружаемыеТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Неотгружать)
	|	И ОтгружаемыеТовары.Номенклатура.Качество <> ЗНАЧЕНИЕ(Перечисление.ГрадацииКачества.Новый)
	|
	|СГРУППИРОВАТЬ ПО
	|	БракованныеТовары.НоменклатураБрак,
	|	БракованныеТовары.ХарактеристикаБрак,
	|	ВЫБОР
	|		КОГДА ОтгружаемыеТовары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ОтгружаемыеТовары.Серия,
	|	ОтгружаемыеТовары.СтатусУказанияСерий,
	|	БракованныеТовары.Номенклатура,
	|	БракованныеТовары.Характеристика,
	|	ОтгружаемыеТовары.Назначение
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОтгружаемыеТовары.Количество) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КачественныеТовары.НоменклатураБрак,
	|	КачественныеТовары.ХарактеристикаБрак,
	|	ВЫБОР
	|		КОГДА ОтгружаемыеТовары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ОтгружаемыеТовары.Серия,
	|	КачественныеТовары.Номенклатура,
	|	КачественныеТовары.Характеристика,
	|	ОтгружаемыеТовары.Назначение,
	|	ОтгружаемыеТовары.СтатусУказанияСерий,
	|	СУММА(ОтгружаемыеТовары.Количество),
	|	СУММА(ОтгружаемыеТовары.Количество)
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК ОтгружаемыеТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленныеКачественныеТоварыПоПараметрам КАК КачественныеТовары
	|		ПО ОтгружаемыеТовары.Номенклатура = КачественныеТовары.Номенклатура
	|			И ОтгружаемыеТовары.Характеристика = КачественныеТовары.Характеристика
	|ГДЕ
	|	ОтгружаемыеТовары.Ссылка = &ДокументОснование
	|	И ОтгружаемыеТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Неотгружать)
	|	И ОтгружаемыеТовары.Номенклатура.Качество = ЗНАЧЕНИЕ(Перечисление.ГрадацииКачества.Новый)
	|
	|СГРУППИРОВАТЬ ПО
	|	КачественныеТовары.НоменклатураБрак,
	|	КачественныеТовары.ХарактеристикаБрак,
	|	ВЫБОР
	|		КОГДА ОтгружаемыеТовары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ОтгружаемыеТовары.Серия,
	|	ОтгружаемыеТовары.СтатусУказанияСерий,
	|	КачественныеТовары.Номенклатура,
	|	КачественныеТовары.Характеристика,
	|	ОтгружаемыеТовары.Назначение
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОтгружаемыеТовары.Количество) > 0
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 12
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ШапкаДокумента.Склад         КАК Склад,
	|	ШапкаДокумента.Помещение     КАК Помещение,
	|	ШапкаДокумента.Ответственный КАК Ответственный
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары КАК ШапкаДокумента
	|ГДЕ
	|	ШапкаДокумента.Ссылка = &ДокументОснование
	|";
	
	Запрос.УстановитьПараметр("ДокументОснование", Основание);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыДокумента = РезультатЗапроса[12].Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыДокумента);
	
	Товары.Загрузить(РезультатЗапроса[11].Выгрузить());
	
КонецПроцедуры

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)

	Ответственный = Пользователи.ТекущийПользователь();
	Склад         = ЗначениеНастроекПовтИсп.ПолучитьСкладПоУмолчанию(Склад);

КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ПроверитьБлокировкуЯчеек(Отказ)
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.БлокировкиСкладскихЯчеек");
	ЭлементБлокировки.Режим          = РежимБлокировкиДанных.Разделяемый;
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ячейка", "Ячейка");
	
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	БлокировкиСкладскихЯчеек.Ячейка КАК Ячейка,
	|	БлокировкиСкладскихЯчеек.ТипБлокировки КАК ТипБлокировки
	|ИЗ
	|	РегистрСведений.БлокировкиСкладскихЯчеек КАК БлокировкиСкладскихЯчеек
	|ГДЕ
	|	БлокировкиСкладскихЯчеек.Ячейка В (&МассивЯчеек)
	|
	|СГРУППИРОВАТЬ ПО
	|	БлокировкиСкладскихЯчеек.Ячейка,
	|	БлокировкиСкладскихЯчеек.ТипБлокировки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ячейка";
	
	Запрос.УстановитьПараметр("МассивЯчеек", Товары.ВыгрузитьКолонку("Ячейка"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ТекстСообщения = НСтр("ru='Ячейка %Ячейка% заблокирована: тип блокировки ""%ТипБлокировки%""';uk='Комірка %Ячейка% заблокована: тип блокування ""%ТипБлокировки%""'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ячейка%", Выборка.Ячейка);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТипБлокировки%", Выборка.ТипБлокировки);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьСписокРегистровДляКонтроля()

	Массив = Новый Массив;
	
	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);

КонецПроцедуры

Процедура ПроверитьОрдерныйСклад(Отказ)
	
	Если НЕ СкладыСервер.ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач(Склад, Дата) Тогда
		
		Отказ = Истина;
		ТекстСообщения = НСтр("ru='На складе ""%Склад%"" на %Дата% не используется ордерная схема при отражении излишков, недостач и порчи.';uk='На складі ""%Склад%"" на %Дата% не використовується ордерна схема при відображенні надлишків, нестач і псування.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%Склад%",Склад);
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%Дата%",Формат(Дата, "ДФ=dd.MM.yyyy"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли