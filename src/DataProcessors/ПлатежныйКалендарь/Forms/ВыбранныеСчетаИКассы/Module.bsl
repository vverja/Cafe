
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтображатьНераспределенныеПоСчетамПоступленияИСписания = 
			Параметры.ОтображатьНераспределенныеПоСчетамПоступленияИСписания;
	
	ВыбранныеБанковскиеСчетаКассы = Параметры.БанковскиеСчетаКассы;
	Организации = Параметры.Организации;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БанковскиеСчетаОрганизаций.Ссылка,
		|	БанковскиеСчетаОрганизаций.Наименование КАК Наименование
		|ПОМЕСТИТЬ ОбъектыУчетаДС
		|ИЗ
		|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
		|ГДЕ
		|	(&НеНуженОтборПоОрганизациям
		|			ИЛИ БанковскиеСчетаОрганизаций.Владелец В (&Организации))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Кассы.Ссылка,
		|	Кассы.Наименование
		|ИЗ
		|	Справочник.Кассы КАК Кассы
		|ГДЕ
		|	(&НеНуженОтборПоОрганизациям
		|			ИЛИ Кассы.Владелец В (&Организации))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбъектыУчетаДС.Ссылка КАК Значение,
		|	ОбъектыУчетаДС.Наименование КАК Представление,
		|	ВЫБОР
		|		КОГДА ОбъектыУчетаДС.Ссылка В (&БанковскиеСчетаКассы)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Пометка
		|ИЗ
		|	ОбъектыУчетаДС КАК ОбъектыУчетаДС
		|
		|УПОРЯДОЧИТЬ ПО
		|	Представление";
	
	Запрос.УстановитьПараметр("БанковскиеСчетаКассы", ВыбранныеБанковскиеСчетаКассы);
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("НеНуженОтборПоОрганизациям", НЕ Организации.Количество());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		БанковскиеСчетаКассы.Добавить(Выборка.Значение, Выборка.Представление, Выборка.Пометка);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	Результат = Новый Структура;
	Результат.Вставить("ОтображатьНераспределенныеПоСчетамПоступленияИСписания",
								ОтображатьНераспределенныеПоСчетамПоступленияИСписания);
	
	ВыбранныеБанковскиеСчетаКассы = Новый СписокЗначений;
	Для Каждого ЭлементСписка из БанковскиеСчетаКассы Цикл
		Если ЭлементСписка.Пометка Тогда
			ВыбранныеБанковскиеСчетаКассы.Добавить(ЭлементСписка.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Результат.Вставить("БанковскиеСчетаКассы", ВыбранныеБанковскиеСчетаКассы);
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы



#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормы



#КонецОбласти

#Область ОбработчикиКомандФормы



#КонецОбласти

#Область СлужебныеПроцедурыИФункции



#КонецОбласти