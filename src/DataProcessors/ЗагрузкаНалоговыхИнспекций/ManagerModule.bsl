#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ЗагрузитьДанныеГНИ(ПараметрыВыгрузки, АдресХранилища) Экспорт
	
	ФайлДанных = ПолучитьИмяВременногоФайла("xml");
	ДвоичныеДанныеФайла = ПараметрыВыгрузки.ДвоичныеДанныеФайла;
	ДвоичныеДанныеФайла.Записать(ФайлДанных);
	
	НеОбновлять = ПараметрыВыгрузки.НеОбновлять;
	
	xmlDoc = Новый ЧтениеXML;
	xmlDoc.ОткрытьФайл(ФайлДанных);
	
	ЗагрузитьСписокНалоговыхИзXML(xmlDoc, Неопределено, Неопределено, Неопределено, Неопределено, НеОбновлять);
	
	xmlDoc.Закрыть();
	УдалитьФайлы(ФайлДанных);
	
	ТекстСообщения =  НСтр("ru='Загрузка данных завершена.';uk='Завантаження даних завершено.'");	
	ПоместитьВоВременноеХранилище(ТекстСообщения, АдресХранилища);
	
КонецПроцедуры

Процедура ЗагрузитьСписокНалоговыхИзXML(xmlDoc, ДанныеНалоговой, ДанныеРегиона, Знач РодительскийЭлемент, Знач ТекущийЭлемент, НеОбновлять)
	
	Пока xmlDoc.Прочитать() Цикл
		
		Если xmlDoc.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			
			// начало информации о регионе (области)
			Если xmlDoc.Имя = "ROW" Тогда
				
				ДанныеРегиона = Новый Соответствие();
				РодительскийЭлемент = xmlDoc.Имя;
				
			КонецЕсли;
			
			// начало информации об инспекции
			Если xmlDoc.Имя = "ROW_STI" Тогда
				
				ДанныеНалоговой = Новый Соответствие();
				РодительскийЭлемент = xmlDoc.Имя;
				
				Если ТипЗнч(ДанныеРегиона) = Тип("Соответствие") Тогда
					
					// преобразуем данные региона в ссылку на группу справочника налоговые инспекции
					СпрНИ = Справочники.НалоговыеИнспекции;
					
					НайденнаяСсылка = СпрНИ.НайтиПоКоду(ДанныеРегиона["C_REG"]);
					Если (НайденнаяСсылка = СпрНИ.ПустаяСсылка()) Тогда
						
						НоваяГруппа = СпрНИ.СоздатьГруппу();
						НоваяГруппа.Наименование = ДанныеРегиона["NAME_REG"];
						НоваяГруппа.Код = ДанныеРегиона["C_REG"];
						НоваяГруппа.Записать();
						
						ДанныеРегиона = НоваяГруппа.Ссылка;
						
					ИначеЕсли НеОбновлять = Ложь Тогда
						
						ОбъектДляЗаписи = НайденнаяСсылка.ПолучитьОбъект(); 
						ОбъектДляЗаписи.Наименование = ДанныеРегиона["NAME_REG"];
						ОбъектДляЗаписи.Записать();
						
						ДанныеРегиона = НайденнаяСсылка;
						
					КонецЕсли;
				
				КонецЕсли;
				
			КонецЕсли;
			
			ЗагрузитьСписокНалоговыхИзXML(xmlDoc, ДанныеНалоговой, ДанныеРегиона, РодительскийЭлемент, xmlDoc.Имя, НеОбновлять);
			
		ИначеЕсли xmlDoc.ТипУзла = ТипУзлаXML.Текст Тогда
			
			Значение = xmlDoc.Значение;
			
			Если РодительскийЭлемент = "ROW" Тогда
				// "реквизиты" региона
				
				Если ТекущийЭлемент = "C_REG" Тогда
					Если СтрДлина(Значение) = 1 Тогда
						//дополним до 2 символов
						Значение = "0"+Значение;
					КонецЕсли;
				КонецЕсли;
				
				ДанныеРегиона.Вставить(ТекущийЭлемент, Значение);
				
			ИначеЕсли РодительскийЭлемент = "ROW_STI" Тогда
				// "реквизиты" налоговой
				
				Если ТекущийЭлемент = "C_STI" Тогда
					Если СтрДлина(Значение) = 3 Тогда
						//дополним до 4-х символов
						Значение = "0"+Значение;
					КонецЕсли;
				КонецЕсли;
				
				Если ТекущийЭлемент = "C_RAJ" Тогда
					Если СтрДлина(Значение) = 1 Тогда
						//дополним до 2-х символов
						Значение = "0"+Значение;
					КонецЕсли;
				КонецЕсли;
				                
				ДанныеНалоговой.Вставить(ТекущийЭлемент, Значение);
				
			КонецЕсли;
			
		ИначеЕсли xmlDoc.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			
			Если ТекущийЭлемент = "ROW_STI" Тогда
				// запишем налоговую
				
				СпрНИ = Справочники.НалоговыеИнспекции;				
				
				//Найдем или создадим новую ДПИ
				НайденнаяССылка =  СпрНИ.НайтиПоРеквизиту("КодДляПоиска", ДанныеНалоговой["C_STI"] + ДанныеНалоговой["C_RAJ"]);
				Если (НайденнаяСсылка = СпрНИ.ПустаяСсылка()) Тогда
					
					НовыйЭлемент = СпрНИ.СоздатьЭлемент();
					НовыйЭлемент.Код 		  = ДанныеНалоговой["C_STI"];
					НовыйЭлемент.КодАдмРайона = ДанныеНалоговой["C_RAJ"];
					НовыйЭлемент.КодДляПоиска = ДанныеНалоговой["C_STI"]+ДанныеНалоговой["C_RAJ"];
				ИначеЕсли НеОбновлять = Истина Тогда
					// не предпринимаем действий
				Иначе
					НовыйЭлемент = НайденнаяСсылка.ПолучитьОбъект();
				КонецЕсли;
				
				НовыйЭлемент.Наименование 			= ДанныеНалоговой["NAME_STI"];
				НовыйЭлемент.НаименованиеАдмРайона 	= ДанныеНалоговой["NAME_RAJ"];
				НовыйЭлемент.ТипДПИ 				= ДанныеНалоговой["T_STI"];
				НовыйЭлемент.Родитель 				= ДанныеРегиона;
				НовыйЭлемент.Записать();
				
			КонецЕсли;

			Возврат;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли