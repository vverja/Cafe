
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ИННПлательщикаНДС) Тогда	
		
		ТаблицаКонтрагентов = ПартнерыИКонтрагенты.ВозможныеГоловныеКонтрагенты(Параметры.ИННПлательщикаНДС, Параметры.Контрагент);
		
		Для Каждого СтрокаТаблицы Из ТаблицаКонтрагентов Цикл
			СписокВыбора.Добавить(
				СтрокаТаблицы.Ссылка,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='%1, Код филиала: %2';uk='%1, Код філії: %2'"),
					СтрокаТаблицы.Представление,
					?(ЗначениеЗаполнено(СтрокаТаблицы.КодФилиала), СтрокаТаблицы.КодФилиала, НСтр("ru='<не заполнен>';uk='<не заповнений>'"))));
		КонецЦикла;
		
	КонецЕсли;
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СписокВыбора.Количество() = 0 Тогда
		Закрыть(ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокВыбора

&НаКлиенте
Процедура СписокВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Закрыть(Элемент.ТекущиеДанные.Значение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ТекущиеДанные = Элементы.СписокВыбора.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru='Контрагент не выбран.';uk='Контрагента не вибрано.'"));
	Иначе
		Закрыть(ТекущиеДанные.Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНового(Команда)
	
	Закрыть(ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));
	
КонецПроцедуры

#КонецОбласти
