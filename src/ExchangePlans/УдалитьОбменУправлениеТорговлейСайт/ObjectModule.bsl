#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ПустаяСтрока(Код) Тогда
		УстановитьНовыйКод();
	КонецЕсли;
	
	Если ПустаяСтрока(Наименование) Тогда
		СформироватьНаименование();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	УдалитьРегламентноеЗадание();
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ДобавитьНепроверяемыеРеквизитыВМассив(МассивНепроверяемыхРеквизитов);

	Если НЕ ОбменТоварами
		И НЕ ОбменЗаказами Тогда
		
		Отказ = Истина;
		Сообщение = НСтр("ru='Режим обмена не выбран!';uk='Режим обміну не вибрано!'");
		Поле = "ОбменТоварами";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Сообщение, ЭтотОбъект, Поле);
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	                      |	Организации.Ссылка
	                      |ИЗ
	                      |	Справочник.Организации КАК Организации");

	Если Запрос.Выполнить().Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Организация = Запрос.Выполнить().Выгрузить()[0][0];
	ОрганизацияВладелецКаталога = Организация;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Код = "";
	ИдентификаторРегламентногоЗадания = НеОпределено;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

Процедура ДобавитьНепроверяемыеРеквизитыВМассив(МассивНепроверяемыхРеквизитов)
	
	// Не проверяем заполнение Кода и Наименования, т.к.
	// принудительно заполняем их ПередЗаписью
	МассивНепроверяемыхРеквизитов.Добавить("Код");
	МассивНепроверяемыхРеквизитов.Добавить("Наименование");

	Если ВыгружатьНаСайт Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("КаталогВыгрузки");
		
	Иначе
		
		МассивНепроверяемыхРеквизитов.Добавить("АдресСайта");
		МассивНепроверяемыхРеквизитов.Добавить("ИмяПользователя");
		
	КонецЕсли;
	
	Если НЕ ОбменТоварами Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ОрганизацияВладелецКаталога");
		
	КонецЕсли;
	
	Если ОбменЗаказами Тогда
		
		Если ЗначениеЗаполнено(Соглашение.Организация) Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("Организация");
			
		КонецЕсли;

		Если ВыгружатьНаСайт Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("ФайлЗагрузки");
			
		КонецЕсли;

	Иначе
		
		МассивНепроверяемыхРеквизитов.Добавить("ВидНоменклатурыТовар");
		МассивНепроверяемыхРеквизитов.Добавить("ВидНоменклатурыУслуга");
		МассивНепроверяемыхРеквизитов.Добавить("ЕдиницаИзмеренияНовойНоменклатуры");
		МассивНепроверяемыхРеквизитов.Добавить("Соглашение");
		МассивНепроверяемыхРеквизитов.Добавить("СпособИдентификацииКонтрагентов");
		
		МассивНепроверяемыхРеквизитов.Добавить("ДополнительныйРеквизитЗаказаКлиентаНомерЗаказаНаСайте");
		МассивНепроверяемыхРеквизитов.Добавить("ДополнительныйРеквизитЗаказаКлиентаДатаЗаказаНаСайте");
		
		МассивНепроверяемыхРеквизитов.Добавить("Организация");
		МассивНепроверяемыхРеквизитов.Добавить("ФайлЗагрузки");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьНаименование()
	
	Если ОбменТоварами И ОбменЗаказами Тогда
		
		Префикс = НСтр("ru='Обмен товарами и заказами';uk='Обмін товарами і замовленнями'");
		
	ИначеЕсли ОбменЗаказами Тогда
		
		Префикс = НСтр("ru='Обмен заказами';uk='Обмін замовленнями'");
		
	Иначе
		
		Префикс = НСтр("ru='Выгрузка товаров';uk='Вивантаження товарів'");
		
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	МАКСИМУМ(ОбменУправлениеТорговлейСайт.Наименование) КАК Наименование
	               |ИЗ
	               |	ПланОбмена.ОбменУправлениеТорговлейСайт КАК ОбменУправлениеТорговлейСайт
	               |ГДЕ
	               |	ОбменУправлениеТорговлейСайт.Наименование ПОДОБНО &Шаблон
	               |
	               |ИМЕЮЩИЕ
	               |	(НЕ МАКСИМУМ(ОбменУправлениеТорговлейСайт.Наименование) ЕСТЬ NULL )";
	
	Запрос.УстановитьПараметр("Шаблон", Префикс + "%");
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Наименование = Префикс;
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Суффикс = Прав(СокрЛП(Выборка.Наименование), 4);
	
	Попытка
		СуффиксЧислом = Число(Суффикс);
	Исключение
		Наименование = Префикс + " 0001";
		Возврат;
	КонецПопытки;
		
	Наименование = Префикс + " " + Формат(СуффиксЧислом + 1, "ЧЦ=4; ЧВН=; ЧГ=");
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ПолучитьРегламентноеЗадание() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Задание = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторРегламентногоЗадания);
	УстановитьПривилегированныйРежим(Ложь);
	Возврат Задание;
КонецФункции

Процедура УдалитьРегламентноеЗадание() Экспорт
	Задание = ПолучитьРегламентноеЗадание();
	Если НЕ Задание = НеОпределено Тогда
		УстановитьПривилегированныйРежим(Истина);
		Задание.Удалить();
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли