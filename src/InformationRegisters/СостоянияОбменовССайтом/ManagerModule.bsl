#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьСостоянияОбменовССайтами(Параметры) Экспорт
	
	Если Не Параметры.Свойство("ВсегоЗаписей") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ОбменССайтом.Ссылка КАК Настройка,
		|	СУММА(1) КАК Количество
		|ИЗ
		|	ПланОбмена.ОбменССайтом КАК ОбменССайтом
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОбменовДанными КАК СостоянияОбменовДанными
		|		ПО ОбменССайтом.Ссылка = СостоянияОбменовДанными.УзелИнформационнойБазы
		|ГДЕ
		|	НЕ ОбменССайтом.Ссылка = &ЭтотУзел
		|
		|СГРУППИРОВАТЬ ПО
		|	ОбменССайтом.Ссылка";
		
		Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.ОбменССайтом.ЭтотУзел());
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Параметры.Вставить("ВсегоЗаписей", Выборка.Количество);
		Иначе
			Параметры.Вставить("ВсегоЗаписей", 0);
		КонецЕсли;
	КонецЕсли;

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ОбменССайтом.Ссылка КАК Настройка
	|ИЗ
	|	ПланОбмена.ОбменССайтом КАК ОбменССайтом
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОбменовДанными КАК СостоянияОбменовДанными
	|		ПО ОбменССайтом.Ссылка = СостоянияОбменовДанными.УзелИнформационнойБазы
	|ГДЕ
	|	НЕ ОбменССайтом.Ссылка = &ЭтотУзел";
	
	Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.ОбменССайтом.ЭтотУзел());
	Результат = Запрос.Выполнить();
	
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		Если Параметры.Свойство("ОбработаноЗаписей") Тогда
			Параметры.ОбработаноЗаписей = Параметры.ОбработаноЗаписей + Выборка.Количество();
		Иначе
			Параметры.Вставить("ОбработаноЗаписей", Выборка.Количество());
		КонецЕсли;
		
		Параметры.ОбработкаЗавершена = Ложь;
		Параметры.ПрогрессВыполнения.ВсегоОбъектов = Параметры.ВсегоЗаписей;
		Параметры.ПрогрессВыполнения.ОбработаноОбъектов = Параметры.ОбработаноЗаписей;
	
		Пока Выборка.Следующий() Цикл
			
			
			НаборЗаписейОбменДанными = РегистрыСведений.СостоянияОбменовДанными.СоздатьНаборЗаписей();
			НаборЗаписейОбменДанными.Отбор.УзелИнформационнойБазы.Установить(Выборка.Настройка);
			НаборЗаписейОбменДанными.Прочитать();
			
			НаборЗаписейОбменССайтом = РегистрыСведений.СостоянияОбменовССайтом.СоздатьНаборЗаписей();
			НаборЗаписейОбменССайтом.Отбор.НастройкаОбменаССайтом.Установить(Выборка.Настройка);
			НаборЗаписейОбменССайтом.Прочитать();
			
			Для Каждого ЗаписьОбменаДанными Из НаборЗаписейОбменДанными Цикл
				
				ЗаписьОбменаССайтом = НаборЗаписейОбменССайтом.Добавить();
				
				ЗаполнитьЗначенияСвойств(ЗаписьОбменаССайтом, ЗаписьОбменаДанными);
				
				ЗаписьОбменаССайтом.НастройкаОбменаССайтом = ЗаписьОбменаДанными.УзелИнформационнойБазы;
				
				Если ЗаписьОбменаДанными.ДействиеПриОбмене = Перечисления.ДействияПриОбмене.ВыгрузкаДанных Тогда
					ДействиеПриОбмене = Перечисления.ДействияПриОбменеССайтом.ВыгрузкаДанных;
				ИначеЕсли ЗаписьОбменаДанными.ДействиеПриОбмене = Перечисления.ДействияПриОбмене.ЗагрузкаДанных Тогда
					ДействиеПриОбмене = Перечисления.ДействияПриОбменеССайтом.ЗагрузкаДанных;
				КонецЕсли;
				ЗаписьОбменаССайтом.ДействиеПриОбмене = ДействиеПриОбмене;
				
				Если ЗаписьОбменаДанными.РезультатВыполненияОбмена = Перечисления.РезультатыВыполненияОбмена.Выполнено Тогда
					Результат = Перечисления.РезультатыОбменаССайтом.Выполнено;
				ИначеЕсли ЗаписьОбменаДанными.РезультатВыполненияОбмена = Перечисления.РезультатыВыполненияОбмена.ВыполненоСПредупреждениями Тогда
					Результат = Перечисления.РезультатыОбменаССайтом.ВыполненоСПредупреждениями;
				ИначеЕсли ЗаписьОбменаДанными.РезультатВыполненияОбмена = Перечисления.РезультатыВыполненияОбмена.Ошибка Тогда
					Результат = Перечисления.РезультатыОбменаССайтом.Ошибка;
				КонецЕсли;
				ЗаписьОбменаССайтом.РезультатВыполненияОбмена = Результат;
				
			КонецЦикла;
		
			НаборЗаписейОбменССайтом.Записать();
			
			НаборЗаписейОбменДанными.Очистить();
			НаборЗаписейОбменДанными.Записать();
			
			НаборЗаписейСостоянияУспешныхОбменов = РегистрыСведений.СостоянияУспешныхОбменовДанными.СоздатьНаборЗаписей();
			НаборЗаписейСостоянияУспешныхОбменов.Отбор.УзелИнформационнойБазы.Установить(Выборка.Настройка);
			НаборЗаписейСостоянияУспешныхОбменов.Прочитать();
			
			НаборЗаписейУспешныеОбменыССайтом = РегистрыСведений.УспешныеОбменыССайтом.СоздатьНаборЗаписей();
			НаборЗаписейУспешныеОбменыССайтом.Отбор.НастройкаОбменаССайтом.Установить(Выборка.Настройка);
			НаборЗаписейУспешныеОбменыССайтом.Прочитать();
			
			Для Каждого ЗаписьОбменаДанными Из НаборЗаписейСостоянияУспешныхОбменов Цикл
				
				ЗаписьОбменаССайтом = НаборЗаписейУспешныеОбменыССайтом.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьОбменаССайтом, ЗаписьОбменаДанными);
				
				ЗаписьОбменаССайтом.НастройкаОбменаССайтом = ЗаписьОбменаДанными.УзелИнформационнойБазы;
				
				Если ЗаписьОбменаДанными.ДействиеПриОбмене = Перечисления.ДействияПриОбмене.ВыгрузкаДанных Тогда
					ДействиеПриОбмене = Перечисления.ДействияПриОбменеССайтом.ВыгрузкаДанных;
				ИначеЕсли ЗаписьОбменаДанными.ДействиеПриОбмене = Перечисления.ДействияПриОбмене.ЗагрузкаДанных Тогда
					ДействиеПриОбмене = Перечисления.ДействияПриОбменеССайтом.ЗагрузкаДанных;
				КонецЕсли;
				ЗаписьОбменаССайтом.ДействиеПриОбмене = ДействиеПриОбмене;
				
			КонецЦикла;
			
			НаборЗаписейУспешныеОбменыССайтом.Записать();
			
			НаборЗаписейСостоянияУспешныхОбменов.Очистить();
			НаборЗаписейСостоянияУспешныхОбменов.Записать();
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
