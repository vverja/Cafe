#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы


Процедура ИсправитьДвижения_ДанныеДляОбновления(Параметры) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ИсправляемыйДокумент.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ИсправляемыйДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПереданнаяВозвратнаяТара КАК ПереданнаяВозвратнаяТара
	|		ПО ИсправляемыйДокумент.Ссылка = ПереданнаяВозвратнаяТара.Регистратор
	|ГДЕ
	|	ИсправляемыйДокумент.Проведен
	|	И ИсправляемыйДокумент.Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	И ИсправляемыйДокумент.ВернутьМногооборотнуюТару
	|	И ПереданнаяВозвратнаяТара.Регистратор ЕСТЬ NULL");
	
	Результат = Запрос.Выполнить().Выгрузить();
	Регистраторы = Результат.ВыгрузитьКолонку("Ссылка");
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
		Параметры,
		Регистраторы,
		"РегистрНакопления.ПереданнаяВозвратнаяТара");
		
КонецПроцедуры

Процедура ИсправитьДвижения(Параметры) Экспорт
	
	Регистраторы = Новый Массив;
	Регистраторы.Добавить("Документ.РеализацияТоваровУслуг");
	
	ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
        Регистраторы,
		"РегистрНакопления.ПереданнаяВозвратнаяТара",
		Параметры.Очередь
    );
	
	Параметры.ОбработкаЗавершена = ОбработкаЗавершена;
	
КонецПроцедуры


#КонецОбласти
	
#КонецОбласти
	
#КонецЕсли
