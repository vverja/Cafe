
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОтборНоменклатура = Параметры.Ключ;
	
	Элементы.СтраницаЦеныНоменклатуры.Видимость = ПравоДоступа("Просмотр",
		Метаданные.РегистрыСведений.ЦеныНоменклатуры);
	Элементы.СтраницаЦеныНоменклатурыПоставщиков.Видимость = ПравоДоступа("Просмотр",
		Метаданные.РегистрыСведений.ЦеныНоменклатурыПоставщиков);
	
	Если Элементы.СтраницаЦеныНоменклатуры.Видимость Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			ЦеныНоменклатуры,
			"Номенклатура",
			ОтборНоменклатура,
			ВидСравненияКомпоновкиДанных.Равно,
			"Номенклатура",
			Истина);
		
		СписокВидовЦен = Новый СписокЗначений;
		СписокВидовЦен.ЗагрузитьЗначения(Справочники.ВидыЦен.ДоступныеВидыЦен());
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			ЦеныНоменклатуры,
			"ВидЦены",
			СписокВидовЦен,
			ВидСравненияКомпоновкиДанных.ВСписке,
			"ВидЦеныОграничениеПоПравам",
			Истина);
		
	КонецЕсли;
	
	Если Элементы.СтраницаЦеныНоменклатурыПоставщиков.Видимость Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			ЦеныНоменклатурыПоставщиков,
			"Номенклатура",
			ОтборНоменклатура,
			ВидСравненияКомпоновкиДанных.Равно,
			"Номенклатура",
			Истина);
		
	КонецЕсли;
	
	СписокВидовЦен = Новый СписокЗначений();
	СписокВидовЦен.ЗагрузитьЗначения(Справочники.ВидыЦен.ДоступныеВидыЦен());
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ЦеныНоменклатуры, 
		"ВидЦены", 
		СписокВидовЦен, 
		ВидСравненияКомпоновкиДанных.ВСписке, 
		"ВидЦеныОграничениеПоПравам", 
		Истина);

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодЦеныНоменклатурыПриИзменении(Элемент)	
	
	ПериодЦеныНоменклатурыПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЦеныНоменклатурыПриИзменении(Элемент)
	
	ВидЦеныНоменклатурыПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодЦеныНоменклатурыПоставщикаПриИзменении(Элемент)
	
	ПериодЦеныНоменклатурыПоставщиковПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодЦеныНоменклатурыПоставщикаОчистка(Элемент, СтандартнаяОбработка)
	
	ПериодЦеныНоменклатурыПоставщиковПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПартнерПриИзменении(Элемент)
	
	ПартнерПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЦеныПоставщикаПриИзменении(Элемент)
	
	ВидЦеныПоставщикаПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура ПериодЦеныНоменклатурыПриИзмененииНаСервере()
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(ЦеныНоменклатуры,"Период","Период");
	
	ГруппаЭлементовОтбора = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(ЦеныНоменклатуры).Элементы,
		"ЦеныНоменклатурыОтборПоПериоду",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаЭлементовОтбора,
		"Период",
		ВидСравненияКомпоновкиДанных.БольшеИлиРавно,
		ПериодЦеныНоменклатуры.ДатаНачала,
		"Период",
		ЗначениеЗаполнено(ПериодЦеныНоменклатуры.ДатаНачала));
	
	// Установить элемент отбора в группе.
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаЭлементовОтбора, "Период", ВидСравненияКомпоновкиДанных.МеньшеИлиРавно, 
		ПериодЦеныНоменклатуры.ДатаОкончания, "Период", ЗначениеЗаполнено(ПериодЦеныНоменклатуры.ДатаОкончания));
	
КонецПроцедуры

&НаСервере
Процедура ВидЦеныНоменклатурыПриИзмененииНаСервере()
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(ЦеныНоменклатуры,,"ВидЦены");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(ЦеныНоменклатуры), "ВидЦены", 
		ВидСравненияКомпоновкиДанных.Равно, ВидЦеныНоменклатуры, "ВидЦены", ЗначениеЗаполнено(ВидЦеныНоменклатуры));
	
КонецПроцедуры

&НаСервере
Процедура ПериодЦеныНоменклатурыПоставщиковПриИзмененииНаСервере()
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(ЦеныНоменклатурыПоставщиков,"Период","Период");
	
	ГруппаЭлементовОтбора = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(ЦеныНоменклатурыПоставщиков).Элементы,
		"ЦеныНоменклатурыОтборПоПериоду", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаЭлементовОтбора, "Период", ВидСравненияКомпоновкиДанных.БольшеИлиРавно,
		ПериодЦеныНоменклатурыПоставщика.ДатаНачала, "Период", ЗначениеЗаполнено(ПериодЦеныНоменклатурыПоставщика.ДатаНачала));
	
	// Установить элемент отбора в группе.
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаЭлементовОтбора, "Период", 
		ВидСравненияКомпоновкиДанных.МеньшеИлиРавно, ПериодЦеныНоменклатурыПоставщика.ДатаОкончания, "Период", 
		ЗначениеЗаполнено(ПериодЦеныНоменклатурыПоставщика.ДатаОкончания));
	
КонецПроцедуры

&НаСервере
Процедура ПартнерПриИзмененииНаСервере()
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(ЦеныНоменклатурыПоставщиков,"Партнер","Партнер");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(ЦеныНоменклатурыПоставщиков), "Партнер",
		ВидСравненияКомпоновкиДанных.Равно, Партнер, "Партнер", ЗначениеЗаполнено(Партнер));
	
КонецПроцедуры

&НаСервере
Процедура ВидЦеныПоставщикаПриИзмененииНаСервере()
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(ЦеныНоменклатурыПоставщиков,"ВидЦеныПоставщика","ВидЦеныПоставщика");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(ЦеныНоменклатурыПоставщиков), "ВидЦеныПоставщика",
		ВидСравненияКомпоновкиДанных.Равно, ВидЦеныПоставщика, "ВидЦеныПоставщика", ЗначениеЗаполнено(ВидЦеныПоставщика));
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
