
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказыватьПриНачалеРаботы = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("RDI",
			"RDI_НовостиЗапуск",
			Истина);
	ПоказыватьПриНачалеРаботыНастройки = НСИССервер.ПолучитьНастройкиЗагрузок().ОткрыватьИнформациюПриСтарте;		
	
	Если Параметры.Свойство("ЗапускПриСтарте") И (НЕ ПоказыватьПриНачалеРаботы ИЛИ НЕ ПоказыватьПриНачалеРаботыНастройки ИЛИ НЕ ПравоДоступа("Просмотр", Метаданные.Обработки.НСИСНовости)) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;	
	
	ПоказатьНовости();
	Элементы.ФормаОбновитьНовости.Видимость = ПравоДоступа("Просмотр", Метаданные.Обработки.НСИСЗапускСервисов);
	Элементы.ФормаРедактироватьНастройки.Видимость = ПравоДоступа("Просмотр", Метаданные.Обработки.НСИСНастройка);
	Элементы.ПоказыватьПриНачалеРаботы.Видимость = ПравоДоступа("СохранениеДанныхПользователя", Метаданные) И (Параметры.Свойство("ЗапускПриСтарте") ИЛИ Параметры.Свойство("ЗапускИзИнтерфейса"));
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьНовости()
	
	Новости = РегистрыСведений.НСИСФайлы.Получить(Новый Структура("Идентификатор", "Новости"));
	НовостиФайл = Новости.Файл.Получить();
	Если НовостиФайл = Неопределено Тогда
		ТекстНовостей = "<html>" + НСтр("ru='Новости не загружены';uk='Новини не завантажені'") + "</html>";
		Возврат;
	КонецЕсли;
	Попытка
		ТекстНовостей = НовостиФайл.ПолучитьТекст();
	Исключение
		ТекстНовостей = "<html>" + НСтр("ru='Новости не загружены';uk='Новини не завантажені'") + "</html>";
	КонецПопытки;	
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьЗагрузку()
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаименованиеФоновогоЗадания = НСтр("ru='Обновление новостей';uk='Оновлення новин'");
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("ВыводитьПротокол",  Ложь);
	ПараметрыЗагрузки.Вставить("ВФоне",  Истина);
	ПараметрыЗагрузки.Вставить("ЗагружатьНормативныеВеличины",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьКлассификаторы",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьКурсыВалют",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьНовости",  Истина);
	ПараметрыЗагрузки.Вставить("ЗагружатьКалендарь",  Ложь);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("Обработки.НСИСЗапускСервисов.ЗапуститьСервисы", ПараметрыЗагрузки, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ГруппаВыполнение.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьНастройки(Команда)
	
	ФормаНастройки = ОткрытьФорму("Обработка.НСИСНастройка.Форма.Форма");	

	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииЗагрузки(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.ГруппаВыполнение.Видимость = Ложь;
	Элементы.ФормаКоманднаяПанель.Доступность = Истина;
	
	ПоказатьНовости();	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНовости(Команда)
	
	Элементы.ГруппаВыполнение.Видимость = Истина;
	Элементы.ФормаКоманднаяПанель.Доступность = Ложь;

	ОперацияЗагрузки = ВыполнитьЗагрузку();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииЗагрузки", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ОперацияЗагрузки, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры


&НаСервере
Процедура ПоказыватьПриНачалеРаботыПриИзмененииНаСервере()
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("RDI",
				"RDI_НовостиЗапуск",
				ПоказыватьПриНачалеРаботы);
КонецПроцедуры


&НаКлиенте
Процедура ПоказыватьПриНачалеРаботыПриИзменении(Элемент)
	ПоказыватьПриНачалеРаботыПриИзмененииНаСервере();
КонецПроцедуры


&НаКлиенте
Процедура ТекстНовостейПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ДанныеСобытия.href) Тогда
		Если СтрНайти(ДанныеСобытия.href,"rdi:") > 0 Тогда
			ПерейтиПоНавигационнойСсылке(СтрЗаменить(ДанныеСобытия.href,"rdi:",""));
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;	
	
	
КонецПроцедуры

