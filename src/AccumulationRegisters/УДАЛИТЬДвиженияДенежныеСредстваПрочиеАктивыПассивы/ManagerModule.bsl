#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбновлениеИнформационнойБазы


#Область ПеренестиДанныеВРегистрДвиженияДенежныеСредстваДоходыРасходы

#Область ПеренестиДвиженияПоДокументуЗаявкаНаРасходованиеДенежныхСредств

Процедура ПеренестиДвиженияПоДокументуЗаявкаНаРасходованиеДенежныхСредствЗаполнитьДанныеДляОбновления(Параметры) Экспорт
	
	ПолноеИмяДокумента = "Документ.ЗаявкаНаРасходованиеДенежныхСредств";
	ЗарегистрироватьРегистраторыПоТипуДокумента(ПолноеИмяДокумента, Параметры)
	
КонецПроцедуры

Процедура ПеренестиДвиженияПоДокументуЗаявкаНаРасходованиеДенежныхСредств(Параметры) Экспорт
	
	ПолноеИмяДокумента = "Документ.ЗаявкаНаРасходованиеДенежныхСредств";
	ПеренестиДвиженияПоТипуДокумента(ПолноеИмяДокумента, Параметры)
	
КонецПроцедуры

#КонецОбласти

#Область ПеренестиДвиженияПоДокументуПоступлениеБезналичныхДенежныхСредств

Процедура ПеренестиДвиженияПоДокументуПоступлениеБезналичныхДенежныхСредствЗаполнитьДанныеДляОбновления(Параметры) Экспорт
	
	ПолноеИмяДокумента = "Документ.ПоступлениеБезналичныхДенежныхСредств";
	ЗарегистрироватьРегистраторыПоТипуДокумента(ПолноеИмяДокумента, Параметры)
	
КонецПроцедуры

Процедура ПеренестиДвиженияПоДокументуПоступлениеБезналичныхДенежныхСредств(Параметры) Экспорт
	
	ПолноеИмяДокумента = "Документ.ПоступлениеБезналичныхДенежныхСредств";
	ПеренестиДвиженияПоТипуДокумента(ПолноеИмяДокумента, Параметры)
	
КонецПроцедуры

#КонецОбласти

#Область ПеренестиДвиженияПоДокументуПриходныйКассовыйОрдер

Процедура ПеренестиДвиженияПоДокументуПриходныйКассовыйОрдерЗаполнитьДанныеДляОбновления(Параметры) Экспорт
	
	ПолноеИмяДокумента = "Документ.ПриходныйКассовыйОрдер";
	ЗарегистрироватьРегистраторыПоТипуДокумента(ПолноеИмяДокумента, Параметры)
	
КонецПроцедуры

Процедура ПеренестиДвиженияПоДокументуПриходныйКассовыйОрдер(Параметры) Экспорт
	
	ПолноеИмяДокумента = "Документ.ПриходныйКассовыйОрдер";
	ПеренестиДвиженияПоТипуДокумента(ПолноеИмяДокумента, Параметры)
	
КонецПроцедуры

#КонецОбласти

#Область ПеренестиДвиженияПоДокументуРасходныйКассовыйОрдер

Процедура ПеренестиДвиженияПоДокументуРасходныйКассовыйОрдерЗаполнитьДанныеДляОбновления(Параметры) Экспорт
	
	ПолноеИмяДокумента = "Документ.РасходныйКассовыйОрдер";
	ЗарегистрироватьРегистраторыПоТипуДокумента(ПолноеИмяДокумента, Параметры)
	
КонецПроцедуры

Процедура ПеренестиДвиженияПоДокументуРасходныйКассовыйОрдер(Параметры) Экспорт
	
	ПолноеИмяДокумента = "Документ.РасходныйКассовыйОрдер";
	ПеренестиДвиженияПоТипуДокумента(ПолноеИмяДокумента, Параметры)
	
КонецПроцедуры

#КонецОбласти

#Область ПеренестиДвиженияПоДокументуСписаниеБезналичныхДенежныхСредств

Процедура ПеренестиДвиженияПоДокументуСписаниеБезналичныхДенежныхСредствЗаполнитьДанныеДляОбновления(Параметры) Экспорт
	
	ПолноеИмяДокумента = "Документ.СписаниеБезналичныхДенежныхСредств";
	ЗарегистрироватьРегистраторыПоТипуДокумента(ПолноеИмяДокумента, Параметры)
	
КонецПроцедуры

Процедура ПеренестиДвиженияПоДокументуСписаниеБезналичныхДенежныхСредств(Параметры) Экспорт
	
	ПолноеИмяДокумента = "Документ.СписаниеБезналичныхДенежныхСредств";
	ПеренестиДвиженияПоТипуДокумента(ПолноеИмяДокумента, Параметры)
	
КонецПроцедуры

#КонецОбласти

Процедура ЗарегистрироватьРегистраторыПоТипуДокумента(ПолноеИмяДокумента, Параметры)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДенежныеСредстваПрочиеАктивыПассивы.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.УдалитьДвиженияДенежныеСредстваПрочиеАктивыПассивы КАК ДенежныеСредстваПрочиеАктивыПассивы
	|ГДЕ
	|	ДенежныеСредстваПрочиеАктивыПассивы.Регистратор ССЫЛКА %ПолноеИмяДокумента
	|";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПолноеИмяДокумента", ПолноеИмяДокумента);
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрНакопления.УдалитьДвиженияДенежныеСредстваПрочиеАктивыПассивы";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
        Параметры, 
        Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор"), 
        ДополнительныеПараметры
    );
	
КонецПроцедуры

Процедура ПеренестиДвиженияПоТипуДокумента(ПолноеИмяДокумента, Параметры)
	
	ПолноеИмяРегистра = "РегистрНакопления.УдалитьДвиженияДенежныеСредстваПрочиеАктивыПассивы";
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуРегистраторовРегистраДляОбработки(
	    Параметры.Очередь, 
		ПолноеИмяДокумента,
		ПолноеИмяРегистра, 
		МенеджерВТ
    );
	
	Если НЕ Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДенежныеСредстваПрочиеАктивыПассивы.Период,
	|	ДенежныеСредстваПрочиеАктивыПассивы.Регистратор,
	|	ДенежныеСредстваПрочиеАктивыПассивы.НомерСтроки,
	|	ДенежныеСредстваПрочиеАктивыПассивы.Активность,
	|	ДенежныеСредстваПрочиеАктивыПассивы.ХозяйственнаяОперация,
	|	ДенежныеСредстваПрочиеАктивыПассивы.Организация,
	|	ДенежныеСредстваПрочиеАктивыПассивы.Подразделение,
	|	ДенежныеСредстваПрочиеАктивыПассивы.ДенежныеСредства,
	|	ДенежныеСредстваПрочиеАктивыПассивы.ТипДенежныхСредств,
	|	ДенежныеСредстваПрочиеАктивыПассивы.СтатьяДвиженияДенежныхСредств,
	|	ДенежныеСредстваПрочиеАктивыПассивы.Валюта,
	|	ДенежныеСредстваПрочиеАктивыПассивы.СтатьяАктивовПассивов КАК СтатьяДоходовРасходов,
	|	ДенежныеСредстваПрочиеАктивыПассивы.АналитикаАктивовПассивов,
    |
	|	ДенежныеСредстваПрочиеАктивыПассивы.Сумма,
	|	ДенежныеСредстваПрочиеАктивыПассивы.СуммаРегл,
	|	ДенежныеСредстваПрочиеАктивыПассивы.СуммаВВалюте,
    |
	|	ДенежныеСредстваПрочиеАктивыПассивы.ИсточникГФУДенежныхСредств
	|ИЗ
	|	РегистрНакопления.УдалитьДвиженияДенежныеСредстваПрочиеАктивыПассивы КАК ДенежныеСредстваПрочиеАктивыПассивы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТДляОбработки КАК ВТДляОбработки
	|	ПО
	|		ДенежныеСредстваПрочиеАктивыПассивы.Регистратор = ВТДляОбработки.Регистратор
	|
	|ИТОГИ ПО
	|	ДенежныеСредстваПрочиеАктивыПассивы.Регистратор
	|";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТДляОбработки", Результат.ИмяВременнойТаблицы);
	Запрос.Выполнить();
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДокументы.Следующий() Цикл
	
		НачатьТранзакцию();
	
		Попытка
            
            Блокировка = Новый БлокировкаДанных;
            
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.УдалитьДвиженияДенежныеСредстваПрочиеАктивыПассивы.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ВыборкаДокументы.Регистратор);
            
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ДвиженияДенежныеСредстваДоходыРасходы.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ВыборкаДокументы.Регистратор);
			
			Блокировка.Заблокировать();
			
			НаборЗаписейДДДР = РегистрыНакопления.ДвиженияДенежныеСредстваДоходыРасходы.СоздатьНаборЗаписей();
			НаборЗаписейДДДР.Отбор.Регистратор.Установить(ВыборкаДокументы.Регистратор);
			
			ВыборкаДвижения = ВыборкаДокументы.Выбрать();
			Пока ВыборкаДвижения.Следующий() Цикл
				НоваяЗапись = НаборЗаписейДДДР.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДвижения);
            КонецЦикла;
            
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписейДДДР);
			
			// Запишем пустой набор в РН ДвиженияДенежныеСредстваПрочиеАктивыПассивы
			НаборЗаписейДДАП = РегистрыНакопления.УдалитьДвиженияДенежныеСредстваПрочиеАктивыПассивы.СоздатьНаборЗаписей();
			НаборЗаписейДДАП.Отбор.Регистратор.Установить(ВыборкаДокументы.Регистратор);
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписейДДАП);
			
			ЗафиксироватьТранзакцию();
		
		Исключение
			ОтменитьТранзакцию();
			ШаблонСообщения = НСтр("ru='Не удалось обработать движения документа %1 по причине: %2';uk='Не вдалося обробити рухи документа %1 з причини: %2'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонСообщения, ВыборкаДокументы.Регистратор, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())); 
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				ВыборкаДокументы.Регистратор.Метаданные(),
				ВыборкаДокументы.Регистратор,
				ТекстСообщения
            );
		КонецПопытки;
	
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(
        Параметры.Очередь, 
        ПолноеИмяРегистра
    );
	
КонецПроцедуры

#КонецОбласти


#КонецОбласти

#КонецЕсли
