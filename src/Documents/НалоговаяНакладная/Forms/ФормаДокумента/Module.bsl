&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// Обработчик подсистемы "Свойства"
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Объект);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	ДополнительныеПараметры.Вставить("ОтложеннаяИнициализация", Истина);
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтаФорма, ДополнительныеПараметры);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа")
		 ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа") Тогда
			ПланыВидовХарактеристик.СтатьиРасходов.ЗаполнитьПризнакАналитикаРасходовОбязательна(Объект.ТоварыПоДаннымПользователя, "СтатьяРасходовУсловнаяПродажа, АналитикаРасходовУсловнаяПродажа");
			ПланыВидовХарактеристик.СтатьиРасходов.ЗаполнитьПризнакАналитикаРасходовЗаказРеализация(Объект.ТоварыПоДаннымПользователя, "СтатьяРасходовУсловнаяПродажа");
		КонецЕсли;
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;

	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ВводНаОсновании.ПриСозданииНаСервере(ЭтаФорма, Элементы.ПодменюСоздатьНаОсновании);
	
	МенюОтчеты.ПриСозданииНаСервере(ЭтаФорма, Элементы.ПодменюОтчеты);
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборот.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ИнтеграцияС1СДокументооборотом

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	

	// Обработчик механизма "ДатыЗапретаИзменения"
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	ПриЧтенииСозданииНаСервере();
	
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);

	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа")
	 ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа") Тогда
		ПланыВидовХарактеристик.СтатьиРасходов.ЗаполнитьПризнакАналитикаРасходовОбязательна(Объект.ТоварыПоДаннымПользователя, "СтатьяРасходовУсловнаяПродажа, АналитикаРасходовУсловнаяПродажа");
		ПланыВидовХарактеристик.СтатьиРасходов.ЗаполнитьПризнакАналитикаРасходовЗаказРеализация(Объект.ТоварыПоДаннымПользователя, "СтатьяРасходовУсловнаяПродажа");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	
	Элементы.ГруппаУточняющийРасчетДатаНомер.Видимость = Объект.ВключаетсяВУточняющийРасчет;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Обработчик механизма "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтаФорма, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	
	Если ИмяСобытия = "ИзмененРеквизитЗависящийОтСтатуса"
		И Параметр.УникальныйИдентификатор = УникальныйИдентификатор Тогда
		Если Объект.Согласован Тогда
			Объект.Согласован = Ложь;
		КонецЕсли;
		ПодключитьОбработчикОжидания("Подключаемый_ПриИзмененииРеквизитаЗависящегоОтСтатуса", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	СобытияФормКлиент.ПередЗаписью(ЭтотОбъект, Отказ, ПараметрыЗаписи);
	
	Если НеВыполнятьПроверкуПередЗаписью Тогда
		НеВыполнятьПроверкуПередЗаписью = Ложь;
		Возврат;
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюФрагментЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
    Если Ответ = КодВозвратаДиалога.Отмена Тогда 
        Возврат;
    КонецЕсли;
	
	Результат = ОбщегоНазначенияУТКлиент.ОбработатьЗаписьОбъектаВФорме(ЭтаФорма, ДополнительныеПараметры.ПараметрыЗаписи, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);
	
	ОбщегоНазначенияУТКлиент.ВыполнитьДействияПослеЗаписи(ЭтаФорма, Объект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
		
	// Обработчик механизма "Свойства"
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа")
	 ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа") Тогда
		ПланыВидовХарактеристик.СтатьиРасходов.ЗаполнитьПризнакАналитикаРасходовОбязательна(Объект.ТоварыПоДаннымПользователя, "СтатьяРасходовУсловнаяПродажа, АналитикаРасходовУсловнаяПродажа");
		ПланыВидовХарактеристик.СтатьиРасходов.ЗаполнитьПризнакАналитикаРасходовЗаказРеализация(Объект.ТоварыПоДаннымПользователя, "СтатьяРасходовУсловнаяПродажа");
	КонецЕсли;

	Если ЭтаФорма.Модифицированность И ТекущийОбъект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыНалоговыхДокументов.Сформирован") Тогда
		ТекущийОбъект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыНалоговыхДокументов.КПроверке");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	
	УстановитьДоступностьЭлементовПоСтатусуСервер();
	
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре();
	
	Элементы.ГруппаКомментарий.Картинка = ОбщегоНазначения.ПолучитьКартинкуКомментария(Объект.Комментарий);
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа")
	 ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа") Тогда
		ПланыВидовХарактеристик.СтатьиРасходов.ЗаполнитьПризнакАналитикаРасходовОбязательна(Объект.ТоварыПоДаннымПользователя, "СтатьяРасходовУсловнаяПродажа, АналитикаРасходовУсловнаяПродажа");
		ПланыВидовХарактеристик.СтатьиРасходов.ЗаполнитьПризнакАналитикаРасходовЗаказРеализация(Объект.ТоварыПоДаннымПользователя, "СтатьяРасходовУсловнаяПродажа");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтаФорма, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	// СтандартныеПодсистемы.Свойства
	Если ТекущаяСтраница.Имя = "ГруппаСтраницыДополнительно"
		И Не ЭтотОбъект.ПараметрыСвойств.ВыполненаОтложеннаяИнициализация Тогда
		
		СвойстваВыполнитьОтложеннуюИнициализацию();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	Если ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены") И 
		Не Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены") ТОгда
		
		Если Объект.ТоварыПоДаннымПользователя.Количество() > 0 Тогда
			
			ТекстВопроса = НСтр("ru='Табличная часть будет очищена, продолжить?';uk='Таблична частина буде очищена, продовжити?'");
			
			ПоказатьВопрос(
				Новый ОписаниеОповещения("ВидОперацииПриИзмененииЗавершение", ЭтотОбъект),
				ТекстВопроса,
				РежимДиалогаВопрос.ДаНет
			);
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВидОперацииПриИзмененииНаСервере();
	УстановитьКодПризнакаСводной();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	Если Ответ <> КодВозвратаДиалога.Да Тогда 
		Объект.ВидОперации = ВидОперации;
        Возврат;
    КонецЕсли;
	
	ВидОперацииПриИзмененииНаСервере();
	УстановитьКодПризнакаСводной();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаПриИзменении(Элемент)
	
	Если НЕ (ЦенообразованиеКлиент.НеобходимПересчетВВалюту(Объект, ВалютаДокумента, "Товары") ИЛИ 
		     ЦенообразованиеКлиент.НеобходимПересчетВВалюту(Объект, ВалютаДокумента, "ТоварыПоДаннымПользователя")) Тогда
		ВалютаДокумента = Объект.Валюта;
		Возврат;	
	КонецЕсли;
		
	ТекстСообщения = НСтр("ru='Пересчитать суммы в документе в валюту ""%Валюта%""?';uk='Перерахувати суми в документі в валюту ""%Валюта%""?'");
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Валюта%", Объект.Валюта);
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения("ВалютаПриИзмененииЗавершение", ЭтотОбъект),
		ТекстСообщения,
		РежимДиалогаВопрос.ДаНетОтмена
	);
		
КонецПроцедуры

&НаКлиенте
Процедура ВалютаПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		// Пересчет всех ТЧ
		ПересчитатьСуммыСервер("Товары", Истина);
		ПересчитатьСуммыСервер("ТоварыПоДаннымПользователя", Истина);
				
		ВалютаДокумента = Объект.Валюта;

		ЦенообразованиеКлиент.ОповеститьОбОкончанииПересчетаСуммВВалюту(ВалютаДокумента, Объект.Валюта);
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		
		// Пересчет только сумм регл
		ПересчитатьСуммыСервер("Товары", Ложь);
		ПересчитатьСуммыСервер("ТоварыПоДаннымПользователя", Ложь);
		
		ВалютаДокумента = Объект.Валюта;
		
	Иначе
		
		// Отказ
		Объект.Валюта = ВалютаДокумента;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоставкаСобственнойОрганизацииПриИзменении(Элемент)
	
	ПоставкаСобственнойОрганизацииПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПартнерПриИзменении(Элемент)
	
	ПартнерПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	КонтрагентПриИзмененииСервер();
		
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	
	ДоговорПриИзмененииНаСервере();
	
КонецПроцедуры


&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	
	Если Объект.Согласован
		И НЕ (Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыНалоговыхДокументов.Проверен")) Тогда
		
		Объект.Согласован = Ложь;
		
	КонецЕсли;
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусРегистрацииВЕРННПриИзменении(Элемент)
	
	Если Объект.СтатусАвтокорректировки = ПредопределенноеЗначение("Перечисление.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТекущийДокумент")
	   И (Объект.СтатусРегистрацииВЕРНН = ПредопределенноеЗначение("Перечисление.СтатусыРегистрацииВЕРНННалоговыхДокументов.НаправленНаРегистрациюВЕРНН") ИЛИ
	      Объект.СтатусРегистрацииВЕРНН = ПредопределенноеЗначение("Перечисление.СтатусыРегистрацииВЕРНННалоговыхДокументов.ЗарегистрированВЕРНН")) Тогда
		Объект.СтатусАвтокорректировки = ПредопределенноеЗначение("Перечисление.СтатусыАвтокорректировкиНалоговыхДокументов.ФормироватьПриложение2");
	КонецЕсли;
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусАвтокорректировкиПриИзменении(Элемент)
	
	Если Объект.СтатусАвтокорректировки = ПредопределенноеЗначение("Перечисление.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТехническуюТЧ") Тогда
		
		ТекстВопроса = НСтр("ru='Сохранить текущий номенклатурный состав (в противном случае табличная часть будет очищена)?';uk='Зберегти поточний номенклатурний склад (в іншому випадку таблична частина буде очищена)?'");
		ПоказатьВопрос(
			Новый ОписаниеОповещения("СтатусАвтокорректировкиПриИзмененииЗавершение", ЭтотОбъект),
			ТекстВопроса,
			РежимДиалогаВопрос.ДаНетОтмена
		);
		
	КонецЕсли;
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусАвтокорректировкиПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаполнитьТЧПоДаннымПользователяИзТоварыСервер();
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПодтверждаетсяГТДПриИзменении(Элемент)
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипПричиныНевыдачиПокупателюПриИзменении(Элемент)
	
	Если Объект.ТипПричиныНевыдачиПокупателю > 0 Тогда
		Объект.СтатусВыдачиПокупателю = ПредопределенноеЗначение("Перечисление.СтатусыВыдачиПокупателюНалоговыхДокументов.НеВыданПокупателю");
	КонецЕсли;
	
	Если ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены") Тогда
		
		Если Объект.ТоварыПоДаннымПользователя.Количество() > 0 Тогда
			ТекстВопроса = НСтр("ru='Перезаполнить текст дополнения к наименованию при продаже ниже обычной цены?';uk='Перезаповнити текст доповнення до найменування при продажі нижче звичайної ціни?'");
			ПоказатьВопрос(
				Новый ОписаниеОповещения("ТипПричиныНевыдачиПокупателюПриИзмененииЗавершение", ЭтотОбъект),
				ТекстВопроса,
				РежимДиалогаВопрос.ДаНет
			);
		КонецЕсли;
		
	КонецЕсли;

	ПроверитьСтатьюДекларацииНДС22(Объект);
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипПричиныНевыдачиПокупателюПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаполнитьТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СпецРежимНалогообложенияПриИзменении(Элемент)
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	НастроитьЭлементыФормы();
	
	ПроверитьСтатьюДекларацииНДС22(Объект);
	
	ЗаполнитьРеквизитыРегл();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары


&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Объект, Ложь);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.Характеристика);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", ТекущаяСтрока.Упаковка);
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ЗаполнитьСодержание", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруЗаполненияСодержанияУслугиВСтрокеТЧ(Объект, Истина));
	СтруктураДействий.Вставить("ЗаполнитьПризнакТипНоменклатуры", Новый Структура("Номенклатура", "ТипНоменклатуры")); 
	
	ТоварыНоменклатураПриИзмененииСервер(ТекущаяСтрока.ПолучитьИдентификатор(), СтруктураДействий, КэшированныеЗначения);

	ЗаполнитьРеквизитыРегл("Товары");
	
КонецПроцедуры



&НаКлиенте
Процедура ТоварыУпаковкаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
    СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	НДСИсходящийКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

	ЗаполнитьРеквизитыРегл("Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоУпаковокПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Объект, Ложь);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	Если Не ТекущаяСтрока.НеПересчитыватьСумму Тогда
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСумму");
	КонецЕсли;
	НДСИсходящийКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
		
	ЗаполнитьРеквизитыРегл("Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если Не ТекущаяСтрока.НеПересчитыватьСумму Тогда	
	
		СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Объект, Ложь);
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСумму");
		
		НДСИсходящийКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	КонецЕсли;	
		
	ЗаполнитьРеквизитыРегл("Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаБезНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	ПроцентНДС = НДСОбщегоНазначенияКлиентСервер.ПолучитьСтавкуНДСЧислом(ТекущаяСтрока.СтавкаНДС);	
	Если ПроцентНДС = 0 Тогда
		ТекущаяСтрока.СуммаБезНДС = ТекущаяСтрока.СуммаСНДС;
	КонецЕсли;
	
	ТекущаяСтрока.СуммаНДС = ТекущаяСтрока.СуммаСНДС - ТекущаяСтрока.СуммаБезНДС;
	
	ЗаполнитьРеквизитыРегл("Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если Не ТекущаяСтрока.НеПересчитыватьСумму Тогда	
	
		СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Объект, Ложь);
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСумму");
		
		НДСИсходящийКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	КонецЕсли;	
	
	ЗаполнитьТипПричиныНевыдачиНаСервере();
	ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательстваСервер(ТекущаяСтрока.СтатьяДекларацииНДСНалоговыеОбязательства, ТекущаяСтрока.СтавкаНДС);
	
	ЗаполнитьРеквизитыРегл("Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	ПроцентНДС = НДСОбщегоНазначенияКлиентСервер.ПолучитьСтавкуНДСЧислом(ТекущаяСтрока.СтавкаНДС);
	Если ПроцентНДС = 0 Тогда
		ТекущаяСтрока.СуммаНДС 	= 0;
	КонецЕсли;
	
	ТекущаяСтрока.СуммаБезНДС = ТекущаяСтрока.СуммаСНДС - ТекущаяСтрока.СуммаНДС;
	
	ЗаполнитьРеквизитыРегл("Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаСНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	ПроцентНДС = НДСОбщегоНазначенияКлиентСервер.ПолучитьСтавкуНДСЧислом(ТекущаяСтрока.СтавкаНДС);
	
	ТекущаяСтрока.СуммаНДС = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(ТекущаяСтрока.СуммаСНДС, ПроцентНДС, Истина);
 	ТекущаяСтрока.СуммаБезНДС = ТекущаяСтрока.СуммаСНДС - ТекущаяСтрока.СуммаНДС;
	
	ТекущаяСтрока.Цена = (ТекущаяСтрока.СуммаСНДС - Окр(ТекущаяСтрока.СуммаНДС,2)) / ?(ТекущаяСтрока.КоличествоУпаковок = 0, 1, ТекущаяСтрока.КоличествоУпаковок);
	
	ЗаполнитьРеквизитыРегл("Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКодУКТВЭДПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	ТоварыКодУКТВЭДПриИзмененииСервер(ТекущаяСтрока.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКодНоменклатурыПоКлассификаторуПриИзменении(Элемент)

	ЗаполнитьТипПричиныНевыдачиНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ТоварыНеПересчитыватьСуммуПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если Не ТекущаяСтрока.НеПересчитыватьСумму Тогда
		ТоварыЦенаПриИзменении(Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыДокументПоставкиПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущаяСтрока.НеПересчитыватьСумму И ЗначениеЗаполнено(ТекущаяСтрока.ДокументПоставки) Тогда
		ТекущаяСтрока.НеПересчитыватьСумму = Ложь;
		ТоварыЦенаПриИзменении(Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	НоменклатураКлиентСервер.ПроверитьЗаполнитьПустуюУпаковку(ТекущиеДанные, "ЕдиницаИзмерения");
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаРеглПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Объект, Ложь);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	НДСИсходящийКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаБезНДСРеглПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Объект, Ложь);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	НДСИсходящийКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаНДСРеглПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	ПроцентНДС = НДСОбщегоНазначенияКлиентСервер.ПолучитьСтавкуНДСЧислом(ТекущаяСтрока.СтавкаНДС);
	Если ПроцентНДС = 0 Тогда
		ТекущаяСтрока.СуммаНДСРегл 	= 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)

	ЗаполнитьТипПричиныНевыдачиНаСервере();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТоварыПоДаннымПользователя


&НаКлиенте
Процедура ТоварыПоДаннымПользователяНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;	
	
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Объект, Ложь);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.Характеристика);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", ТекущаяСтрока.Упаковка);
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ЗаполнитьСодержание", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруЗаполненияСодержанияУслугиВСтрокеТЧ(Объект, Истина));
	СтруктураДействий.Вставить("ЗаполнитьПризнакТипНоменклатуры", Новый Структура("Номенклатура", "ТипНоменклатуры"));
	
	ТоварыПоДаннымПользователяНоменклатураПриИзмененииСервер(ТекущаяСтрока.ПолучитьИдентификатор(), СтруктураДействий, КэшированныеЗначения);
		
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены") Тогда
		ПересчитатьСуммуПревышения(ТекущаяСтрока);
	КонецЕсли;

	ЗаполнитьРеквизитыРегл("ТоварыПоДаннымПользователя");

КонецПроцедуры


&НаКлиенте
Процедура ТоварыПоДаннымПользователяОбъектЗаполненияСодержанияУсловнаяПродажаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;	
	
	ЗаполнитьСодержаниеДляУсловнойПродажиСервер(ТекущаяСтрока.ОбъектЗаполненияСодержанияУсловнаяПродажа, ТекущаяСтрока.Содержание);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяКодУКТВЭДПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	
	ТоварыПоДаннымПользователяКодУКТВЭДПриИзмененииСервер(ТекущаяСтрока.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяКодНоменклатурыПоКлассификаторуПриИзменении(Элемент)
	
	ЗаполнитьТипПричиныНевыдачиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяУпаковкаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	НДСИсходящийКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяКоличествоУпаковокПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Объект, Ложь);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	Если Не ТекущаяСтрока.НеПересчитыватьСумму Тогда
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	КонецЕсли;
		
	НДСИсходящийКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены") Тогда
		ПересчитатьСуммуПревышения(ТекущаяСтрока);
	КонецЕсли;
	
	ЗаполнитьРеквизитыРегл("ТоварыПоДаннымПользователя");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяЦенаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	
	Если Не ТекущаяСтрока.НеПересчитыватьСумму Тогда
		
		СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Объект, Ложь);
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
		
		НДСИсходящийКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	КонецЕсли;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены") Тогда
		ПересчитатьСуммуПревышения(ТекущаяСтрока);
	КонецЕсли;

	ЗаполнитьРеквизитыРегл("ТоварыПоДаннымПользователя");

КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяЦенаОбычнаяПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	
	ПересчитатьСуммуПревышения(ТекущаяСтрока);
	
	ЗаполнитьРеквизитыРегл("ТоварыПоДаннымПользователя");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяСуммаБезНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Объект, Ложь);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьЦенуСкидкуПоСуммеВПродажах", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	
	НДСИсходящийКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены") Тогда
		ПересчитатьСуммуПревышения(ТекущаяСтрока);
	КонецЕсли;
	ЗаполнитьРеквизитыРегл("ТоварыПоДаннымПользователя");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяСуммаПревышенияПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	
	ПересчитатьСуммуНДСПревышения(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяСтавкаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	
	Если Не ТекущаяСтрока.НеПересчитыватьСумму Тогда
	
		СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Объект, Ложь);
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
		
		НДСИсходящийКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	КонецЕсли;
	
	ЗаполнитьТипПричиныНевыдачиНаСервере();
	ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательстваСервер(ТекущаяСтрока.СтатьяДекларацииНДСНалоговыеОбязательства, ТекущаяСтрока.СтавкаНДС);
		
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены") Тогда
		ПересчитатьСуммуНДСПревышения(ТекущаяСтрока);
	КонецЕсли;

	ЗаполнитьРеквизитыРегл("ТоварыПоДаннымПользователя");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяСуммаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	
	ТекущаяСтрока.СуммаСНДС = ТекущаяСтрока.СуммаБезНДС + ТекущаяСтрока.СуммаНДС;
	
	ЗаполнитьРеквизитыРегл("ТоварыПоДаннымПользователя");
	
КонецПроцедуры


&НаКлиенте
Процедура ТоварыПоДаннымПользователяЦенаРеглПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Объект, Ложь);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	НДСИсходящийКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяСуммаБезНДСРеглПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Объект, Ложь);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	
	НДСИсходящийКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяСуммаНДСРеглПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	
	ПроцентНДС = НДСОбщегоНазначенияКлиентСервер.ПолучитьСтавкуНДСЧислом(ТекущаяСтрока.СтавкаНДС);
	Если ПроцентНДС = 0 Тогда
		ТекущаяСтрока.СуммаНДСРегл 	= 0;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ТоварыПоДаннымПользователяТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦеныАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	ДанныеВыбора.ЗагрузитьЗначения(ЗаполнитьСписокВыбораТекстДополнения(Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные.ОсновнаяНалоговаяПриПродажеНижеОбычнойЦены, Объект.Дата, Истина));
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяСтатьяРасходовПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	Если ЗначениеЗаполнено(СтрокаТаблицы.СтатьяРасходовУсловнаяПродажа) Тогда
		ПрочиеРасходыСтатьяРасходовПриИзмененииСервер(КэшированныеЗначения);
	Иначе
		СтрокаТаблицы.АналитикаРасходовУсловнаяПродажа = Неопределено;
		СтрокаТаблицы.АналитикаРасходовУсловнаяПродажаОбязательна = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяАналитикаРасходовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтрокаТаблицы = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	Если СтрокаТаблицы.АналитикаРасходовЗаказРеализация Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("ОбщаяФорма.ВыборАналитикиРасходов", , Элемент);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяАналитикаРасходовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтрокаТаблицы = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыбранноеЗначение);
		СтрокаТаблицы.АналитикаРасходовУсловнаяПродажа = ВыбранноеЗначение.АналитикаРасходов;
		СтандартнаяОбработка = Ложь;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяАналитикаРасходовАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Текст) И Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные.АналитикаРасходовЗаказРеализация Тогда
		СтандартнаяОбработка = Ложь;
		АналитикаРасходовПолучениеДанныхВыбора(ДанныеВыбора, Текст);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяАналитикаРасходовОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Текст) И Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные.АналитикаРасходовЗаказРеализация Тогда
		СтандартнаяОбработка = Ложь;
		АналитикаРасходовПолучениеДанныхВыбора(ДанныеВыбора, Текст);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяОбъектРасчетовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗначенияОтбора = Новый Структура;
	
	ЗначенияОтбора.Вставить("Организация",  Объект.Организация);
	ЗначенияОтбора.Вставить("Контрагент", 	Объект.Контрагент);
	ЗначенияОтбора.Вставить("Валюта", 		Объект.Валюта);
	
	НастройкиВыбора = ФинансыКлиент.ПараметрыВыбораДокументаРасчетов();
	НастройкиВыбора.РедактируемыйДокумент = Объект.Ссылка;
	НастройкиВыбора.ЭтоРасчетыСКлиентами  = Истина;
	
	ФинансыКлиент.ДокументРасчетовНачалоВыбора(ЗначенияОтбора, НастройкиВыбора, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяОбъектРасчетовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаТаблицы = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		СтрокаТаблицы.ОбъектРасчетов = ВыбранноеЗначение.Заказ;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекущиеДанные = Элементы.ТоварыПоДаннымПользователя.ТекущиеДанные;
	НоменклатураКлиентСервер.ПроверитьЗаполнитьПустуюУпаковку(ТекущиеДанные, "ЕдиницаИзмерения");
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоДаннымПользователяПослеУдаления(Элемент)

	ЗаполнитьТипПричиныНевыдачиНаСервере();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств(Команда)
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтаФорма, Объект.Ссылка);
	
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьПоПревышениямОбычнойЦеныЗаПериод(Команда)
	
	ОчиститьСообщения();
	
	Отказ = Ложь;	
	Если Объект.Валюта <> ВалютаРегламентированногоУчета Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Заполнение возможно только для документов введенных в валюте регламентированного учета ';uk='Заповнення можливо тільки для документів введених у валюті регламентованого обліку '") + """" + Строка(ВалютаРегламентированногоУчета) + """", , "Объект.Валюта", , Отказ);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Поле ""Организация"" не заполнено';uk='Поле ""Організація"" не заповнене'"), , "Объект.Организация", , Отказ);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.ОбычныйВидЦены) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Поле ""Обычный вид цены"" не заполнено';uk='Поле ""Звичайний вид ціни"" не заповнено'"), , "Объект.ОбычныйВидЦены", , Отказ);
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ТоварыПоДаннымПользователя.Количество() > 0 Тогда
		
		ТекстВопроса = НСтр("ru='Перед заполнением табличная часть будет очищена, продолжить?';uk='Перед заповненням таблична частина буде очищена, продовжити?'");
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ЗаполнитьПоПревышениямОбычнойЦеныЗаПериодНачало", ЭтотОбъект),
			ТекстВопроса,
			РежимДиалогаВопрос.ДаНет
		);
		
	Иначе
		
		ЗаполнитьПоПревышениямОбычнойЦеныЗаПериодПродолжение();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоПревышениямОбычнойЦеныЗаПериодНачало(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Объект.ТоварыПоДаннымПользователя.Очистить();
		
		ЗаполнитьПоПревышениямОбычнойЦеныЗаПериодПродолжение();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоПревышениямОбычнойЦеныЗаПериодПродолжение() Экспорт
	
	ДатаПериода = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДата());
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Если ЗначениеЗаполнено(Объект.Дата) Тогда
		Диалог.Период = Новый СтандартныйПериод(НачалоМесяца(Объект.Дата), КонецМесяца(Объект.Дата));	
	Иначе
		Диалог.Период = Новый СтандартныйПериод(ВариантСтандартногоПериода.ЭтотМесяц);
	КонецЕсли;	
	
	Диалог.Показать(Новый ОписаниеОповещения("ЗаполнитьПоПревышениямОбычнойЦеныЗаПериодЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоПревышениямОбычнойЦеныЗаПериодЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	ПериодЗаполнения = РезультатВыбора;
	
	Если ЗначениеЗаполнено(ПериодЗаполнения) Тогда
		ЗаполнитьПоПревышениямОбычнойЦеныЗаПериодСервер(ПериодЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтатьюРасходовПоВыделеннойСтроке(Команда)
	
	Строка = Элементы.ТоварыПоДаннымПользователя.ДанныеСтроки(Элементы.ТоварыПоДаннымПользователя.ВыделенныеСтроки[0]);
	
	ЗаполнитьСтатьюРасходовПоВыделеннойСтрокеСервер(Строка.СтатьяРасходовУсловнаяПродажа, Строка.АналитикаРасходовУсловнаяПродажа);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры
//Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтаФорма, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
		ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтаФорма, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтаФорма, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуОтчет(Команда)
	
	МенюОтчетыКлиент.ВыполнитьПодключаемуюКомандуОтчет(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуСоздатьНаОсновании(Команда)
	
	ВводНаОснованииКлиент.ВыполнитьПодключаемуюКомандуСоздатьНаОсновании(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОбщегоНазначенияУТКлиент.ПровестиИЗакрыть(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства

&НаСервере
Процедура СвойстваВыполнитьОтложеннуюИнициализацию()
	УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	
	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ТоварыУпаковка
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыУпаковка.Имя);
	//Поле: ТоварыНоменклатураЕдиницаИзмерения
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыНоменклатураЕдиницаИзмерения.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ТоварыЕдиницаИзмерения
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыЕдиницаИзмерения.Имя);
	//Поле: ТоварыУпаковкаЗаголовок
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыУпаковкаЗаголовок.Имя);
	//Поле: ТоварыЕдиницаИзмеренияЗаголовок
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыЕдиницаИзмеренияЗаголовок.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ТоварыПоДаннымПользователяУпаковка
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПоДаннымПользователяУпаковка.Имя);
	//Поле: ТоварыПоДаннымПользователяНоменклатураЕдиницаИзмерения
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПоДаннымПользователяНоменклатураЕдиницаИзмерения.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ТоварыПоДаннымПользователя.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ТоварыПоДаннымПользователяЕдиницаИзмерения
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПоДаннымПользователяЕдиницаИзмерения.Имя);
	//Поле: ТоварыПоДаннымПользователяУпаковкаЗаголовок
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПоДаннымПользователяУпаковкаЗаголовок.Имя);
	//Поле: ТоварыПоДаннымПользователяЕдиницаИзмеренияЗаголовок
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПоДаннымПользователяЕдиницаИзмеренияЗаголовок.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ТоварыПоДаннымПользователя.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ТоварыНоменклатураЕдиницаИзмерения
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыНоменклатураЕдиницаИзмерения.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.Упаковка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);


	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ТоварыУпаковкаЗаголовок
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыУпаковкаЗаголовок.Имя);
	//Поле: ТоварыЕдиницаИзмеренияЗаголовок
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыЕдиницаИзмеренияЗаголовок.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Ложь;
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);


	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ТоварыПоДаннымПользователяНоменклатураЕдиницаИзмерения
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПоДаннымПользователяНоменклатураЕдиницаИзмерения.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ТоварыПоДаннымПользователя.Упаковка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);


	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ТоварыПоДаннымПользователяУпаковкаЗаголовок
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПоДаннымПользователяУпаковкаЗаголовок.Имя);
	//Поле: ТоварыПоДаннымПользователяЕдиницаИзмеренияЗаголовок
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПоДаннымПользователяЕдиницаИзмеренияЗаголовок.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Ложь;
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);


	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ТоварыХарактеристика
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыХарактеристика.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.ХарактеристикиИспользуются");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<характеристики не используются>';uk='<характеристики не використовуються>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);


	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ТоварыПоДаннымПользователяХарактеристика
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПоДаннымПользователяХарактеристика.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ТоварыПоДаннымПользователя.ХарактеристикиИспользуются");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<характеристики не используются>';uk='<характеристики не використовуються>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);


	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ТоварыПоДаннымПользователяТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПоДаннымПользователяТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.Имя);
	//Поле: ТоварыПоДаннымПользователяОсновнаяНалоговаяПриПродажеНижеОбычнойЦены
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПоДаннымПользователяОсновнаяНалоговаяПриПродажеНижеОбычнойЦены.Имя);
	//Поле: ТоварыПоДаннымПользователяДатаОтгрузкиОплаты
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПоДаннымПользователяДатаОтгрузкиОплаты.Имя);
	//Поле: ТоварыПоДаннымПользователяОсновнаяНалоговаяПриПродажеНижеОбычнойЦеныДата
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПоДаннымПользователяОсновнаяНалоговаяПриПродажеНижеОбычнойЦеныДата.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВидОперации");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены");

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ТоварыСодержание
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСодержание.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.ТипНоменклатуры");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга"));
	СписокЗначений.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Работа"));
	ОтборЭлемента.ПравоеЗначение = СписокЗначений;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);


	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: Партнер
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Партнер.Имя);
	//Поле: Контрагент
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Контрагент.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВидОперации");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены");

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);


	////


	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ТоварыПоДаннымПользователяОбъектРасчетов
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПоДаннымПользователяОбъектРасчетов.Имя);
	
	//Поле: ТоварыНеПересчитыватьСуммуНадпись
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыНеПересчитыватьСуммуНадпись.Имя);
	
	//Поле: ТоварыНеПересчитыватьСумму
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыНеПересчитыватьСумму.Имя);
	
	//Поле: ТоварыПоДаннымПользователяНеПересчитыватьСумму
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПоДаннымПользователяНеПересчитыватьСумму.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВидОперации");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации"));
	СписокЗначений.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации"));
	СписокЗначений.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.НеНДСОперации"));
	ОтборЭлемента.ПравоеЗначение = СписокЗначений;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	
	////
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	//Поле: ТоварыНеПересчитыватьСумму
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыНеПересчитыватьСумму.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.ДокументПоставки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	
	////
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	//Поле: ТоварыНеПересчитыватьСуммуНадпись
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыНеПересчитыватьСуммуНадпись.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.ДокументПоставки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение", ПредопределенноеЗначение("ГоризонтальноеПоложение.Лево"));
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<Не используется>';uk='<Не використовується>'"));
	
	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ДатаОтгрузкиОплаты
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДатаОтгрузкиОплаты.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВключаетсяВУточняющийРасчет");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);


	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ТоварыПоДаннымПользователяДатаОтгрузкиОплаты
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПоДаннымПользователяДатаОтгрузкиОплаты.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ТоварыПоДаннымПользователя.ОсновнаяНалоговаяПриПродажеНижеОбычнойЦены");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);


	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ТоварыКодНоменклатурыПоКлассификатору
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыКодНоменклатурыПоКлассификатору.Имя);

	//Группа отбора
	ГруппаОтбора1 = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	//Отбор
	ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.РазрешитьРедактированиеКодаПоКлассификатору");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	////

	Элемент = УсловноеОформление.Элементы.Добавить();

	//Поле: ТоварыКодУКТВЭД
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыКодУКТВЭД.Имя);

	//Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.РазрешитьРедактированиеКодаПоКлассификатору");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	//Оформления
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		
КонецПроцедуры

&НаСервереБезКонтекста
Процедура АналитикаРасходовПолучениеДанныхВыбора(ДанныеВыбора, Текст)
	
	ДанныеВыбора = Новый СписокЗначений;
	ПродажиСервер.ЗаполнитьДанныеВыбораАналитикиРасходов(ДанныеВыбора, Текст);
	
КонецПроцедуры

&НаСервере
Процедура ПрочиеРасходыСтатьяРасходовПриИзмененииСервер(КэшированныеЗначения)
	
	СтрокаТаблицы = Объект.ТоварыПоДаннымПользователя.НайтиПоИдентификатору(Элементы.ТоварыПоДаннымПользователя.ТекущаяСтрока);
	
	ДоходыИРасходыСервер.СтатьяРасходовПриИзменении(
		Объект,
		СтрокаТаблицы.СтатьяРасходовУсловнаяПродажа,
		СтрокаТаблицы.АналитикаРасходовУсловнаяПродажа);
	
	СтруктураДействий = Новый Структура("ЗаполнитьПризнакАналитикаРасходовОбязательна, ЗаполнитьПризнакАналитикаРасходовЗаказРеализация",
										"СтатьяРасходовУсловнаяПродажа, АналитикаРасходовУсловнаяПродажа",
										Новый Структура("СтатьяРасходовУсловнаяПродажа", "АналитикаРасходовЗаказРеализация"));
	ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТаблицы, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

#Область КонтрольНесогласованныхИзменений

&НаКлиенте
Процедура КонтрольНеСогласованныхИзмененийОбработатьСобытиеПриИзменении(Элемент)
	
	Если Элемент.Имя = "Дата" Тогда
		ДатаПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ПоставкаСобственнойОрганизации" Тогда
		ПоставкаСобственнойОрганизацииПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "Партнер" Тогда
		ПартнерПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "Контрагент" Тогда
		КонтрагентПриИзменении(Элемент);
		
	ИначеЕсли Элемент.Имя = "ВидОперации" Тогда
		ВидОперацииПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "Организация" Тогда
		ОрганизацияПриИзменении(Элемент);
	
	ИначеЕсли Элемент.Имя = "Договор" Тогда
		ДоговорПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "Валюта" Тогда
		ВалютаПриИзменении(Элемент);
		
	ИначеЕсли Элемент.Имя = "СтатусРегистрацииВЕРНН" Тогда
		СтатусРегистрацииВЕРННПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "СтатусАвтокорректировки" Тогда
		СтатусАвтокорректировкиПриИзменении(Элемент);
		
	ИначеЕсли Элемент.Имя = "ВключаетсяВУточняющийРасчет" Тогда
		ВключаетсяВУточняющийРасчетПриИзменении(Элемент);
		
	ИначеЕсли Элемент.Имя = "ПодтверждаетсяГТД" Тогда
		ПодтверждаетсяГТДПриИзменении(Элемент);
		
	ИначеЕсли Элемент.Имя = "ТипПричиныНевыдачиПокупателю" Тогда
		ТипПричиныНевыдачиПокупателюПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "СпецРежимНалогообложения" Тогда
		СпецРежимНалогообложенияПриИзменении(Элемент);
	
	
	ИначеЕсли Элемент.Имя = "ТоварыНоменклатура" Тогда
		ТоварыНоменклатураПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыКодУКТВЭД" Тогда
		ТоварыКодУКТВЭДПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыКодНоменклатурыПоКлассификатору" Тогда
		ТоварыКодНоменклатурыПоКлассификаторуПриИзменении(Элемент);     
	ИначеЕсли Элемент.Имя = "ТоварыУпаковка" Тогда
		ТоварыУпаковкаПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыКоличествоУпаковок" Тогда
		ТоварыКоличествоУпаковокПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыЦена" Тогда
		ТоварыЦенаПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыСуммаБезНДС" Тогда
		ТоварыСуммаБезНДСПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыЦенаРегл" Тогда
		ТоварыЦенаРеглПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыСуммаБезНДСРегл" Тогда
		ТоварыСуммаБезНДСРеглПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыСуммаНДСРегл" Тогда
		ТоварыСуммаНДСРеглПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыСтавкаНДС" Тогда
		ТоварыСтавкаНДСПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыСуммаНДС" Тогда
		ТоварыСуммаНДСПриИзменении(Элемент)
	ИначеЕсли Элемент.Имя = "ТоварыСуммаСНДС" Тогда
		ТоварыСуммаСНДСПриИзменении(Элемент)
	ИначеЕсли Элемент.Имя = "ТоварыНеПересчитыватьСумму" Тогда
		ТоварыНеПересчитыватьСуммуПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыДокументПоставки" Тогда
		ТоварыДокументПоставкиПриИзменении(Элемент);
		
		
	ИначеЕсли Элемент.Имя = "ТоварыПоДаннымПользователяНоменклатура" Тогда
		ТоварыПоДаннымПользователяНоменклатураПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыПоДаннымПользователяОбъектЗаполненияСодержанияУсловнаяПродажа" Тогда
		ТоварыПоДаннымПользователяОбъектЗаполненияСодержанияУсловнаяПродажаПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыПоДаннымПользователяКодУКТВЭД" Тогда
		ТоварыПоДаннымПользователяКодУКТВЭДПриИзменении(Элемент);     
	ИначеЕсли Элемент.Имя = "ТоварыПоДаннымПользователяКодНоменклатурыПоКлассификатору" Тогда
		ТоварыПоДаннымПользователяКодНоменклатурыПоКлассификаторуПриИзменении(Элемент);     
	ИначеЕсли Элемент.Имя = "ТоварыПоДаннымПользователяУпаковка" Тогда
		ТоварыПоДаннымПользователяУпаковкаПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыПоДаннымПользователяКоличествоУпаковок" Тогда
		ТоварыПоДаннымПользователяКоличествоУпаковокПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыПоДаннымПользователяЦена" Тогда
		ТоварыПоДаннымПользователяЦенаПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыПоДаннымПользователяЦенаОбычная" Тогда
		ТоварыПоДаннымПользователяЦенаОбычнаяПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыПоДаннымПользователяСуммаБезНДС" Тогда
		ТоварыПоДаннымПользователяСуммаБезНДСПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыПоДаннымПользователяСтавкаНДС" Тогда
		ТоварыПоДаннымПользователяСтавкаНДСПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыПоДаннымПользователяСуммаНДС" Тогда
		ТоварыПоДаннымПользователяСуммаНДСПриИзменении(Элемент)
	ИначеЕсли Элемент.Имя = "ТоварыПоДаннымПользователяСуммаПревышения" Тогда
		ТоварыПоДаннымПользователяСуммаПревышенияПриИзменении(Элемент)
	ИначеЕсли Элемент.Имя = "ТоварыПоДаннымПользователяСтатьяРасходовУсловнаяПродажа" Тогда
		ТоварыПоДаннымПользователяСтатьяРасходовПриИзменении(Элемент)
	ИначеЕсли Элемент.Имя = "ТоварыПоДаннымПользователяЦенаРегл" Тогда
		ТоварыПоДаннымПользователяЦенаРеглПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыПоДаннымПользователяСуммаБезНДСРегл" Тогда
		ТоварыПоДаннымПользователяСуммаБезНДСРеглПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "ТоварыПоДаннымПользователяСуммаНДСРегл" Тогда
		ТоварыПоДаннымПользователяСуммаНДСРеглПриИзменении(Элемент);
		
	Иначе
		ОбщегоНазначенияУТКлиент.КонтрольНеСогласованныхИзмененийВызватьИсключение(ЭтаФорма, Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрольНеСогласованныхИзмененийОбработатьСобытиеКоманды(Команда)
	
	Если Команда.Имя = "ЗаполнитьПоПревышениямОбычнойЦеныЗаПериод" Тогда
		ЗаполнитьПоПревышениямОбычнойЦеныЗаПериод(Команда);
	ИначеЕсли Команда.Имя = "ЗаполнитьСтатьюРасходовПоВыделеннойСтроке" Тогда
		ЗаполнитьСтатьюРасходовПоВыделеннойСтроке(Команда);
	Иначе
		ОбщегоНазначенияУТКлиент.КонтрольНеСогласованныхИзмененийВызватьИсключение(ЭтаФорма, Команда);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрольНеСогласованныхИзмененийОбработатьСобытиеНажатие(Элемент)
	ОбщегоНазначенияУТКлиент.КонтрольНеСогласованныхИзмененийВызватьИсключение(ЭтаФорма, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура КонтрольНеСогласованныхИзмененийОбработатьСобытиеПередНачаломИзменения(Элемент, Отказ)
	ОбщегоНазначенияУТКлиент.КонтрольНеСогласованныхИзмененийВызватьИсключение(ЭтаФорма, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура КонтрольНеСогласованныхИзмененийОбработатьСобытиеПередУдалением(Элемент, Отказ)
	ОбщегоНазначенияУТКлиент.КонтрольНеСогласованныхИзмененийВызватьИсключение(ЭтаФорма, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура КонтрольНеСогласованныхИзмененийОбработатьСобытиеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	ОбщегоНазначенияУТКлиент.КонтрольНеСогласованныхИзмененийВызватьИсключение(ЭтаФорма, Элемент);
КонецПроцедуры

&НаКлиенте
Функция ВыполнитьПроверкиПередИзменениемСтатуса()  
	
	РеквизитМожноИзменить = Ложь;
	
	ДополнительныеПараметры = Новый Структура;
	Оповещение = Новый ОписаниеОповещения(
			"РезультатВопросаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	Если СтруктураДействийКонтрольНеСогласованныхИзменений = Неопределено
		Или СтруктураДействийКонтрольНеСогласованныхИзменений.ОжидаетсяОповещение Тогда
		
		ВыполнитьОбработкуОповещения(Оповещение, "ИзменитьРеквизит");
		РеквизитМожноИзменить = Истина;
		
	КонецЕсли;
	
	ОчиститьСообщения();  
	
	Если Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыНалоговыхДокументов.Проверен") Тогда
		
		ТекстСообщения = НСтр("ru='Статус документа изменен на ""К проверке"".';uk='Статус документа змінено на ""До перевірки"".'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		ВыполнитьОбработкуОповещения(Оповещение, "ИзменитьРеквизитИСтатус");
		
	Иначе
		
		ВыполнитьОбработкуОповещения(Оповещение, "ИзменитьРеквизит");
		
	КонецЕсли;
	
	РеквизитМожноИзменить = Истина;
	
	Возврат РеквизитМожноИзменить;
	
КонецФункции

&НаКлиенте
Процедура РезультатВопросаЗавершение(КодОтвета, ДополнительныеПараметры) Экспорт
	
	Если КодОтвета = "ИзменитьРеквизитИСтатус" Тогда
		Если НЕ Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыНалоговыхДокументов.КПроверке") Тогда
			Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыНалоговыхДокументов.КПроверке");
			СтатусПриИзменении(Элементы.Статус);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзменении_УстановитьДоступностьЭлементовПоСтатусуСервер(Элемент)
	
	Если Не ВыполнитьПроверкиПередИзменениемСтатуса() Тогда
		ОбщегоНазначенияУТКлиент.ВернутьПредыдущееЗначениеРеквизита(ЭтаФорма, Элемент);
		Возврат;
	Иначе
		ОбщегоНазначенияУТКлиент.СохранитьЗначениеРеквизита(ЭтаФорма, Элемент);
	КонецЕсли;
	
	Если ОбщегоНазначенияУТКлиент.ПриДействииСЭлементомЗависящимОтСтатуса(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураДействийКонтрольНеСогласованныхИзменений.ПриИзменении.Свойство(Элемент.Имя) Тогда
		КонтрольНеСогласованныхИзмененийОбработатьСобытиеПриИзменении(Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Нажатие_УстановитьДоступностьЭлементовПоСтатусуСервер(Элемент)
	
	Если ОбщегоНазначенияУТКлиент.ПриДействииСЭлементомЗависящимОтСтатуса(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураДействийКонтрольНеСогласованныхИзменений.Нажатие.Свойство(Элемент.Имя) Тогда
		КонтрольНеСогласованныхИзмененийОбработатьСобытиеНажатие(Элемент);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Команда_УстановитьДоступностьЭлементовПоСтатусуСервер(Команда)
	
	Если Не ВыполнитьПроверкиПередИзменениемСтатуса() Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияУТКлиент.ПриДействииСЭлементомЗависящимОтСтатуса(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураДействийКонтрольНеСогласованныхИзменений.Команды.Свойство(Команда.Имя) Тогда
		КонтрольНеСогласованныхИзмененийОбработатьСобытиеКоманды(Команда);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПередНачаломИзменения_УстановитьДоступностьЭлементовПоСтатусуСервер(Элемент, Отказ)
	
	Если СтруктураДействийКонтрольНеСогласованныхИзменений = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СтруктураДействийКонтрольНеСогласованныхИзменений.ЗначенияРеквизитов[Элемент.Имя], Элемент.ТекущиеДанные);
	
	Если СтруктураДействийКонтрольНеСогласованныхИзменений.ПередНачаломИзменения.Свойство(Элемент.Имя) Тогда
		КонтрольНеСогласованныхИзмененийОбработатьСобытиеПередНачаломИзменения(Элемент, Отказ);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПередУдалением_УстановитьДоступностьЭлементовПоСтатусуСервер(Элемент, Отказ)
	
	Если Не ВыполнитьПроверкиПередИзменениемСтатуса() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияУТКлиент.ПриДействииСЭлементомЗависящимОтСтатуса(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	Имя = Элемент.Имя;
	Если СтруктураДействийКонтрольНеСогласованныхИзменений.ПередУдалением.Свойство(Имя) Тогда
		КонтрольНеСогласованныхИзмененийОбработатьСобытиеПередУдалением(Элемент, Отказ);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПередНачаломДобавления_УстановитьДоступностьЭлементовПоСтатусуСервер(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Не ВыполнитьПроверкиПередИзменениемСтатуса() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияУТКлиент.ПриДействииСЭлементомЗависящимОтСтатуса(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	Имя = Элемент.Имя;
	Если СтруктураДействийКонтрольНеСогласованныхИзменений.ПередНачаломДобавления.Свойство(Имя) Тогда
		КонтрольНеСогласованныхИзмененийОбработатьСобытиеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииРеквизитаЗависящегоОтСтатуса()
	
	УстановитьДоступностьЭлементовПоСтатусуСервер();
	ОбщегоНазначенияУТКлиент.ПослеИзмененияРеквизитаЗависящегоОтСтатуса(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// При изменении реквизитов

&НаСервере
Процедура ВидОперацииПриИзмененииНаСервере()
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены
	 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа
	 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа
	 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РаботыОтНерезидента Тогда
		Если Объект.СтатусАвтокорректировки <> Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТехническуюТЧ Тогда
			Объект.СтатусАвтокорректировки = Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТехническуюТЧ;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ (Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации ИЛИ
		     Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации ИЛИ
		     Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.НеНДСОперации) Тогда
		Если ПоставкаСобственнойОрганизации Тогда
			ПоставкаСобственнойОрганизации = Ложь;
			ПоставкаСобственнойОрганизацииПриИзмененииСервер();
		КонецЕсли;
		
		ОчиститьФлагНеИзменятьСумму(Объект.Товары);
		ОчиститьФлагНеИзменятьСумму(Объект.ТоварыПоДаннымПользователя);
		
		Объект.НаНачальныйОстатокАванса = Ложь;
		Объект.НаправлениеДеятельности = Справочники.НаправленияДеятельности.ПустаяСсылка();
	КонецЕсли;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации
	 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации Тогда
	 	Если Не ЗначениеЗаполнено(Объект.Партнер) Тогда
			Объект.Партнер = Справочники.Партнеры.РозничныйПокупатель;
			ПартнерПриИзмененииСервер();
		КонецЕсли;	
	КонецЕсли;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации
	 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации
	 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации
	 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации
	 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа
	 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа
	 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены Тогда
		Если НЕ (Объект.Валюта = ВалютаРегламентированногоУчета) Тогда
			Объект.Валюта = ВалютаРегламентированногоУчета;
			ПересчитатьСуммыСервер("Товары", Истина);
			ПересчитатьСуммыСервер("ТоварыПоДаннымПользователя", Истина);
					
			ВалютаДокумента = Объект.Валюта;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены И 
		Не Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены ТОгда
		
		Объект.ТоварыПоДаннымПользователя.Очистить();
		
		Если ЗначениеЗаполнено(Объект.Договор) И ЗначениеЗаполнено(Объект.Валюта) 
		   И Объект.Договор.ВалютаВзаиморасчетов <> Объект.Валюта Тогда
		   
			Объект.Договор = Неопределено;			
			
		КонецЕсли;
		
	ИначеЕсли (НЕ ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации    И Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации)
		  ИЛИ (НЕ ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации И Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации) Тогда
		
		Объект.СтатусАвтокорректировки = Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТекущийДокумент;
		
	КонецЕсли;

	Если Объект.СтатусАвтокорректировки = Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТехническуюТЧ Тогда
		Документы.НалоговаяНакладная.ОбновитьЗначениеЛьготыНДС(Объект.ВидОперации, Объект.ЛьготаНДС, , Объект.ТоварыПоДаннымПользователя.Выгрузить().ВыгрузитьКолонку("Номенклатура"), Объект.ЛьготаНДСОписание);
	Иначе
		Документы.НалоговаяНакладная.ОбновитьЗначениеЛьготыНДС(Объект.ВидОперации, Объект.ЛьготаНДС, , Объект.Товары.Выгрузить().ВыгрузитьКолонку("Номенклатура"), Объект.ЛьготаНДСОписание);
	КонецЕсли;
	
	
	//необходимо перезаполнить ставки НДС в табличных частях
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Объект, Ложь);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);

	ОчиститьРеквизитыУсловныхПродаж = Ложь;
	Если ВидОперации <> Объект.ВидОперации И
		 (Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа Или
		  Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа) Тогда
		ОчиститьРеквизитыУсловныхПродаж = Истина;
	КонецЕсли;

	Для каждого ТекущаяСтрока из Объект.ТоварыПоДаннымПользователя Цикл
		
		СтараяСтавкаНДС = ТекущаяСтрока.СтавкаНДС;
		ТекущаяСтрока.СтавкаНДС = ЗаполнитьСтавкуНДС(ТекущаяСтрока.Номенклатура);
		
		Если ОчиститьРеквизитыУсловныхПродаж Тогда
			ТекущаяСтрока.ОбъектЗаполненияСодержанияУсловнаяПродажа = Неопределено;	
			ТекущаяСтрока.НалоговоеНазначение 						= Справочники.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка();
		    ТекущаяСтрока.НалоговоеНазначениеПоФактуУсловнаяПродажа = Справочники.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка();
			ТекущаяСтрока.СтатьяРасходовУсловнаяПродажа 			= ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
			ТекущаяСтрока.АналитикаРасходовУсловнаяПродажа 			= Неопределено;
			ТекущаяСтрока.Содержание 								= "";
			// По умолчанию будем ставить ставку 20%, так как в большинстве случаев, она будет превалирующей
			ТекущаяСтрока.СтавкаНДС									= Перечисления.СтавкиНДС.НДС20;
		КонецЕсли;
		
		Если ТекущаяСтрока.СтавкаНДС <> СтараяСтавкаНДС Тогда
			
			НДСИсходящийСервер.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
			
		КонецЕсли;
		
		ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательстваСервер(ТекущаяСтрока.СтатьяДекларацииНДСНалоговыеОбязательства, ТекущаяСтрока.СтавкаНДС);
		
	КонецЦикла;

	ЗаполнитьРеквизитыРегл("ТоварыПоДаннымПользователя");
	
	ОчиститьДокументПоставки = (Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации 
									ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации);
	
	Для каждого ТекущаяСтрока из Объект.Товары Цикл
		
		СтараяСтавкаНДС = ТекущаяСтрока.СтавкаНДС;
		ТекущаяСтрока.СтавкаНДС = ЗаполнитьСтавкуНДС(ТекущаяСтрока.Номенклатура);
		Если ТекущаяСтрока.СтавкаНДС <> СтараяСтавкаНДС Тогда
			
			НДСИсходящийСервер.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
			
		КОнецЕсли;
		
		ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательстваСервер(ТекущаяСтрока.СтатьяДекларацииНДСНалоговыеОбязательства, ТекущаяСтрока.СтавкаНДС);
		
		Если ОчиститьДокументПоставки Тогда
			ТекущаяСтрока.ДокументПоставки = Неопределено;
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьРеквизитыРегл("Товары");

	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа")
	 ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа") Тогда
		ПланыВидовХарактеристик.СтатьиРасходов.ЗаполнитьПризнакАналитикаРасходовОбязательна(Объект.ТоварыПоДаннымПользователя, "СтатьяРасходовУсловнаяПродажа, АналитикаРасходовУсловнаяПродажа");
		ПланыВидовХарактеристик.СтатьиРасходов.ЗаполнитьПризнакАналитикаРасходовЗаказРеализация(Объект.ТоварыПоДаннымПользователя, "СтатьяРасходовУсловнаяПродажа");
		
	КонецЕсли;

	ВидОперацииДопускаетТолькоСводную = Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа
	 	ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены; 

	Если ВидОперацииДопускаетТолькоСводную Тогда 
		Объект.Сводная = Истина;
	КонецЕсли;

	ВидОперацииДопускаетСводную = (ВидОперацииДопускаетТолькоСводную 
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации 
	 	ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации)
		И Объект.ВидОперации <> Перечисления.ВидыОперацийНалоговыхДокументов.УсловнаяПродажаСписаниеОС;

	Если НЕ ВидОперацииДопускаетСводную Тогда 
		Объект.Сводная = Ложь;
	КонецЕсли;	 

	ВидОперации = Объект.ВидОперации;	

	Документы.НалоговаяНакладная.УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию(Объект);
	ПроверитьСтатьюДекларацииНДС22(Объект);

	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииСервер()
	
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		
		ЗаполнитьДоговорПоУмолчанию();
		
		Если НЕ Объект.Организация.ВестиУчетДенежныхСредствПоОбособленнымПодразделениям Тогда
			Объект.ОбособленноеПодразделение = Справочники.ОбособленныеПодразделенияОрганизаций.ПустаяСсылка();
		КонецЕсли;
		
		Если ИспользоватьНаправленияДеятельности Тогда
			НаправленияДеятельностиСервер.ЗаполнитьНаправлениеПоУмолчанию(Объект.НаправлениеДеятельности, , Объект.Договор);
		КонецЕсли;
		
	КонецЕсли;
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПартнерПриИзмененииСервер()
	
	ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Объект.Партнер, Объект.Контрагент);
	
	КонтрагентПриИзмененииСервер()
	
КонецПроцедуры

&НаСервере
Процедура КонтрагентПриИзмененииСервер()
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		
		ЗаполнитьДоговорПоУмолчанию();
		
	КонецЕсли;
		
	Документы.НалоговаяНакладная.УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию(Объект);
	ПроверитьСтатьюДекларацииНДС22(Объект);

	Если ИспользоватьНаправленияДеятельности Тогда
		НаправленияДеятельностиСервер.ЗаполнитьНаправлениеПоУмолчанию(Объект.НаправлениеДеятельности, , Объект.Договор);
	КонецЕсли;
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаСервере
Процедура ДоговорПриИзмененииНаСервере()
	
	
	Если ИспользоватьНаправленияДеятельности Тогда
		НаправленияДеятельностиСервер.ЗаполнитьНаправлениеПоУмолчанию(Объект.НаправлениеДеятельности, , Объект.Договор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоставкаСобственнойОрганизацииПриИзмененииСервер()
	
	Если ПоставкаСобственнойОрганизации Тогда
		Объект.Партнер = Справочники.Партнеры.НашеПредприятие;
		Объект.Контрагент = Неопределено;
		Объект.Договор = Неопределено;
	Иначе
		Объект.Партнер = Неопределено;
		Объект.Контрагент = Неопределено;
	КонецЕсли;
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// Прочее

&НаКлиенте
Процедура Подключаемый_ЗакрытьФорму()
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();

	ПоставкаСобственнойОрганизации = (ТипЗнч(Объект.Контрагент) = Тип("СправочникСсылка.Организации"));
	
	ИспользоватьНаправленияДеятельности = Истина;
	
	ВалютаДокумента = Объект.Валюта;	
	ВидОперации = Объект.ВидОперации;
	
	УстановитьДоступностьЭлементовПоСтатусуСервер();
	
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре();

	
	Если Элементы.СпецРежимНалогообложения.СписокВыбора.Количество() = 0 Тогда
		// Список должен быть заполнен до НастроитьЭлементыФормы для корректного отображения ...Декорация
        НДСОбщегоНазначенияСервер.ЗаполнитьСписокВыбораСпецРежимаНалогообложения(Элементы.СпецРежимНалогообложения.СписокВыбора);
	КонецЕсли;
	
	Если Элементы.КодПризнакаСводной.СписокВыбора.Количество() = 0 Тогда
		// Список должен быть заполнен до НастроитьЭлементыФормы для корректного отображения ...Декорация
	    НДСОбщегоНазначенияСервер.ЗаполнитьСписокВыбораКодПризнакаСводной(Элементы.КодПризнакаСводной.СписокВыбора);
	КонецЕсли;
	
	Элементы.ГруппаКомментарий.Картинка = ОбщегоНазначения.ПолучитьКартинкуКомментария(Объект.Комментарий);
	
	Элементы.ВидОперации.СписокВыбора.ЗагрузитьЗначения(ЗаполнитьСписокВыбораВидаОперации());
	
	НастроитьЭлементыФормы();
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма, РеквизитФормыВЗначение("Объект"));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДоговорПоУмолчанию()
	
	Если ПоставкаСобственнойОрганизации Тогда
		Договор = Неопределено; // Всегда не равен пустой ссылке. Перезаполняем подчиненные реквизиты 
	Иначе
		Договор = ПродажиСервер.ПолучитьДоговорПоУмолчанию(
			Объект,
			,//ХозяйственнаяОперацияДоговора
			Объект.Валюта
		);
	КонецЕсли;
	
	Если Договор <> Объект.Договор Тогда
		Объект.Договор = Договор;
		ДоговорПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ТоварыНоменклатураПриИзмененииСервер(ИдентификаторСтроки, СтруктураДействий, КэшированныеЗначения)
	
	ТекущаяСтрока = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	ОбщееНоменклатураПриИзмененииСервер(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаСервере
Процедура ТоварыПоДаннымПользователяНоменклатураПриИзмененииСервер(ИдентификаторСтроки, СтруктураДействий, КэшированныеЗначения)
	
	ТекущаяСтрока = Объект.ТоварыПоДаннымПользователя.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	ОбщееНоменклатураПриИзмененииСервер(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаСервере
Процедура ОбщееНоменклатураПриИзмененииСервер(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения)
	
	ТекущаяСтрока.СтавкаНДС = ЗаполнитьСтавкуНДС(ТекущаяСтрока.Номенклатура);
	ЗаполнитьНоменклатуруГТДПоУмолчанию(ТекущаяСтрока);
	НДСИсходящийСервер.ЗаполнитьНоменклатуруГТДПоУмолчанию(ТекущаяСтрока, ТекущаяСтрока.Свойство("РазрешитьРедактированиеКодаПоКлассификатору"));
	
	НДСИсходящийСервер.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	ОбновитьЗначениеЛьготыНДССтрокаНаСервере(ТекущаяСтрока.Номенклатура);
	Документы.НалоговаяНакладная.УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию(Объект);
	ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательстваСервер(ТекущаяСтрока.СтатьяДекларацииНДСНалоговыеОбязательства, ТекущаяСтрока.СтавкаНДС);
	
КонецПроцедуры

&НаСервере
Процедура ТоварыКодУКТВЭДПриИзмененииСервер(ИдентификаторСтроки)
	
	ТекущаяСтрока = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если ЗначениеЗаполнено(ТекущаяСтрока.НомерГТД) Тогда
		ТекущаяСтрока.КодНоменклатурыПоКлассификатору = ТекущаяСтрока.НомерГТД.КодУКТВЭД;
	ИначеЕсли ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		ВестиУчетПоГТД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрока.Номенклатура, "ВестиУчетПоГТД");
		Если ВестиУчетПоГТД Тогда
			ТекущаяСтрока.КодНоменклатурыПоКлассификатору = Справочники.КлассификаторУКТВЭД.ПустаяСсылка();
		КонецЕсли; 	
	КонецЕсли;

	Документы.НалоговаяНакладная.УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию(Объект);

КонецПроцедуры

&НаСервере
Процедура ТоварыПоДаннымПользователяКодУКТВЭДПриИзмененииСервер(ИдентификаторСтроки)
	
	ТекущаяСтрока = Объект.ТоварыПоДаннымПользователя.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если ЗначениеЗаполнено(ТекущаяСтрока.НомерГТД) Тогда
		ТекущаяСтрока.КодНоменклатурыПоКлассификатору = ТекущаяСтрока.НомерГТД.КодУКТВЭД;
	ИначеЕсли ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		ВестиУчетПоГТД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрока.Номенклатура, "ВестиУчетПоГТД");
		Если ВестиУчетПоГТД Тогда
			ТекущаяСтрока.КодНоменклатурыПоКлассификатору = Справочники.КлассификаторУКТВЭД.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;

	Документы.НалоговаяНакладная.УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию(Объект);
	
КонецПроцедуры

&НаКлиенте
Функция ПересчитатьСуммуПревышения(ТекущаяСтрока)
	
	ТекущаяСтрока.СуммаПревышения = Макс(0, (ТекущаяСтрока.ЦенаОбычная - ТекущаяСтрока.Цена) * ТекущаяСтрока.КоличествоУпаковок);
	
				
	ПересчитатьСуммуНДСПревышения(ТекущаяСтрока);
	
КонецФункции

&НаКлиенте
Функция ПересчитатьСуммуНДСПревышения(ТекущаяСтрока)
	
	ПроцентНДС = НДСОбщегоНазначенияКлиентСервер.ПолучитьСтавкуНДСЧислом(ТекущаяСтрока.СтавкаНДС);
	ТекущаяСтрока.СуммаНДСПревышения = ТекущаяСтрока.СуммаПревышения * ПроцентНДС;
	
КонецФункции


&НаСервере
Процедура УстановитьДоступностьЭлементовПоСтатусуСервер()
	
	УстановитьПодписку = Объект.Согласован;
	
	МассивЭлементов = Новый Массив;
	
	// Элементы управления шапки
	МассивЭлементов.Добавить("Дата");
	МассивЭлементов.Добавить("ПоставкаСобственнойОрганизации");
	МассивЭлементов.Добавить("Партнер");
	МассивЭлементов.Добавить("Контрагент");
	
	МассивЭлементов.Добавить("ВидОперации");
	МассивЭлементов.Добавить("Организация");
	МассивЭлементов.Добавить("ВключаетсяВУточняющийРасчет");
	МассивЭлементов.Добавить("ДатаОтгрузкиОплаты");
	
	МассивЭлементов.Добавить("Договор");
	МассивЭлементов.Добавить("НаправлениеДеятельности");
	
	МассивЭлементов.Добавить("Валюта");
	
	//Остальные элементы управления шапки
	МассивЭлементов.Добавить("ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных");
	
	МассивЭлементов.Добавить("СтатусАвтокорректировки");
	МассивЭлементов.Добавить("ОбособленноеПодразделение");
	МассивЭлементов.Добавить("ОбычныйВидЦены");
	
	МассивЭлементов.Добавить("ЭлектронныйДокумент");
	МассивЭлементов.Добавить("УслугиНерезидентаДляНехозДеят");
	МассивЭлементов.Добавить("ПодтверждаетсяГТД");
	МассивЭлементов.Добавить("НомерГТД");
	МассивЭлементов.Добавить("Ответственный");
	МассивЭлементов.Добавить("КтоВыписалНалоговуюНакладную");
	
	МассивЭлементов.Добавить("ЛьготаНДС");
	МассивЭлементов.Добавить("ТипПричиныНевыдачиПокупателю");
	МассивЭлементов.Добавить("СпецРежимНалогообложения");
	МассивЭлементов.Добавить("ЛьготаНДСОписание");
	МассивЭлементов.Добавить("ПоставкаДипПредставительству");
	
	// Элементы управления, связанные с товарами
	МассивЭлементов.Добавить("Товары;ПередНачаломДобавления,ПередУдалением,ПередНачаломИзменения");
	
	МассивЭлементов.Добавить("ТоварыКонтекстноеМенюДобавить");
	МассивЭлементов.Добавить("ТоварыКонтекстноеМенюСкопировать");
	МассивЭлементов.Добавить("ТоварыКонтекстноеМенюУдалить");
	МассивЭлементов.Добавить("ТоварыКонтекстноеМенюПереместитьВверх");
	МассивЭлементов.Добавить("ТоварыКонтекстноеМенюПереместитьВниз");
	
	МассивЭлементов.Добавить("ТоварыДобавить");
	МассивЭлементов.Добавить("ТоварыИзменить");
	МассивЭлементов.Добавить("ТоварыСкопировать");
	МассивЭлементов.Добавить("ТоварыУдалить");
	МассивЭлементов.Добавить("ТоварыПереместитьВверх");
	МассивЭлементов.Добавить("ТоварыПереместитьВниз");
	
	МассивЭлементов.Добавить("ТоварыПоДаннымПользователя;ПередНачаломДобавления,ПередУдалением,ПередНачаломИзменения");
	
	МассивЭлементов.Добавить("ТоварыПоДаннымПользователяКонтекстноеМенюДобавить");
	МассивЭлементов.Добавить("ТоварыПоДаннымПользователяКонтекстноеМенюСкопировать");
	МассивЭлементов.Добавить("ТоварыПоДаннымПользователяКонтекстноеМенюУдалить");
	МассивЭлементов.Добавить("ТоварыПоДаннымПользователяКонтекстноеМенюПереместитьВверх");
	МассивЭлементов.Добавить("ТоварыПоДаннымПользователяКонтекстноеМенюПереместитьВниз");
	
	МассивЭлементов.Добавить("ТоварыПоДаннымПользователяДобавить");
	МассивЭлементов.Добавить("ТоварыПоДаннымПользователяИзменить");
	МассивЭлементов.Добавить("ТоварыПоДаннымПользователяСкопировать");
	МассивЭлементов.Добавить("ТоварыПоДаннымПользователяУдалить");
	МассивЭлементов.Добавить("ТоварыПоДаннымПользователяПереместитьВверх");
	МассивЭлементов.Добавить("ТоварыПоДаннымПользователяПереместитьВниз");
	
	МассивЭлементов.Добавить("ТоварыПоДаннымПользователяЗаполнитьПоПревышениямОбычнойЦеныЗаПериод");
	МассивЭлементов.Добавить("ТоварыПоДаннымПользователяЗаполнитьСтатьюРасходовПоВыделеннойСтроке");
	
	ОбщегоНазначенияУТ.УстановитьПодпискуНаСобытияИзмененияЭлементовФормы(ЭтаФорма, МассивЭлементов, УстановитьПодписку);
	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТЧПоДаннымПользователяИзТоварыСервер()
	
	Объект.ТоварыПоДаннымПользователя.Очистить();
	
	Для каждого СтрокаТовары из Объект.Товары Цикл
		
		ТекущаяСтрока = Объект.ТоварыПоДаннымПользователя.Добавить();
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаТовары);
		
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()

	ОбъектДата = ?(ЗначениеЗаполнено(Объект.Ссылка), Объект.Дата, ТекущаяДата());
	
	ЭтаФорма.УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация", Объект.Организация));
	
	ЭтоУсловнаяПродажа = (Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа
							ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа);

	ЭтоЛиквидацияОС = Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.УсловнаяПродажаСписаниеОС;
	
// ГруппаШапка

	Если НЕ Объект.Организация.ВестиУчетДенежныхСредствПоОбособленнымПодразделениям Тогда
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОбособленноеПодразделение", "Доступность", Ложь);
	Иначе
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОбособленноеПодразделение", "Доступность", Истина);
	КонецЕсли;
	
	ВидимостьВзаиморасчетов = НЕ (
		ЭтоЛиквидацияОС ИЛИ
		Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации ИЛИ
		Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации ИЛИ
		ЭтоУсловнаяПродажа
	);
	
	ДокументПроверен = (Объект.Статус = Перечисления.СтатусыНалоговыхДокументов.Проверен);
	ДокументВыдаетсяПокупателю = (Объект.ТипПричиныНевыдачиПокупателю = 0);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СтатусВыдачиПокупателю", "Доступность", ДокументПроверен И ДокументВыдаетсяПокупателю);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СтатусРегистрацииВЕРНН", "Доступность", ДокументПроверен);
	
	ЕстьВозможностьАвтокорректировки = НЕ (НДСИсходящийКлиентСервер.ПолучитьСписокАвтокорректируемыхВидовОперацийНН().НайтиПоЗначению(Объект.ВидОперации) = Неопределено);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СтатусАвтокорректировки", "Видимость", ЕстьВозможностьАвтокорректировки);
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации
	 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации
	 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.НеНДСОперации Тогда
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПоставкаСобственнойОрганизации", 	"Видимость", Истина);
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НаНачальныйОстатокАванса", 		"Видимость", Истина);
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НаправлениеДеятельности", 		"Видимость", Истина);
	Иначе
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПоставкаСобственнойОрганизации", 	"Видимость", Ложь);
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НаНачальныйОстатокАванса", 		"Видимость", Ложь);
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НаправлениеДеятельности", 		"Видимость", Ложь);
	КонецЕсли;
	Если ПоставкаСобственнойОрганизации Тогда
		ОписаниеТипа = Новый ОписаниеТипов("СправочникСсылка.Организации");
	Иначе
		ОписаниеТипа = Новый ОписаниеТипов("СправочникСсылка.Контрагенты");
	КонецЕсли;
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Контрагент", "ОграничениеТипа", ОписаниеТипа);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Партнер", "Видимость", ВидимостьВзаиморасчетов И НЕ ПоставкаСобственнойОрганизации);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Контрагент", "Видимость", ВидимостьВзаиморасчетов);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Договор", "Видимость", ВидимостьВзаиморасчетов И НЕ ПоставкаСобственнойОрганизации);
	
	ПродажаНижеОбычнойЦены = (Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены);
	
    // При продаже ниже обычной цены отбор по договору может быть указан в любой валюте, несмотря на то что документ всегда в гривне
	МассивСвязей = Новый Массив;
	Для каждого СвязьМетаданные Из Метаданные.Документы.НалоговаяНакладная.Реквизиты.Договор.СвязиПараметровВыбора Цикл
		Если ПродажаНижеОбычнойЦены И СвязьМетаданные.ПутьКДанным = "Валюта" Тогда
			Продолжить;
		КонецЕсли;
		Связь = Новый СвязьПараметраВыбора(СвязьМетаданные.Имя, "Объект."+СвязьМетаданные.ПутьКДанным, СвязьМетаданные.ИзменениеЗначения);
		МассивСвязей.Добавить(Связь);
	КонецЦикла;
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Договор", "СвязиПараметровВыбора", Новый ФиксированныйМассив(МассивСвязей));

	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Партнер", "КнопкаОчистки", ПродажаНижеОбычнойЦены);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Контрагент", "КнопкаОчистки", ПродажаНижеОбычнойЦены);
	
	МассивПараметровВыбора = Новый Массив;
	Если Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РаботыОтНерезидента Тогда
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "УслугиНерезидентаДляНехозДеят", "Видимость", ОбъектДата < НДСОбщегоНазначенияПовтИсп.ДатаВступленияВСилуПриказа1307());
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Партнер", "Заголовок", "Поставщик");
		
		МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Поставщик", Истина));
	Иначе
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "УслугиНерезидентаДляНехозДеят", "Видимость", Ложь);
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Партнер", "Заголовок", "");
		
		МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Клиент", Истина));
	КонецЕсли;
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Партнер", "ПараметрыВыбора", Новый ФиксированныйМассив(МассивПараметровВыбора));
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОбычныйВидЦены", "Видимость", ПродажаНижеОбычнойЦены);

// ГруппаСтраницаДанные, ГруппаСтраницаДанныеПользователя

	РежимАвтокорректировки = (Объект.СтатусАвтокорректировки <> Перечисления.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТехническуюТЧ);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСтраницаДанные", "Видимость", РежимАвтокорректировки);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСтраницаДанныеПользователя", "Видимость", Не РежимАвтокорректировки);
	
	МассивДопустимыхСтавокНДС = ОпределитьДопустимыеСтавкиНДС().ВыгрузитьЗначения();
	Элементы.ТоварыПоДаннымПользователяСтавкаНДС.СписокВыбора.ЗагрузитьЗначения(МассивДопустимыхСтавокНДС);
	Элементы.ТоварыСтавкаНДС.СписокВыбора.ЗагрузитьЗначения(МассивДопустимыхСтавокНДС);
	
	МассивИменЭлементов = Новый Массив;
	МассивИменЭлементов.Добавить("ТоварыПоДаннымПользователяЗаполнитьПоПревышениямОбычнойЦеныЗаПериод");
	МассивИменЭлементов.Добавить("ТоварыПоДаннымПользователяСуммаПревышения");
	МассивИменЭлементов.Добавить("ТоварыПоДаннымПользователяСуммаНДСПревышения");
	МассивИменЭлементов.Добавить("ТоварыПоДаннымПользователяЦенаОбычная");
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивИменЭлементов, "Видимость", ПродажаНижеОбычнойЦены);

	МассивИменЭлементов = Новый Массив;
	МассивИменЭлементов.Добавить("ТоварыПоДаннымПользователяЦенаРегл");	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивИменЭлементов, "Видимость", НЕ ПродажаНижеОбычнойЦены);
	
	МассивИменЭлементов = Новый Массив;
	МассивИменЭлементов.Добавить("ТоварыПоДаннымПользователяНалоговоеНазначение");
	МассивИменЭлементов.Добавить("ТоварыПоДаннымПользователяНалоговоеНазначениеПоФактуУсловнаяПродажа");
	МассивИменЭлементов.Добавить("ТоварыПоДаннымПользователяСуммаНДСРеглПоРегиструУсловнаяПродажа");
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивИменЭлементов, "Видимость", ЭтоУсловнаяПродажа);
	
	МассивИменЭлементов = Новый Массив;
	МассивИменЭлементов.Добавить("ТоварыПоДаннымПользователяОбъектЗаполненияСодержанияУсловнаяПродажа");
	МассивИменЭлементов.Добавить("ТоварыПоДаннымПользователяСтатьяРасходовУсловнаяПродажа");
	МассивИменЭлементов.Добавить("ТоварыПоДаннымПользователяАналитикаРасходовУсловнаяПродажа");
	МассивИменЭлементов.Добавить("ТоварыПоДаннымПользователяЗаполнитьСтатьюРасходовПоВыделеннойСтроке");
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивИменЭлементов, "Видимость", ЭтоУсловнаяПродажа ИЛИ ЭтоЛиквидацияОС);
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа Тогда
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(Тип("ДокументСсылка.РегистрацияВходящегоНалоговогоДокумента"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ТаможеннаяДекларацияИмпорт"));
		ОписаниеТипа = Новый ОписаниеТипов(МассивТипов);
		ЗаголовокВходящийНалоговый = НСтр("ru='Входящий налоговый документ';uk='Вхідний податковий документ'");
		ОтметкаНезаполненогоОбъектЗаполненияСодержанияУсловнаяПродажа = Истина;
	Иначе
		ОписаниеТипа = Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.СтатьиРасходов");
		ЗаголовокВходящийНалоговый = НСтр("ru='Статья расходов (Номенклатура НН)';uk='Стаття витрат (Номенклатура ПН)'");
		ОтметкаНезаполненогоОбъектЗаполненияСодержанияУсловнаяПродажа = Ложь;
	КонецЕсли;
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТоварыПоДаннымПользователяОбъектЗаполненияСодержанияУсловнаяПродажа", "ОграничениеТипа", ОписаниеТипа);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТоварыПоДаннымПользователяОбъектЗаполненияСодержанияУсловнаяПродажа", "Заголовок", ЗаголовокВходящийНалоговый);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТоварыПоДаннымПользователяОбъектЗаполненияСодержанияУсловнаяПродажа", "АвтоОтметкаНезаполненного", ОтметкаНезаполненогоОбъектЗаполненияСодержанияУсловнаяПродажа);
	
	МассивИменЭлементов = Новый Массив;
	МассивИменЭлементов.Добавить("ТоварыПоДаннымПользователяКодУКТВЭД");
	МассивИменЭлементов.Добавить("ТоварыПоДаннымПользователяГруппаКодНоменклатурыПоКлассификатору");
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивИменЭлементов, "Видимость", НЕ ЭтоУсловнаяПродажа);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивИменЭлементов, "Доступность", НЕ ЭтоУсловнаяПродажа);

// ГруппаСтраницыДополнительно	

	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НомерГТД", "Доступность", Объект.ПодтверждаетсяГТД);
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации
	 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации
	 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации
	 ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации
	 ИЛИ ЭтоУсловнаяПродажа
	 ИЛИ ЭтоЛиквидацияОС
	 ИЛИ (Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены И Объект.Валюта = ВалютаРегламентированногоУчета) Тогда
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Валюта", "Доступность", Ложь);
	Иначе
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Валюта", "Доступность", Истина);
	КонецЕсли;
	
	ПоказыватьЛьготу = (
		Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации ИЛИ
		Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.НеНДСОперации ИЛИ
		Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации ИЛИ
		Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации
	);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЛьготаНДС", "Видимость", ПоказыватьЛьготу);
	
    Документы.НалоговаяНакладная.ЗаполнитьСписокВыбораТипПричиныНевыдачиПокупателю(Элементы.ТипПричиныНевыдачиПокупателю.СписокВыбора, ПродажаНижеОбычнойЦены, ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДата()));
	
	ЭлементСпискаЗначений = Элементы.ТипПричиныНевыдачиПокупателю.СписокВыбора.НайтиПоЗначению(Объект.ТипПричиныНевыдачиПокупателю);
	Элементы.ТипПричиныНевыдачиПокупателюДекорация.Заголовок = ?(ЭлементСпискаЗначений = Неопределено, "", ЭлементСпискаЗначений.Представление);

	ЭлементСпискаЗначений = Элементы.СпецРежимНалогообложения.СписокВыбора.НайтиПоЗначению(Объект.СпецРежимНалогообложения);
	Элементы.СпецРежимНалогообложенияДекорация.Заголовок = ?(ЭлементСпискаЗначений = Неопределено, "", ЭлементСпискаЗначений.Представление);
	
	ЭлементСпискаЗначений = Элементы.КодПризнакаСводной.СписокВыбора.НайтиПоЗначению(Объект.КодПризнакаСводной);
	Элементы.КодПризнакаСводнойДекорация.Заголовок = ?(ЭлементСпискаЗначений = Неопределено, "", ЭлементСпискаЗначений.Представление);
	
// ГруппаСтраницыИтого
 
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСтраницыИтого", "ТекущаяСтраница", ?(РежимАвтокорректировки, Элементы.ГруппаИтогоТовары, Элементы.ГруппаИтогоТоварыПоДаннымПользователя));

	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТоварыПоДаннымПользователяИтогСуммаПревышения", "Видимость", ПродажаНижеОбычнойЦены);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТоварыПоДаннымПользователяИтогСуммаСНДС", "Видимость", Не ПродажаНижеОбычнойЦены);
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПоставкаДипПредставительству", "Видимость", (ОбъектДата >= '2014-12-01') И (Объект.ТипПричиныНевыдачиПокупателю <= 2) И (ОбъектДата < НДСОбщегоНазначенияПовтИсп.ДатаВступленияВСилуПриказа1307()));

	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЭлектронныйДокумент", "Доступность", (ОбъектДата < '2015-01-01'));

	ДоступностьВозвратнойТары = Документы.НалоговаяНакладная.МассивВидовОперацийНеИспользуетсяВозвратнаяТара().Найти(Объект.ВидОперации) = Неопределено;
	Элементы.СуммаВозвратнойТары.Доступность = ДоступностьВозвратнойТары;
	Элементы.СуммаВозвратнойТары1.Доступность = ДоступностьВозвратнойТары;
	
	ВидимостьСводная = ((ОбъектДата >= НДСОбщегоНазначенияПовтИсп.ДатаВступленияВСилуПриказа1307())
		И (Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации 
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации
		) ИЛИ ЭтоУсловнаяПродажа ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены
		ИЛИ ЭтоЛиквидацияОС
	);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Сводная", "Видимость", ВидимостьСводная);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Сводная", "Доступность", НЕ (ЭтоУсловнаяПродажа ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены ИЛИ ЭтоЛиквидацияОС));
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаКодПризнакаСводной", "Видимость", Объект.Сводная);

	Если Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации Тогда
		
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТоварыДокументПоставки", "ОграничениеТипа", Новый ОписаниеТипов("ДокументСсылка.ОтчетОРозничныхПродажах"));
		
	Иначе
		
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТоварыДокументПоставки", "ОграничениеТипа", Новый ОписаниеТипов);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗначениеЛьготыНДССтрокаНаСервере(Номенклатура)
	
	Документы.НалоговаяНакладная.ОбновитьЗначениеЛьготыНДС(Объект.ВидОперации, Объект.ЛьготаНДС, Номенклатура, ,Объект.ЛьготаНДСОписание);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательстваСервер(СтатьяДекларацииНДСНалоговыеОбязательства, СтавкаНДС)
	
	Документы.НалоговаяНакладная.ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(Объект,  СтатьяДекларацииНДСНалоговыеОбязательства, СтавкаНДС);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСодержаниеДляУсловнойПродажиСервер(ОбъектЗаполненияСодержанияУсловнаяПродажа, Содержание)
	
	Документы.НалоговаяНакладная.ЗаполнитьСодержаниеДляУсловнойПродажи(ОбъектЗаполненияСодержанияУсловнаяПродажа, Содержание);
	
КонецПроцедуры

&НаСервере
Функция ОпределитьДопустимыеСтавкиНДС() Экспорт
	
	СписокСтавокНДС = Новый СписокЗначений();
	
	Если    Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации 
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации Тогда
		
		СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.НДС20);
        СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.НДС14);
		СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.НДС7);
		СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.НДС0);
		
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены Тогда
		
		СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.НДС20);
        СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.НДС14);
		СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.НДС7);
		СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.НДС0);
		СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.БезНДС);
		
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа
		  ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа Тогда
		
		СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.НДС20);
        СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.НДС14);
		СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.НДС7);

	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.НеНДСОперации Тогда
		
		СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.НеНДС);
		
	ИначеЕсли  Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации 
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации 
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации Тогда
		
		СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.БезНДС);
		
	ИначеЕсли  Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.РаботыОтНерезидента Тогда
		
		СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.НДС20);
		СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.НДС14);
		СписокСтавокНДС.Добавить(Перечисления.СтавкиНДС.НДС7);
		
	КонецЕсли;
		
	Возврат СписокСтавокНДС;
	
КонецФункции

&НаСервере
Функция ЗаполнитьСтавкуНДС(Номенклатура)

	// СтавкаНДС прироритетно определеятся видом операции документа
	СписокДопустимыхСтавокНДС = ОпределитьДопустимыеСтавкиНДС();	
	
	Если СписокДопустимыхСтавокНДС.Количество() > 1 Тогда
		Если СписокДопустимыхСтавокНДС.НайтиПоЗначению(Номенклатура.СтавкаНДС) = Неопределено Тогда 
			// в карточке товара указана недопустимая в данном контексте ставка НДС
			СтавкаНДСНовая = Перечисления.СтавкиНДС.ПустаяСсылка();
		Иначе
			СтавкаНДСНовая = Номенклатура.СтавкаНДС;
		КонецЕсли;
	Иначе
		СтавкаНДСНовая = СписокДопустимыхСтавокНДС[0].Значение;
	КонецЕсли;
	
	Возврат СтавкаНДСНовая;

КонецФункции

&НаСервере
Функция ЗаполнитьСписокВыбораТекстДополнения(ОсновнаяНалоговаяПриПродажеНижеОбычнойЦены, Дата, ВернутьМассивом = Ложь)
	
	СписокВыбораДляТекстаДополнения = Новый СписокЗначений();
	
	Если Дата < '2015-01-01' Тогда
		
		НоменНН = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ОсновнаяНалоговаяПриПродажеНижеОбычнойЦены.Номер, Истина, Истина);
		
		СписокВыбораДляТекстаДополнения.Добавить(1,   "(перевищення звичайної ціни над фактичною за товарами, послугами, указаними в податковій накладній №" + НоменНН + ")");
		СписокВыбораДляТекстаДополнения.Добавить("1_1","(перевищення звичайної ціни над фактичною за товарами, послугами, указаними в податковій накладній №" + НоменНН + "); сума процентів нарахованих або таких, що мають бути нараховані на суму номіналу процентного векселя)");
		СписокВыбораДляТекстаДополнения.Добавить(12,  "(звичайна ціна за товарами, послугами, указаними в податковій накладній №" + НоменНН +")");
		
	Иначе
		
		Если ЗначениеЗаполнено(ОсновнаяНалоговаяПриПродажеНижеОбычнойЦены) Тогда
			
			КодСпецРежима = Формат(ОсновнаяНалоговаяПриПродажеНижеОбычнойЦены.СпецРежимНалогообложения);
			НомерФилиала  = ?(ЗначениеЗаполнено(ОсновнаяНалоговаяПриПродажеНижеОбычнойЦены.ОбособленноеПодразделение.Префикс), Формат(Число(ОсновнаяНалоговаяПриПродажеНижеОбычнойЦены.ОбособленноеПодразделение.Префикс),"ЧЦ=4; ЧГ=0"), "");
			ТекНомер = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ОсновнаяНалоговаяПриПродажеНижеОбычнойЦены.Номер, Истина, Истина);

			Если ЗначениеЗаполнено(НомерФилиала) Тогда
				НоменНН = СокрЛ(ТекНомер) + "/" + СокрЛ(КодСпецРежима) + "/" + СокрЛ(НомерФилиала);
			ИначеЕсли ЗначениеЗаполнено(КодСпецРежима) Тогда
				НоменНН = СокрЛ(ТекНомер) + "/" + СокрЛ(КодСпецРежима);
			Иначе
				НоменНН = СокрЛ(ТекНомер);	
			КонецЕсли;
			
		Иначе
			
			НомерНН = "";
			
		КонецЕсли;
		
		Если Дата >= '2017-03-16' Тогда
			СписокВыбораДляТекстаДополнения.Добавить(15,  "(перевищення бази оподаткування, визначеної відповідно до статей 188 і 189 Податкового кодексу України, над фактичною ціною постачання)");
		Иначе
			//СписокВыбораДляТекстаДополнения.Добавить(1,   "(перевищення звичайної ціни над фактичною за товарами, послугами, указаними в податковій накладній №" + НоменНН + ")");
			//СписокВыбораДляТекстаДополнения.Добавить("1_1","(перевищення звичайної ціни над фактичною за товарами, послугами, указаними в податковій накладній №" + НоменНН + "; сума процентів нарахованих або таких, що мають бути нараховані на суму номіналу процентного векселя)");
			//СписокВыбораДляТекстаДополнения.Добавить(12,  "(звичайна ціна за товарами, послугами, указаними  в  податковій  накладній №" + НоменНН + ")");
			СписокВыбораДляТекстаДополнения.Добавить(15,  "(перевищення ціни придбання над фактичною ціною постачання товарів/послуг, указаних в податковій накладній №" + НоменНН + ")");
			СписокВыбораДляТекстаДополнения.Добавить(16,  "(перевищення балансової (залишкової) вартості над фактичною ціною постачання необоротних активів, указаних в податковій накладні №" + НоменНН + ")");
	        // СписокВыбораДляТекстаДополнения.Добавить(17,  "(перевищення собівартості самостійно виготовлених товарів/послуг над фактичною ціною їх постачання, указаних в податковій накладній №" + НоменНН + ")"); 
			СписокВыбораДляТекстаДополнения.Добавить(17,  "(перевищення звичайної ціни самостійно виготовлених товарів/послуг, указаних у податковій накладній №" + НоменНН + ")"); 
		КонецЕсли;

	КонецЕсли;
	
	Если ВернутьМассивом Тогда
		МассивДляТекстаДополнения = Новый Массив;
		Для каждого Строка из СписокВыбораДляТекстаДополнения Цикл
			МассивДляТекстаДополнения.Добавить(Строка.Представление);
		КонецЦикла;
		Возврат МассивДляТекстаДополнения;
	Иначе
		Возврат СписокВыбораДляТекстаДополнения;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПоПревышениямОбычнойЦеныЗаПериодСервер(СтандартныйПериод)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&НачалоПериода КАК ДатаОбычнойЦены,
	|	ЕСТЬNULL(КурсыВалют.Курс, 0) / ЕСТЬNULL(КурсыВалют.Кратность, 1) КАК КоэффициентПересчетаВВалютуРегл
	|ПОМЕСТИТЬ КоэффициентыПересчетаВВалютуРеглОбычныйВидЦены
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&НачалоПериода, Валюта = &ОбычныйВидЦеныВалюта) КАК КурсыВалют
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	КурсыВалют.Период,
	|	ЕСТЬNULL(КурсыВалют.Курс, 0) / ЕСТЬNULL(КурсыВалют.Кратность, 1)
	|ИЗ
	|	РегистрСведений.КурсыВалют КАК КурсыВалют
	|ГДЕ
	|   КурсыВалют.Валюта = &ОбычныйВидЦены
	|   И КурсыВалют.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалоговаяНакладная.Ссылка,
	|	НАЧАЛОПЕРИОДА(НалоговаяНакладная.Дата, ДЕНЬ) КАК ДатаНалоговой,
	|	НалоговаяНакладнаяТовары.Номенклатура,
	|	НалоговаяНакладнаяТовары.Содержание,
	|	НалоговаяНакладнаяТовары.Характеристика,
	|	НалоговаяНакладнаяТовары.Упаковка,
	|	НалоговаяНакладнаяТовары.КоличествоУпаковок,
	|	НалоговаяНакладнаяТовары.Количество,
	|	НалоговаяНакладнаяТовары.НомерГТД,
	|	НалоговаяНакладнаяТовары.СтатьяДекларацииНДСНалоговыеОбязательства,
	|	НалоговаяНакладнаяТовары.ЦенаРегл КАК ЦенаРегл,
	|	НалоговаяНакладнаяТовары.СуммаБезНДСРегл КАК СуммаБезНДСРегл,
	|	НалоговаяНакладнаяТовары.СуммаНДСРегл КАК СуммаНДСРегл,
	|	(НалоговаяНакладнаяТовары.СуммаБезНДСРегл + НалоговаяНакладнаяТовары.СуммаНДСРегл) КАК СуммаСНДСРегл,
	|	НалоговаяНакладнаяТовары.СтавкаНДС КАК СтавкаНДС
	|ПОМЕСТИТЬ НоменклатураНалоговых
	|ИЗ
	|	Документ.НалоговаяНакладная.Товары КАК НалоговаяНакладнаяТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НалоговаяНакладная КАК НалоговаяНакладная
	|		ПО НалоговаяНакладнаяТовары.Ссылка = НалоговаяНакладная.Ссылка
	|ГДЕ
    |	НалоговаяНакладная.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НалоговаяНакладная.Проведен
    |	И НалоговаяНакладнаяТовары.СтавкаНДС В (ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20), ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС14), ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7))
	|	И НалоговаяНакладная.СтатусАвтокорректировки <> ЗНАЧЕНИЕ(Перечисление.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТехническуюТЧ)
	|	И НалоговаяНакладная.Организация = &Организация
	|	И НалоговаяНакладная.ОбособленноеПодразделение = &ОбособленноеПодразделение
	|	И (&НетОтбораПоПартнеру ИЛИ НалоговаяНакладная.Партнер = &Партнер)
	|	И (&НетОтбораПоКонтрагенту ИЛИ НалоговаяНакладная.Контрагент = &Контрагент)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НалоговаяНакладная.Ссылка,
	|	НАЧАЛОПЕРИОДА(НалоговаяНакладная.Дата, ДЕНЬ) КАК ДатаНалоговой,
	|	НалоговаяНакладнаяТоварыПоДаннымПользователя.Номенклатура,
	|	НалоговаяНакладнаяТоварыПоДаннымПользователя.Содержание,
	|	НалоговаяНакладнаяТоварыПоДаннымПользователя.Характеристика,
	|	НалоговаяНакладнаяТоварыПоДаннымПользователя.Упаковка,
	|	НалоговаяНакладнаяТоварыПоДаннымПользователя.КоличествоУпаковок,
	|	НалоговаяНакладнаяТоварыПоДаннымПользователя.Количество,
	|	НалоговаяНакладнаяТоварыПоДаннымПользователя.НомерГТД,
	|	НалоговаяНакладнаяТоварыПоДаннымПользователя.СтатьяДекларацииНДСНалоговыеОбязательства,
	|	НалоговаяНакладнаяТоварыПоДаннымПользователя.ЦенаРегл КАК ЦенаРегл,
	|	НалоговаяНакладнаяТоварыПоДаннымПользователя.СуммаБезНДСРегл КАК СуммаБезНДСРегл,
	|	НалоговаяНакладнаяТоварыПоДаннымПользователя.СуммаНДСРегл КАК СуммаНДСРегл,
	|	(НалоговаяНакладнаяТоварыПоДаннымПользователя.СуммаБезНДСРегл + НалоговаяНакладнаяТоварыПоДаннымПользователя.СуммаНДСРегл) КАК СуммаСНДСРегл,
	|	НалоговаяНакладнаяТоварыПоДаннымПользователя.СтавкаНДС
	|ИЗ
	|	Документ.НалоговаяНакладная.ТоварыПоДаннымПользователя КАК НалоговаяНакладнаяТоварыПоДаннымПользователя
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НалоговаяНакладная КАК НалоговаяНакладная
	|		ПО НалоговаяНакладнаяТоварыПоДаннымПользователя.Ссылка = НалоговаяНакладная.Ссылка
	|ГДЕ
    |	НалоговаяНакладная.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НалоговаяНакладная.Проведен
    |	И НалоговаяНакладнаяТоварыПоДаннымПользователя.СтавкаНДС В (ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20), ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС14), ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7))
	|	И НалоговаяНакладная.СтатусАвтокорректировки = ЗНАЧЕНИЕ(Перечисление.СтатусыАвтокорректировкиНалоговыхДокументов.ИсправлятьТехническуюТЧ)
	|	И НалоговаяНакладная.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены)
	|	И НалоговаяНакладная.Организация = &Организация
	|	И НалоговаяНакладная.ОбособленноеПодразделение = &ОбособленноеПодразделение
	|	И (&НетОтбораПоПартнеру ИЛИ НалоговаяНакладная.Партнер = &Партнер)
	|	И (&НетОтбораПоКонтрагенту ИЛИ НалоговаяНакладная.Контрагент = &Контрагент)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураНалоговыхСвернуто.ДатаНалоговой КАК ДатаНалоговой,
	|	МАКСИМУМ(КоэффициентыПересчетаВВалютуРеглОбычныйВидЦены.ДатаОбычнойЦены) КАК ДатаОбычнойЦены
	|ПОМЕСТИТЬ СоответсвиеДатыНалоговойИДатыОбычнойЦены
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		НоменклатураНалоговых.ДатаНалоговой
	|	ИЗ
	|		НоменклатураНалоговых КАК НоменклатураНалоговых) КАК НоменклатураНалоговыхСвернуто
	|   ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыПересчетаВВалютуРеглОбычныйВидЦены КАК КоэффициентыПересчетаВВалютуРеглОбычныйВидЦены
    |   ПО НоменклатураНалоговыхСвернуто.ДатаНалоговой >= КоэффициентыПересчетаВВалютуРеглОбычныйВидЦены.ДатаОбычнойЦены
	|
	|СГРУППИРОВАТЬ ПО 
	|	НоменклатураНалоговыхСвернуто.ДатаНалоговой
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Ссылка КАК ОсновнаяНалоговаяПриПродажеНижеОбычнойЦены,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Содержание,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Упаковка,
	|	СУММА(ВложенныйЗапрос.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
	|	ВложенныйЗапрос.НомерГТД,
	|	ВложенныйЗапрос.СтатьяДекларацииНДСНалоговыеОбязательства,
	|	ВложенныйЗапрос.ЦенаРегл КАК Цена,
	|	0 КАК ЦенаРегл, // Особенность этой операции
	|	СУММА(ВложенныйЗапрос.СуммаБезНДСРегл) КАК СуммаБезНДС,
	|	СУММА(ВложенныйЗапрос.СуммаНДСРегл) КАК СуммаНДС,
	|	СУММА(ВложенныйЗапрос.СуммаСНДСРегл) КАК СуммаСНДС,
	|	ВложенныйЗапрос.СтавкаНДС,
	|	ВложенныйЗапрос.ЦенаОбычнаяРегл КАК ЦенаОбычная,
	|	СУММА((ВложенныйЗапрос.ЦенаОбычнаяРегл - ВложенныйЗапрос.ЦенаРегл) * ВложенныйЗапрос.КоличествоУпаковок) КАК СуммаПревышения,
	|	СУММА((ВложенныйЗапрос.ЦенаОбычнаяРегл - ВложенныйЗапрос.ЦенаРегл) * ВложенныйЗапрос.КоличествоУпаковок) КАК СуммаБезНДСРегл,
	|	СУММА((ВложенныйЗапрос.ЦенаОбычнаяРегл - ВложенныйЗапрос.ЦенаРегл) * ВложенныйЗапрос.КоличествоУпаковок * ВЫБОР
	|			КОГДА ВложенныйЗапрос.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20)
	|				ТОГДА 0.2
    |			КОГДА ВложенныйЗапрос.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС14)
    |				ТОГДА 0.14
    |			КОГДА ВложенныйЗапрос.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7)
    |				ТОГДА 0.07
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаНДСПревышения,
	|	СУММА((ВложенныйЗапрос.ЦенаОбычнаяРегл - ВложенныйЗапрос.ЦенаРегл) * ВложенныйЗапрос.КоличествоУпаковок * ВЫБОР
	|			КОГДА ВложенныйЗапрос.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20)
	|				ТОГДА 0.2
    |			КОГДА ВложенныйЗапрос.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС14)
    |				ТОГДА 0.14
    |			КОГДА ВложенныйЗапрос.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7)
    |				ТОГДА 0.07
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаНДСРегл
	|ИЗ
	|	(ВЫБРАТЬ
	|		НоменклатураНалоговых.Ссылка КАК Ссылка,
	|		НоменклатураНалоговых.Номенклатура КАК Номенклатура,
	|		НоменклатураНалоговых.Содержание КАК Содержание,
	|		НоменклатураНалоговых.Характеристика КАК Характеристика,
	|		НоменклатураНалоговых.Упаковка КАК Упаковка,
	|		НоменклатураНалоговых.КоличествоУпаковок КАК КоличествоУпаковок,
	|		НоменклатураНалоговых.Количество КАК Количество,
	|		НоменклатураНалоговых.НомерГТД КАК НомерГТД,
	|		НоменклатураНалоговых.СтатьяДекларацииНДСНалоговыеОбязательства КАК СтатьяДекларацииНДСНалоговыеОбязательства,
	|		НоменклатураНалоговых.ЦенаРегл КАК ЦенаРегл,
	|		НоменклатураНалоговых.СуммаБезНДСРегл КАК СуммаБезНДСРегл,
	|		НоменклатураНалоговых.СуммаНДСРегл КАК СуммаНДСРегл,
	|		НоменклатураНалоговых.СуммаСНДСРегл КАК СуммаСНДСРегл,
	|		НоменклатураНалоговых.СтавкаНДС КАК СтавкаНДС,
	|		ВЫБОР
	|			КОГДА НоменклатураНалоговых.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|				ТОГДА &НоменклатураНалоговыхКоэффициентУпаковки
	|			ИНАЧЕ 1
	|		КОНЕЦ 
	|		* ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) 
	|		/ ЕСТЬNULL(&ЦеныНоменклатурыКоэффициентУпаковки, 1)
	|		* ЕСТЬNULL(КоэффициентыПересчетаВВалютуРеглОбычныйВидЦены.КоэффициентПересчетаВВалютуРегл, 0) КАК ЦенаОбычнаяРегл
	|	ИЗ
	|		НоменклатураНалоговых КАК НоменклатураНалоговых
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|				&ДатаДляПолученияОбычныхЦен,
	|				ВидЦены = &ОбычныйВидЦены
	|					И (Номенклатура, Характеристика) В
	|						(ВЫБРАТЬ
	|							НоменклатураНалоговых.Номенклатура,
	|							НоменклатураНалоговых.Характеристика
	|						ИЗ
	|							НоменклатураНалоговых КАК НоменклатураНалоговых)) КАК ЦеныНоменклатурыСрезПоследних
	|			ПО ЦеныНоменклатурыСрезПоследних.Номенклатура = НоменклатураНалоговых.Номенклатура
	|				И ЦеныНоменклатурыСрезПоследних.Характеристика = НоменклатураНалоговых.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ СоответсвиеДатыНалоговойИДатыОбычнойЦены КАК СоответсвиеДатыНалоговойИДатыОбычнойЦены
	|       ПО НоменклатураНалоговых.ДатаНалоговой = СоответсвиеДатыНалоговойИДатыОбычнойЦены.ДатаНалоговой
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыПересчетаВВалютуРеглОбычныйВидЦены КАК КоэффициентыПересчетаВВалютуРеглОбычныйВидЦены
	|       ПО СоответсвиеДатыНалоговойИДатыОбычнойЦены.ДатаОбычнойЦены = КоэффициентыПересчетаВВалютуРеглОбычныйВидЦены.ДатаОбычнойЦены
	|	) КАК ВложенныйЗапрос
	|ГДЕ
	|	ВложенныйЗапрос.ЦенаОбычнаяРегл - ВложенныйЗапрос.ЦенаРегл > 0.001
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Содержание,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.ЦенаРегл,
	|	ВложенныйЗапрос.ЦенаОбычнаяРегл,
	|	ВложенныйЗапрос.Ссылка,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.НомерГТД,
	|	ВложенныйЗапрос.СтатьяДекларацииНДСНалоговыеОбязательства,
	|	ВложенныйЗапрос.СтавкаНДС";
	
	ДатаПолучения = КонецДня(?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДата()));
	
	Запрос.УстановитьПараметр("ДатаДляПолученияОбычныхЦен", ДатаПолучения);
	Запрос.УстановитьПараметр("НачалоПериода", СтандартныйПериод.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода",  ?(ЗначениеЗаполнено(СтандартныйПериод.ДатаОкончания), СтандартныйПериод.ДатаОкончания, '39991231235959'));
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ОбособленноеПодразделение", Объект.ОбособленноеПодразделение);
	Запрос.УстановитьПараметр("ОбычныйВидЦены", Объект.ОбычныйВидЦены);
	Запрос.УстановитьПараметр("ОбычныйВидЦеныВалюта", Объект.ОбычныйВидЦены.ВалютаЦены);
	Запрос.УстановитьПараметр("ВалютаРегл", ВалютаРегламентированногоУчета);
	Запрос.УстановитьПараметр("Партнер", Объект.Партнер);
	Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	Запрос.УстановитьПараметр("НетОтбораПоПартнеру", Не ЗначениеЗаполнено(Объект.Партнер));
	Запрос.УстановитьПараметр("НетОтбораПоКонтрагенту", Не ЗначениеЗаполнено(Объект.Контрагент));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&НоменклатураНалоговыхКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"НоменклатураНалоговых.Упаковка",
		"НоменклатураНалоговых.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ЦеныНоменклатурыКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ЦеныНоменклатурыСрезПоследних.Упаковка",
		"ЦеныНоменклатурыСрезПоследних.Номенклатура"));
	
	Объект.ТоварыПоДаннымПользователя.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Документы.НалоговаяНакладная.УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию(Объект);
	ПроверитьСтатьюДекларацииНДС22(Объект);

	ЗаполнитьТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтатьюРасходовПоВыделеннойСтрокеСервер(СтатьяРасходовУсловнаяПродажа, АналитикаРасходовУсловнаяПродажа)
	
	Для каждого Строка из Объект.ТоварыПоДаннымПользователя Цикл
		
		Строка.СтатьяРасходовУсловнаяПродажа = СтатьяРасходовУсловнаяПродажа;
		Строка.АналитикаРасходовУсловнаяПродажа = АналитикаРасходовУсловнаяПродажа;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьСуммыСервер(ИмяТабЧасти, ПересчитатьВалютныеСуммы)
	
	Если ПересчитатьВалютныеСуммы Тогда
		СтараяВалюта                = ВалютаДокумента;
		НоваяВалюта                 = Объект.Валюта;
		ДатаДокумента               = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДата());
		СтруктураКурсовСтаройВалюты = РаботаСКурсамиВалют.ПолучитьКурсВалюты(СтараяВалюта, ДатаДокумента);
		СтруктураКурсовНовойВалюты  = РаботаСКурсамиВалют.ПолучитьКурсВалюты(НоваяВалюта,  ДатаДокумента);
	КонецЕсли;
	
	Для Каждого ТекущаяСтрока ИЗ Объект[ИмяТабЧасти] Цикл
		
		ПроцентНДС = НДСОбщегоНазначенияКлиентСервер.ПолучитьСтавкуНДСЧислом(ТекущаяСтрока.СтавкаНДС);
			
		Если ПересчитатьВалютныеСуммы Тогда
			
			ТекущаяСтрока.Цена = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(
				ТекущаяСтрока.Цена,
				СтараяВалюта,НоваяВалюта,
				СтруктураКурсовСтаройВалюты.Курс,СтруктураКурсовНовойВалюты.Курс,
				СтруктураКурсовСтаройВалюты.Кратность,СтруктураКурсовНовойВалюты.Кратность
			);

			ТекущаяСтрока.СуммаБезНДС = ТекущаяСтрока.Цена * ТекущаяСтрока.КоличествоУпаковок;
			ТекущаяСтрока.СуммаНДС	  = ТекущаяСтрока.СуммаБезНДС * ПроцентНДС;
			ТекущаяСтрока.СуммаСНДС	  = ТекущаяСтрока.СуммаБезНДС + ТекущаяСтрока.СуммаНДС;

			Если ИмяТабЧасти = "ТоварыПоДаннымПользователя" И Объект.ВидОперации = Перечисления.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены Тогда
				ТекущаяСтрока.ЦенаОбычная = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(
					ТекущаяСтрока.ЦенаОбычная,
					СтараяВалюта,НоваяВалюта,
					СтруктураКурсовСтаройВалюты.Курс,СтруктураКурсовНовойВалюты.Курс,
					СтруктураКурсовСтаройВалюты.Кратность,СтруктураКурсовНовойВалюты.Кратность
				);
				
				ТекущаяСтрока.СуммаПревышения = Макс(0, (ТекущаяСтрока.ЦенаОбычная - ТекущаяСтрока.Цена) * ТекущаяСтрока.КоличествоУпаковок);
				ТекущаяСтрока.СуммаНДСПревышения = ТекущаяСтрока.СуммаПревышения * ПроцентНДС;
				
			КонецЕсли;
						
		КонецЕсли;
		
	КонецЦикла; 
	
	ЗаполнитьРеквизитыРегл(ИмяТабЧасти);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыРегл(ИмяТЧ = Неопределено)
	
	Документы.НалоговаяНакладная.ЗаполнитьСуммыРеглВТабличныхЧастях(Объект, ИмяТЧ);
	
КонецПроцедуры

&НаКлиенте
Процедура ВключаетсяВУточняющийРасчетПриИзменении(Элемент)
	Элементы.ГруппаУточняющийРасчетДатаНомер.Видимость = Объект.ВключаетсяВУточняющийРасчет;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуре()
	ПараметрыЗаполненияРеквизитов = Новый Структура;
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются",
											Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакТипНоменклатуры",
											Новый Структура("Номенклатура", "ТипНоменклатуры"));
											
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.Товары,ПараметрыЗаполненияРеквизитов);
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.ТоварыПоДаннымПользователя,ПараметрыЗаполненияРеквизитов);	
	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены()
	
	ДатаПолучения = КонецДня(?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДата()));
	
	Для Каждого СтрокаТовары Из Объект.ТоварыПоДаннымПользователя Цикл

		СписокВыбораДляТекстаДополнения = ЗаполнитьСписокВыбораТекстДополнения(СтрокаТовары.ОсновнаяНалоговаяПриПродажеНижеОбычнойЦены, ДатаПолучения);
		ПодходящееЗначение = СписокВыбораДляТекстаДополнения.НайтиПоЗначению(Объект.ТипПричиныНевыдачиПокупателю);
		Если НЕ ПодходящееЗначение = Неопределено Тогда
			ТекстДополнения = ПодходящееЗначение.Представление;	
		Иначе
			ТекстДополнения = "";
		КонецЕсли;
		
		СтрокаТовары.ТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены = ТекстДополнения;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОчиститьФлагНеИзменятьСумму(ТабличнаяЧасть)
	
	Для каждого СтрокаТЧ Из ТабличнаяЧасть Цикл
		СтрокаТЧ.НеПересчитыватьСумму = Ложь;
	КонецЦикла; 

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНоменклатуруГТДПоУмолчанию(СтрокаТовары)
	
	Если ЗначениеЗаполнено(СтрокаТовары.Номенклатура) Тогда
		РеквизитыУчетаПоГТД = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТовары.Номенклатура, "ВестиУчетПоГТД,НоменклатураГТД");
		Если РеквизитыУчетаПоГТД.ВестиУчетПоГТД Тогда
			СтрокаТовары.НомерГТД = РеквизитыУчетаПоГТД.НоменклатураГТД;
		Иначе
			СтрокаТовары.НомерГТД = Справочники.НомераГТД.ПустаяСсылка();
		КонецЕсли;
	Иначе
		СтрокаТовары.НомерГТД = Справочники.НомераГТД.ПустаяСсылка();	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьСписокВыбораВидаОперации()

	СписокВидовОпераций = Новый Массив;
	СписокВидовОпераций.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации"));
	СписокВидовОпераций.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации"));
	СписокВидовОпераций.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.НеНДСОперации"));
	СписокВидовОпераций.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОблагаемыеОперации"));
	СписокВидовОпераций.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ИтоговаяРозницаОсвобожденныеОперации"));
	СписокВидовОпераций.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа"));
	СписокВидовОпераций.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа"));
	СписокВидовОпераций.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОблагаемыеОперации"));
	СписокВидовОпераций.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.РозницаКонрагентуОсвобожденныеОперации"));
	СписокВидовОпераций.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.РаботыОтНерезидента"));
	СписокВидовОпераций.Добавить(ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены"));
	
	Возврат СписокВидовОпераций;
	
КонецФункции


&НаКлиенте
Процедура УстановитьКодПризнакаСводной()
	СтароеЗначениеКодПризнакаСводной = Объект.КодПризнакаСводной;
	Если НЕ Объект.Сводная Тогда
		Объект.КодПризнакаСводной = 0;
	Иначе 
		Если ( Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.СводнаяУсловнаяПродажа")
			  ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.УсловнаяПродажа")) Тогда
			  
			// не изменяем, если уже был установлен подходящий признак 
			Если НЕ (СтароеЗначениеКодПризнакаСводной = 1 ИЛИ СтароеЗначениеКодПризнакаСводной = 2) Тогда 
			  	Объект.КодПризнакаСводной =  1;
			КонецЕсли;
			  
		ИначеЕсли (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ОблагаемыеОперации")
			  ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ОсвобожденныеОперации")) Тогда
			  
			Если НЕ (СтароеЗначениеКодПризнакаСводной = 4) Тогда 
			  	Объект.КодПризнакаСводной =  4;
			КонецЕсли;
			  
		 ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийНалоговыхДокументов.ПродажаНижеОбычнойЦены") Тогда
			  
			Если НЕ (СтароеЗначениеКодПризнакаСводной = 3) Тогда 
			  	Объект.КодПризнакаСводной = 3;
			КонецЕсли;			
			  
	    КонецЕсли;
	КонецЕсли; 
	Если НЕ Объект.КодПризнакаСводной = СтароеЗначениеКодПризнакаСводной Тогда
		КодПризнакаСводнойПриИзменении(ЭтаФорма.Элементы.КодПризнакаСводной);	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СводнаяПриИзменении(Элемент)
	
	УстановитьКодПризнакаСводной();
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура КодПризнакаСводнойПриИзменении(Элемент)
	НастроитьЭлементыФормы();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПроверитьСтатьюДекларацииНДС22(Объект)
	
	Для каждого СтрокаТоваров Из Объект.Товары Цикл
		ПроверитьСтатьюДекларацииНДС22ВСтроке(Объект, СтрокаТоваров);
	КонецЦикла;
	
	Для каждого СтрокаТоваров Из Объект.ТоварыПоДаннымПользователя Цикл
		ПроверитьСтатьюДекларацииНДС22ВСтроке(Объект, СтрокаТоваров);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПроверитьСтатьюДекларацииНДС22ВСтроке(Объект, ТекущаяСтрока)
	
	Если Объект.Дата >= '2020-01-01' И Объект.ТипПричиныНевыдачиПокупателю = 7 Тогда
		Если ТекущаяСтрока.СтатьяДекларацииНДСНалоговыеОбязательства = ПредопределенноеЗначение("Справочник.СтатьиНалоговыхДеклараций.НДС_НООсвобожден") Тогда
			ТекущаяСтрока.СтатьяДекларацииНДСНалоговыеОбязательства = ПредопределенноеЗначение("Справочник.СтатьиНалоговыхДеклараций.НДС_НОНеОблагЭкспорт");
		КонецЕсли;
	Иначе
		Если ТекущаяСтрока.СтатьяДекларацииНДСНалоговыеОбязательства = ПредопределенноеЗначение("Справочник.СтатьиНалоговыхДеклараций.НДС_НОНеОблагЭкспорт") Тогда
			ТекущаяСтрока.СтатьяДекларацииНДСНалоговыеОбязательства = ПредопределенноеЗначение("Справочник.СтатьиНалоговыхДеклараций.НДС_НООсвобожден");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТипПричиныНевыдачиНаСервере()

	Документы.НалоговаяНакладная.УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию(Объект);
	
КонецПроцедуры

#КонецОбласти
