
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.ПутьКФайлу) Тогда
		Элементы.ИмяФайла.Видимость = Ложь;
		ИнициализироватьКомпоновку(Параметры.ПутьКФайлу);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла.Фильтр = НСтр("ru='Файл результатов сравнения (*.xml)|*.xml';uk='Файл результатів порівняння (*..xml)|*.xml'");
	ДиалогВыбораФайла.Заголовок = Заголовок;
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.Расширение = "xml";
	ДиалогВыбораФайла.ИндексФильтра = 0;
	ДиалогВыбораФайла.ПолноеИмяФайла = Элемент.ТекстРедактирования;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Ложь;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		ИмяФайла = ДиалогВыбораФайла.ПолноеИмяФайла;
		ДанныеТестирования = Новый ДвоичныеДанные(ИмяФайла);
		АдресХранилища = ПоместитьВоВременноеХранилище(ДанныеТестирования, УникальныйИдентификатор);
		ИнициализироватьКомпоновку(АдресХранилища);
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьКомпоновку(АдресХранилища)
	
	СхемаКомпоновкиДанных = Новый СхемаКомпоновкиДанных;
	КомпоновщикНастроек = Отчет.КомпоновщикНастроек;
	
	Источник = СхемаКомпоновкиДанных.ИсточникиДанных.Добавить();
	Источник.Имя = "ЛокальнаяБаза";
	Источник.СтрокаСоединения = "";
	Источник.ТипИсточникаДанных = "Local";
	
	НастройкиКомпоновкиДанных = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	ВнешниеНаборыДанных = Новый Структура();
	
	Отчет.ПутьКФайлу = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогВременныхФайлов())
		+ Формат(ТекущаяДата(), "ДФ=yyyyMMddHHmmss") + "_" + Строка(Новый УникальныйИдентификатор())+".xml";
	
	ФайлОтчета = ПолучитьИзВременногоХранилища(АдресХранилища);
	ФайлОтчета.Записать(Отчет.ПутьКФайлу);
	Чтение = Новый ЧтениеXML;
	Чтение.ОткрытьФайл(Отчет.ПутьКФайлу);
	
	Пока Чтение.Прочитать() Цикл
		Если ЗначениеЗаполнено(Чтение.Значение) Тогда
			Если Чтение.КонтекстПространствИмен.Глубина = 4 Тогда // это объект метаданных, формируем по нему группировку и таблицу.
				ОбъектМетаданных = ОбщегоНазначения.ЗначениеИзСтрокиXML(Чтение.Значение);
				
				Если СхемаКомпоновкиДанных.НаборыДанных.Найти(ОбъектМетаданных) <> Неопределено Тогда // уже добавлен в схему
					НаборДанных = СхемаКомпоновкиДанных.НаборыДанных[ОбъектМетаданных];
					Продолжить;
				КонецЕсли;
				
				// каждый объект метаданных это новый отдельный набор данных для СКД.
				НаборДанных = СхемаКомпоновкиДанных.НаборыДанных.Добавить(Тип("НаборДанныхОбъектСхемыКомпоновкиДанных"));
				НаборДанных.Имя = ОбъектМетаданных;
				НаборДанных.ИмяОбъекта = ОбъектМетаданных;
				НаборДанных.ИсточникДанных = "ЛокальнаяБаза";
				
				// группировка таблиц по объектам метаданных: один объект - одна таблица.
				Таблица = НастройкиКомпоновкиДанных.Структура.Добавить(Тип("ТаблицаКомпоновкиДанных"));
				Таблица.Имя = ОбъектМетаданных;
				
				НастройкиКомпоновкиДанных.ПараметрыВывода.УстановитьЗначениеПараметра("АвтоПозицияРесурсов", АвтоПозицияРесурсовКомпоновкиДанных.НеИспользовать);
				ЗаголовокТаблицы = Таблица.ПараметрыВывода.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Заголовок"));
				Если ЗаголовокТаблицы <> Неопределено Тогда
					ЗаголовокТаблицы.Значение = "Таблица: " + ОбъектМетаданных;
					ЗаголовокТаблицы.Использование = Истина;
				КонецЕсли;
				
				Строки = Таблица.Строки.Добавить();
				Строки.ПараметрыВывода.УстановитьЗначениеПараметра("АвтоПозицияРесурсов", АвтоПозицияРесурсовКомпоновкиДанных.НеИспользовать);
				
				ПолеГруппировкаРегистратор = Строки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
				ПолеГруппировкаРегистратор.Поле = Новый ПолеКомпоновкиДанных(ОбъектМетаданных + "_Регистратор");
				ПолеГруппировкаРегистратор.Использование = Истина;
				
				ГруппировкаРегистратор = Строки.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
				ГруппировкаРегистратор.Поле = Новый ПолеКомпоновкиДанных(ОбъектМетаданных + "_Регистратор");
				ГруппировкаРегистратор.Использование = Истина;
				
				ГруппировкаДельта = Строки.Структура.Добавить();
				// поля группировки определим позже
				
				ДетальныеЗаписи = ГруппировкаДельта.Структура.Добавить();
				ДетальныеЗаписи.Имя = ГруппировкаДельта.Имя + "_ДетальныеЗаписи";
				ДетальныеЗаписи.Использование = Истина;
				// поля группировки определим позже
				
			ИначеЕсли Чтение.КонтекстПространствИмен.Глубина = 5 Тогда // это записи, выводим их в таблицу.
				
				Набор = ОбщегоНазначения.ЗначениеИзСтрокиXML(Чтение.Значение);
				Для Каждого Колонка Из Набор.Колонки Цикл
					
					ИмяПоля = "" + ОбъектМетаданных + "_" + Колонка.Имя;
					
					Если НаборДанных.Поля.Найти(ИмяПоля) <> Неопределено Тогда // уже есть в схеме, добавлять не надо
						Продолжить;
					КонецЕсли;
					
					Поле             = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
					Поле.Заголовок   = Колонка.Имя;
					Поле.Поле        = Колонка.Имя;
					Поле.ПутьКДанным = ИмяПоля;
					Поле.ТипЗначения = Колонка.ТипЗначения;
					
					МаксимальнаяШирина = Поле.Оформление.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("МаксимальнаяШирина"));
					Если МаксимальнаяШирина <> Неопределено Тогда
						МаксимальнаяШирина.Значение = 15;
						МаксимальнаяШирина.Использование = Истина;
					КонецЕсли;
					
					Если Колонка.ТипЗначения.СодержитТип(Тип("Число")) Тогда // создаем вычисляемое поле и рассчитываем дельту старых и новых записей.
						
						ВычисляемоеПолеПутьКДанным = ОбъектМетаданных + "_Вычисляемое_" + Колонка.Имя;
						Если СхемаКомпоновкиДанных.ВычисляемыеПоля.Найти(ВычисляемоеПолеПутьКДанным) <> Неопределено Тогда
							Продолжить;
						КонецЕсли;
						ВычисляемоеПоле = СхемаКомпоновкиДанных.ВычисляемыеПоля.Добавить();
						ВычисляемоеПоле.ПутьКДанным = ОбъектМетаданных + "_Вычисляемое_" + Колонка.Имя;
						Шаблон = "ВЫБОР КОГДА %ОбъектМетаданных%_ТипЗаписи = ""ИсходныеЗаписи"" ТОГДА -%ИмяПоля% ИНАЧЕ %ИмяПоля% КОНЕЦ";
						ВычисляемоеПоле.Выражение = СтрЗаменить(Шаблон, "%ИмяПоля%", ИмяПоля);
						ВычисляемоеПоле.Выражение = СтрЗаменить(ВычисляемоеПоле.Выражение, "%ОбъектМетаданных%", ОбъектМетаданных);
						ВычисляемоеПоле.ТипЗначения = Колонка.ТипЗначения;
						ВычисляемоеПоле.Заголовок = Колонка.Имя;
						ВычисляемоеПоле.Оформление.УстановитьЗначениеПараметра("ВыделятьОтрицательные", Истина);
						ВычисляемоеПоле.Оформление.УстановитьЗначениеПараметра("МаксимальнаяШирина", 15);
						
						ПолеИтога = СхемаКомпоновкиДанных.ПоляИтога.Добавить();
						ПолеИтога.ПутьКДанным = ВычисляемоеПоле.ПутьКДанным;
						ПолеИтога.Выражение = "СУММА(" + ВычисляемоеПоле.ПутьКДанным + ")";
						ПолеИтога.Группировки.Добавить(ОбъектМетаданных + "_Регистратор");
						
						ПолеЗначения = СхемаКомпоновкиДанных.ПоляИтога.Добавить();
						ПолеЗначения.ПутьКДанным = ВычисляемоеПоле.ПутьКДанным;
						ПолеЗначения.Выражение = ИмяПоля;
						ПолеЗначения.Группировки.Добавить(ОбъектМетаданных + "_ТипЗаписи");
						
						ИмяПоля = ВычисляемоеПоле.ПутьКДанным;
						ПолеГруппировкиДельта = ГруппировкаДельта.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
						ПолеГруппировкиДельта.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
					Иначе
						Если Колонка.Имя <> "ТипЗаписи"
							//И Колонка.Имя <> "ИмяРегистра"
						Тогда // добавим в группировку по дельте
							ПолеГруппировкиДельта = ГруппировкаДельта.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
							ПолеГруппировкиДельта.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
							ПолеГруппировкиДельта.Использование = Истина;

							ПолеГруппировкиДельта = ГруппировкаДельта.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
							ПолеГруппировкиДельта.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
						КонецЕсли;
						Если Колонка.Имя <> "Регистратор" Тогда // выведем в детальные записи
							ПолеГруппировкиДетальныеЗаписи = ДетальныеЗаписи.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
							ПолеГруппировкиДетальныеЗаписи.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
							ПолеГруппировкиДетальныеЗаписи.Использование = Истина;
						КонецЕсли;
					КонецЕсли;
					Если Колонка.Имя <> "Регистратор" И Колонка.Имя <> "ИмяРегистра" Тогда // добавим в группировку по дельте
						ПолеГруппировкиДетальныеЗаписи = ДетальныеЗаписи.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
						ПолеГруппировкиДетальныеЗаписи.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
					КонецЕсли;
					Если Колонка.Имя <> "ТипЗаписи" Тогда 
						ПолеПорядка = ДетальныеЗаписи.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
						ПолеПорядка.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
						ПолеПорядка.Использование = Истина;
						ПолеПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
					КонецЕсли;
				КонецЦикла;
				ВнешниеНаборыДанных.Вставить(ОбъектМетаданных, Набор);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиКомпоновкиДанных);
	
	Отчет.КомпоновщикНастроек = КомпоновщикНастроек;
		
	НастройкиКомпоновкиДанных = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Макет = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновкиДанных);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет, ВнешниеНаборыДанных);
	
	Чтение.Закрыть(); // освобождаем файл
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	УдалитьВременныеФайлыНаСервере();
КонецПроцедуры

&НаСервере
Процедура УдалитьВременныеФайлыНаСервере()
	УдалитьФайлы(Отчет.ПутьКФайлу);
КонецПроцедуры