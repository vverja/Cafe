
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	НесколькоВидовНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВидовНоменклатуры");
	Элементы.ДекорацияРасшифровкаОтбора.Видимость = Ложь;

	Если ЗначениеЗаполнено(Параметры.ТекущийВид) Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																				"Ссылка",
																				Параметры.ТекущийВид,
																				ВидСравненияКомпоновкиДанных.НеРавно,
																				,
																				Истина);
	ИначеЕсли Параметры.Свойство("Номенклатура") Тогда
		
		НастроитьСписок(Параметры.Номенклатура);	
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НастроитьСписок(Номенклатура = Неопределено, ОтключениеОтбора = Ложь)
	
	УсловияВыбораНовогоВидаНоменклатуры = Справочники.Номенклатура.УсловияВыбораНовогоВидаНоменклатуры();
	
	ТекстЗапроса = 	"
	|ВЫБРАТЬ
	|	НовыйВидНоменклатуры.Ссылка,
	|	НовыйВидНоменклатуры.ПометкаУдаления,
	|	НовыйВидНоменклатуры.Родитель,
	|	НовыйВидНоменклатуры.ЭтоГруппа,
	|	НовыйВидНоменклатуры.Наименование,
	|	НовыйВидНоменклатуры.ВариантОказанияУслуг,
	|	НовыйВидНоменклатуры.ВариантОформленияПродажи,
	|	НовыйВидНоменклатуры.ВариантПредставленияНабораВПечатныхФормах,
	|	НовыйВидНоменклатуры.ВариантРасчетаЦеныНабора,
	|	НовыйВидНоменклатуры.ВестиУчетПоГТД,
	|	НовыйВидНоменклатуры.ВестиУчетСертификатовНоменклатуры,
	|	НовыйВидНоменклатуры.ВладелецСерий,
	|	НовыйВидНоменклатуры.ВладелецТоварныхКатегорий,
	|	НовыйВидНоменклатуры.ВладелецХарактеристик,
	|	НовыйВидНоменклатуры.ГруппаАналитическогоУчета,
	|	НовыйВидНоменклатуры.ГруппаДоступа,
	|	НовыйВидНоменклатуры.ГруппаФинансовогоУчета,
	|	НовыйВидНоменклатуры.ЕдинаяНастройкаСерийДляПодразделений,
	|	НовыйВидНоменклатуры.ЕдинаяНастройкаСерийДляСкладов,
	|	НовыйВидНоменклатуры.ЕдиницаДляОтчетов,
	|	НовыйВидНоменклатуры.ЕдиницаИзмерения,
	|	НовыйВидНоменклатуры.ЗапретРедактированияНаименованияДляПечатиНоменклатуры,
	|	НовыйВидНоменклатуры.ЗапретРедактированияНаименованияДляПечатиХарактеристики,
	|	НовыйВидНоменклатуры.ЗапретРедактированияРабочегоНаименованияНоменклатуры,
	|	НовыйВидНоменклатуры.ЗапретРедактированияРабочегоНаименованияХарактеристики,
	|	НовыйВидНоменклатуры.ИспользованиеХарактеристик,
	|	НовыйВидНоменклатуры.ИспользоватьИндивидуальноеНаименованиеПриПечати,
	|	НовыйВидНоменклатуры.ИспользоватьКоличествоСерии,
	|	НовыйВидНоменклатуры.ИспользоватьНомерСерии,
	|	НовыйВидНоменклатуры.ИспользоватьСерии,
	|	НовыйВидНоменклатуры.ИспользоватьСрокГодностиСерии,
	|	НовыйВидНоменклатуры.ИспользоватьУпаковки,
	|	НовыйВидНоменклатуры.ИспользоватьХарактеристики,
	|	НовыйВидНоменклатуры.КонтролироватьДублиНоменклатуры,
	|	НовыйВидНоменклатуры.КонтролироватьДублиХарактеристик,
	|	НовыйВидНоменклатуры.КоэффициентЕдиницыДляОтчетов,
	|	НовыйВидНоменклатуры.НаборСвойств,
	|	НовыйВидНоменклатуры.НаборСвойствСерий,
	|	НовыйВидНоменклатуры.НаборСвойствХарактеристик,
	|	НовыйВидНоменклатуры.НаборУпаковок,
	|	НовыйВидНоменклатуры.НаименованиеДляПечати,
	|	НовыйВидНоменклатуры.НастройкаИспользованияСерий,
	|	НовыйВидНоменклатуры.НоменклатураМногооборотнаяТара,
	|	НовыйВидНоменклатуры.ОбособленнаяЗакупкаПродажа,
	|	НовыйВидНоменклатуры.НастройкиСерийБерутсяИзДругогоВидаНоменклатуры,
	|	НовыйВидНоменклатуры.Описание,
	|	НовыйВидНоменклатуры.ПодакцизныйТовар,
	|	НовыйВидНоменклатуры.ПоставляетсяВМногооборотнойТаре,
	|	НовыйВидНоменклатуры.СезоннаяГруппа,
	|	НовыйВидНоменклатуры.ПолитикаУчетаСерийДляПодразделений,
	|	НовыйВидНоменклатуры.ПолитикаУчетаСерийДляСкладов,
	|	НовыйВидНоменклатуры.СкладскаяГруппа,
	|	НовыйВидНоменклатуры.ТипНоменклатуры,
	|	НовыйВидНоменклатуры.ТочностьУказанияСрокаГодностиСерии,
	|	НовыйВидНоменклатуры.ХарактеристикаМногооборотнаяТара,
	|	НовыйВидНоменклатуры.ТоварныеКатегорииОбщиеСДругимВидомНоменклатуры,
	|	НовыйВидНоменклатуры.ШаблонНаименованияДляПечатиНоменклатуры,
	|	НовыйВидНоменклатуры.ШаблонНаименованияДляПечатиХарактеристики,
	|	НовыйВидНоменклатуры.ШаблонРабочегоНаименованияНоменклатуры,
	|	НовыйВидНоменклатуры.ШаблонРабочегоНаименованияСерии,
	|	НовыйВидНоменклатуры.ШаблонРабочегоНаименованияХарактеристики,
	|	НовыйВидНоменклатуры.ШаблонЦенника,
	|	НовыйВидНоменклатуры.ШаблонЭтикетки,
	|	НовыйВидНоменклатуры.ШаблонЭтикеткиСерии,
	|	НовыйВидНоменклатуры.Представление
	|ИЗ
	|	Справочник.ВидыНоменклатуры КАК НовыйВидНоменклатуры ";
	
	Если Не ЗначениеЗаполнено(Номенклатура)
		Или ОтключениеОтбора Тогда
		Список.ТекстЗапроса = ТекстЗапроса;
		Элементы.ДекорацияРасшифровкаОтбора.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|ГДЕ
	|	НовыйВидНоменклатуры.ЭтоГруппа
	|	ИЛИ ИСТИНА ";
	
	Для Каждого КлючЗначение Из УсловияВыбораНовогоВидаНоменклатуры Цикл
		
		ТекстЗапроса = ТекстЗапроса + " И НЕ " + КлючЗначение.Значение;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	ПараметрыЗапроса = Запрос.НайтиПараметры();

	ИменаРеквизитов = Новый Структура;
	Для Каждого Параметр из ПараметрыЗапроса Цикл
		ИменаРеквизитов.Вставить(Параметр.Имя, "ВидНоменклатуры." + Параметр.Имя);
	КонецЦикла;
	
	СтарыеРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, ИменаРеквизитов);
	
	Список.ТекстЗапроса = ТекстЗапроса;
	
	Для Каждого Параметр из ПараметрыЗапроса Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
																			Параметр.Имя,
																			СтарыеРеквизиты[Параметр.Имя],
																			Истина);
	КонецЦикла;
	
	Элементы.Список.Обновить();
	
	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Отобраны виды номенклатуры, выбор которых в номенклатуре, которая ранее уже использовалась в программе, не приведет к проблемам в учете.';uk='Відібрані види номенклатури, вибір яких в номенклатурі, яка раніше вже використовувалася у програмі, не призведе до проблем в обліку.'")));
	
	Если Пользователи.РолиДоступны("РедактированиеРеквизитовОбъектов") Тогда
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Отключить отбор';uk='Відключити відбір'"),
								,
								,
								,
								"ОтключитьОтбор"));
	КонецЕсли;
	Элементы.ДекорацияРасшифровкаОтбора.Видимость = Истина;
	Элементы.ДекорацияРасшифровкаОтбора.Заголовок = Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРасшифровкаОтбораОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылка = "ОтключитьОтбор" Тогда 
		НастроитьСписок(,Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

