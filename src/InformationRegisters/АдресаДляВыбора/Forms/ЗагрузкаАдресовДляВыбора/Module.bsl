
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") И ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		
		АвтоЗаголовок = Ложь;
		Заголовок = Параметры.Заголовок;
		
	КонецЕсли;
	
	ИнициализироватьТабличныйДокумент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	ЗакрытьФормуКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	
	ОчиститьСообщения();
	Состояние(НСтр("ru='Осуществляется первичная обработка введенных данных. Пожалуйста подождите...';uk='Здійснюється первинна обробка введених даних. Будь ласка, зачекайте...'"),,,БиблиотекаКартинок.Информация32);
	
	ЗагрузитьТабличныйДокумент();
	
	Элементы.Шаги.ТекущаяСтраница = Элементы.Шаг2;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьКОАТУУ(Команда)
	
	ОчиститьСообщения();
	Состояние(НСтр("ru='Осуществляется запись введенных данных. Пожалуйста подождите...';uk='Здійснюється запис введених даних. Будь ласка, зачекайте...'"),,,БиблиотекаКартинок.Информация32);
	
	ЗаписатьКодыИзТаблицы();
	
	ЗакрытьФормуКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПреобразоватьВДерево(Команда)
	
	ОчиститьСообщения();
	Состояние(НСтр("ru='Осуществляется преобразование в дерево. Пожалуйста подождите...';uk='Здійснюється перетворення в дерево. Будь ласка, зачекайте...'"),,,БиблиотекаКартинок.Информация32);
	
	Ошибка = ПреобразоватьВДеревоСервер();
	
	Если НЕ Ошибка Тогда
		Элементы.СпособыОтображения.ТекущаяСтраница  = Элементы.Шаг2ГруппаДерево;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура ЗакрытьФормуКлиент()
	Закрыть();
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьТабличныйДокумент()
	
	ТабличныйДокумент.Очистить();
	
	ОбластьЗаполнения = ТабличныйДокумент.Область(, 1, , 3);
	
	ОбластьЗаполнения.ЦветРамки = ЦветаСтиля.ЦветЛинииОтчета;
	ОбластьЗаполнения.ГраницаСверху = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
	ОбластьЗаполнения.ГраницаСнизу  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
	ОбластьЗаполнения.ГраницаСлева  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
	ОбластьЗаполнения.ГраницаСправа = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
	
	ОбластьШапки = ТабличныйДокумент.Область(1, 1, 1, 3);
	
	ОбластьШапки.Шрифт = WindowsШрифты.ШрифтДиалоговИМеню;
	ОбластьШапки.ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
	ОбластьШапки.ЦветФона = Новый Цвет(245, 242, 221);
	
	ОбластьКод = ТабличныйДокумент.Область(1, 1, 1, 1);
	ОбластьПризнак = ТабличныйДокумент.Область(1, 2, 1, 2);
	ОбластьНаименование = ТабличныйДокумент.Область(1, 3, 1, 3);
	
	ОбластьКод.Текст = НСтр("ru='Код КОАТУУ';uk='Код КОАТУУ'");
	ОбластьКод.ШиринаКолонки = 15;
	ОбластьПризнак.Текст = НСтр("ru='Признак';uk='Ознака'");
	ОбластьПризнак.ШиринаКолонки = 10;
	ОбластьНаименование.Текст = НСтр("ru='Наименование';uk='Найменування'");
	ОбластьНаименование.ШиринаКолонки = 70;
	
	ТабличныйДокумент.ФиксацияСверху = 1;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТабличныйДокумент()
	
	ТаблицаДляЗаписи = РеквизитФормыВЗначение("ТаблицаКОАТУУ");
	
	НомераКолонок = Новый Соответствие;
	НомерКолонки = 1;
	
	НомераКолонок.Вставить("КолонкаКОАТУУ", "C" + Формат(НомерКолонки, "ЧН=0; ЧГ=0"));
	НомерКолонки = НомерКолонки +1;
	НомераКолонок.Вставить("КолонкаПризнак", "C" + Формат(НомерКолонки, "ЧН=0; ЧГ=0"));
	НомерКолонки = НомерКолонки +1;
	НомераКолонок.Вставить("КолонкаНаименование", "C" + Формат(НомерКолонки, "ЧН=0; ЧГ=0"));
	
	НомерСтроки = 2;
	СтроковыйНомер = Формат(НомерСтроки, "ЧН=0; ЧГ=0");
	ЗаполненыКОАТУУНаименование = ЗначениеЗаполнено(ТабличныйДокумент.Область("R" + СтроковыйНомер + НомераКолонок.Получить("КолонкаКОАТУУ")).Текст)
			И ЗначениеЗаполнено(ТабличныйДокумент.Область("R" + СтроковыйНомер + НомераКолонок.Получить("КолонкаНаименование")).Текст);
	ЕстьОшибкиРаспознавания = Ложь;
	ТекстОшибки = "";
	
	ИдентификаторРодителяУровень1 = 0;
	ИдентификаторРодителяУровень2 = 0;
	ИдентификаторРодителяУровень3 = 0;
	
	ПредыдущийКодКОАТУУ = 0;
	
	Пока ЗаполненыКОАТУУНаименование Цикл
		
		Попытка
			КодКОАТУУ = ТабличныйДокумент.Область("R" + СтроковыйНомер + НомераКолонок.Получить("КолонкаКОАТУУ")).Текст;
			Наименование = ТабличныйДокумент.Область("R" + СтроковыйНомер + НомераКолонок.Получить("КолонкаНаименование")).Текст;
			Признак = ТабличныйДокумент.Область("R" + СтроковыйНомер + НомераКолонок.Получить("КолонкаПризнак")).Текст;
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
		Пропустить = Ложь;
		
		Пока СтрДлина(КодКОАТУУ) < 10 Цикл
			КодКОАТУУ = "0" + КодКОАТУУ;
		КонецЦикла;
		
		КодКОАТУУЧислом = Число(КодКОАТУУ);
		Если НЕ (ПредыдущийКодКОАТУУ < КодКОАТУУЧислом) Тогда
			ТекстОшибки = НСтр("ru='Таблица не отсортирована по кодам КОАТУУ!';uk='Таблиця не відсортована за кодами КОАТУУ!'");
			ЕстьОшибкиРаспознавания = Истина;
			Прервать;
		Иначе
			ПредыдущийКодКОАТУУ = КодКОАТУУЧислом;
		КонецЕсли;
		
		Код1Уровня = Сред(КодКОАТУУ, 1, 2);
		Признак2Уровня = Сред(КодКОАТУУ, 3, 1);
		Код2Уровня = Сред(КодКОАТУУ, 4, 2);
		Признак3Уровня = Сред(КодКОАТУУ, 6, 1);
		Код3Уровня = Сред(КодКОАТУУ, 7, 2);
		Код4Уровня = Сред(КодКОАТУУ, 9, 2);
		
		КодДо4Уровня = Код1Уровня + Признак2Уровня + Код2Уровня + Признак3Уровня + Код3Уровня;
		КодДо3Уровня = Код1Уровня + Признак2Уровня + Код2Уровня;
		
		Префикс = "";
		
		Если Признак = "C" ИЛИ Признак = "С" Тогда
			Префикс = "село ";
		ИначеЕсли Признак = "Щ" Тогда
			Префикс = "селище ";
		ИначеЕсли Признак = "М" Тогда
			Префикс = "місто ";
		ИначеЕсли Признак = "Т" Тогда
			Префикс = "смт. ";
		ИначеЕсли Признак = "Р" Тогда
			//район города
		КонецЕсли;
		
		//Признак 2-го уровня - 3-тий розряд кода
		// "0" - Это область, АРК, Киев или Севастополь
		// "1" - Это город областного значения
		// "2" - Это район области/АРК
		// "3" - Это район Киева или Севастополя
		Если Признак2Уровня = "0" Тогда
			//Это область, АРК, Киев или Севастополь
		ИначеЕсли Признак2Уровня = "1" ИЛИ Признак2Уровня = "2" Тогда
			Если Код2Уровня = "00" Тогда
				//Это заглавие городов областного значения, районов и т.д., записывать не нужно
				Пропустить = Истина;
			КонецЕсли;
		ИначеЕсли Признак2Уровня = "3" Тогда
			Если Код3Уровня = "00" Тогда
				//Это заглавие районов в городах, записывать не нужно
				Пропустить = Истина;
			КонецЕсли;
		Иначе
			//Ошибка
			ТекстОшибки = НСтр("ru='Некорректный признак 2-го уровня в строке ';uk='Некоректна ознака 2-го рівня в рядку '") + СтроковыйНомер;
			ЕстьОшибкиРаспознавания = Истина;
			Прервать;
			
		КонецЕсли;
		
		//Признак 3-го уровня - 6-той розряд кода
		// "0" - предыдущий уровень
		// "1" - города районного значения
		// "2" - не используется
		// "3" - райони в городах обл. значения
		// "4" - пгт, которые входят в состав горсовета
		// "5" - пгт, которые входят в состав райсовета
		// "6" - пгт, которые входят в состав райсовета в городе
		// "7" - города, которые входят в состав горсовета
		// "8" - сельсоветы, которые входят в состав райсовета
		// "9" - сельсоветы, села, которые входят в состав райсовета города, горсовета
		Если Признак3Уровня = "0" Тогда
			//предыдущий уровень
			Если Признак2Уровня = "1" Тогда
				//Это непостредственно город областного значения
				Префикс = "місто ";
			КонецЕсли;
		Иначе
			
			Если Код3Уровня = "00" И Код4Уровня = "00" Тогда
				Пропустить = Истина;
			КонецЕсли;
			
			Если  Признак3Уровня = "1" Тогда
				//города районного значения
			ИначеЕсли  Признак3Уровня = "2" Тогда
				//не используется
			ИначеЕсли  Признак3Уровня = "3" Тогда
				//райони в городах обл. значения, не загружаем
				Пропустить = Истина;
			ИначеЕсли  Признак3Уровня = "4" Тогда
				//пгт, которые входят в состав горсовета
			ИначеЕсли  Признак3Уровня = "5" Тогда
				//пгт, которые входят в состав райсовета
			ИначеЕсли  Признак3Уровня = "6" Тогда
				//пгт, которые входят в состав райсовета в городе
			ИначеЕсли  Признак3Уровня = "7" Тогда
				//города, которые входят в состав горсовета
			ИначеЕсли  Признак3Уровня = "8" ИЛИ Признак3Уровня = "9" Тогда
				//сельсоветы, села
				Если Код4Уровня = "00" Тогда
					//Это заглавие сельсоветов, записывать не нужно
					Пропустить = Истина;
				КонецЕсли;
			Иначе
				//Ошибка
				ТекстОшибки = НСтр("ru='Некорректный признак 3-го уровня в строке ';uk='Некоректна ознака 3-го рівня в рядку '") + СтроковыйНомер;
				ЕстьОшибкиРаспознавания = Истина;
				Прервать;
				
			КонецЕсли;
				
		КонецЕсли;
		
		Если НЕ Пропустить Тогда
			НомерВхождения = СтрНайти(Наименование, "ПІДПОРЯДКОВАНІ") + СтрНайти(Наименование, "Р-НУ");
			Если НомерВхождения <> 0 Тогда
				Пропустить = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если Пропустить Тогда
			
		Иначе
			
			НомерВхождения = СтрНайти(Наименование, "/");
			Если НомерВхождения <> 0 Тогда
				Наименование = Сред(Наименование, 1, НомерВхождения - 1);
			КонецЕсли;
			
			Если Признак2Уровня = "0" И СтрНачинаетсяС(Наименование, "М.") Тогда
				Префикс = "місто ";
				Наименование = Сред(Наименование, 3);
			КонецЕсли;
			
			Наименование = ТРег(Наименование);
			Наименование = СтрЗаменить(СтрЗаменить(Наименование, "Область", "область"), "Район", "район");
			Наименование = Префикс + Наименование;
			
			НоваяСтрока = ТаблицаДляЗаписи.Добавить();
			
			НоваяСтрока.Идентификатор = 990000000000 + КодКОАТУУЧислом;
			
			НоваяСтрока.КодСтраны = 804;
			НоваяСтрока.ПочтовыйИндекс = 0;
			НоваяСтрока.Наименование = Наименование;
			НоваяСтрока.КодКОАТУУ = КодКОАТУУЧислом;
			НоваяСтрока.ВведенВручную = Ложь;
			
			
			Если ИдентификаторРодителяУровень3 <> 0 И КодДо4Уровня = Сред(Формат(ИдентификаторРодителяУровень3, "ЧЦ=12; ЧВН=; ЧГ="), 3, 8) Тогда
				
				НоваяСтрока.ИдентификаторРодителя = ИдентификаторРодителяУровень3;
				//Это уровень для выбора из регистра
				НоваяСтрока.Уровень = 6;
				
			ИначеЕсли ИдентификаторРодителяУровень2 <> 0 И КодДо3Уровня = Сред(Формат(ИдентификаторРодителяУровень2, "ЧЦ=12; ЧВН=; ЧГ="), 3, 5) Тогда
				
				ИдентификаторРодителяУровень3 = 0;
				Если Код3Уровня <> "00" И Код4Уровня = "00" Тогда
					ИдентификаторРодителяУровень3 = НоваяСтрока.Идентификатор;
				КонецЕсли;
				
				НоваяСтрока.ИдентификаторРодителя = ИдентификаторРодителяУровень2;
				//Это уровень для выбора из регистра
				НоваяСтрока.Уровень = 4;
				
			ИначеЕсли ИдентификаторРодителяУровень1 <> 0 И Код1Уровня = Сред(Формат(ИдентификаторРодителяУровень1, "ЧЦ=12; ЧВН=; ЧГ="), 3, 2) Тогда
				
				ИдентификаторРодителяУровень3 = 0;
				ИдентификаторРодителяУровень2 = 0;
				Если (Код2Уровня <> "00" ИЛИ Код3Уровня <> "00") И Код4Уровня = "00" Тогда
					ИдентификаторРодителяУровень2 = НоваяСтрока.Идентификатор;
				КонецЕсли;
				
				НоваяСтрока.ИдентификаторРодителя = ИдентификаторРодителяУровень1;
				//Это уровень для выбора из регистра
				НоваяСтрока.Уровень = 3;
				
			Иначе
				//Это должна быть область, Киев или Севастополь
				
				Если Признак2Уровня = "0" Тогда
					//Это область, АРК, Киев или Севастополь
					ИдентификаторРодителяУровень1 = НоваяСтрока.Идентификатор;
					НоваяСтрока.ИдентификаторРодителя = 0;
					//Это уровень для выбора из регистра
					НоваяСтрока.Уровень = 1;
				Иначе
					//Ошибка
					ТекстОшибки = НСтр("ru='Некорректный признак 2-го уровня в строке ';uk='Некоректна ознака 2-го рівня в рядку '") + СтроковыйНомер;
					ЕстьОшибкиРаспознавания = Истина;
					Прервать;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
			
		НомерСтроки = НомерСтроки + 1;
		СтроковыйНомер = Формат(НомерСтроки, "ЧН=0; ЧГ=0");
		
		Попытка
			ЗаполненыКОАТУУНаименование = ЗначениеЗаполнено(ТабличныйДокумент.Область("R" + СтроковыйНомер + НомераКолонок.Получить("КолонкаКОАТУУ")).Текст)
					И ЗначениеЗаполнено(ТабличныйДокумент.Область("R" + СтроковыйНомер + НомераКолонок.Получить("КолонкаНаименование")).Текст);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;
	
	Если ЕстьОшибкиРаспознавания Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	Иначе
		ЗначениеВРеквизитФормы(ТаблицаДляЗаписи, "ТаблицаКОАТУУ");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПреобразоватьВДеревоСервер()
	
	ДеревоКодов = РеквизитФормыВЗначение("ДеревоКОАТУУ");
	ТаблицаДляЗаписи = РеквизитФормыВЗначение("ТаблицаКОАТУУ");
	
	ОшибкаПреобразования = Ложь;
	
	Для каждого Строка Из ТаблицаДляЗаписи Цикл
		
		Если Строка.Уровень = 1 Тогда
			НоваяСтрокаУровня1 = ДеревоКодов.Строки.Добавить();
			НоваяСтрокаУровня1.Идентификатор = Строка.Идентификатор;
			НоваяСтрокаУровня1.КодКОАТУУ     = Строка.КодКОАТУУ;
			НоваяСтрокаУровня1.Уровень       = Строка.Уровень;
			НоваяСтрокаУровня1.Наименование  = Строка.Наименование;
		ИначеЕсли Строка.Уровень = 3 Тогда
			НоваяСтрокаУровня2 = НоваяСтрокаУровня1.Строки.Добавить();
			НоваяСтрокаУровня2.Идентификатор = Строка.Идентификатор;
			НоваяСтрокаУровня2.КодКОАТУУ     = Строка.КодКОАТУУ;
			НоваяСтрокаУровня2.Уровень       = Строка.Уровень;
			НоваяСтрокаУровня2.Наименование  = Строка.Наименование;
		ИначеЕсли Строка.Уровень = 4 Тогда
			НоваяСтрокаУровня3 = НоваяСтрокаУровня2.Строки.Добавить();
			НоваяСтрокаУровня3.Идентификатор = Строка.Идентификатор;
			НоваяСтрокаУровня3.КодКОАТУУ     = Строка.КодКОАТУУ;
			НоваяСтрокаУровня3.Уровень       = Строка.Уровень;
			НоваяСтрокаУровня3.Наименование  = Строка.Наименование;
		ИначеЕсли Строка.Уровень = 6 Тогда
			НоваяСтрокаУровня4 = НоваяСтрокаУровня3.Строки.Добавить();
			НоваяСтрокаУровня4.Идентификатор = Строка.Идентификатор;
			НоваяСтрокаУровня4.КодКОАТУУ     = Строка.КодКОАТУУ;
			НоваяСтрокаУровня4.Уровень       = Строка.Уровень;
			НоваяСтрокаУровня4.Наименование  = Строка.Наименование;
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ошибка заполнения уровней!!!");
			ОшибкаПреобразования = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ОшибкаПреобразования Тогда
		ЗначениеВРеквизитФормы(ДеревоКодов, "ДеревоКОАТУУ");
	КонецЕсли;
	
	Возврат ОшибкаПреобразования;
	
КонецФункции

&НаСервере
Процедура ЗаписатьКодыИзТаблицы()
	
	ТаблицаДляЗаписи = РеквизитФормыВЗначение("ТаблицаКОАТУУ");
	
	НаборЗаписей = РегистрыСведений.АдресаДляВыбора.СоздатьНаборЗаписей();
	
	НаборЗаписей.Загрузить(ТаблицаДляЗаписи);
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти
