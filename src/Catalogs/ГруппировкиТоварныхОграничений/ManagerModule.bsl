#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ИмяНовойГруппировкиПоУмолчанию(КлючНастройки) Экспорт
	
	ТаблицаГруппировок = Новый ТаблицаЗначений();
	ТаблицаГруппировок.Колонки.Добавить("Номенклатура",
		Новый ОписаниеТипов("СправочникСсылка.ТоварныеКатегории, СправочникСсылка.Номенклатура"));
	ТаблицаГруппировок.Колонки.Добавить("Склад",
		Новый ОписаниеТипов("СправочникСсылка.Склады, СправочникСсылка.ФорматыМагазинов"));
	ТаблицаГруппировок.Колонки.Добавить("Порядок", Новый ОписаниеТипов("Число"));
	
	Если ЗначениеЗаполнено(КлючНастройки.Склад)
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьТоварныеКатегории")
		И ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов") Тогда
		
		НоваяСтрока = ТаблицаГруппировок.Добавить();
		НоваяСтрока.Номенклатура  = Неопределено;
		НоваяСтрока.Склад         = КлючНастройки.Склад;
		НоваяСтрока.Порядок = 0;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КлючНастройки.Формат) И Не ПолучитьФункциональнуюОпцию("ИспользоватьТоварныеКатегории") Тогда
		
		НоваяСтрока = ТаблицаГруппировок.Добавить();
		НоваяСтрока.Номенклатура  = Неопределено;
		НоваяСтрока.Склад         = КлючНастройки.Формат;
		НоваяСтрока.Порядок = 1;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КлючНастройки.Категория) И Не ПолучитьФункциональнуюОпцию("ИспользоватьФорматыМагазинов") Тогда
		
		НоваяСтрока = ТаблицаГруппировок.Добавить();
		НоваяСтрока.Номенклатура  = КлючНастройки.Категория;
		НоваяСтрока.Склад         = Неопределено;
		НоваяСтрока.Порядок = 2;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КлючНастройки.Категория) И ЗначениеЗаполнено(КлючНастройки.Формат) Тогда
		
		НоваяСтрока = ТаблицаГруппировок.Добавить();
		НоваяСтрока.Номенклатура  = КлючНастройки.Категория;
		НоваяСтрока.Склад         = КлючНастройки.Формат;
		НоваяСтрока.Порядок = 3;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КлючНастройки.Категория) И ЗначениеЗаполнено(КлючНастройки.Склад) Тогда
		
		НоваяСтрока = ТаблицаГруппировок.Добавить();
		НоваяСтрока.Номенклатура  = КлючНастройки.Категория;
		НоваяСтрока.Склад         = КлючНастройки.Склад;
		НоваяСтрока.Порядок = 4;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КлючНастройки.Номенклатура)
		И (Не ПолучитьФункциональнуюОпцию("ИспользоватьФорматыМагазинов")
			Или ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КлючНастройки.Номенклатура, "ТипНоменклатуры")
				= Перечисления.ТипыНоменклатуры.Работа) Тогда
		
		НоваяСтрока = ТаблицаГруппировок.Добавить();
		НоваяСтрока.Номенклатура  = КлючНастройки.Номенклатура;
		НоваяСтрока.Склад         = Неопределено;
		НоваяСтрока.Порядок = 5;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КлючНастройки.Номенклатура) И ЗначениеЗаполнено(КлючНастройки.Формат) Тогда
		
		НоваяСтрока = ТаблицаГруппировок.Добавить();
		НоваяСтрока.Номенклатура  = КлючНастройки.Номенклатура;
		НоваяСтрока.Склад         = КлючНастройки.Формат;
		НоваяСтрока.Порядок = 6;
		
	КонецЕсли;
	
	ТаблицаГруппировок.Сортировать("Порядок");
	Номенклатура = Неопределено;
	Склад        = Неопределено;
	Если ТаблицаГруппировок.Количество() > 0 Тогда
		
		СтрокаТаблицы = ТаблицаГруппировок[ТаблицаГруппировок.Количество() - 1];
		Номенклатура = СтрокаТаблицы.Номенклатура;
		Склад        = СтрокаТаблицы.Склад;
		ШаблонИмени = ПредставлениеГруппировки(Номенклатура, Склад);
		
	Иначе
		ШаблонИмени = НСтр("ru='Новый';uk='Новий'");
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ШаблонИмени", ШаблонИмени + "%");
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СпрГруппыНастроек.Наименование КАК Наименование
		|ИЗ
		|	Справочник.ГруппировкиТоварныхОграничений КАК СпрГруппыНастроек
		|ГДЕ
		|	СпрГруппыНастроек.Наименование ПОДОБНО &ШаблонИмени";
	
	Номер = -1;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НомерВБазе = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтрЗаменить(Выборка.Наименование, ШаблонИмени, ""));
		Номер = Макс(НомерВБазе, Номер);
		
	КонецЦикла;
	СтруктураРезультата = Новый Структура("Номенклатура, Склад, Наименование", Номенклатура, Склад,
		?(Номер = -1, ШаблонИмени, ШаблонИмени + " " + Строка(Номер + 1)));
	Возврат СтруктураРезультата;
	
КонецФункции

Функция ДанныеВыбора(КлючНастройки, Все) Экспорт
	
	ТаблицаГруппировок = Новый ТаблицаЗначений();
	ТаблицаГруппировок.Колонки.Добавить("Номенклатура",
		Новый ОписаниеТипов("СправочникСсылка.ТоварныеКатегории, СправочникСсылка.Номенклатура"));
	ТаблицаГруппировок.Колонки.Добавить("Склад",
		Новый ОписаниеТипов("СправочникСсылка.Склады, СправочникСсылка.ФорматыМагазинов"));
	ТаблицаГруппировок.Колонки.Добавить("Порядок", Новый ОписаниеТипов("Число"));
	
	Если ЗначениеЗаполнено(КлючНастройки.Склад) И Не ПолучитьФункциональнуюОпцию("ИспользоватьТоварныеКатегории") Тогда
		
		НоваяСтрока = ТаблицаГруппировок.Добавить();
		НоваяСтрока.Номенклатура  = Неопределено;
		НоваяСтрока.Склад         = КлючНастройки.Склад;
		НоваяСтрока.Порядок = -1;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КлючНастройки.Формат) И Не ПолучитьФункциональнуюОпцию("ИспользоватьТоварныеКатегории") Тогда
		
		НоваяСтрока = ТаблицаГруппировок.Добавить();
		НоваяСтрока.Номенклатура  = Неопределено;
		НоваяСтрока.Склад         = КлючНастройки.Формат;
		НоваяСтрока.Порядок = 0;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КлючНастройки.Категория) И Не ПолучитьФункциональнуюОпцию("ИспользоватьФорматыМагазинов") Тогда
		
		НоваяСтрока = ТаблицаГруппировок.Добавить();
		НоваяСтрока.Номенклатура  = КлючНастройки.Категория;
		НоваяСтрока.Склад         = Неопределено;
		НоваяСтрока.Порядок = 1;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КлючНастройки.Категория) И ЗначениеЗаполнено(КлючНастройки.Формат) Тогда
		
		НоваяСтрока = ТаблицаГруппировок.Добавить();
		НоваяСтрока.Номенклатура  = КлючНастройки.Категория;
		НоваяСтрока.Склад         = КлючНастройки.Формат;
		НоваяСтрока.Порядок = 2;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КлючНастройки.Категория) И ЗначениеЗаполнено(КлючНастройки.Склад) Тогда
		
		НоваяСтрока = ТаблицаГруппировок.Добавить();
		НоваяСтрока.Номенклатура  = КлючНастройки.Категория;
		НоваяСтрока.Склад         = КлючНастройки.Склад;
		НоваяСтрока.Порядок = 3;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КлючНастройки.Номенклатура) И Не ПолучитьФункциональнуюОпцию("ИспользоватьФорматыМагазинов") Или
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КлючНастройки.Номенклатура, "ТипНоменклатуры") = Перечисления.ТипыНоменклатуры.Работа Тогда
		
		НоваяСтрока = ТаблицаГруппировок.Добавить();
		НоваяСтрока.Номенклатура  = КлючНастройки.Номенклатура;
		НоваяСтрока.Склад         = Неопределено;
		НоваяСтрока.Порядок = 4;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КлючНастройки.Номенклатура) И ЗначениеЗаполнено(КлючНастройки.Формат) Тогда
		
		НоваяСтрока = ТаблицаГруппировок.Добавить();
		НоваяСтрока.Номенклатура  = КлючНастройки.Номенклатура;
		НоваяСтрока.Склад         = КлючНастройки.Формат;
		НоваяСтрока.Порядок = 5;
		
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.Номенклатура  КАК Номенклатура,
		|	Таблица.Склад         КАК Склад,
		|	Таблица.Порядок       КАК Порядок
		|ПОМЕСТИТЬ ВтТаблица
		|ИЗ
		|	&Таблица КАК Таблица
		|;
		|
		|//////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таблица.Порядок КАК Порядок,
		|	МАКСИМУМ(СпрГруппыНастроек.Ссылка) КАК Группировка,
		|	МАКСИМУМ(Таблица.Номенклатура)     КАК Номенклатура,
		|	МАКСИМУМ(Таблица.Склад)            КАК Склад,
		|	СУММА(ВЫБОР КОГДА ТаблицаНастроек.Группировка ЕСТЬ NULL ТОГДА 0 ИНАЧЕ 1 КОНЕЦ)         КАК Количество
		|ИЗ
		|	ВтТаблица КАК Таблица
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппировкиТоварныхОграничений КАК СпрГруппыНастроек
		|		ПО СпрГруппыНастроек.Номенклатура = Таблица.Номенклатура
		|		 И СпрГруппыНастроек.Склад        = Таблица.Склад
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТоварныеОграничения КАК ТаблицаНастроек
		|		ПО ТаблицаНастроек.Группировка = СпрГруппыНастроек.Ссылка
		|		 И НЕ ТаблицаНастроек.ЭтоГруппа
		|		
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Порядок
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	100                      КАК Порядок,
		|	СпрГруппыНастроек.Ссылка КАК Группировка,
		|	НЕОПРЕДЕЛЕНО             КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО             КАК Склад,
		|	СУММА(ВЫБОР КОГДА ТаблицаНастроек.Группировка ЕСТЬ NULL ТОГДА 0 ИНАЧЕ 1 КОНЕЦ)         КАК Количество
		|ИЗ
		|	Справочник.ГруппировкиТоварныхОграничений КАК СпрГруппыНастроек
		|	
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТоварныеОграничения КАК ТаблицаНастроек
		|		ПО ТаблицаНастроек.Группировка = СпрГруппыНастроек.Ссылка
		|		 И НЕ ТаблицаНастроек.ЭтоГруппа
		|ГДЕ
		|	СпрГруппыНастроек.Номенклатура = НЕОПРЕДЕЛЕНО
		|	И СпрГруппыНастроек.Склад = НЕОПРЕДЕЛЕНО
		|	И &Все
		|	
		|СГРУППИРОВАТЬ ПО
		|	СпрГруппыНастроек.Ссылка
		|	
		|УПОРЯДОЧИТЬ ПО
		|	Порядок, Количество УБЫВ";
	
	Запрос.УстановитьПараметр("Все", Все);
	Запрос.УстановитьПараметр("Таблица", ТаблицаГруппировок);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Таблица = РезультатЗапроса[1].Выгрузить();
	
	Если ЗначениеЗаполнено(КлючНастройки.Номенклатура) И ЗначениеЗаполнено(КлючНастройки.Склад) Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	СпрНоменклатура.Ссылка КАК Группировка,
			|	СпрНоменклатура.Ссылка КАК Номенклатура,
			|	МАКСИМУМ(&Склад) КАК Склад,
			|	СУММА(ВЫБОР КОГДА ТаблицаНастроек.Группировка ЕСТЬ NULL ТОГДА 0 ИНАЧЕ 1 КОНЕЦ)         КАК Количество
			|	
			|ИЗ
			|	Справочник.Номенклатура КАК СпрНоменклатура
			|	
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТоварныеОграничения КАК ТаблицаНастроек
			|		ПО ТаблицаНастроек.Номенклатура = &Номенклатура
			|		И ТаблицаНастроек.Склад = &Склад
			|		И ТаблицаНастроек.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
			|ГДЕ
			|	СпрНоменклатура.Ссылка = &Номенклатура
			|	И СпрНоменклатура.ИспользованиеХарактеристик <> ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать)
			|	
			|СГРУППИРОВАТЬ ПО
			|	СпрНоменклатура.Ссылка";
		
		Запрос.УстановитьПараметр("Номенклатура", КлючНастройки.Номенклатура);
		Запрос.УстановитьПараметр("Склад",        КлючНастройки.Склад);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(Таблица.Добавить(), Выборка);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Таблица;
	
КонецФункции

Функция УдалитьНеиспользуемыеГруппировки(МассивСсылок) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ГруппировкиТоварныхОграничений КАК Таблица
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТоварныеОграничения КАК ТаблицаТоварныеОграничения
		|		ПО ТаблицаТоварныеОграничения.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|		И ТаблицаТоварныеОграничения.Группировка = Таблица.Ссылка
		|ГДЕ
		|	Таблица.Ссылка В(&МассивСсылок)
		|	И ТаблицаТоварныеОграничения.Группировка ЕСТЬ NULL";
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	Таблица = Запрос.Выполнить().Выгрузить();
	СсылкиНаУдаление = Таблица.ВыгрузитьКолонку("Ссылка");
	
	Для Каждого Ссылка Из СсылкиНаУдаление Цикл
		
		Набор = РегистрыСведений.ТоварныеОграничения.СоздатьНаборЗаписей();
		Набор.Отбор.Группировка.Установить(Ссылка);
		Набор.Отбор.Номенклатура.Установить(Справочники.Номенклатура.ПустаяСсылка());
		Набор.Записать();
		
	КонецЦикла;
	УстановитьПривилегированныйРежим(Истина);
	УдалитьОбъекты(СсылкиНаУдаление, Ложь);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеГруппировки(Номенклатура, Склад)
	
	Если Не ЗначениеЗаполнено(Номенклатура) И ТипЗнч(Склад) = Тип("СправочникСсылка.ФорматыМагазинов") Тогда
		
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Любые товары на складах формата ""%1""';uk='Будь-які товари на складах формату ""%1""'"), Склад);
		
	ИначеЕсли Не ЗначениеЗаполнено(Номенклатура) И ТипЗнч(Склад) = Тип("СправочникСсылка.Склады") Тогда
		
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Любые товары на складе ""%1""';uk='Будь-які товари на складі ""%1""'"), Склад);
		
	ИначеЕсли ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Номенклатура")
		И ТипЗнч(Склад) = Тип("СправочникСсылка.ФорматыМагазинов") Тогда
		
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='""%1"" на складах формата ""%2""';uk='""%1"" на складах формату ""%2""'"), Номенклатура, Склад);
		
	ИначеЕсли ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Номенклатура")
		И Не ЗначениеЗаполнено(Склад) Тогда
		
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='""%1"" на любом складе';uk='""%1"" на будь-якому складі'"), Номенклатура);
		
	ИначеЕсли ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Номенклатура")
		И ТипЗнч(Склад) = Тип("СправочникСсылка.ФорматыМагазинов") Тогда
		
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='""%1"" на складах формата ""%2""';uk='""%1"" на складах формату ""%2""'"), Номенклатура, Склад);
		
	ИначеЕсли ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Номенклатура")
		И ТипЗнч(Склад) = Тип("СправочникСсылка.Склады") Тогда
		
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='""%1"" с любой характеристикой на складе ""%2""';uk='""%1"" з будь-якою характеристикою на складі ""%2""'"), Номенклатура, Склад);
		
	ИначеЕсли ТипЗнч(Номенклатура) = Тип("СправочникСсылка.ТоварныеКатегории")
		И Не ЗначениеЗаполнено(Склад) Тогда
		
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Товары категории ""%1"" на любом складе';uk='Товари категорії ""%1"" на будь-якому складі'"), Номенклатура);
		
	ИначеЕсли ТипЗнч(Номенклатура) = Тип("СправочникСсылка.ТоварныеКатегории")
		И ТипЗнч(Склад) = Тип("СправочникСсылка.ФорматыМагазинов") Тогда
		
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Товары категории ""%1"" на складах формата ""%2""';uk='Товари категорії ""%1"" на складах формату ""%2""'"), Номенклатура, Склад);
		
	ИначеЕсли ТипЗнч(Номенклатура) = Тип("СправочникСсылка.ТоварныеКатегории")
		И ТипЗнч(Склад) = Тип("СправочникСсылка.Склады") Тогда
		
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Товары категории ""%1"" на складе ""%2""';uk='Товари категорії ""%1"" на складі ""%2""'"), Номенклатура, Склад);
		
	Иначе
		
		Представление = НСтр("ru='Новый';uk='Новий'");
		
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов") Тогда
		Представление = СтрЗаменить(Представление, " на любом складе", "");
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

#КонецОбласти

#КонецЕсли