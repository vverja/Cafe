#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Период                        КАК Период,
	|	Таблица.Регистратор                   КАК Регистратор,
	|	Таблица.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Таблица.Организация                   КАК Организация,
	|	Таблица.Подразделение                 КАК Подразделение,
	|	Таблица.Лимит                         КАК Лимит,
	|	Таблица.РасходВПределахЛимита         КАК РасходВПределахЛимита
	|ПОМЕСТИТЬ ДвиженияЛимитыРасходаДенежныхСредствПередЗаписью
	|ИЗ
	|	РегистрНакопления.ЛимитыРасходаДенежныхСредств КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	|";
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Период                        КАК Период,
	|	ТаблицаИзменений.Регистратор                   КАК Регистратор,
	|	ТаблицаИзменений.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ТаблицаИзменений.Организация                   КАК Организация,
	|	ТаблицаИзменений.Подразделение                 КАК Подразделение,
	|	СУММА(ТаблицаИзменений.Лимит)                  КАК ЛимитИзменение,
	|	СУММА(ТаблицаИзменений.РасходВПределахЛимита)  КАК РасходВПределахЛимитаИзменение
	|ПОМЕСТИТЬ ДвиженияЛимитыРасходаДенежныхСредствИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Период                        КАК Период,
	|		Таблица.Регистратор                   КАК Регистратор,
	|		Таблица.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|		Таблица.Организация                   КАК Организация,
	|		Таблица.Подразделение                 КАК Подразделение,
	|		Таблица.Лимит                         КАК Лимит,
	|		Таблица.РасходВПределахЛимита         КАК РасходВПределахЛимита
	|	ИЗ
	|		ДвиженияЛимитыРасходаДенежныхСредствПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период,
	|		Таблица.Регистратор,
	|		Таблица.СтатьяДвиженияДенежныхСредств,
	|		Таблица.Организация,
	|		Таблица.Подразделение,
	|		- Таблица.Лимит,
	|		- Таблица.РасходВПределахЛимита
	|	ИЗ
	|		РегистрНакопления.ЛимитыРасходаДенежныхСредств КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Период,
	|	ТаблицаИзменений.Регистратор,
	|	ТаблицаИзменений.СтатьяДвиженияДенежныхСредств,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Подразделение
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.Лимит) > 0
	|	ИЛИ СУММА(ТаблицаИзменений.РасходВПределахЛимита) <> 0
	|;
	|
	|//////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияЛимитыРасходаДенежныхСредствПередЗаписью
	|";
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	// Новые изменения были помещены во временную таблицу.
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияЛимитыРасходаДенежныхСредствИзменение", Выборка.Количество > 0);

КонецПроцедуры

#КонецОбласти

#КонецЕсли