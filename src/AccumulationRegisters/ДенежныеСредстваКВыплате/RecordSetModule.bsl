#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	БлокироватьДляИзменения = Истина;

	//Для регистра контроль выполняется на наличие положительных остатков.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств,
	|	Таблица.СтатьяДвиженияДенежныхСредств       КАК СтатьяДвиженияДенежныхСредств,
	|	Таблица.АналитикаУчетаПоПартнерам           КАК АналитикаУчетаПоПартнерам,
	|	Таблица.ПодотчетноеЛицо                     КАК ПодотчетноеЛицо,
	|	Таблица.СтатьяРасходов                      КАК СтатьяРасходов,
	|	Таблица.АналитикаРасходов                   КАК АналитикаРасходов,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			-Таблица.Сумма
	|		ИНАЧЕ
	|			Таблица.Сумма
	|	КОНЕЦ                                       КАК СуммаПередЗаписью
	|ПОМЕСТИТЬ ДвиженияДенежныеСредстваКВыплатеПередЗаписью
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваКВыплате КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	|";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу "ДвиженияДенежныеСредстваКВыплатеИзменение".

	Запрос = Новый Запрос;
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств,
	|	ТаблицаИзменений.СтатьяДвиженияДенежныхСредств       КАК СтатьяДвиженияДенежныхСредств,
	|	ТаблицаИзменений.АналитикаУчетаПоПартнерам           КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаИзменений.ПодотчетноеЛицо                     КАК ПодотчетноеЛицо,
	|	ТаблицаИзменений.СтатьяРасходов                      КАК СтатьяРасходов,
	|	ТаблицаИзменений.АналитикаРасходов                   КАК АналитикаРасходов,
	|	СУММА(ТаблицаИзменений.СуммаИзменение)               КАК СуммаИзменение
	|
	|ПОМЕСТИТЬ ДвиженияДенежныеСредстваКВыплатеИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств,
	|		Таблица.СтатьяДвиженияДенежныхСредств       КАК СтатьяДвиженияДенежныхСредств,
	|		Таблица.АналитикаУчетаПоПартнерам           КАК АналитикаУчетаПоПартнерам,
	|		Таблица.ПодотчетноеЛицо                     КАК ПодотчетноеЛицо,
	|		Таблица.СтатьяРасходов                      КАК СтатьяРасходов,
	|		Таблица.АналитикаРасходов                   КАК АналитикаРасходов,
	|		Таблица.СуммаПередЗаписью                   КАК СуммаИзменение
	|	ИЗ
	|		ДвиженияДенежныеСредстваКВыплатеПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств,
	|		Таблица.СтатьяДвиженияДенежныхСредств       КАК СтатьяДвиженияДенежныхСредств,
	|		Таблица.АналитикаУчетаПоПартнерам           КАК АналитикаУчетаПоПартнерам,
	|		Таблица.ПодотчетноеЛицо                     КАК ПодотчетноеЛицо,
	|		Таблица.СтатьяРасходов                      КАК СтатьяРасходов,
	|		Таблица.АналитикаРасходов                   КАК АналитикаРасходов,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				Таблица.Сумма
	|			ИНАЧЕ
	|				-Таблица.Сумма
	|		КОНЕЦ                                       КАК СуммаИзменение
	|	ИЗ
	|		РегистрНакопления.ДенежныеСредстваКВыплате КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.ЗаявкаНаРасходованиеДенежныхСредств,
	|	ТаблицаИзменений.СтатьяДвиженияДенежныхСредств,
	|	ТаблицаИзменений.АналитикаУчетаПоПартнерам,
	|	ТаблицаИзменений.ПодотчетноеЛицо,
	|	ТаблицаИзменений.СтатьяРасходов,
	|	ТаблицаИзменений.АналитикаРасходов
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.СуммаИзменение) > 0
	|;
	|УНИЧТОЖИТЬ ДвиженияДенежныеСредстваКВыплатеПередЗаписью
	|";
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();

	СтруктураВременныеТаблицы.Вставить("ДвиженияДенежныеСредстваКВыплатеИзменение", Выборка.Количество > 0);

КонецПроцедуры

#КонецОбласти

#КонецЕсли