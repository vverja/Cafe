#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Процедура ИсправитьДвижения_ДанныеДляОбновления(Параметры) Экспорт
	
	ДопПараметрыОтметки = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДопПараметрыОтметки.ЭтоДвижения = Истина;
	ДопПараметрыОтметки.ПолноеИмяРегистра = "РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц";
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
    // Движения по регистру с незаполненным ресурсом "К отчету"
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДанныеРегистра
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДанныеРегистра2
	|	ПО
	|		ДанныеРегистра2.Регистратор = ДанныеРегистра.Регистратор
	|		И ДанныеРегистра2.КОтчету = ДанныеРегистра.Сумма
	|	
	|ГДЕ
	|	ДанныеРегистра.КОтчету = 0
	|	И ДанныеРегистра2.Регистратор ЕСТЬ NULL
	|	И ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) В (
	|		ТИП(Документ.АвансовыйОтчет),
	|		ТИП(Документ.ВводОстатков),
	|		ТИП(Документ.ПоступлениеБезналичныхДенежныхСредств),
	|		ТИП(Документ.ПриходныйКассовыйОрдер),
	|		ТИП(Документ.РасходныйКассовыйОрдер),
	|		ТИП(Документ.СписаниеБезналичныхДенежныхСредств)
	|	)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДанныеРегистра
	|	
	|ГДЕ
	|	ДанныеРегистра.ХозяйственнаяОперация = &ПустаяХО
	|	И ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) В (
	|		ТИП(Документ.АвансовыйОтчет)
	|	)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДанныеРегистра
	|	
	|ГДЕ
	|	ДанныеРегистра.СтатьяДвиженияДенежныхСредств = &ПустаяСтатьяДДС
	|	И &КонтролироватьВыдачуПодОтчетВРазрезеЦелей
	|	И ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) В (
	|		ТИП(Документ.АвансовыйОтчет)
	|	)
	|";
    
	Запрос.УстановитьПараметр("ПустаяХО", Перечисления.ХозяйственныеОперации.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяСтатьяДДС", Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка());
	Запрос.УстановитьПараметр("КонтролироватьВыдачуПодОтчетВРазрезеЦелей", ПолучитьФункциональнуюОпцию("КонтролироватьВыдачуПодОтчетВРазрезеЦелей"));
	
	ДанныеКОбработке = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДанныеКОбработке, ДопПараметрыОтметки);
	
	// Поступления от подотчетника должны быть проведены по регистру
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Регистратор
	|	
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДанныеРегистра
	|	ПО
	|		ДанныеРегистра.Регистратор = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
	|	И ДанныеДокумента.Проведен
	|	И ДанныеРегистра.Регистратор ЕСТЬ NULL
	|	
	|";
	
	ДанныеКОбработке = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДанныеКОбработке, ДопПараметрыОтметки);
	
КонецПроцедуры

Процедура ИсправитьДвижения(Параметры) Экспорт
	
	Регистраторы = Новый Массив;
	Регистраторы.Добавить("Документ.АвансовыйОтчет");
	Регистраторы.Добавить("Документ.ВводОстатков");
	Регистраторы.Добавить("Документ.ПоступлениеБезналичныхДенежныхСредств");
	Регистраторы.Добавить("Документ.ПоступлениеТоваровУслуг");
	Регистраторы.Добавить("Документ.ПриходныйКассовыйОрдер");
	Регистраторы.Добавить("Документ.РасходныйКассовыйОрдер");
	Регистраторы.Добавить("Документ.СписаниеБезналичныхДенежныхСредств");
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
		Регистраторы,
		"РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц",
		Параметры.Очередь
    );
	
КонецПроцедуры



#КонецОбласти


#КонецОбласти

#КонецЕсли