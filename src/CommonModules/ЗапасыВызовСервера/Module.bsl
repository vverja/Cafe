
// Вспомогательная функция интерактивного пакетного создания номенклатуры ГТД 
//
Функция ЗаполнитьНоменклатуруГТДИнтерактивно(Знач ДанныеФормыКоллекцияТовары, Знач МассивСтрок, Знач ИмяКолонкиНоменклатура, КодУКТВЭД, НомерГТД, СоздаватьЭлементы, Комментарий, Отказ, ТекстОшибки) Экспорт
	
	Если МассивСтрок = Неопределено Тогда
		МассивРеальныхСтрок = Неопределено;
	Иначе
		МассивРеальныхСтрок = Новый Массив;
		Для Каждого ИдентификаторСтроки Из МассивСтрок Цикл
			МассивРеальныхСтрок.Добавить(ДанныеФормыКоллекцияТовары.НайтиПоИдентификатору(ИдентификаторСтроки));
		КонецЦикла;
	КонецЕсли;

	ИсходныеДанные = ДанныеФормыКоллекцияТовары.Выгрузить(МассивРеальныхСтрок, ИмяКолонкиНоменклатура + ", НомерГТД");
	// При интерактивном заполнении сохранность ссылки не принципиальна. Однако для работы процедуры ЗаполнитьНоменклатуруГТД(...) необходима.
	ИсходныеДанные.Колонки[ИмяКолонкиНоменклатура].Имя = "Номенклатура";
	ИсходныеДанные.Колонки.НомерГТД.Имя = "СтараяНоменклатураГТД";
	ИсходныеДанные.Свернуть("Номенклатура, СтараяНоменклатураГТД");
	ИсходныеДанные.Колонки.Добавить("КодУКТВЭД", Новый ОписаниеТипов("СправочникСсылка.КлассификаторУКТВЭД"));
	ИсходныеДанные.Колонки.Добавить("НомерГТД", Новый ОписаниеТипов("СправочникСсылка.НомераГТД"));
	
	ИсходныеДанные.ЗаполнитьЗначения(КодУКТВЭД, "КодУКТВЭД");
	ИсходныеДанные.ЗаполнитьЗначения(НомерГТД,  "НомерГТД");
	
	// Создаем требуемую номенклатуру ГТД
	// Права проверены при открытии формы ОбщаяФорма.ВводНоменклатурыГТД
	Если СоздаватьЭлементы Тогда		
		НачатьТранзакцию();
	КонецЕсли;
	
	ЗапасыСервер.ЗаполнитьНоменклатуруГТД(ИсходныеДанные, СоздаватьЭлементы, Ложь, Комментарий, Отказ, ТекстОшибки);
	
	Если СоздаватьЭлементы Тогда
		Если Отказ Тогда
			ОтменитьТранзакцию();
		Иначе
			ЗафиксироватьТранзакцию();
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СоответствиеНоменклатурыИНоменклатурыГТД = Новый Соответствие;
	Для Каждого СтрокаИсходныхДанных Из ИсходныеДанные Цикл
		СоответствиеНоменклатурыИНоменклатурыГТД[СтрокаИсходныхДанных.Номенклатура] = СтрокаИсходныхДанных.НоменклатураГТД;
	КонецЦикла; 
	
	Возврат СоответствиеНоменклатурыИНоменклатурыГТД;
	
КонецФункции // ЗаполнитьНоменклатуруГТДИнтерактивно
