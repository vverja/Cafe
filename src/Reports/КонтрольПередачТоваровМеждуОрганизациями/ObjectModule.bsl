#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриСозданииНаСервере = Истина;
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   Отказ - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Передается из параметров обработчика "как есть".
//
// См. также:
//   "УправляемаяФорма.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	Если ЭтаФорма.Параметры.Свойство("ПараметрКоманды") Тогда
		ЭтаФорма.ФормаПараметры.Отбор.Вставить("Документ", ЭтаФорма.Параметры.ПараметрКоманды);
	КонецЕсли;
	
	Параметры = ЭтаФорма.ФормаПараметры;
	Отчет = ЭтаФорма.Отчет;
	
	Если Параметры.Свойство("Отбор")
		И Параметры.Отбор.Свойство("Документ") Тогда
		Если ТипЗнч(Параметры.Отбор.Документ) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
			Реквизиты = Документы.ПередачаТоваровМеждуОрганизациями.РеквизитыДокумента(Параметры.Отбор.Документ);
			Параметры.Отбор.Вставить("Отправитель", Реквизиты.ОрганизацияОтправитель);
			Параметры.Отбор.Вставить("Получатель", Реквизиты.ОрганизацияПолучатель);
		ИначеЕсли ТипЗнч(Параметры.Отбор.Документ) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями")
			ИЛИ ТипЗнч(Параметры.Отбор.Документ) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациямиОСписании")
		Тогда
			Если ТипЗнч(Параметры.Отбор.Документ) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями") Тогда
				Реквизиты = Документы.ОтчетПоКомиссииМеждуОрганизациями.РеквизитыДокумента(Параметры.Отбор.Документ);
			Иначе
				Реквизиты = Документы.ОтчетПоКомиссииМеждуОрганизациямиОСписании.РеквизитыДокумента(Параметры.Отбор.Документ);
			КонецЕсли;
			Параметры.Отбор.Вставить("Комитент", Реквизиты.ОрганизацияОтправитель);
			Параметры.Отбор.Вставить("Комиссионер", Реквизиты.ОрганизацияПолучатель);
			
			Если ЗначениеЗаполнено(Реквизиты.НачалоПериода)
				ИЛИ ЗначениеЗаполнено(Реквизиты.КонецПериода) Тогда
				
				Период = Новый СтандартныйПериод;
				Период.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
				Период.ДатаНачала = Реквизиты.НачалоПериода;
				Период.ДатаОкончания = Реквизиты.КонецПериода;
				Параметры.Отбор.Вставить("Период", Период);
			КонецЕсли;
		ИначеЕсли ТипЗнч(Параметры.Отбор.Документ) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
			Реквизиты = Документы.ВозвратТоваровМеждуОрганизациями.РеквизитыДокумента(Параметры.Отбор.Документ);
			Параметры.Отбор.Вставить("Отправитель", Реквизиты.ОрганизацияОтправитель);
			Параметры.Отбор.Вставить("Получатель", Реквизиты.ОрганизацияПолучатель);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	ПараметрДокумент = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Документ");
	Если ПараметрДокумент <> Неопределено 
		И ЗначениеЗаполнено(ПараметрДокумент.Значение) Тогда
		Документ = ПараметрДокумент.Значение;
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
			ПоТоварамКОформлению = Документы.ПередачаТоваровМеждуОрганизациями.РеквизитыДокумента(Документ).ПоТоварамКОформлению;
		ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
			ПоТоварамКОформлению = Документы.ВозвратТоваровМеждуОрганизациями.РеквизитыДокумента(Документ).ПоТоварамКОформлению;
		Иначе	
			ПоТоварамКОформлению = Истина;
		КонецЕсли;
		
		Если Не ПоТоварамКОформлению Тогда
			Текст = НСтр("ru='Документ не заполнен ""По товарам к оформлению"". Отчет формировать не требуется.';uk='Документ не заповнено ""За товарами до оформлення"". Звіт формувати не потрібно.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				Документ,
				"ПоТоварамКОформлению",
				,
				Отказ);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли
