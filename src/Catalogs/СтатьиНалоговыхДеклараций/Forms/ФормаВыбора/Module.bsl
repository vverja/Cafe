#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, "ВидНалоговойДекларации, НаборСтатейНалоговыхДеклараций");
	
	Если ЗначениеЗаполнено(ВидНалоговойДекларации) Тогда		
		Элементы.ФильтрВидНалоговойДекларации.Доступность = Ложь;
		ФильтрВидНалоговойДекларацииПриИзмененииНаСервере();
	КонецЕсли;
	
	// Для корреткного отображения динамического списка необходимо обеспечить отсутствие расслоения
	// выходной таблицы при использовании ЛЕВОГО СОЕДИНЕНИЯ. Для этого необходимо
	// установить отбор по всем измерениям кроме ссылки на данный справочник
	Список.Параметры.УстановитьЗначениеПараметра("Набор", НаборСтатейНалоговыхДеклараций);
		
	Если ЗначениеЗаполнено(НаборСтатейНалоговыхДеклараций) Тогда		
		Элементы.ФильтрНаборСтатейНалоговыхДеклараций.РежимВыбораИзСписка = Истина;
		Элементы.ФильтрНаборСтатейНалоговыхДеклараций.СписокВыбора.Добавить(НаборСтатейНалоговыхДеклараций);
		Элементы.ФильтрНаборСтатейНалоговыхДеклараций.СписокВыбора.Добавить(Справочники.НаборыСтатейНалоговыхДеклараций.ПустаяСсылка(), НСтр("ru='<Без отбора>';uk='<Без відбору>'"));
		
		ФильтрНаборСтатейНалоговыхДекларацийПриИзмененииНаСервере();
	Иначе
		Элементы.ФильтрНаборСтатейНалоговыхДеклараций.Доступность = ЗначениеЗаполнено(ВидНалоговойДекларации);
		// Если НаборСтатейНалоговыхДеклараций не передан извне то не отображаем инфо сообщение
		Элементы.ДекорацияФильтрНаборСтатейНалоговыхДеклараций.Заголовок = ""; 
	КонецЕсли; 
	
	Элементы.Описание.Видимость = (ВидНалоговойДекларации = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхДеклараций.ДекларацияПоНалогуНаПрибыльНКУ"));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФильтрВидНалоговойДекларацииПриИзменении(Элемент)
	
	ФильтрВидНалоговойДекларацииПриИзмененииНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ФильтрНаборСтатейНалоговыхДекларацийПриИзменении(Элемент)
	
	ФильтрНаборСтатейНалоговыхДекларацийПриИзмененииНаСервере();

	Элементы.ДекорацияФильтрНаборСтатейНалоговыхДеклараций.Видимость = ЗначениеЗаполнено(НаборСтатейНалоговыхДеклараций);

КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	 
	Если ЗначениеЗаполнено(НаборСтатейНалоговыхДеклараций) 
	   И Не Элемент.ТекущиеДанные.ЭтоРекомендованнаяСтатья Тогда
	   
		СтандартнаяОбработка = Ложь;
		Элементы.Список.Развернуть(ВыбраннаяСтрока);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ФильтрВидНалоговойДекларацииПриИзмененииНаСервере()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор,
		"ВидНалоговойДекларации",
		ВидНалоговойДекларации,
		ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(ВидНалоговойДекларации)
	);
	
	Если (Не ЗначениеЗаполнено(ВидНалоговойДекларации)
	 ИЛИ ВидНалоговойДекларации<>НаборСтатейНалоговыхДеклараций.ВидНалоговойДекларации) Тогда
	 
		НаборСтатейНалоговыхДеклараций = Неопределено;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Список.Отбор,
			"Ссылка",
			Новый СписокЗначений,
			ВидСравненияКомпоновкиДанных.ВСписке,,Ложь
		);
		
	КонецЕсли;
	
	Элементы.ФильтрНаборСтатейНалоговыхДеклараций.Доступность = ЗначениеЗаполнено(ВидНалоговойДекларации);	
	Элементы.Описание.Видимость = (ВидНалоговойДекларации = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхДеклараций.ДекларацияПоНалогуНаПрибыльНКУ"));
	
КонецПроцедуры

&НаСервере
Процедура ФильтрНаборСтатейНалоговыхДекларацийПриИзмененииНаСервере()
	
	Если Не ЗначениеЗаполнено(НаборСтатейНалоговыхДеклараций) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Список.Отбор,
			"Ссылка",
			Новый СписокЗначений,
			ВидСравненияКомпоновкиДанных.ВСписке,,Ложь
		);
		Возврат;
	КонецЕсли;
			
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор,
		"Ссылка",
		Справочники.СтатьиНалоговыхДеклараций.ПолучитьСписокСтатейПоНаборуСтатей(НаборСтатейНалоговыхДеклараций, Истина),
		ВидСравненияКомпоновкиДанных.ВСписке,,Истина
	);
	
КонецПроцедуры
 
#КонецОбласти
