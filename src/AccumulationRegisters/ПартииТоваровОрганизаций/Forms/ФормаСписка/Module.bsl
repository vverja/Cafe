
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура АктуализироватьПартии(Команда)
	
	АктуализироватьПартииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьГраницуПоследовательности(Команда)
	
	СтрокаСписка = Элементы.Список.ТекущиеДанные;
	Если СтрокаСписка <> Неопределено Тогда
		СброситьГраницуПоследовательностиНаДокумент(СтрокаСписка.Регистратор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьВсеПоследовательности(Команда)
	
	СброситьВсеПоследовательностиСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура АктуализироватьПартииНаДату(Команда)
	СтрокаСписка = Элементы.Список.ТекущиеДанные;
	Если СтрокаСписка <> Неопределено Тогда
		АктуализироватьПартииНаДатуНаСервере(КонецМесяца(СтрокаСписка.Период));
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура АктуализироватьПартииСервер()
	
	ПартионныйУчет.РассчитатьВсе(КонецМесяца(ТекущаяДатаСеанса()));
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура СброситьГраницуПоследовательностиНаДокумент(Документ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Месяц,
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ДанныеРегистра.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.Регистратор = &Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка", Документ);
	
	РегистрыСведений.ЗаданияКРасчетуСебестоимости.СоздатьЗаписиРегистраПоДаннымВыборки(Запрос.Выполнить().Выбрать());
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура СброситьВсеПоследовательностиСервер()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	МИНИМУМ(Таблица.Период)	КАК Период
	|ИЗ
	|	(ВЫБРАТЬ
	|		МИНИМУМ(Товары.Период) КАК Период
	|	ИЗ 
	|		РегистрНакопления.ТоварыОрганизаций КАК Товары
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		МИНИМУМ(ТоварыНаКомиссии.Период) КАК Период
	|	ИЗ 
	|		РегистрНакопления.ТоварыПереданныеНаКомиссию КАК ТоварыНаКомиссии
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		МИНИМУМ(Затраты.Период) КАК Период
	|	ИЗ 
	|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК Затраты
	|	) КАК Таблица
	|ГДЕ
	|	НЕ Таблица.Период ЕСТЬ NULL
	|
	|ИМЕЮЩИЕ
	|	МИНИМУМ(Таблица.Период) ЕСТЬ НЕ NULL
	|");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		РегистрыСведений.ЗаданияКРасчетуСебестоимости.СоздатьЗаписьРегистра(НачалоМесяца(Выборка.Период)); // по всем организациям
	КонецЕсли;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура АктуализироватьПартииНаДатуНаСервере(ОкончаниеПериодаРасчета)
	ПартионныйУчет.РассчитатьВсе(ОкончаниеПериодаРасчета);
КонецПроцедуры

#КонецОбласти
