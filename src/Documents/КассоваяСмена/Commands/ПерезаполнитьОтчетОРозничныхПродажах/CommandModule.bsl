
&НаСервере
Функция ПерезаполнитьОтчетыОРозничныхПродажахНаСервере(ПараметрКоманды)
	
	ОтчетыОРозничныхПродажах = Новый Массив;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КассоваяСмена.Ссылка КАК КассоваяСмена
	|ИЗ
	|	Документ.КассоваяСмена КАК КассоваяСмена
	|ГДЕ
	|	КассоваяСмена.Проведен
	|	И КассоваяСмена.Ссылка В (&КассоваяСмена)
	|	И КассоваяСмена.СтатусКассовойСмены <> &СтатусКассовойСменыОткрыта
	|	И КассоваяСмена.КассаККМ.ТипКассы = &ТипКассыФискальныйРегистратор";
	
	Запрос.УстановитьПараметр("КассоваяСмена", ПараметрКоманды);
	Запрос.УстановитьПараметр("СтатусКассовойСменыОткрыта", Перечисления.СтатусыКассовойСмены.Открыта);
	Запрос.УстановитьПараметр("ТипКассыФискальныйРегистратор", Перечисления.ТипыКассККМ.ФискальныйРегистратор);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ОписаниеОшибки = "";
		ОтчетОРозничныхПродажах = РозничныеПродажи.ЗаполнитьОтчетОРозничныхПродажахПоКассовойСмене(Выборка.КассоваяСмена, ОписаниеОшибки);
		
		Если ОтчетОРозничныхПродажах = Неопределено Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ОписаниеОшибки;
			Сообщение.Сообщить();
			
		Иначе
			ОтчетыОРозничныхПродажах.Добавить(ОтчетОРозничныхПродажах);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОтчетыОРозничныхПродажах;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОчиститьСообщения();
	
	МассивДокументов = ПерезаполнитьОтчетыОРозничныхПродажахНаСервере(ПараметрКоманды);
	// Все результирующие документы выводим пользователю.
	Для Каждого Документ Из МассивДокументов Цикл
		
		ОткрытьФорму("Документ.ОтчетОРозничныхПродажах.ФормаОбъекта", Новый Структура("Ключ, ПровестиПриОткрытии", Документ, Истина));
		
	КонецЦикла;
	
	Оповестить("Запись_ОтчетОРозничныхПродажах", Новый Структура, ПараметрКоманды);
	
	ПоказатьОповещениеПользователя(НСтр("ru='Заполнение отчетов о розничных продажах выполнено';uk='Заповнення звітів про роздрібні продажі виконане'"));
	
КонецПроцедуры
