#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура УдалитьСвязь(ID, Тип, Объект) Экспорт
	
	// синхронизация
	Если Не ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP")  Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ИнтегрированныеОбъекты.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.Объект.Установить(Объект);
	НаборЗаписей.Отбор.ТипОбъектаДокументооборота.Установить(Тип);
	НаборЗаписей.Отбор.ИдентификаторОбъектаДокументооборота.Установить(ID);
	
	НаборЗаписей.Записать();

КонецПроцедуры

Процедура ДобавитьСвязь(ID, Тип, Объект) Экспорт
	
	Если Не ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.3.2.3.CORP")  Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборот.ПроверитьОбновитьДанныеСвязанныхОбъектов();
	
	НаборЗаписей = РегистрыСведений.ИнтегрированныеОбъекты.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.Объект.Установить(Объект);
	НаборЗаписей.Отбор.ТипОбъектаДокументооборота.Установить(Тип);
	НаборЗаписей.Отбор.ИдентификаторОбъектаДокументооборота.Установить(ID);
	
	Запись = НаборЗаписей.Добавить();
	
	Запись.Объект = Объект;
	Запись.ТипОбъектаДокументооборота = Тип;
	Запись.ИдентификаторОбъектаДокументооборота = ID;
	
	НаборЗаписей.Записать();

КонецПроцедуры

Функция СсылкаНаОбъектПоДаннымДокументооборота(ID, Тип, ТипОбъекта) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИнтегрированныеОбъекты.Объект
		|ИЗ
		|	РегистрСведений.ИнтегрированныеОбъекты КАК ИнтегрированныеОбъекты
		|ГДЕ
		|	ИнтегрированныеОбъекты.ТипОбъектаДокументооборота = &ТипОбъектаДокументооборота
		|	И ИнтегрированныеОбъекты.ИдентификаторОбъектаДокументооборота = &ИдентификаторОбъектаДокументооборота
		|	И ТИПЗНАЧЕНИЯ(ИнтегрированныеОбъекты.Объект) = &ТипОбъекта";
		
	Запрос.УстановитьПараметр("ТипОбъектаДокументооборота", Тип);
	Запрос.УстановитьПараметр("ИдентификаторОбъектаДокументооборота", ID);
	Запрос.УстановитьПараметр("ТипОбъекта", ТипОбъекта);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
		
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		Возврат Выборка.Объект;
	
	КонецЕсли; 
	
КонецФункции 

#КонецЕсли