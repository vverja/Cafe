
////////////////////////////////////////////////////////////////////////////////
// Подсистема "Интернет-поддержка пользователей".
// ОбщийМодуль.КлиентЛицензирования.
//
// Внимание. При проверке текста модуля на версии 8.3.6 платформы
// отображаются сообщения об ошибке: "Процедура или функция с указанным
// именем не определена (<Имя процедуры или функции>)".
// Данное сообщение платформы не является ошибкой, т.к.
// код модуля вызывается только при работе на версии платформы 
// не ниже 8.3.7.
// Появление сообщений является особенностью библиотеки.
// При работе на версии 8.3.6 не приводит к ошибкам работы конфигурации.
//
// Код модуля вызывается только при работе на версии
// платформы не ниже версии 8.3.7.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

// Вызывается при изменении данных аутентификации БИП.
//
Процедура ПриИзмененииДанныхАутентификации(Логин, Пароль) Экспорт
	
	ЗаписатьДанныеАутентификацииВНастройкиКлиентаЛицензирования(Логин, Пароль);
	
КонецПроцедуры

// Проверяет соответствие настроек клиента лицензирования данным аутентификации
// Интернет-поддержки.
// При несоответствии настроек логин и пароль ИПП записываются в настройки
// клиента лицензирования.
// Не используется при работе в модели сервиса и на версии платформы
// ниже 8.3.7.
//
// Возвращаемое значение:
//	Булево - Истина, если пользователю необходимо ввести логин и пароль,
//		Ложь - в противном случае.
//
Функция ПроверитьНастройкиКлиентаЛицензирования() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеАутентификации = Неопределено;
	Если ОбновлениеИнформационнойБазы.ВыполняетсяОбновлениеИнформационнойБазы() Тогда
		
		// Если информационная база еще не обновлена, тогда
		// настройки параметров ИПП могут быть еще не перенесены
		// в безопасное хранилище данных.
		ДанныеАутентификации = ДанныеАутентификацииПользователяИнтернетПоддержкиИзУстаревшихДанных();
		
	КонецЕсли;
	
	Если ДанныеАутентификации = Неопределено Тогда
		ДанныеАутентификации = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
	КонецЕсли;
	
	Если ДанныеАутентификации = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Записать настройки клиента лицензирования в ИБ
	Если ИмяКлиентаЛицензирования() = ДанныеАутентификации.Логин Тогда
		// Логин совпадает, некорректный пароль пользователя, необходимо потребовать от
		// пользователя повторный ввод логина и пароля.
		Возврат Ложь;
	Иначе
		
		ЗаписатьДанныеАутентификацииВНастройкиКлиентаЛицензирования(ДанныеАутентификации.Логин, ДанныеАутентификации.Пароль);
		Возврат Истина;
		
	КонецЕсли;
	
КонецФункции

// Записывает настройки клиента лицензирования.
//
Процедура ЗаписатьДанныеАутентификацииВНастройкиКлиентаЛицензирования(Логин, Пароль)
	
	НастройкиПодключения = ИнтернетПоддержкаПользователейСлужебныйПовтИсп.НастройкиСоединенияССерверамиИПП();
	
	// Внимание. При проверке текста модуля на версии 8.3.6 платформы 
	// отображается сообщение об ошибке: "Процедура или функция с указанным именем не определена
	// (УстановитьНастройкиКлиентаЛицензирования).
	// Появление этого сообщения является особенностью библиотеки.
	// Код модуля вызывается только при использовании версии платформы не
	// ниже версии 8.3.7.
	// При работе на версии 8.3.6 не приводит к ошибкам работы конфигурации.
	УстановитьНастройкиКлиентаЛицензирования(
		Логин,
		Пароль,
		ЗначениеДопПараметра(НастройкиПодключения.ДоменРасположенияСерверовИПП));
	
КонецПроцедуры

// Возвращается имя клиента лицензирования.
//
Функция ИмяКлиентаЛицензирования() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	
	// Внимание. При проверке текста модуля на версии 8.3.6 платформы
	// отображается сообщение об ошибке: "Процедура или функция с указанным именем не определена
	// (ПолучитьИмяКлиентаЛицензирования).
	// Появление этого сообщения является особенностью библиотеки.
	// Код модуля вызывается только при использовании версии платформы не
	// ниже версии 8.3.7.
	// При работе на версии 8.3.6 не приводит к ошибкам работы конфигурации.
	Возврат ПолучитьИмяКлиентаЛицензирования();
	
КонецФункции

// Обработчик события "Изменена доменная зона в настройках".
//
Процедура ПриИзмененииДоменнойЗоныСерверовИПП(ДоменнаяЗона) Экспорт
	
	// Внимание. При проверке текста модуля на версии 8.3.6 платформы
	// отображается сообщение об ошибке: "Процедура или функция с указанным именем не определена
	// (УстановитьНастройкиКлиентаЛицензирования).
	// Появление этого сообщения является особенностью библиотеки.
	// Код модуля вызывается только при использовании версии платформы не
	// ниже версии 8.3.7.
	// При работе на версии 8.3.6 не приводит к ошибкам работы конфигурации.
	// Устанавливается только доп. параметр
	УстановитьНастройкиКлиентаЛицензирования(, , ЗначениеДопПараметра(ДоменнаяЗона));
	
КонецПроцедуры

// Возвращает идентификатор конфигурации.
//
Функция ИДКонфигурации() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Внимание. При проверке текста модуля на версии 8.3.6 платформы
	// отображается сообщение об ошибке: "Процедура или функция с указанным именем не определена
	// (ПолучитьИдентификаторКонфигурации).
	// Появление этого сообщения является особенностью библиотеки.
	// Код модуля вызывается только при использовании версии платформы не
	// ниже версии 8.3.7.
	// При работе на версии 8.3.6 не приводит к ошибкам работы конфигурации.
	Возврат ПолучитьИдентификаторКонфигурации();
	
КонецФункции

// Возвращается значение дополнительного параметра
// настроек клиента лицензирования.
//
Функция ЗначениеДопПараметра(ДоменнаяЗона) Экспорт
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"domain=%1;",
		?(ДоменнаяЗона = 0, "ru", "eu"));
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// Получение данных из устаревших объектов метаданных

Функция ДанныеАутентификацииПользователяИнтернетПоддержкиИзУстаревшихДанных()
	
	ЗапросПараметров = Новый Запрос(
	"ВЫБРАТЬ
	|	ПараметрыИнтернетПоддержкиПользователей.Имя КАК ИмяПараметра,
	|	ПараметрыИнтернетПоддержкиПользователей.Значение КАК ЗначениеПараметра
	|ИЗ
	|	РегистрСведений.УдалитьПараметрыИнтернетПоддержкиПользователей КАК ПараметрыИнтернетПоддержкиПользователей
	|ГДЕ
	|	ПараметрыИнтернетПоддержкиПользователей.Имя В (""login"", ""password"")
	|	И ПараметрыИнтернетПоддержкиПользователей.Пользователь = &ПустойИдентификатор");
	
	ЗапросПараметров.УстановитьПараметр("ПустойИдентификатор",
		Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	
	
	ЛогинПользователя  = Неопределено;
	ПарольПользователя = Неопределено;
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаПараметров = ЗапросПараметров.Выполнить().Выбрать();
	Пока ВыборкаПараметров.Следующий() Цикл
		
		// В запросе регистр символов не учитывается
		ИмяПараметраНРег = НРег(ВыборкаПараметров.ИмяПараметра);
		Если ИмяПараметраНРег = "login" Тогда
			ЛогинПользователя = ВыборкаПараметров.ЗначениеПараметра;
			
		ИначеЕсли ИмяПараметраНРег = "password" Тогда
			ПарольПользователя = ВыборкаПараметров.ЗначениеПараметра;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЛогинПользователя <> Неопределено И ПарольПользователя <> Неопределено Тогда
		Возврат Новый Структура("Логин, Пароль", ЛогинПользователя, ПарольПользователя);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти
