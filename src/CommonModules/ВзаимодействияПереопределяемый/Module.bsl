
////////////////////////////////////////////////////////////////////////////////
// Подсистема "Взаимодействия"
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает текст запроса, отбирающего контакты (участников) предмета взаимодействия.
// Используется, если в конфигурации определен хотя бы один предмет взаимодействий.
//
// Параметры:
//  ПомещатьВоВременнуюТаблицу - Булево - признак того, что результат выполнения запроса 
//                                        необходимо поместить во временную таблицу.
//  ИмяТаблицы                 - Строка - имя таблицы предмета взаимодействий, в котором будет выполнен поиск.
//
Функция ПолучитьТекстЗапросаПоискКонтактовПоПредмету(
	ПомещатьВоВременнуюТаблицу,
	ИмяТаблицы,
	Объединить = Ложь) Экспорт
	
	ШаблонВыбрать = ?(Объединить,"ВЫБРАТЬ РАЗЛИЧНЫЕ","ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ");
	
	ТекстВременнаяТаблица = ?(НЕ ПомещатьВоВременнуюТаблицу, "", "
		|ПОМЕСТИТЬ табКонтакты");
		
	ТекстЗапроса =" 
	|%ШаблонВыбрать%
	|	ТаблицаПартнерыИКонтактныеЛица.Партнер КАК Контакт" + ТекстВременнаяТаблица + "
	|ИЗ
	|	" + ИмяТаблицы + ".ПартнерыИКонтактныеЛица КАК ТаблицаПартнерыИКонтактныеЛица
	|ГДЕ
	|	ТаблицаПартнерыИКонтактныеЛица.Ссылка = &Предмет
	|	И (НЕ ТаблицаПартнерыИКонтактныеЛица.Партнер ЕСТЬ NULL )
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаПартнерыИКонтактныеЛица.КонтактноеЛицо
	|ИЗ
	|	" + ИмяТаблицы + ".ПартнерыИКонтактныеЛица КАК ТаблицаПартнерыИКонтактныеЛица
	|ГДЕ
	|	ТаблицаПартнерыИКонтактныеЛица.Ссылка = &Предмет
	|	И (НЕ ТаблицаПартнерыИКонтактныеЛица.КонтактноеЛицо ЕСТЬ NULL )";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ШаблонВыбрать%",ШаблонВыбрать);
	
	Если Объединить Тогда
		
		ТекстЗапроса = "
		| ОБЪЕДИНИТЬ
		|" + ТекстЗапроса;
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возможность переопределить владельца присоединенных файлов для письма.
// Такая необходимость может возникнуть например при массовых рассылках. В этом случае имеет смысл 
// хранить одни и те же присоединенные файлы в одном месте, а не тиражировать их на все письма рассылки.
//
// Параметры:
//  Письмо  - ДокументСсылка, ДокументОбъект - документ электронное письмо для которого необходимо получить вложения.
//
// Возвращаемое значение:
//  Структура,Неопределено  - Неопределено, если присоединенные файлы хранятся при письме.
//                            Структура, если присоединенные файлы хранятся в другом объекте. Формат структуры:
//                              - Владелец - владелец присоединенных файлов,
//                              - ИмяСправочникаПрисоединенныеФайлы - имя объекта метаданных присоединенных файлов.
//
Функция ПолучитьДанныеОбъектаМетаданныхПрисоединенныхФайловПисьма(Письмо) Экспорт
	
	Если ВзаимодействияКлиентСервер.ЯвляетсяВзаимодействием(Письмо) Тогда
		РеквизитыПисьма = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Письмо, "ВзаимодействиеОснование");
		Если ТипЗнч(РеквизитыПисьма.ВзаимодействиеОснование) = Тип("ДокументСсылка.РассылкаКлиентам") Тогда
			Возврат Новый Структура ("Владелец, ИмяСправочникаПрисоединенныеФайлы",
			                         РеквизитыПисьма.ВзаимодействиеОснование, "РассылкаКлиентамПрисоединенныеФайлы"); 
		КонецЕсли;
	ИначеЕсли ТипЗнч(Письмо) = Тип("СправочникСсылка.ШаблоныСообщений") Тогда
		Возврат Новый Структура("Владелец, ИмяСправочникаПрисоединенныеФайлы",
			                         Письмо, "ШаблоныСообщенийПрисоединенныеФайлы");
	ИначеЕсли ТипЗнч(Письмо) = Тип("ДокументОбъект.ЭлектронноеПисьмоИсходящее")
		      И ТипЗнч(Письмо.ВзаимодействиеОснование) = Тип("ДокументСсылка.РассылкаКлиентам") Тогда
		Возврат Новый Структура("Владелец, ИмяСправочникаПрисоединенныеФайлы",
		                         Письмо.ВзаимодействиеОснование, "РассылкаКлиентамПрисоединенныеФайлы");
	Иначе
		Если ТипЗнч(Письмо) =Тип("ДокументСсылка.РассылкаКлиентам") Тогда
			Возврат Новый Структура("Владелец, ИмяСправочникаПрисоединенныеФайлы",
			                         Письмо, "РассылкаКлиентамПрисоединенныеФайлы"); 
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти
