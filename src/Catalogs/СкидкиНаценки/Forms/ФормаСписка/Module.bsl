
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ИспользоватьНесколькоСкладов                   = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов")
	                                                 И ПравоДоступа("Чтение", Метаданные.Справочники.Склады);
	
	ИспользоватьТиповыеСоглашенияСКлиентами        = ПолучитьФункциональнуюОпцию("ИспользоватьТиповыеСоглашенияСКлиентами")
	                                                 И ПравоДоступа("Чтение", Метаданные.Справочники.СоглашенияСКлиентами);
	
	ИспользоватьИндивидуальныеСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьИндивидуальныеСоглашенияСКлиентами")
	                                                 И ПравоДоступа("Чтение", Метаданные.Справочники.СоглашенияСКлиентами);
	
	ИспользоватьКартыЛояльности                    = ПолучитьФункциональнуюОпцию("ИспользоватьКартыЛояльности")
	                                                 И ПравоДоступа("Чтение", Метаданные.Справочники.ВидыКартЛояльности);

	Элементы.ГруппаИндивидуальныеСоглашения.Видимость      = ИспользоватьИндивидуальныеСоглашенияСКлиентами;
	Элементы.ДействуетВИндивидуальныхСоглашениях.Видимость = ИспользоватьИндивидуальныеСоглашенияСКлиентами;
	Элементы.ГруппаТиповыеСоглашения.Видимость             = ИспользоватьТиповыеСоглашенияСКлиентами;
	Элементы.ДействуетВТиповыхСоглашениях.Видимость        = ИспользоватьТиповыеСоглашенияСКлиентами;
	Элементы.ГруппаКартыЛояльности.Видимость               = ИспользоватьКартыЛояльности;
	Элементы.ДействуетВКартахЛояльности.Видимость          = ИспользоватьКартыЛояльности;
	Элементы.ГруппаСклады.Видимость                        = ИспользоватьНесколькоСкладов;
	Элементы.ДействуетНаСкладах.Видимость                  = ИспользоватьНесколькоСкладов;
	
	Список.ТекстЗапроса = ТекстЗапросаДинамическогоСписка();
	Если ИспользоватьТиповыеСоглашенияСКлиентами Тогда
		ИспользованиеВТиповыхСоглашенияхСКлиентами.ТекстЗапроса = ТекстЗапросаДинамическогоСпискаТиповыеСоглашения();
		ИспользованиеВТиповыхСоглашенияхСКлиентами.ОсновнаяТаблица = "Справочник.СоглашенияСКлиентами";
		ИспользованиеВТиповыхСоглашенияхСКлиентами.ДинамическоеСчитываниеДанных = Истина;
	КонецЕсли;
	Если ИспользоватьИндивидуальныеСоглашенияСКлиентами Тогда
		ИспользованиеВИндивидуальныхСоглашенияхСКлиентами.ТекстЗапроса = ТекстЗапросаДинамическогоСпискаИндивидуальныеСоглашения();
		ИспользованиеВИндивидуальныхСоглашенияхСКлиентами.ОсновнаяТаблица = "Справочник.СоглашенияСКлиентами";
		ИспользованиеВИндивидуальныхСоглашенияхСКлиентами.ДинамическоеСчитываниеДанных = Истина;
	КонецЕсли;
	
	УстановитьУсловноеОформление();

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

	ДатаСреза = ТекущаяДатаСеанса();
	
	ДинамическиеСписки = Новый Массив;
	Если ИспользоватьИндивидуальныеСоглашенияСКлиентами Тогда
		ДинамическиеСписки.Добавить(ИспользованиеВИндивидуальныхСоглашенияхСКлиентами);
	КонецЕсли;
	Если ИспользоватьТиповыеСоглашенияСКлиентами Тогда
		ДинамическиеСписки.Добавить(ИспользованиеВТиповыхСоглашенияхСКлиентами);
	КонецЕсли;
	Если ИспользоватьКартыЛояльности Тогда
		ДинамическиеСписки.Добавить(ИспользованиеВКартахЛояльности);
	КонецЕсли;
	Если ИспользоватьНесколькоСкладов Тогда
		ДинамическиеСписки.Добавить(ИспользованиеНаСкладах);
	КонецЕсли;
	ДинамическиеСписки.Добавить(Список);
	
	Для Каждого ДинамическийСписок Из ДинамическиеСписки Цикл
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			ДинамическийСписок,
			"ТекущаяДата",
			ДатаСреза,
			Истина);
			
		Если ДинамическийСписок = Список Тогда
			Продолжить;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			ДинамическийСписок,
			"СкидкаНаценка",
			Справочники.СкидкиНаценки.ПустаяСсылка(),
			Истина);
			
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"СкладПоУмолчанию",
		Справочники.Склады.ПустаяСсылка(),
		Истина);
		
	Если ИспользоватьНесколькоСкладов Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			ИспользованиеНаСкладах,
			"Статус",
			Перечисления.СтатусыДействияСкидок.Действует,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			ИспользованиеНаСкладахВариантОтображенияСкидокНаценок <> "Все");
	КонецЕсли;
		
	Если ИспользоватьТиповыеСоглашенияСКлиентами Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			ИспользованиеВТиповыхСоглашенияхСКлиентами,
			"Статус",
			Перечисления.СтатусыДействияСкидок.Действует,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			ИспользованиеВТиповыхСоглашенияхСКлиентамиВариантОтображенияСкидокНаценок <> "Все");
	КонецЕсли;
	
	Если ИспользоватьИндивидуальныеСоглашенияСКлиентами Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			ИспользованиеВИндивидуальныхСоглашенияхСКлиентами,
			"Статус",
			Перечисления.СтатусыДействияСкидок.Действует,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			ИспользованиеВИндивидуальныхСоглашенияхСКлиентамиВариантОтображенияСкидокНаценок <> "Все");
	КонецЕсли;
	
	Если ИспользоватьКартыЛояльности Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			ИспользованиеВКартахЛояльности,
			"Статус",
			Перечисления.СтатусыДействияСкидок.Действует,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			ИспользованиеВКартахЛояльностиВариантОтображенияСкидокНаценок <> "Все");
	КонецЕсли;
	
	ИспользоватьУпрощенныйРежим =   Не ИспользоватьНесколькоСкладов
	                              И Не ИспользоватьИндивидуальныеСоглашенияСКлиентами
	                              И Не ИспользоватьТиповыеСоглашенияСКлиентами
	                              И Не ИспользоватьКартыЛояльности;
	
	Элементы.ГруппаИспользованиеТекущейСкидки.Видимость = Не ИспользоватьУпрощенныйРежим;
	
	Элементы.ДействуетНаСкладах.Видимость                  = ИспользоватьНесколькоСкладов;
	Элементы.ДействуетВКартахЛояльности.Видимость          = ИспользоватьКартыЛояльности;
	Элементы.ДействуетВИндивидуальныхСоглашениях.Видимость = ИспользоватьИндивидуальныеСоглашенияСКлиентами;
	Элементы.ДействуетВТиповыхСоглашениях.Видимость        = ИспользоватьТиповыеСоглашенияСКлиентами;
	
	Если ИспользоватьУпрощенныйРежим Тогда
		Элементы.УстановитьСтатус.Заголовок = НСтр("ru='Установить статус';uk='Встановити статус'");
	Иначе
		Элементы.УстановитьСтатус.Заголовок = НСтр("ru='Установить общий статус';uk='Встановити загальний статус'");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаСоздать",
		"Видимость",
		ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Справочники.СкидкиНаценки));
	
	СкидкиНаценкиСервер.ДобавитьКомандыСозданияНовыхСкидокНаценок(ЭтотОбъект, Элементы.ГруппаСоздать);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеНаСервере(СкидкаНаценка)
	Элементы.Список.Обновить();
	Элементы.ИспользованиеВКартахЛояльности.Обновить();
	Элементы.ИспользованиеВИндивидуальныхСоглашенияхСКлиентами.Обновить();
	Элементы.ИспользованиеВТиповыхСоглашенияхСКлиентами.Обновить();
	Элементы.ИспользованиеНаСкладах.Обновить();
	ОбновитьИспользованиеСкидокНаценок(СкидкаНаценка);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ДействиеСкидокНаценок" Тогда
		СкидкаНаценка = ТекущаяСкидка();
		ОбновитьДанныеНаСервере(СкидкаНаценка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Статус = Настройки.Получить("Статус");
	УстановитьОтборПоСтатусу(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийСписка

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("Подключаемый_СписокПриАктивизацииСтроки", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтатусОтборПриИзменении(Элемент)
	
	УстановитьОтборПоСтатусу(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборДатаСрезаПриИзменении(Элемент)
	
	ДинамическиеСписки = Новый Массив;
	Если ИспользоватьИндивидуальныеСоглашенияСКлиентами Тогда
		ДинамическиеСписки.Добавить(ИспользованиеВИндивидуальныхСоглашенияхСКлиентами);
	КонецЕсли;
	Если ИспользоватьТиповыеСоглашенияСКлиентами Тогда
		ДинамическиеСписки.Добавить(ИспользованиеВТиповыхСоглашенияхСКлиентами);
	КонецЕсли;
	Если ИспользоватьКартыЛояльности Тогда
		ДинамическиеСписки.Добавить(ИспользованиеВКартахЛояльности);
	КонецЕсли;
	Если ИспользоватьНесколькоСкладов Тогда
		ДинамическиеСписки.Добавить(ИспользованиеНаСкладах);
	КонецЕсли;
	ДинамическиеСписки.Добавить(Список);
	
	Для Каждого ДинамическийСписок Из ДинамическиеСписки Цикл
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			ДинамическийСписок,
			"ТекущаяДата",
			ДатаСреза,
			Истина);
			
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборДатаСрезаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДатаСреза = ТекущаяДата();
	
	ДинамическиеСписки = Новый Массив;
	Если ИспользоватьИндивидуальныеСоглашенияСКлиентами Тогда
		ДинамическиеСписки.Добавить(ИспользованиеВИндивидуальныхСоглашенияхСКлиентами);
	КонецЕсли;
	Если ИспользоватьТиповыеСоглашенияСКлиентами Тогда
		ДинамическиеСписки.Добавить(ИспользованиеВТиповыхСоглашенияхСКлиентами);
	КонецЕсли;
	Если ИспользоватьКартыЛояльности Тогда
		ДинамическиеСписки.Добавить(ИспользованиеВКартахЛояльности);
	КонецЕсли;
	Если ИспользоватьНесколькоСкладов Тогда
		ДинамическиеСписки.Добавить(ИспользованиеНаСкладах);
	КонецЕсли;
	ДинамическиеСписки.Добавить(Список);
	
	Для Каждого ДинамическийСписок Из ДинамическиеСписки Цикл
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			ДинамическийСписок,
			"ТекущаяДата",
			ДатаСреза,
			Истина);
			
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если Строка = Неопределено Или Строка = ПредопределенноеЗначение("Справочник.СкидкиНаценки.ПустаяСсылка") Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеНаСкладахВариантОтображенияСкидокПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ИспользованиеНаСкладах,
		"Статус",
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.Действует"),
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ИспользованиеНаСкладахВариантОтображенияСкидокНаценок <> "Все");
		
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеВИндивидуальныхСоглашенияхСКлиентамиВариантОтображенияСкидокПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ИспользованиеВИндивидуальныхСоглашенияхСКлиентами,
		"Статус",
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.Действует"),
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ИспользованиеВИндивидуальныхСоглашенияхСКлиентамиВариантОтображенияСкидокНаценок <> "Все");
		
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеВТиповыхСоглашенияхСКлиентамиВариантОтображенияСкидокПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ИспользованиеВТиповыхСоглашенияхСКлиентами,
		"Статус",
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.Действует"),
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ИспользованиеВТиповыхСоглашенияхСКлиентамиВариантОтображенияСкидокНаценок <> "Все");
		
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеВКартахЛояльностиВариантОтображенияСкидокПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ИспользованиеВКартахЛояльности,
		"Статус",
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.Действует"),
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ИспользованиеВКартахЛояльностиВариантОтображенияСкидокНаценок <> "Все");
		
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеНаСкладахВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле <> Элементы.ИспользованиеНаСкладахСклад Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеВКартахЛояльностиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле <> Элементы.ИспользованиеВКартахЛояльностиВидКартыЛояльности Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеВИндивидуальныхСоглашенияхСКлиентамиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле <> Элементы.ИспользованиеВИндивидуальныхСоглашенияхСКлиентамиСоглашение Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеВТиповыхСоглашенияхСКлиентамиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле <> Элементы.ИспользованиеВТиповыхСоглашенияхСКлиентамиСоглашение Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПереместитьЭлементВверх(Команда)
	
	НастройкаПорядкаЭлементовКлиент.ПереместитьЭлементВверхВыполнить(Список, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьЭлементВниз(Команда)
	
	НастройкаПорядкаЭлементовКлиент.ПереместитьЭлементВнизВыполнить(Список, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	СкидкиНаценкиКлиент.ОбработатьКомандуДобавленияСкидкиНаценки(
		ЭтотОбъект,
		Команда,
		Элементы.Список.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусНеДействует(Команда)
	
	Источники = Новый Массив;
	Источники.Добавить(ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка"));
	
	СкидкиНаценки = Новый Массив;
	Для Каждого СкидкаНаценка Из Элементы.Список.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.Список.ДанныеСтроки(СкидкаНаценка);
		Если ДанныеСтроки.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		СкидкиНаценки.Добавить(СкидкаНаценка);
	КонецЦикла;
	
	Если СкидкиНаценки.Количество() = 0 Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru='Не выбраны скидки (наценки)';uk='Не вибрані знижки (націнки)'"));
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуИзмененияСтатуса(
		СкидкиНаценки,
		Источники,
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.НеДействует"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусДействует(Команда)
	
	Источники = Новый Массив;
	Источники.Добавить(ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка"));
	
	СкидкиНаценки = Новый Массив;
	Для Каждого СкидкаНаценка Из Элементы.Список.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.Список.ДанныеСтроки(СкидкаНаценка);
		Если ДанныеСтроки.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		СкидкиНаценки.Добавить(СкидкаНаценка);
	КонецЦикла;
	
	Если СкидкиНаценки.Количество() = 0 Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru='Не выбраны скидки (наценки)';uk='Не вибрані знижки (націнки)'"));
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуИзмененияСтатуса(
		СкидкиНаценки,
		Источники,
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.Действует"));
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеНаСкладахУстановитьСтатусНеДействует(Команда)
	
	Источники = Новый Массив;
	Для Каждого ИдентификаторСтроки Из Элементы.ИспользованиеНаСкладах.ВыделенныеСтроки Цикл
		Источники.Добавить(Элементы.ИспользованиеНаСкладах.ДанныеСтроки(ИдентификаторСтроки).Склад);
	КонецЦикла;
	
	Если Источники.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СкидкаНаценка = ТекущаяСкидка();
	
	ОткрытьФормуИзмененияСтатуса(
		СкидкаНаценка,
		Источники,
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.НеДействует"));
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеНаСкладахУстановитьСтатусДействует(Команда)
	
	Источники = Новый Массив;
	Для Каждого ИдентификаторСтроки Из Элементы.ИспользованиеНаСкладах.ВыделенныеСтроки Цикл
		Источники.Добавить(Элементы.ИспользованиеНаСкладах.ДанныеСтроки(ИдентификаторСтроки).Склад);
	КонецЦикла;
	
	Если Источники.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СкидкаНаценка = ТекущаяСкидка();
	
	ОткрытьФормуИзмененияСтатуса(
		СкидкаНаценка,
		Источники,
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.Действует"));
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеВКартахЛояльностиУстановитьСтатусНеДействует(Команда)
	
	Источники = Новый Массив;
	Для Каждого Источник Из Элементы.ИспользованиеВКартахЛояльности.ВыделенныеСтроки Цикл
		Источники.Добавить(Источник);
	КонецЦикла;
	
	Если Источники.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СкидкаНаценка = ТекущаяСкидка();
	
	ОткрытьФормуИзмененияСтатуса(
		СкидкаНаценка,
		Источники,
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.НеДействует"));
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеВКартахЛояльностиУстановитьСтатусДействует(Команда)
	
	Источники = Новый Массив;
	Для Каждого Источник Из Элементы.ИспользованиеВКартахЛояльности.ВыделенныеСтроки Цикл
		Источники.Добавить(Источник);
	КонецЦикла;
	
	Если Источники.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СкидкаНаценка = ТекущаяСкидка();
	
	ОткрытьФормуИзмененияСтатуса(
		СкидкаНаценка,
		Источники,
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.Действует"));
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеВИндивидуальныхСоглашенияхСКлиентамиУстановитьСтатусНеДействует(Команда)
	
	Источники = Новый Массив;
	Для Каждого Источник Из Элементы.ИспользованиеВИндивидуальныхСоглашенияхСКлиентами.ВыделенныеСтроки Цикл
		Источники.Добавить(Источник);
	КонецЦикла;
	
	Если Источники.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СкидкаНаценка = ТекущаяСкидка();
	
	ОткрытьФормуИзмененияСтатуса(
		СкидкаНаценка,
		Источники,
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.НеДействует"));
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеВИндивидуальныхСоглашенияхСКлиентамиУстановитьСтатусДействует(Команда)
	
	Источники = Новый Массив;
	Для Каждого Источник Из Элементы.ИспользованиеВИндивидуальныхСоглашенияхСКлиентами.ВыделенныеСтроки Цикл
		Источники.Добавить(Источник);
	КонецЦикла;
	
	Если Источники.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СкидкаНаценка = ТекущаяСкидка();
	
	ОткрытьФормуИзмененияСтатуса(
		СкидкаНаценка,
		Источники,
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.Действует"));
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеВТиповыхСоглашенияхСКлиентамиУстановитьСтатусНеДействует(Команда)
	
	Источники = Новый Массив;
	Для Каждого Источник Из Элементы.ИспользованиеВТиповыхСоглашенияхСКлиентами.ВыделенныеСтроки Цикл
		Источники.Добавить(Источник);
	КонецЦикла;
	
	Если Источники.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СкидкаНаценка = ТекущаяСкидка();
	
	ОткрытьФормуИзмененияСтатуса(
		СкидкаНаценка,
		Источники,
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.НеДействует"));
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеВТиповыхСоглашенияхСКлиентамиУстановитьСтатусДействует(Команда)
	
	Источники = Новый Массив;
	Для Каждого Источник Из Элементы.ИспользованиеВТиповыхСоглашенияхСКлиентами.ВыделенныеСтроки Цикл
		Источники.Добавить(Источник);
	КонецЦикла;
	
	Если Источники.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СкидкаНаценка = ТекущаяСкидка();
	
	ОткрытьФормуИзмененияСтатуса(
		СкидкаНаценка,
		Источники,
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.Действует"));
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияДействия(Команда)
	
	ОткрытьФормуИстории(ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка"));
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияДействияНаСкладе(Команда)
	
	ТекущиеДанные = Элементы.ИспользованиеНаСкладах.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуИстории(ТекущиеДанные.Склад);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияДействияВВидеКартыЛояльности(Команда)
	
	ТекущиеДанные = Элементы.ИспользованиеВКартахЛояльности.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуИстории(ТекущиеДанные.ВидКартыЛояльности);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияДействияВТиповомСоглашении(Команда)
	
	ТекущиеДанные = Элементы.ИспользованиеВТиповыхСоглашенияхСКлиентами.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуИстории(ТекущиеДанные.Соглашение);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияДействияВИндивидуальномСоглашении(Команда)
	
	ТекущиеДанные = Элементы.ИспользованиеВИндивидуальныхСоглашенияхСКлиентами.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуИстории(ТекущиеДанные.Соглашение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Список.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Управляемая");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Новый Цвет(0, 128, 0));

	Если ИспользоватьКартыЛояльности  Тогда
		
		//
		
		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИспользованиеВКартахЛояльностиДатаОкончания.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользованиеВКартахЛояльности.ДатаОкончания");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = '00010101';

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользованиеВКартахЛояльности.ДатаНачала");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
		ОтборЭлемента.ПравоеЗначение = '00010101';

		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<бессрочно>';uk='<безстроково>'"));
		
	КонецЕсли;
	
	Если ИспользоватьИндивидуальныеСоглашенияСКлиентами  Тогда
		
		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИспользованиеВИндивидуальныхСоглашенияхСКлиентамиДатаОкончания.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользованиеВИндивидуальныхСоглашенияхСКлиентами.ДатаОкончания");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = '00010101';

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользованиеВИндивидуальныхСоглашенияхСКлиентами.ДатаНачала");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
		ОтборЭлемента.ПравоеЗначение = '00010101';

		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<бессрочно>';uk='<безстроково>'"));
		
	КонецЕсли;
	
	Если ИспользоватьТиповыеСоглашенияСКлиентами Тогда
		
		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИспользованиеВТиповыхСоглашенияхСКлиентамиДатаОкончания.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользованиеВТиповыхСоглашенияхСКлиентами.ДатаОкончания");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = '00010101';

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользованиеВТиповыхСоглашенияхСКлиентами.ДатаНачала");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
		ОтборЭлемента.ПравоеЗначение = '00010101';

		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<бессрочно>';uk='<безстроково>'"));
		
	КонецЕсли;
	
	Если ИспользоватьНесколькоСкладов Тогда
	
		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИспользованиеНаСкладахДатаОкончания.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользованиеНаСкладах.ДатаОкончания");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = '00010101';

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользованиеНаСкладах.ДатаНачала");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
		ОтборЭлемента.ПравоеЗначение = '00010101';

		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<бессрочно>';uk='<безстроково>'"));
	
	КонецЕсли;
	
	Если ИспользоватьНесколькоСкладов Тогда
	
		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИспользованиеНаСкладах.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользованиеНаСкладах.Статус");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДействияСкидок.НеДействует;

		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	
	КонецЕсли;
	
	Если ИспользоватьКартыЛояльности Тогда
	
		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИспользованиеВКартахЛояльности.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользованиеВКартахЛояльности.Статус");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДействияСкидок.НеДействует;

		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	
	КонецЕсли;
	
	Если ИспользоватьИндивидуальныеСоглашенияСКлиентами Тогда
	
		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИспользованиеВИндивидуальныхСоглашенияхСКлиентами.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользованиеВИндивидуальныхСоглашенияхСКлиентами.Статус");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДействияСкидок.НеДействует;

		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	
	КонецЕсли;
	
	Если ИспользоватьТиповыеСоглашенияСКлиентами Тогда
	
		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИспользованиеВТиповыхСоглашенияхСКлиентами.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользованиеВТиповыхСоглашенияхСКлиентами.Статус");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДействияСкидок.НеДействует;

		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	
	КонецЕсли;
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДатаОкончания.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДатаОкончания");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = '00010101';

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДатаНачала");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = '00010101';

	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<бессрочно>';uk='<безстроково>'"));
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Список.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Статус");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДействияСкидок.НеДействует;
	
	Если ИспользоватьНесколькоСкладов Тогда
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетНаСкладах");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
	КонецЕсли;
	
	Если ИспользоватьКартыЛояльности Тогда
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВКартахЛояльности");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
	КонецЕсли;

	Если ИспользоватьИндивидуальныеСоглашенияСКлиентами Тогда
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВИндивидуальныхСоглашениях");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
	КонецЕсли;
	
	Если ИспользоватьТиповыеСоглашенияСКлиентами Тогда
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВТиповыхСоглашениях");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
	КонецЕсли;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	
	Если ИспользоватьИндивидуальныеСоглашенияСКлиентами Тогда
	
		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Статус.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Статус");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДействияСкидок.НеДействует;

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВИндивидуальныхСоглашениях");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Истина;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Действует с ограничениями';uk='Діє з обмеженнями'"));
	
	КонецЕсли;
	
	Если ИспользоватьТиповыеСоглашенияСКлиентами Тогда
	
		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Статус.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Статус");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДействияСкидок.НеДействует;

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВТиповыхСоглашениях");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Истина;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Действует с ограничениями';uk='Діє з обмеженнями'"));
	
	КонецЕсли;
	
	Если ИспользоватьКартыЛояльности Тогда
	
		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Статус.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Статус");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДействияСкидок.НеДействует;

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВКартахЛояльности");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Истина;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Действует с ограничениями';uk='Діє з обмеженнями'"));
	
	КонецЕсли;
	
	Если ИспользоватьНесколькоСкладов Тогда
	
		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Статус.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Статус");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДействияСкидок.НеДействует;

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетНаСкладах");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Истина;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Действует с ограничениями';uk='Діє з обмеженнями'"));
	
	КонецЕсли;
	
	Если ИспользоватьНесколькоСкладов Тогда
	
		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Статус.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Статус");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДействияСкидок.НеДействует;

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетНаСкладах");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Истина;
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВКартахЛояльности");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВТиповыхСоглашениях");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВИндивидуальныхСоглашениях");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Действует на складах';uk='Діє на складах'"));
	
	КонецЕсли;
	
	Если ИспользоватьКартыЛояльности Тогда
	
		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Статус.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Статус");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДействияСкидок.НеДействует;

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетНаСкладах");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВКартахЛояльности");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Истина;
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВТиповыхСоглашениях");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВИндивидуальныхСоглашениях");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Действует в картах лояльности';uk='Діє в картах лояльності'"));
	
	КонецЕсли;
	
	Если ИспользоватьИндивидуальныеСоглашенияСКлиентами
		Или ИспользоватьТиповыеСоглашенияСКлиентами Тогда
	
		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Статус.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Статус");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДействияСкидок.НеДействует;

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетНаСкладах");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВКартахЛояльности");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВТиповыхСоглашениях");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Истина;
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВИндивидуальныхСоглашениях");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Действует в соглашениях';uk='Діє в офертах'"));
		
		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Статус.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Статус");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДействияСкидок.НеДействует;

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетНаСкладах");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВКартахЛояльности");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВТиповыхСоглашениях");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДействуетВИндивидуальныхСоглашениях");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Истина;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Действует в соглашениях';uk='Діє в офертах'"));
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СписокПриАктивизацииСтроки()
	
	СкидкаНаценка = ТекущаяСкидка();
	Если СкидкаНаценка <> АктивизированнаяСкидкаНаценка Тогда
		СписокПриАктивизацииСтрокиНаСервере(СкидкаНаценка);
		АктивизированнаяСкидкаНаценка = СкидкаНаценка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоСтатусу(Форма)
	
	ГруппаОтбора = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(Форма.Список).Элементы, "ОтборПоСтатусу",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ГруппаОтбора,
		"Статус",
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.Действует"),
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Форма.Статус));
		
	Если Форма.ИспользоватьНесколькоСкладов Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ГруппаОтбора,
			"ДействуетНаСкладах",
			Истина,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			ЗначениеЗаполнено(Форма.Статус));
	КонецЕсли;
	
	Если Форма.ИспользоватьКартыЛояльности Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ГруппаОтбора,
			"ДействуетВКартахЛояльности",
			Истина,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			ЗначениеЗаполнено(Форма.Статус));
	КонецЕсли;
	
	Если Форма.ИспользоватьТиповыеСоглашенияСКлиентами Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ГруппаОтбора,
			"ДействуетВТиповыхСоглашениях",
			Истина,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			ЗначениеЗаполнено(Форма.Статус));
	КонецЕсли;
	
	Если Форма.ИспользоватьИндивидуальныеСоглашенияСКлиентами Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ГруппаОтбора,
			"ДействуетВИндивидуальныхСоглашениях",
			Истина,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			ЗначениеЗаполнено(Форма.Статус));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ТекущаяСкидка()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТекущиеДанные.ЭтоГруппа Тогда
		Возврат Неопределено;
	Иначе
		Возврат ТекущиеДанные.Ссылка;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуИзмененияСтатуса(СкидкаНаценка, Источник, Статус)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ДатаНачала", ДатаСреза);
	ПараметрыОткрытия.Вставить("СкидкаНаценка", СкидкаНаценка);
	ПараметрыОткрытия.Вставить("Источник", Источник);
	ПараметрыОткрытия.Вставить("Статус", Статус);
	
	ОткрытьФорму(
		"Справочник.СкидкиНаценки.Форма.УстановкаСтатусаДействия",
		ПараметрыОткрытия,
		ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуИстории(Источник)
	
	СкидкаНаценка = ТекущаяСкидка();
	Если СкидкаНаценка = Неопределено Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru='Не выбрана скидка (наценка)';uk='Не вибрана знижка (націнка)'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Источник", Источник);
	ПараметрыОткрытия.Вставить("СкидкаНаценка", СкидкаНаценка);
	ОткрытьФорму(
		"Справочник.СкидкиНаценки.Форма.ИсторияДействияСкидкиНаценки",
		ПараметрыОткрытия,
		ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИспользованиеСкидокНаценок(СкидкаНаценка)
	
	ИспользованиеСкидкиНаценки = СкидкиНаценкиСервер.ИспользованиеСкидкиНаценки(СкидкаНаценка, ТекущаяДатаСеанса());
	СкидкиНаценкиСервер.СформироватьИнформационнуюНадписьИспользованиеСкидокНаценок(ИнформацияОДействииСкидок,
	                                                                                ИспользованиеСкидкиНаценки);
	СкидкиНаценкиСервер.СформироватьИнформациюОКоличествеИспользуемыхСкидок(ЭтотОбъект, ИспользованиеСкидкиНаценки);
	
КонецПроцедуры

&НаСервере
Процедура СписокПриАктивизацииСтрокиНаСервере(СкидкаНаценка)
	
	ДинамическиеСписки = Новый Массив;
	Если ИспользоватьИндивидуальныеСоглашенияСКлиентами Тогда
		ДинамическиеСписки.Добавить(ИспользованиеВИндивидуальныхСоглашенияхСКлиентами);
	КонецЕсли;
	Если ИспользоватьТиповыеСоглашенияСКлиентами Тогда
		ДинамическиеСписки.Добавить(ИспользованиеВТиповыхСоглашенияхСКлиентами);
	КонецЕсли;
	Если ИспользоватьКартыЛояльности Тогда
		ДинамическиеСписки.Добавить(ИспользованиеВКартахЛояльности);
	КонецЕсли;
	Если ИспользоватьНесколькоСкладов Тогда
		ДинамическиеСписки.Добавить(ИспользованиеНаСкладах);
	КонецЕсли;
	
	Элементы.ГруппаИспользованиеТекущейСкидки.Доступность = СкидкаНаценка <> Неопределено;
	
	Если СкидкаНаценка = Неопределено Тогда
		
		Для Каждого ДинамическийСписок Из ДинамическиеСписки Цикл
			
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
				ДинамическийСписок,
				"СкидкаНаценка",
				ПредопределенноеЗначение("Справочник.СкидкиНаценки.ПустаяСсылка"),
				Истина);
			
		КонецЦикла;
		
	Иначе
		
		Для Каждого ДинамическийСписок Из ДинамическиеСписки Цикл
			
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
				ДинамическийСписок,
				"СкидкаНаценка",
				СкидкаНаценка,
				Истина);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбновитьИспользованиеСкидокНаценок(СкидкаНаценка);
	
КонецПроцедуры

&НаСервере
Функция ТекстЗапросаДинамическогоСписка()
	
	Текст = 
	"ВЫБРАТЬ
	|	СправочникСкидкиНаценки.Наименование,
	|	СправочникСкидкиНаценки.ЭтоГруппа,
	|	ВЫБОР
	|		КОГДА СправочникСкидкиНаценки.ЭтоГруппа
	|			ТОГДА ВЫБОР
	|					КОГДА СправочникСкидкиНаценки.ВариантСовместногоПрименения = ЗНАЧЕНИЕ(Перечисление.ВариантыСовместногоПримененияСкидокНаценок.Максимум)
	|						ТОГДА 8
	|					КОГДА СправочникСкидкиНаценки.ВариантСовместногоПрименения = ЗНАЧЕНИЕ(Перечисление.ВариантыСовместногоПримененияСкидокНаценок.Минимум)
	|						ТОГДА 16
	|					КОГДА СправочникСкидкиНаценки.ВариантСовместногоПрименения = ЗНАЧЕНИЕ(Перечисление.ВариантыСовместногоПримененияСкидокНаценок.Сложение)
	|						ТОГДА 0
	|					КОГДА СправочникСкидкиНаценки.ВариантСовместногоПрименения = ЗНАЧЕНИЕ(Перечисление.ВариантыСовместногоПримененияСкидокНаценок.Умножение)
	|						ТОГДА 4
	|					КОГДА СправочникСкидкиНаценки.ВариантСовместногоПрименения = ЗНАЧЕНИЕ(Перечисление.ВариантыСовместногоПримененияСкидокНаценок.Вытеснение)
	|						ТОГДА 12
	|				КОНЕЦ + ВЫБОР КОГДА СправочникСкидкиНаценки.ПометкаУдаления ТОГДА 3 ИНАЧЕ 0 КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СправочникСкидкиНаценки.СпособПредоставления = ЗНАЧЕНИЕ(Перечисление.СпособыПредоставленияСкидокНаценок.Процент)
	|					ТОГДА ВЫБОР
	|							КОГДА СправочникСкидкиНаценки.ЗначениеСкидкиНаценки < 0
	|								ТОГДА 32
	|							ИНАЧЕ 28
	|						КОНЕЦ
	|				КОГДА СправочникСкидкиНаценки.СпособПредоставления = ЗНАЧЕНИЕ(Перечисление.СпособыПредоставленияСкидокНаценок.Подарок) ИЛИ СправочникСкидкиНаценки.СпособПредоставления = ЗНАЧЕНИЕ(Перечисление.СпособыПредоставленияСкидокНаценок.Количество)
	|					ТОГДА 36
	|				КОГДА СправочникСкидкиНаценки.СпособПредоставления = ЗНАЧЕНИЕ(Перечисление.СпособыПредоставленияСкидокНаценок.Сумма)
	|					ТОГДА ВЫБОР
	|							КОГДА СправочникСкидкиНаценки.ЗначениеСкидкиНаценки < 0
	|								ТОГДА 40
	|							ИНАЧЕ 44
	|						КОНЕЦ
	|				КОГДА СправочникСкидкиНаценки.СпособПредоставления = ЗНАЧЕНИЕ(Перечисление.СпособыПредоставленияСкидокНаценок.ОкруглениеСуммы)
	|					ТОГДА ВЫБОР
	|							КОГДА СправочникСкидкиНаценки.ЗначениеСкидкиНаценки < 0
	|								ТОГДА 40
	|							ИНАЧЕ 44
	|						КОНЕЦ
	|						
	|				КОГДА СправочникСкидкиНаценки.СпособПредоставления = ЗНАЧЕНИЕ(Перечисление.СпособыПредоставленияСкидокНаценок.СуммаДляКаждойСтроки)
	|					ТОГДА ВЫБОР
	|							КОГДА СправочникСкидкиНаценки.ЗначениеСкидкиНаценки < 0
	|								ТОГДА 40
	|							ИНАЧЕ 44
	|						КОНЕЦ
	|				КОГДА СправочникСкидкиНаценки.СпособПредоставления = ЗНАЧЕНИЕ(Перечисление.СпособыПредоставленияСкидокНаценок.ВидЦены)
	|					ТОГДА 48
	|				КОГДА СправочникСкидкиНаценки.СпособПредоставления = ЗНАЧЕНИЕ(Перечисление.СпособыПредоставленияСкидокНаценок.Сообщение)
	|					ТОГДА 52
	|				КОГДА СправочникСкидкиНаценки.СпособПредоставления = ЗНАЧЕНИЕ(Перечисление.СпособыПредоставленияСкидокНаценок.КартаЛояльности)
	|					ТОГДА 52
	|				КОГДА СправочникСкидкиНаценки.СпособПредоставления ССЫЛКА Справочник.ДополнительныеОтчетыИОбработки
	|					ТОГДА 56
	|			КОНЕЦ + ВЫБОР КОГДА СправочникСкидкиНаценки.ПометкаУдаления ТОГДА 3 ИНАЧЕ 0 КОНЕЦ
	|	КОНЕЦ КАК Картинка,
	|	СправочникСкидкиНаценки.РеквизитДопУпорядочивания КАК РеквизитДопУпорядочивания,
	|	СправочникСкидкиНаценки.Управляемая,
	|	СправочникСкидкиНаценки.ПометкаУдаления,
	|	
	|	СправочникСкидкиНаценки.Ссылка,
	|	СправочникСкидкиНаценки.Родитель,
	|	
	|	ВЫБОР КОГДА ЕСТЬNULL(ИспользованиеНаСкладах.Количество, 0) > 0 ТОГДА
	|		Истина
	|	ИНАЧЕ
	|		Ложь
	|	КОНЕЦ КАК ДействуетНаСкладах,
	|	ЕСТЬNULL(ИспользованиеНаСкладах.Количество, 0) КоличествоСкладов,
	|";
	
	Если ИспользоватьТиповыеСоглашенияСКлиентами Тогда
		Текст = Текст +
		"	ВЫБОР КОГДА ЕСТЬNULL(ИспользованиеВТиповыхСоглашениях.Количество, 0) > 0 ТОГДА
		|		Истина
		|	ИНАЧЕ
		|		Ложь
		|	КОНЕЦ КАК ДействуетВТиповыхСоглашениях,
		|	ЕСТЬNULL(ИспользованиеВТиповыхСоглашениях.Количество, 0) КоличествоТиповыхСоглашений,
		|";
	Иначе
		Текст = Текст +
		"	Ложь КАК ДействуетВТиповыхСоглашениях,
		|	0 КАК КоличествоТиповыхСоглашений,
		|";
	КонецЕсли;
	
	Если ИспользоватьИндивидуальныеСоглашенияСКлиентами Тогда
		Текст = Текст +
		"	ВЫБОР КОГДА ЕСТЬNULL(ИспользованиеВИндивидуальныхСоглашениях.Количество, 0) > 0 ТОГДА
		|		Истина
		|	ИНАЧЕ
		|		Ложь
		|	КОНЕЦ КАК ДействуетВИндивидуальныхСоглашениях,
		|	ЕСТЬNULL(ИспользованиеВИндивидуальныхСоглашениях.Количество, 0) КоличествоИндивидуальныхСоглашений,
		|";
	Иначе
		Текст = Текст +
		"	Ложь КАК ДействуетВИндивидуальныхСоглашениях,
		|	0 КАК КоличествоИндивидуальныхСоглашений,
		|";
	КонецЕсли;
	
	Если ИспользоватьКартыЛояльности Тогда
		Текст = Текст +
		"	ВЫБОР КОГДА ЕСТЬNULL(ИспользованиеВВидахКартЛояльности.Количество, 0) > 0 ТОГДА
		|		Истина
		|	ИНАЧЕ
		|		Ложь
		|	КОНЕЦ КАК ДействуетВКартахЛояльности,
		|	ЕСТЬNULL(ИспользованиеВВидахКартЛояльности.Количество, 0) КоличествоВидовКартЛояльности,
		|";
	Иначе
		Текст = Текст +
		"	Ложь КАК ДействуетВКартахЛояльности,
		|	0 КАК КоличествоВидовКартЛояльности,
		|";
	КонецЕсли;
	
	Текст = Текст +
	"	ВЫБОР КОГДА СправочникСкидкиНаценки.ЭтоГруппа ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДействиеСкидокНаценокСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.НеДействует))
	|	КОНЕЦ КАК Статус,
	|	
	|	ЕСТЬNULL(ДействиеСкидокНаценокСрезПоследних.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаНачала,
	|	
	|	ВЫБОР КОГДА ДействиеСкидокНаценокСрезПоследних.Период ЕСТЬ NULL ТОГДА
	|		ЕСТЬNULL(ДобавитьКДате(ДействиеСкидокНаценокСрезПервых.Период, ДЕНЬ, -1), ДАТАВРЕМЯ(1, 1, 1)) 
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДобавитьКДате(Таблица.Период, ДЕНЬ, -1), ДАТАВРЕМЯ(1, 1, 1))
	|	КОНЕЦ КАК ДатаОкончания
	|ИЗ
	|	Справочник.СкидкиНаценки КАК СправочникСкидкиНаценки
	|	
	|	{ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ТекущаяДата, Источник = &СкладПоУмолчанию) КАК ДействиеСкидокНаценокСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				МИНИМУМ(Т.Период) КАК Период,
	|				Т.СкидкаНаценка КАК СкидкаНаценка
	|			ИЗ
	|				РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ТекущаяДата, Источник = &СкладПоУмолчанию) КАК Срез
	|					ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДействиеСкидокНаценок КАК Т
	|					ПО (Т.СкидкаНаценка = Срез.СкидкаНаценка)
	|						И (Т.Период > Срез.Период)
	|			            И Т.Источник = &СкладПоУмолчанию
	|			            И Т.Статус <> Срез.Статус
	|			СГРУППИРОВАТЬ ПО
	|				Т.СкидкаНаценка) КАК Таблица
	|			ПО (Таблица.СкидкаНаценка = ДействиеСкидокНаценокСрезПоследних.СкидкаНаценка)
	|	ПО ДействиеСкидокНаценокСрезПоследних.СкидкаНаценка = СправочникСкидкиНаценки.Ссылка}
	|	
	|	{ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ДействиеСкидокНаценок.СрезПервых(&ТекущаяДата, Источник = &СкладПоУмолчанию) КАК ДействиеСкидокНаценокСрезПервых
	|	ПО ДействиеСкидокНаценокСрезПервых.СкидкаНаценка = СправочникСкидкиНаценки.Ссылка}
	|";
	
	Текст = Текст +
	"	{ЛЕВОЕ СОЕДИНЕНИЕ
	|		(ВЫБРАТЬ
	|			Т.СкидкаНаценка КАК СкидкаНаценка,
	|			Количество(Т.Источник) КАК Количество
	|		ИЗ РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ТекущаяДата, Источник ССЫЛКА Справочник.Склады И Источник <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК Т
	|		ГДЕ
	|			Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.Действует) СГРУППИРОВАТЬ ПО Т.СкидкаНаценка
	|		) КАК ИспользованиеНаСкладах
	|	ПО ИспользованиеНаСкладах.СкидкаНаценка = СправочникСкидкиНаценки.Ссылка}
	|";
	
	Если ИспользоватьКартыЛояльности Тогда
		Текст = Текст +
		"	{ЛЕВОЕ СОЕДИНЕНИЕ
		|		(ВЫБРАТЬ
		|			Т.СкидкаНаценка КАК СкидкаНаценка,
		|			Количество(Т.Источник) КАК Количество
		|		ИЗ РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ТекущаяДата, Источник ССЫЛКА Справочник.ВидыКартЛояльности) КАК Т
		|		ГДЕ
		|			Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.Действует) СГРУППИРОВАТЬ ПО Т.СкидкаНаценка
		|		) КАК ИспользованиеВВидахКартЛояльности
		|	ПО ИспользованиеВВидахКартЛояльности.СкидкаНаценка = СправочникСкидкиНаценки.Ссылка}
		|";
	КонецЕсли;
	
	Если ИспользоватьТиповыеСоглашенияСКлиентами Тогда
		Текст = Текст +
		"	{ЛЕВОЕ СОЕДИНЕНИЕ
		|		(ВЫБРАТЬ
		|			Т.СкидкаНаценка КАК СкидкаНаценка,
		|			Количество(Т.Источник) КАК Количество
		|		ИЗ РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ТекущаяДата, Источник ССЫЛКА Справочник.СоглашенияСКлиентами И ВЫРАЗИТЬ(Источник КАК Справочник.СоглашенияСКлиентами).Типовое) КАК Т
		|		ГДЕ
		|			Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.Действует) СГРУППИРОВАТЬ ПО Т.СкидкаНаценка
		|		) КАК ИспользованиеВТиповыхСоглашениях
		|	ПО ИспользованиеВТиповыхСоглашениях.СкидкаНаценка = СправочникСкидкиНаценки.Ссылка}
		|";
	КонецЕсли;
	
	Если ИспользоватьИндивидуальныеСоглашенияСКлиентами Тогда
		Текст = Текст +
		"	{ЛЕВОЕ СОЕДИНЕНИЕ
		|		(ВЫБРАТЬ
		|			Т.СкидкаНаценка КАК СкидкаНаценка,
		|			Количество(Т.Источник) КАК Количество
		|		ИЗ РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(&ТекущаяДата, Источник ССЫЛКА Справочник.СоглашенияСКлиентами И ВЫРАЗИТЬ(Источник КАК Справочник.СоглашенияСКлиентами).Типовое = ЛОЖЬ) КАК Т
		|		ГДЕ
		|			Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.Действует) СГРУППИРОВАТЬ ПО Т.СкидкаНаценка
		|		) КАК ИспользованиеВИндивидуальныхСоглашениях
		|	ПО ИспользованиеВИндивидуальныхСоглашениях.СкидкаНаценка = СправочникСкидкиНаценки.Ссылка}
		|";
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаДинамическогоСпискаИндивидуальныеСоглашения()
	
	Текст = 
	"ВЫБРАТЬ
	|	СоглашенияСКлиентами.Ссылка КАК Соглашение,
	|	СоглашенияСКлиентами.Статус КАК СтатусСоглашения,
	|	СоглашенияСКлиентами.Партнер КАК Партнер,
	|	ЕСТЬNULL(ДействиеСкидокНаценокСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.НеДействует)) КАК Статус,
	|	ВЫБОР
	|		КОГДА ДействиеСкидокНаценокСрезПоследних.Статус ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Использование,
	|	ЕСТЬNULL(ДействиеСкидокНаценокСрезПоследних.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА ДействиеСкидокНаценокСрезПоследних.Период ЕСТЬ NULL 
	|			ТОГДА ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ДействиеСкидокНаценокСрезПервых.Период, ДЕНЬ, -1), ДАТАВРЕМЯ(1, 1, 1))
	|		ИНАЧЕ ЕСТЬNULL(ДОБАВИТЬКДАТЕ(Таблица.Период, ДЕНЬ, -1), ДАТАВРЕМЯ(1, 1, 1))
	|	КОНЕЦ КАК ДатаОкончания
	|ИЗ
	|	Справочник.СоглашенияСКлиентами КАК СоглашенияСКлиентами
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(
	|				&ТекущаяДата,
	|				СкидкаНаценка = &СкидкаНаценка
	|					И Источник ССЫЛКА Справочник.СоглашенияСКлиентами) КАК ДействиеСкидокНаценокСрезПоследних
	|		ПО (ДействиеСкидокНаценокСрезПоследних.Источник = СоглашенияСКлиентами.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МИНИМУМ(Т.Период) КАК Период,
	|			Т.Источник КАК Источник
	|		ИЗ
	|			РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(
	|					&ТекущаяДата,
	|					СкидкаНаценка = &СкидкаНаценка
	|						И Источник ССЫЛКА Справочник.СоглашенияСКлиентами) КАК Срез
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДействиеСкидокНаценок КАК Т
	|				ПО Срез.Источник = Т.Источник
	|					И (Т.СкидкаНаценка = &СкидкаНаценка)
	|					И (Т.Период > Срез.Период)
	|					И (Т.Статус <> Срез.Статус)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Т.Источник) КАК Таблица
	|		ПО (Таблица.Источник = СоглашенияСКлиентами.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДействиеСкидокНаценок.СрезПервых(
	|				&ТекущаяДата,
	|				СкидкаНаценка = &СкидкаНаценка
	|					И Источник ССЫЛКА Справочник.СоглашенияСКлиентами) КАК ДействиеСкидокНаценокСрезПервых
	|		ПО (ДействиеСкидокНаценокСрезПервых.Источник = СоглашенияСКлиентами.Ссылка)
	|ГДЕ
	|	НЕ СоглашенияСКлиентами.Типовое";
	
	Возврат Текст;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаДинамическогоСпискаТиповыеСоглашения()
	
	Текст = 
	"ВЫБРАТЬ
	|	СоглашенияСКлиентами.Ссылка КАК Соглашение,
	|	СоглашенияСКлиентами.Ссылка.Статус КАК СтатусСоглашения,
	|	ЕСТЬNULL(ДействиеСкидокНаценокСрезПоследних.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыДействияСкидок.НеДействует)) КАК Статус,
	|	ВЫБОР
	|		КОГДА ДействиеСкидокНаценокСрезПоследних.Статус ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Использование,
	|	ЕСТЬNULL(ДействиеСкидокНаценокСрезПоследних.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА ДействиеСкидокНаценокСрезПоследних.Период ЕСТЬ NULL 
	|			ТОГДА ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ДействиеСкидокНаценокСрезПервых.Период, ДЕНЬ, -1), ДАТАВРЕМЯ(1, 1, 1))
	|		ИНАЧЕ ЕСТЬNULL(ДОБАВИТЬКДАТЕ(Таблица.Период, ДЕНЬ, -1), ДАТАВРЕМЯ(1, 1, 1))
	|	КОНЕЦ КАК ДатаОкончания
	|ИЗ
	|	Справочник.СоглашенияСКлиентами КАК СоглашенияСКлиентами
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(
	|				&ТекущаяДата,
	|				СкидкаНаценка = &СкидкаНаценка
	|					И Источник ССЫЛКА Справочник.СоглашенияСКлиентами) КАК ДействиеСкидокНаценокСрезПоследних
	|		ПО (ДействиеСкидокНаценокСрезПоследних.Источник = СоглашенияСКлиентами.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МИНИМУМ(Т.Период) КАК Период,
	|			Т.Источник КАК Источник
	|		ИЗ
	|			РегистрСведений.ДействиеСкидокНаценок.СрезПоследних(
	|					&ТекущаяДата,
	|					СкидкаНаценка = &СкидкаНаценка
	|						И Источник ССЫЛКА Справочник.СоглашенияСКлиентами) КАК Срез
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДействиеСкидокНаценок КАК Т
	|				ПО Срез.Источник = Т.Источник
	|					И (Т.СкидкаНаценка = &СкидкаНаценка)
	|					И (Т.Период > Срез.Период)
	|					И (Т.Статус <> Срез.Статус)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Т.Источник) КАК Таблица
	|		ПО (Таблица.Источник = СоглашенияСКлиентами.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДействиеСкидокНаценок.СрезПервых(
	|				&ТекущаяДата,
	|				СкидкаНаценка = &СкидкаНаценка
	|					И Источник ССЫЛКА Справочник.СоглашенияСКлиентами) КАК ДействиеСкидокНаценокСрезПервых
	|		ПО (ДействиеСкидокНаценокСрезПервых.Источник = СоглашенияСКлиентами.Ссылка)
	|ГДЕ
	|	СоглашенияСКлиентами.Типовое";
	
	Возврат Текст;
	
КонецФункции

#КонецОбласти
