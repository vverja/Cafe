
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Готовит дерево значений, в котором будет храниться список регламентированных 
// отчетов, на основе списка, хранящегося в макете обработке. Показывает дерево
// для установи пользователем отметок - какие именно отчеты следует обновлять.
//   На основе подготовленого списка выполяет обновление списка отчетов в 
// справочнике регламентированные отчеты.
//
&НаКлиенте
Процедура ОбновитьСписокОтчетов()
		
	ЗаполнитьДеревоОтчетов();

	Если ДеревоОтчетов.ПолучитьЭлементы().Количество() = 0 Тогда
		// нет обновлений
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДеревоОтчетов", ДеревоОтчетов);
	
	ФормаВыбораОтчетов = ПолучитьФорму("Обработка.ОбновлениеРегламентированнойОтчетности.Форма.ФормаВыбораОтчетов", ПараметрыФормы, , "дляВыбораРегламОтчетов");
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбновитьСписокОтчетовЗавершение", ЭтотОбъект);
	ФормаВыбораОтчетов.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
	ФормаВыбораОтчетов.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ФормаВыбораОтчетов.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокОтчетовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ДеревоВыбранныхОтчетов = Результат;


	Если ДеревоВыбранныхОтчетов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КопироватьДанныеФормы(ДеревоВыбранныхОтчетов, ДеревоОтчетов);
	

	ЗаполнитьСписокОтчетов();
	
	Оповестить("Обновить дерево отчетов", "Обновить дерево отчетов", ЭтаФорма)
	
КонецПроцедуры

// ЗаполнитьДеревоОтчетов()
//
&НаСервере
Процедура ЗаполнитьДеревоОтчетов()
	
	СписокОтчетов = РеквизитФормыВЗначение("Объект").ПолучитьСписокОтчетов();
	
	Если СписокОтчетов.Строки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
			
	// Заполним список отчетов
	ЗначениеВРеквизитФормы(СписокОтчетов, "ДеревоОтчетов");
		
КонецПроцедуры // ЗаполнитьДеревоОтчетов()

// ЗаполнитьСписокОтчетов()
//
&НаСервере
Процедура ЗаполнитьСписокОтчетов()
	
	РеквизитФормыВЗначение("Объект").ЗаполнитьСписокОтчетов(РеквизитФормыВЗначение("ДеревоОтчетов"));
	РеквизитФормыВЗначение("Объект").СкрытьВосстановитьОтчеты(РеквизитФормыВЗначение("ДеревоОтчетов"));
	РеквизитФормыВЗначение("Объект").УстановитьСнятьПометкуНаУдаление(РеквизитФормыВЗначение("ДеревоОтчетов"));
	
КонецПроцедуры // ЗаполнитьСписокОтчетов()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// ПриОткрытии()
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Отказ = Истина;

	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru='Обновить комплект отчетности?';uk='Оновити комплект звітності?'"), РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ОбновитьСписокОтчетов();
		
	КонецЕсли;
	
КонецПроцедуры