 
#Область ПрограммныйИнтерфейс


//Процедура обновляет кеш ключевых реквизитов текущей строки товаров.
// Параметры
//		ТаблицаФормы - таблица формы, отображающая ТЧ товаров
//      КэшированныеЗначения - переменная модуля формы, в которой храняться кешируемые значения
//      ПараметрыУказанияСерий - структура параметров указания серий, возвращаемая соответствующей процедурой модуля менеджера документа
//      Копирование - признак, что кешированная строка скопирована (параметр события ПриНачалеРедактирования)
//
Процедура ОбновитьКешированныеЗначенияДляТЧСУпаковочнымиЛистами(ТаблицаФормы, КэшированныеЗначения,
	ПараметрыУказанияСерий = Неопределено,Копирование = Ложь) Экспорт
	
	Если КэшированныеЗначения = Неопределено Тогда
		КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	КонецЕсли;
	КэшированныеЗначения.Вставить("ДобавлениеСтрок", Ложь);
	КэшированныеЗначения.Вставить("РедактированиеСтрок", Ложь);
	КэшированныеЗначения.Вставить("УпаковочныйЛист", ПредопределенноеЗначение("Документ.УпаковочныйЛист.ПустаяСсылка"));
	КэшированныеЗначения.Вставить("Номенклатура", ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
	КэшированныеЗначения.Вставить("ЭтоУпаковочныйЛист", Ложь);
	КэшированныеЗначения.Вставить("УдалениеСтрок", Ложь);
	
	ТекущиеДанные = ТаблицаФормы.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(КэшированныеЗначения, ТекущиеДанные);
	КонецЕсли;
	
	Если ПараметрыУказанияСерий <> Неопределено Тогда
		НоменклатураКлиент.ОбновитьКешированныеЗначенияДляУчетаСерий(
			ТаблицаФормы,КэшированныеЗначения,ПараметрыУказанияСерий, Копирование);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриНачалеРедактированияТЧСУпаковочнымиЛистами(Форма, КэшированныеЗначения, НоваяСтрока) Экспорт
	
	ИмяТЧТовары = ПараметрыУказанияСерий(Форма).ИмяТЧТовары;
	
	ТекущиеДанные = Форма.Элементы[ИмяТЧТовары].ТекущиеДанные;
	
	Если НоваяСтрока Тогда
		Форма.МаксимальныйНомерСтроки = Форма.МаксимальныйНомерСтроки + 1;
		ТекущиеДанные.НомерСтрокиОтображаемый = Форма.МаксимальныйНомерСтроки;
		Если ЗначениеЗаполнено(Форма.УпаковочныйЛистРодитель) Тогда
			ТекущиеДанные.УпаковочныйЛистРодитель = Форма.УпаковочныйЛистРодитель;
		КонецЕсли;
	КонецЕсли;
	ОбновитьКешированныеЗначенияДляТЧСУпаковочнымиЛистами(Форма.Элементы[ИмяТЧТовары], КэшированныеЗначения);
	КэшированныеЗначения.РедактированиеСтрок = Истина;
	
КонецПроцедуры

Процедура ПриОкончанииРедактированияТЧСУпаковочнымиЛистами(Форма, НоваяСтрока, ОтменаРедактирования) Экспорт
	
	Если НоваяСтрока
		И ОтменаРедактирования Тогда
		Форма.МаксимальныйНомерСтроки = Форма.МаксимальныйНомерСтроки - 1;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередНачаломДобавленияВТЧСУпаковочнымиЛистами(ТаблицаФормы, Отказ, Копирование, КэшированныеЗначения) Экспорт
	
	ОбновитьКешированныеЗначенияДляТЧСУпаковочнымиЛистами(ТаблицаФормы, КэшированныеЗначения);
	ТекущиеДанные = ТаблицаФормы.ТекущиеДанные;
	Если Копирование
		И ТекущиеДанные <> Неопределено
		И ТекущиеДанные.ЭтоУпаковочныйЛист Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(,НСтр("ru='Копирование строк с упаковочными листами не предусмотрено';uk='Копіювання рядків з пакувальними листами не передбачено'"));
	КонецЕсли;
	
	КэшированныеЗначения.ДобавлениеСтрок = Истина;
	
КонецПроцедуры

Процедура РаспаковатьУпаковочныйЛист(Форма) Экспорт
	ИмяТЧТовары = ПараметрыУказанияСерий(Форма).ИмяТЧТовары;
	
	ТекущиеДанные = Форма.Элементы[ИмяТЧТовары].ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ТекстПредупреждения = НСтр("ru='Выберите строку для распаковки.';uk='Виберіть рядок для розпакування.'");
		ПоказатьПредупреждение(,ТекстПредупреждения);
		Возврат;
	ИначеЕсли Не ЗначениеЗаполнено(ТекущиеДанные.УпаковочныйЛистРодитель)
		И Не ЗначениеЗаполнено(ТекущиеДанные.УпаковочныйЛист) Тогда
		ТекстПредупреждения = НСтр("ru='Выбранная строка не включена в упаковочный лист, распаковка невозможна.';uk='Обраний рядок не включений в пакувальний лист, розпакування неможливе.'");
		ПоказатьПредупреждение(,ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru='Распаковать?';uk='Розпакувати?'");
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОРаспаковке", Форма);
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

Функция ПроверитьПодготовитьПереходВУпаковочныйЛистПриВыборе(ТаблицаФормы, ИмяПоля, ИмяТЧ = "Товары") Экспорт
	
	ТекущиеДанные = ТаблицаФормы.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если (ИмяПоля = ИмяТЧ + "Картинка"
		Или ИмяПоля = ИмяТЧ + "НомерСтрокиОтображаемый")
	  И ТекущиеДанные.ЭтоУпаковочныйЛист
	  И ЗначениеЗаполнено(ТекущиеДанные.УпаковочныйЛист) Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ПередУдалениемСтрокТЧСУпаковочнымиЛистами(ТаблицаФормы, КэшированныеЗначения, ПараметрыУказанияСерий) Экспорт
	
	ОбновитьКешированныеЗначенияДляТЧСУпаковочнымиЛистами(ТаблицаФормы, КэшированныеЗначения, ПараметрыУказанияСерий);
	КэшированныеЗначения.УдалениеСтрок = Истина;
	
КонецПроцедуры

Процедура НачалоВыбораТоварногоМеста(Форма, Элемент, СтандартнаяОбработка, РежимПросмотраПоТоварам = Ложь) Экспорт
	
	Если Не Форма.ИспользоватьУпаковочныеЛисты Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимПросмотраПоТоварам Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ИмяТЧТовары = ПараметрыУказанияСерий(Форма).ИмяТЧТовары;
	ТекущиеДанные = Форма.Элементы[ИмяТЧТовары].ТекущиеДанные;
	
	Список = Новый СписокЗначений;
	ПредставлениеНоменклатуры = НСтр("ru='Номенклатура';uk='Номенклатура'");
	ПредставлениеУпаковочногоЛиста = НСтр("ru='Упаковочный лист';uk='Пакувальний лист'");
	Список.Добавить(ПредставлениеНоменклатуры);
	Список.Добавить(ПредставлениеУпаковочногоЛиста);
	ДополнительныеПараметры = Новый Структура("Форма", Форма);
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ПослеВыбораТипаТоварногоМеста", ЭтотОбъект, ДополнительныеПараметры);
	Форма.ПоказатьВыборИзСписка(ОписаниеОповещенияОЗакрытии, Список, Элемент);
	
КонецПроцедуры

Процедура ПослеВыбораТипаТоварногоМеста(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТЧТовары = ПараметрыУказанияСерий(ДополнительныеПараметры.Форма).ИмяТЧТовары;
	ТекущиеДанные = ДополнительныеПараметры.Форма.Элементы[ИмяТЧТовары].ТекущиеДанные;
	
	ПредставлениеНоменклатуры      = НСтр("ru='Номенклатура';uk='Номенклатура'");
	ПредставлениеУпаковочногоЛиста = НСтр("ru='Упаковочный лист';uk='Пакувальний лист'");
	
	Если Результат.Значение = ПредставлениеНоменклатуры Тогда
		ПараметрыФормы = Новый Структура;
		ОтборПоТипу = Новый Массив;
		ОтборПоТипу.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар"));
		ОтборПоТипу.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара"));
		ПараметрыФормы.Вставить("Отбор", Новый Структура("ТипНоменклатуры", ОтборПоТипу));
		ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаВыбора", ПараметрыФормы, ДополнительныеПараметры.Форма);
	ИначеЕсли Результат.Значение = ПредставлениеУпаковочногоЛиста Тогда
		ПараметрыФормы = Новый Структура("Отбор, Штрихкод",Новый Структура("Проведен",Истина), "");
		ЗаполнитьЗначенияСвойств(ПараметрыФормы, ТекущиеДанные);
		ОткрытьФорму("Документ.УпаковочныйЛист.Форма.ФормаВыбора", ПараметрыФормы, ДополнительныеПараметры.Форма);
	КонецЕсли;
КонецПроцедуры

//Обработчик команды "Разбить строку" с упаковочными листами
//
//	Параметры:
//		ТЧ - ДанныеФормыКоллеция
//		ДанныеФормы - ТаблицаФормы
//		ОповещениеПослеРазбиения - ОписаниеОповещения
//		ПараметрыРазбиенияСтроки - см. ОбщегоНазначенияУТКлиент.ПараметрыРазбиенияСтроки
//
Процедура РазбитьСтрокуТЧСУпаковочнымиЛистами(ТЧ, ДанныеФормы, ОповещениеПослеРазбиения, ПараметрыРазбиенияСтроки = Неопределено) Экспорт
	
	ТекущаяСтрока = ДанныеФормы.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		ТекстСообщения = НСтр("ru='Для выполнения команды требуется выбрать строку табличной части.';uk='Для виконання команди потрібно вибрати рядок табличної частини.'");
		ПоказатьПредупреждение(,ТекстСообщения);
		ВыполнитьОбработкуОповещения(ОповещениеПослеРазбиения, Неопределено);
	ИначеЕсли ТекущаяСтрока.ЭтоУпаковочныйЛист Тогда
		ТекстСообщения = НСтр("ru='Невозможно разбить строку с упаковочным листом.';uk='Неможливо розбити рядок з пакувальним листом.'");
		ПоказатьПредупреждение(,ТекстСообщения);
		ВыполнитьОбработкуОповещения(ОповещениеПослеРазбиения, Неопределено);
	Иначе
		ОбщегоНазначенияУТКлиент.РазбитьСтрокуТЧ(ТЧ, ДанныеФормы, ОповещениеПослеРазбиения, ПараметрыРазбиенияСтроки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыУказанияСерий(Форма)
	
	Если Форма.ПараметрыУказанияСерий.Свойство("ОтгружаемыеТовары") Тогда
		Возврат Форма.ПараметрыУказанияСерий.ОтгружаемыеТовары;
	Иначе
		Возврат Форма.ПараметрыУказанияСерий;
	КонецЕсли;	
	
КонецФункции

#КонецОбласти




