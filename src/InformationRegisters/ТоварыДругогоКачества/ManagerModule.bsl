#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ПроверитьНаличиеГрадации(Номенклатура, НекачественныйТовар) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТоварыДругогоКачества.НоменклатураБрак КАК НекачественныйТовар
	|ИЗ
	|	РегистрСведений.ТоварыДругогоКачества КАК ТоварыДругогоКачества
	|ГДЕ
	|	ТоварыДругогоКачества.Номенклатура = &Номенклатура
	|			И ТоварыДругогоКачества.НоменклатураБрак = &НоменклатураБрак
	|			И ТоварыДругогоКачества.ГрадацияКачества = &ГрадацияКачества";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("НоменклатураБрак", НекачественныйТовар);
	Запрос.УстановитьПараметр("ГрадацияКачества", НекачественныйТовар.Качество);
	Результат = Запрос.Выполнить();
	ЕстьСоответствие = Не Результат.Пустой();
	Возврат ЕстьСоответствие;
КонецФункции

Процедура ЗаписатьСвязьСТоваромДругогоКачества(КачественныйТовар, НекачественныйТовар) Экспорт
	
	ТекстСообщения = "";
	Если НекачественныйТовар.Качество = Перечисления.ГрадацииКачества.Новый Тогда
		ТекстСообщения = НСтр("ru='Невозможно установить соответствие. Некачественный товар не может иметь качество ""Новый""';uk='Неможливо встановити відповідність. Неякісний товар не може мати якість ""Новий""'");
	ИначеЕсли  НекачественныйТовар.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга Тогда
		ТекстСообщения = НСтр("ru='Невозможно установить соответствие. Некачественный товар не может иметь тип номенклатуры ""Услуга""';uk='Неможливо встановити відповідність. Неякісний товар не може мати тип номенклатури ""Послуга""'");
	ИначеЕсли  НекачественныйТовар.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа Тогда
		ТекстСообщения = НСтр("ru='Невозможно установить соответствие. Некачественный товар не может иметь тип номенклатуры ""Работа""';uk='Неможливо встановити відповідність. Неякісний товар не може мати тип номенклатури ""Робота""'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат
	КонецЕсли;
		
	Если КачественныйТовар.Качество <> Перечисления.ГрадацииКачества.Новый Тогда
		ЗапросПоСоответствиюНоменклатуры = Новый Запрос;
		ЗапросПоСоответствиюНоменклатуры.УстановитьПараметр("НоменклатураБрак", КачественныйТовар);
		ЗапросПоСоответствиюНоменклатуры.Текст = 
		"ВЫБРАТЬ
		|	ТоварыДругогоКачества.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрСведений.ТоварыДругогоКачества КАК ТоварыДругогоКачества
		|ГДЕ
		|	ТоварыДругогоКачества.НоменклатураБрак = &НоменклатураБрак";
		Результат = ЗапросПоСоответствиюНоменклатуры.Выполнить();
		Если Результат.Пустой() Тогда
			ШаблонТекстаСообщенияОбОшибке = НСтр("ru='Невозможно найти качественный товар соответствующий товару %НекачественныйТовар%';uk='Неможливо знайти якісний товар, який відповідає товару %НекачественныйТовар%'");
			ТекстСообщения = СтрЗаменить(ШаблонТекстаСообщенияОбОшибке, "%НекачественныйТовар%", НекачественныйТовар.Наименование);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		Иначе
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			КачественныйТовар = Выборка.Номенклатура;
		КонецЕсли;
	КонецЕсли;
	
	ЗаписьРегистраСведений = РегистрыСведений.ТоварыДругогоКачества.СоздатьМенеджерЗаписи();
	ЗаписьРегистраСведений.Номенклатура 	= КачественныйТовар;
	ЗаписьРегистраСведений.НоменклатураБрак = НекачественныйТовар;
	ЗаписьРегистраСведений.ГрадацияКачества = НекачественныйТовар.Качество;
	ЗаписьРегистраСведений.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СпрНоменклатура.ЕстьТоварыДругогоКачества КАК ЕстьТоварыДругогоКачестваВБазе,
	|	ВЫБОР
	|		КОГДА ТоварыДругогоКачества.НоменклатураБрак ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьТоварыДругогоКачестваПоФакту
	|ИЗ
	|	Справочник.Номенклатура КАК СпрНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТоварыДругогоКачества КАК ТоварыДругогоКачества
	|		ПО СпрНоменклатура.Ссылка = ТоварыДругогоКачества.Номенклатура
	|ГДЕ
	|	СпрНоменклатура.Ссылка = &Номенклатура
	|	И СпрНоменклатура.ЕстьТоварыДругогоКачества <> ВЫБОР
	|			КОГДА ТоварыДругогоКачества.НоменклатураБрак ЕСТЬ NULL 
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ";
	
	Запрос.УстановитьПараметр("Номенклатура",КачественныйТовар);	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Объект = КачественныйТовар.ПолучитьОбъект();
		Объект.ЕстьТоварыДругогоКачества = Выборка.ЕстьТоварыДругогоКачестваПоФакту;		
		Объект.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьТоварИсходногоКачества(Товар) Экспорт
	Если Товар.Качество <> Перечисления.ГрадацииКачества.Новый Тогда
		КачественныйТовар = Неопределено;
		ЗапросПоКачественномуТовару = Новый Запрос;
		ЗапросПоКачественномуТовару.УстановитьПараметр("Номенклатура", Товар);
		ЗапросПоКачественномуТовару.Текст =
		"ВЫБРАТЬ
		|	ТоварыДругогоКачества.Номенклатура КАК КачественныйТовар
		|ИЗ
		|	РегистрСведений.ТоварыДругогоКачества КАК ТоварыДругогоКачества
		|ГДЕ
		|	ТоварыДругогоКачества.Номенклатура.Качество = ЗНАЧЕНИЕ(Перечисление.ГрадацииКачества.Новый)
		|	И ТоварыДругогоКачества.НоменклатураБрак = &Номенклатура";
		Выборка = ЗапросПоКачественномуТовару.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			КачественныйТовар = Выборка.КачественныйТовар;
		КонецЦикла;
		Возврат КачественныйТовар;
	Иначе 
		Возврат Товар;
	КонецЕсли;
КонецФункции


#КонецЕсли