
#Область ПрограммныйИнтерфейс

#Область ПроцедурыИФункцииФормированияОтложенныхДвижений

// Метод вызывается из регламентного задания "Формирование движений по расчету с партнерами".
Процедура ОтложенноеПроведениеПоРасчетамСПартнерами() Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено ИЛИ ЗакрытиеМесяцаУТВызовСервера.АктивноЗаданиеЗакрытияМесяца() Тогда
		Возврат;
	КонецЕсли;
	
	КонецРасчета = КонецМесяца(ТекущаяДатаСеанса())+1;
	РаспределениеВзаиморасчетов.РаспределитьРасчетыФоновымЗаданием(КонецРасчета);
КонецПроцедуры

// Метод возвращает массив ключей аналитики, по которым будет выполняться расчет.
// Параметры:
//	ПоляОтбора - Структура - Содержит в себе значения полей: организация, партнер и контрагент.
// ВозвращаемоеЗначение:
//	Массив - Массив ключей аналитик по партнерам
Функция АналитикиКРасчету(ПоляОтбора) Экспорт
	Организация = Неопределено;
	Партнер = Неопределено;
	Контрагент = Неопределено;
	Договор = Неопределено;
	
	ПоляОтбора.Свойство("Организация", Организация);
	ПоляОтбора.Свойство("Партнер", Партнер);
	ПоляОтбора.Свойство("Контрагент", Контрагент);
	ПоляОтбора.Свойство("Договор", Договор);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Ключи.КлючАналитики КАК Ключ
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПоПартнерам КАК Ключи
	|ГДЕ
	|	(Ключи.Организация В (&Организация) ИЛИ &ПоВсемОрганизациям)
	|	И (Ключи.Партнер В (&Партнер) ИЛИ &ПоВсемПартнерам)
	|	И (Ключи.Контрагент В (&Контрагент) ИЛИ &ПоВсемКонтрагентам)
	|	И (Ключи.Договор В (&Договор) ИЛИ &ПоВсемДоговорам)
	|");

	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", НЕ ЗначениеЗаполнено(ПоляОтбора.Организация));
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.УстановитьПараметр("ПоВсемПартнерам", НЕ ЗначениеЗаполнено(ПоляОтбора.Партнер));
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("ПоВсемКонтрагентам", НЕ ЗначениеЗаполнено(ПоляОтбора.Контрагент));
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("ПоВсемДоговорам", НЕ ЗначениеЗаполнено(ПоляОтбора.Договор));
	
	Результат = Запрос.Выполнить().Выгрузить();
	Возврат Результат.ВыгрузитьКолонку("Ключ");
КонецФункции

// Возвращает имена полей по умолчанию для использования в отчетах.
// ВозвращаемоеЗначение:
//  Структура - Содержит в себе поля "Организация, Партнер, Контрагент, Период"
Функция ПоляОтбораПоУмолчанию() Экспорт
	Возврат Новый Структура("Организация, Партнер, Контрагент, Договор, Период", "Организация", "Партнер", "Контрагент", "Договор", "Период");
КонецФункции

// Метод вызывается из отчетов. Восстанавливает указанные расчеты только по указанным отборам в отчете.
// Параметры:
//		КомпоновщикНастроек - КомпоновщикНастроек -  Компоновщик настроек отчета.
//		ПоляОтбора - Структура - Содержит в себе обязательные поля аналитик с указанием,
//								 как данные поля называются в отчете.
//		ДопСвойства - Структура - Содержит в себе доп. свойства отчета. При запуске расчета 
//								  в доп. свойствах взводится признак "ГраницаВзаиморасчетов".
//								  Проверяется другими механизмами, показывает что расчет не актуален.
//		РасчетКВосстановлению - Строка - Имя расчета к восстановлению. Если параметр не заполнен, 
//										то выполнятся все расчеты.
Процедура ВосстановитьРасчетыПоОтборам(КомпоновщикНастроек, ПоляОтбора, ДопСвойства, РасчетКВосстановлению = Неопределено) Экспорт
	
	Если НЕ Константы.АктуализироватьВзаморасчетыПриФормированииОтчетов.Получить() Тогда
		Возврат;
	КонецЕсли;
	
	КонецРасчета = ОтчетыУТКлиентСервер.ГраницаРасчета(КомпоновщикНастроек, ПоляОтбора.Период);
	
	ЗначенияПолей = Новый Структура;
	ОперацииСравнения = Новый Массив();
	ОперацииСравнения.Добавить(ВидСравненияКомпоновкиДанных.Равно);
	ОперацииСравнения.Добавить(ВидСравненияКомпоновкиДанных.ВСписке);
	
	Для Каждого Поле Из ПоляОтбора Цикл
		Значение = ОтчетыУТКлиентСервер.ПолучитьЗначениеОтбора(КомпоновщикНастроек, Поле.Ключ, ОперацииСравнения);
		ЗначенияПолей.Вставить(Поле.Ключ, Значение);
	КонецЦикла;

	АналитикиКРасчету = АналитикиКРасчету(ЗначенияПолей);
	РаспределитьРасчетыФоновымЗаданием(КонецРасчета, АналитикиКРасчету, РасчетКВосстановлению);
	
	НачалоРасчета = НачалоРасчетов(КонецРасчета, АналитикиКРасчету, РасчетКВосстановлению);
	ДопСвойства.Вставить("ГраницаВзаиморасчетов", НачалоРасчета);
	Если ЗначениеЗаполнено(НачалоРасчета) Тогда
		ИмяРасчета = ?(ЗначениеЗаполнено(РасчетКВосстановлению), РасчетКВосстановлению, "Взаиморасчеты");
		КРасчету = ВсегоДокументовКРасчету(НачалоРасчета, КонецРасчета, АналитикиКРасчету, РасчетКВосстановлению);
		ДопСвойства.Вставить("НомерЗадания", 0);
		ДопСвойства.Вставить("АналитикиКРасчету", АналитикиКРасчету);
		ДопСвойства.Вставить("ИмяРасчета", ИмяРасчета);
		ДопСвойства.Вставить("КРасчету", КРасчету);
	КонецЕсли;
КонецПроцедуры

// Возвращает минимальную дату, на которую неактуален расчет (расчеты).
// Параметры:
//		КонецРасчета - Дата - Верхняя граница анализа актуальности расчетов. 
//		АналитикиКРасчету - Массив - Массив аналитик по партнерам, по которым важно знать актуальность.
//		РасчетКВосстановлению - Строка - Имя расчета для анализа. Если параметр не указан,
//										 то анализируются все расчеты.
//		НомерЗадания - Число - Если заполнено, то анализируются расчеты только по этому номеру.
Функция НачалоРасчетов(КонецРасчета, АналитикиКРасчету = Неопределено, РасчетКВосстановлению = Неопределено, НомерЗадания = Неопределено) Экспорт
	Если РасчетКВосстановлению = "РасчетыСКлиентами" Тогда
		НомерЗадания = ?(ЗначениеЗаполнено(НомерЗадания), НомерЗадания, Константы.НомерЗаданияКРаспределениюРасчетовСКлиентами.Получить());
		ГраницаРасчетовСКлиентами = НачалоРасчета("ЗаданияКРаспределениюРасчетовСКлиентами", КонецРасчета, АналитикиКРасчету, НомерЗадания);
		ГраницаРасчетовСПоставщиками = Дата("39991212");
	ИначеЕсли РасчетКВосстановлению = "РасчетыСПоставщиками" Тогда
		НомерЗадания = ?(ЗначениеЗаполнено(НомерЗадания), НомерЗадания, Константы.НомерЗаданияКРаспределениюРасчетовСПоставщиками.Получить());
		ГраницаРасчетовСПоставщиками = НачалоРасчета("ЗаданияКРаспределениюРасчетовСПоставщиками", КонецРасчета, АналитикиКРасчету, НомерЗадания);
		ГраницаРасчетовСКлиентами = Дата("39991212");
	Иначе
		НомерЗадания = Константы.НомерЗаданияКРаспределениюРасчетовСКлиентами.Получить();
		ГраницаРасчетовСКлиентами = НачалоРасчета("ЗаданияКРаспределениюРасчетовСКлиентами", КонецРасчета, АналитикиКРасчету, НомерЗадания);
		НомерЗадания = Константы.НомерЗаданияКРаспределениюРасчетовСПоставщиками.Получить();
		ГраницаРасчетовСПоставщиками = НачалоРасчета("ЗаданияКРаспределениюРасчетовСПоставщиками", КонецРасчета, АналитикиКРасчету, НомерЗадания);
	КонецЕсли;
	
	Если Мин(ГраницаРасчетовСКлиентами, ГраницаРасчетовСПоставщиками) < КонецРасчета Тогда
		ГраницаРасчетов = Мин(ГраницаРасчетовСКлиентами, ГраницаРасчетовСПоставщиками);
	Иначе
		ГраницаРасчетов = Неопределено;
	КонецЕсли;
	Возврат ГраницаРасчетов;
КонецФункции

// Выполняет полный расчеты с клиентами и поставщиками по указанную дату.
// Параметры:
//		ОкончаниеПериодаРасчета - Дата - Граница по которую выполняется расчет.
// 		АналитикиРасчета - Массив - Массив ключей аналитик, по которым требуется выполнить расчет.
Процедура РассчитатьВсе(ОкончаниеПериодаРасчета, АналитикиРасчета = Неопределено) Экспорт
	
	ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(НСтр("ru='Движения по расчетам с партнерами';uk='Рухи за розрахунками з партнерами'"));
	
	УстановитьПривилегированныйРежим(Истина);
	
	РаспределитьВсеРасчетыСКлиентами(ОкончаниеПериодаРасчета, АналитикиРасчета);
	РаспределитьВсеРасчетыСПоставщиками(ОкончаниеПериодаРасчета, АналитикиРасчета);
	
КонецПроцедуры

// Выполняет полный расчет с клиентами по указанную дату.
// Параметры:
//		ОкончаниеПериодаРасчета - Дата - Граница по которую выполняется расчет.
// 		АналитикиРасчета - Массив - Массив ключей аналитик, по которым требуется выполнить расчет.
Процедура РаспределитьВсеРасчетыСКлиентами(ОкончаниеПериодаРасчета, АналитикиКРасчету = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	УровеньИнформация = УровеньЖурналаРегистрации.Информация;
	ФорматПериода = "ДФ='yyyy.MM.dd HH:mm:ss'";
	КодЯзыка = Метаданные.ОсновнойЯзык.КодЯзыка;

	
	Комментарий = НСтр("ru='Выполняется распределение расчетов с клиентами по %ОкончаниеПериода%';uk='Виконується розподіл розрахунків з клієнтами по %ОкончаниеПериода%'",КодЯзыка);
	Комментарий = СтрЗаменить(Комментарий, "%ОкончаниеПериода%", Формат(ОкончаниеПериодаРасчета, ФорматПериода));
	ЗаписьЖурналаРегистрации(
		НСтр("ru='Взаиморасчеты.Начало распределение расчетов';uk='Взаєморозрахунки.Початок розподіл розрахунків'",КодЯзыка), УровеньИнформация, , ОкончаниеПериодаРасчета, Комментарий);
		
	ИмяРегистраЗаданий = "ЗаданияКРаспределениюРасчетовСКлиентами";
	ИмяКонстантыЗаданий = "НомерЗаданияКРаспределениюРасчетовСКлиентами";
	ИмяОперативногоРегистра = "РасчетыСКлиентами";
	
	НачальныеГраницы = НДСОбщегоНазначенияСервер.ПодготовитьТаблицуНачальныхГраниц();
	
	ПервыйПериод = НачалоРасчета(ИмяРегистраЗаданий, ОкончаниеПериодаРасчета, АналитикиКРасчету);
	НачалоПериода = НачалоМесяца(ПервыйПериод);
	ПервыйПроход = Истина;
	Пока НачалоПериода < ОкончаниеПериодаРасчета Цикл
		Если ПервыйПроход Тогда
			НомерЗаданияДоРасчета = УвеличитьНомерЗадания(ИмяКонстантыЗаданий);
			ПервыйПроход = Ложь;
		КонецЕсли;
		ОкончаниеПериода = КонецМесяца(НачалоПериода);
		
		// количество аналитик расчета каждый месяц может меняться
		АналитикиРасчета = АналитикиРасчета(ИмяОперативногоРегистра, ИмяРегистраЗаданий, НачалоПериода, НомерЗаданияДоРасчета, АналитикиКРасчету);
		СписокОрганизаций = СписокОрганизаций(АналитикиРасчета);
		
		РасчетныеРасчетыСКлиентами = Неопределено;
		// Этап 1Х: распределение расчетов с клиентами
		РассчитатьРасчетыСКлиентами(НачалоПериода, ОкончаниеПериода, АналитикиРасчета, РасчетныеРасчетыСКлиентами);
		
		АналитикиПослеРасчета = РасчетныеРасчетыСКлиентами.ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
		НДСОбщегоНазначенияСервер.ДополнитьТаблицуНачальныхГраниц(НачальныеГраницы, НачалоПериода, АналитикиПослеРасчета);
		
		// Этап 2Х: Переоценка расчетов на конец периода
		Документы.ПереоценкаВалютныхСредств.ПереоценитьРасчетыСКлиентами(СписокОрганизаций, КонецМесяца(НачалоПериода), АналитикиРасчета);
		
		// Этап 3Х: Установка новых границ расчета
		УстановитьГраницыРасчетов(НачалоПериода, ОкончаниеПериода, ИмяРегистраЗаданий, ИмяОперативногоРегистра, ИмяКонстантыЗаданий, АналитикиРасчета, НомерЗаданияДоРасчета);
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru='Взаиморасчеты. Распределены расчеты с клиентами за период';uk='Взаєморозрахунки. Розподілені розрахунки з клієнтами за період'",КодЯзыка), УровеньИнформация, , Формат(НачалоПериода, "ДФ='yyyy.MM'"));
		
		ПрошлыйПериод = НачалоПериода;
		НачалоПериода = НачалоРасчета(ИмяРегистраЗаданий, ОкончаниеПериодаРасчета, АналитикиКРасчету, НомерЗаданияДоРасчета);
	КонецЦикла;
	
	НДСИсходящийСервер.ВыполнитьПроведениеДокументовПоВозникшемуНДС(НачальныеГраницы);
	
	Комментарий = НСтр("ru='Распределены расчеты с клиентами с %НачалоПериода% по %ОкончаниеПериода%';uk='Розподілені розрахунки з клієнтами з %НачалоПериода% по %ОкончаниеПериода%'",КодЯзыка);
	Комментарий = СтрЗаменить(Комментарий, "%НачалоПериода%", Формат(ПервыйПериод, ФорматПериода));
	Комментарий = СтрЗаменить(Комментарий, "%ОкончаниеПериода%", Формат(ОкончаниеПериодаРасчета, ФорматПериода));
	ЗаписьЖурналаРегистрации(
		НСтр("ru='Взаиморасчеты.Окончание распределения расчетов';uk='Взаєморозрахунки.Закінчення розподілу розрахунків'",КодЯзыка), УровеньИнформация, , ОкончаниеПериодаРасчета, Комментарий);
КонецПроцедуры

// Выполняет полный расчет с поставщиками по указанную дату.
// Параметры:
//		ОкончаниеПериодаРасчета - Дата - Граница по которую выполняется расчет.
// 		АналитикиРасчета - Массив - Массив ключей аналитик, по которым требуется выполнить расчет.
Процедура РаспределитьВсеРасчетыСПоставщиками(ОкончаниеПериодаРасчета, АналитикиКРасчету = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	УровеньИнформация = УровеньЖурналаРегистрации.Информация;
	ФорматПериода = "ДФ='yyyy.MM.dd HH:mm:ss'";
	КодЯзыка = Метаданные.ОсновнойЯзык.КодЯзыка;

	
	Комментарий = НСтр("ru='Выполняется распределение расчетов с поставщиками по %ОкончаниеПериода%';uk='Виконується розподіл розрахунків з постачальниками по %ОкончаниеПериода%'",КодЯзыка);
	Комментарий = СтрЗаменить(Комментарий, "%ОкончаниеПериода%", Формат(ОкончаниеПериодаРасчета, ФорматПериода));
	ЗаписьЖурналаРегистрации(
		НСтр("ru='Взаиморасчеты.Начало распределение расчетов';uk='Взаєморозрахунки.Початок розподіл розрахунків'",КодЯзыка), УровеньИнформация, , ОкончаниеПериодаРасчета, Комментарий);
		
	ИмяРегистраЗаданий = "ЗаданияКРаспределениюРасчетовСПоставщиками";
	ИмяКонстантыЗаданий = "НомерЗаданияКРаспределениюРасчетовСПоставщиками";
	ИмяОперативногоРегистра = "РасчетыСПоставщиками";
	
	НачальныеГраницы = НДСОбщегоНазначенияСервер.ПодготовитьТаблицуНачальныхГраниц();
	
	ПервыйПериод = НачалоРасчета(ИмяРегистраЗаданий, ОкончаниеПериодаРасчета, АналитикиКРасчету);
	НачалоПериода = НачалоМесяца(ПервыйПериод);
	ПервыйПроход = Истина;
	Пока НачалоПериода < ОкончаниеПериодаРасчета Цикл
		Если ПервыйПроход Тогда
			НомерЗаданияДоРасчета = УвеличитьНомерЗадания(ИмяКонстантыЗаданий);
			ПервыйПроход = Ложь;
		КонецЕсли;
		ОкончаниеПериода = КонецМесяца(НачалоПериода);
		
		// количество аналитик расчета каждый месяц может меняться
		АналитикиРасчета = АналитикиРасчета(ИмяОперативногоРегистра, ИмяРегистраЗаданий, НачалоПериода, НомерЗаданияДоРасчета, АналитикиКРасчету);
		СписокОрганизаций = СписокОрганизаций(АналитикиРасчета);
		
		РасчетныеРасчетыСПоставщиками = Неопределено;
		// Этап 1Х: распределение расчетов с поставщиками
		РассчитатьРасчетыСПоставщиками(НачалоПериода, ОкончаниеПериода, АналитикиРасчета, РасчетныеРасчетыСПоставщиками);
		
		АналитикиПослеРасчета = РасчетныеРасчетыСПоставщиками.ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
		НДСОбщегоНазначенияСервер.ДополнитьТаблицуНачальныхГраниц(НачальныеГраницы, НачалоПериода, АналитикиПослеРасчета);
		
		// Этап 2Х: Переоценка расчетов на конец периода
		Документы.ПереоценкаВалютныхСредств.ПереоценитьРасчетыСПоставщиками(СписокОрганизаций, КонецМесяца(НачалоПериода), АналитикиРасчета);
		
		// Этап 3Х: Установка новых границ расчета
		УстановитьГраницыРасчетов(НачалоПериода, ОкончаниеПериода, ИмяРегистраЗаданий, ИмяОперативногоРегистра, ИмяКонстантыЗаданий, АналитикиРасчета, НомерЗаданияДоРасчета);
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru='Взаиморасчеты. Распределены расчеты с клиентами за период';uk='Взаєморозрахунки. Розподілені розрахунки з клієнтами за період'",КодЯзыка), УровеньИнформация, , Формат(НачалоПериода, "ДФ='yyyy.MM'"));
		
		ПрошлыйПериод = НачалоПериода;
		НачалоПериода = НачалоРасчета(ИмяРегистраЗаданий, ОкончаниеПериодаРасчета, АналитикиКРасчету, НомерЗаданияДоРасчета);
	КонецЦикла;
	
	НДСВходящийСервер.ВыполнитьПроведениеДокументовПоВозникшемуНДС(НачальныеГраницы);
	
	Комментарий = НСтр("ru='Распределены расчеты с клиентами с %НачалоПериода% по %ОкончаниеПериода%';uk='Розподілені розрахунки з клієнтами з %НачалоПериода% по %ОкончаниеПериода%'",КодЯзыка);
	Комментарий = СтрЗаменить(Комментарий, "%НачалоПериода%", Формат(ПервыйПериод, ФорматПериода));
	Комментарий = СтрЗаменить(Комментарий, "%ОкончаниеПериода%", Формат(ОкончаниеПериодаРасчета, ФорматПериода));
	ЗаписьЖурналаРегистрации(
		НСтр("ru='Взаиморасчеты.Окончание распределения расчетов';uk='Взаєморозрахунки.Закінчення розподілу розрахунків'",КодЯзыка), УровеньИнформация, , ОкончаниеПериодаРасчета, Комментарий);
КонецПроцедуры

// Выполняет актуализацию расчетов в фоновом задании.
// Параметры:
//		КонецРасчета - Дата - Дата окончания актуализации.
//		АналитикиКРасчету - Массив - Массив аналитик расчетов с партнерами к актуализации.
//		РасчетКВосстановлению - Строка - Имя расчета к актуализации. Если не заполнено,
//										 то актуализириются все расчеты.
Процедура РаспределитьРасчетыФоновымЗаданием(КонецРасчета = Неопределено, АналитикиКРасчету = Неопределено, РасчетКВосстановлению = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(КонецРасчета) Тогда
		КонецРасчета = ТекущаяДатаСеанса();
	КонецЕсли;

	Если РасчетКВосстановлению = "РасчетыСКлиентами" Тогда
		РаспределитьРасчетыСКлиентамиФоновымЗаданием(КонецРасчета, АналитикиКРасчету, РасчетКВосстановлению);
	ИначеЕсли РасчетКВосстановлению = "РасчетыСПоставщиками" Тогда
		РаспределитьРасчетыСПоставщикамиФоновымЗаданием(КонецРасчета, АналитикиКРасчету, РасчетКВосстановлению);
	Иначе
		РаспределитьРасчетыСКлиентамиФоновымЗаданием(КонецРасчета, АналитикиКРасчету, РасчетКВосстановлению);
		РаспределитьРасчетыСПоставщикамиФоновымЗаданием(КонецРасчета, АналитикиКРасчету, РасчетКВосстановлению);
	КонецЕсли;
КонецПроцедуры

// Выводит в макет информационную надпись о запущенном фоновом задании.
Процедура ВывестиАктуальностьРасчетов(Макет, ПараметрыРасчета) Экспорт
	Если ЗначениеЗаполнено(ПараметрыРасчета) И ПараметрыРасчета.Свойство("ГраницаВзаиморасчетов") Тогда
		Если ЗначениеЗаполнено(ПараметрыРасчета.ГраницаВзаиморасчетов) Тогда
			ТаблицаПредупреждение = Новый ТабличныйДокумент;
			ОбластьПредупреждение = ТаблицаПредупреждение.Область(1,1,1,1);
			ТекстПредупреждения = НСтр("ru='Распределение расчетов выполнено до %ДатаАктуальности. 
                                        |Запущено задание по распределению расчетов с %ДатаНачалаРаспределения (требуется распределить расчеты для %КоличествоДокументов). 
                                        |После распределения Вам будет предложено переформировать отчет.'
                                        |;uk='Розподіл розрахунків виконано до %ДатаАктуальности. 
                                        |Запущено завдання розподілу розрахунків з %ДатаНачалаРаспределения (потрібно розподілити розрахунки для %КоличествоДокументов). 
                                        |Після розподілу Вам буде запропоновано переформувати звіт.'");
			ДатаАктуальности = КонецМесяца(ПараметрыРасчета.ГраницаВзаиморасчетов - 1);
			ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения,"%ДатаАктуальности", Формат(ДатаАктуальности, "ДФ=dd.MM.yyyy; ДЛФ=D"));
			ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения,"%ДатаНачалаРаспределения", Формат(ПараметрыРасчета.ГраницаВзаиморасчетов, "ДФ=dd.MM.yyyy; ДЛФ=D"));
			ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения,"%КоличествоДокументов", ОбщегоНазначенияУТ.ЧислоДокументовПрописью(ПараметрыРасчета.КРасчету));
			ОбластьПредупреждение.Текст = ТекстПредупреждения;
			ОбластьПредупреждение.ЦветТекста = ЦветаСтиля.ЦветОтрицательногоЧисла;
			Макет.ВставитьОбласть(ОбластьПредупреждение, Макет.Область(1,1,1,1), ТипСмещенияТабличногоДокумента.ПоВертикали);
		Иначе
			ПараметрыРасчета.Удалить("ГраницаВзаиморасчетов");
			ПараметрыРасчета.Удалить("НомерЗадания");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Возвращает имя фонового задания распределения взаиморасчетов с клиентами.
Функция ИмяФоновогоЗаданияРасчетовСКлиентами() Экспорт
	Возврат "ВзаиморасчетыСКлиентами";
КонецФункции

// Возвращает имя фонового задания распределения взаиморасчетов с поставщиками.
Функция ИмяФоновогоЗаданияРасчетовСПоставщиками() Экспорт
	Возврат "ВзаиморасчетыСПоставщиками";
КонецФункции

// Проверяет активно ли задание распределения взаиморасчетов.
// Параметры:
//	ИмяЗадания - Строка - имя фонового задания: расчеты с клиентами или расчеты с поставщиками.
//				 Если имя не задано, то проверяются все задания распределения взаиморасчетов.
// Возврат:
//	Истина, Ложь.
Функция АктивноФоновоеЗаданиеВзаиморасчетов(ИмяЗадания = Неопределено) Экспорт
	Если ИмяЗадания = ИмяФоновогоЗаданияРасчетовСКлиентами() 
		ИЛИ ИмяЗадания = ИмяФоновогоЗаданияРасчетовСПоставщиками()
	Тогда
		ЗаданиеАктивно = ФоновоеЗаданиеАктивно(ИмяЗадания);
	Иначе // проверяем все взаиморасчеты
		ИмяФоновогоЗаданияРасчетовСКлиентами = ИмяФоновогоЗаданияРасчетовСКлиентами();
		ЗаданиеРасчетыСКлиентамиАктивно = ФоновоеЗаданиеАктивно(ИмяФоновогоЗаданияРасчетовСКлиентами);
		
		ИмяФоновогоЗаданияРасчетовСПоставщиками = ИмяФоновогоЗаданияРасчетовСПоставщиками();
		ЗаданиеРасчетыСПоставщикамиАктивно = ФоновоеЗаданиеАктивно(ИмяФоновогоЗаданияРасчетовСПоставщиками);
		
		ИмяЗаданияЗакрытияМесяца = ЗакрытиеМесяцаУТВызовСервера.ИмяФоновогоЗадания();
		ЗаданиеЗакрытияМесяцаАктивно = ФоновоеЗаданиеАктивно(ИмяЗаданияЗакрытияМесяца);
		
		ЗаданиеАктивно = ЗаданиеРасчетыСКлиентамиАктивно ИЛИ ЗаданиеРасчетыСПоставщикамиАктивно ИЛИ ЗаданиеЗакрытияМесяцаАктивно;
	КонецЕсли;
	Возврат ЗаданиеАктивно;
КонецФункции

// Возвращает сведения о возможности запуска распределения взаиморасчетов в фоне.
// Используется при формировании проводк.
//	ВозвращаемоеЗначение:
//		Булево - Запуск возможен/не возможен.
Функция МожноЗапускатьВосстановлениеВзаиморасчетов()Экспорт
	Если АктивноФоновоеЗаданиеВзаиморасчетов() ИЛИ ЗакрытиеМесяцаУТВызовСервера.АктивноЗаданиеЗакрытияМесяца() Тогда
		ЗапускВозможен = Ложь;
	Иначе
		ЗапускВозможен = Истина;
	КонецЕсли;
	Возврат ЗапускВозможен;
КонецФункции
#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РасчетВФоне

Процедура РаспределитьРасчетыСПоставщикамиФоновымЗаданием(Период, АналитикиКРасчету, РасчетКВосстановлению)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Ключ = ИмяФоновогоЗаданияРасчетовСПоставщиками();
	
	Отбор = Новый Структура();
	Отбор.Вставить("Ключ", Ключ);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	АктивныеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	
	Если АктивныеЗадания.Количество() > 0 ИЛИ ЗакрытиеМесяцаУТВызовСервера.АктивноЗаданиеЗакрытияМесяца() Тогда
		Возврат;
	КонецЕсли;
	
	НаименованиеЗадания = НСтр("ru='Выполняется распределение расчетов с поставщиками';uk='Виконується розподіл розрахунків з постачальниками'");
	
	ПараметрыЭкспортнойПроцедуры = Новый Массив();
	ПараметрыЭкспортнойПроцедуры.Добавить(Период);
	ПараметрыЭкспортнойПроцедуры.Добавить(АналитикиКРасчету);
	
	ПараметрыЗадания = Новый Массив();
	ПараметрыЗадания.Добавить("РаспределениеВзаиморасчетов.РаспределитьВсеРасчетыСПоставщиками");
	ПараметрыЗадания.Добавить(ПараметрыЭкспортнойПроцедуры);

	ФоновыеЗадания.Выполнить("РаботаВБезопасномРежиме.ВыполнитьМетодКонфигурации", ПараметрыЗадания, Ключ, НаименованиеЗадания);
	
КонецПроцедуры

Процедура РаспределитьРасчетыСКлиентамиФоновымЗаданием(Период, АналитикиКРасчету, РасчетКВосстановлению)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Ключ = ИмяФоновогоЗаданияРасчетовСКлиентами();
	
	Отбор = Новый Структура();
	Отбор.Вставить("Ключ", Ключ);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	АктивныеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	
	Если АктивныеЗадания.Количество() > 0 ИЛИ ЗакрытиеМесяцаУТВызовСервера.АктивноЗаданиеЗакрытияМесяца() Тогда
		Возврат;
	КонецЕсли;
	
	НаименованиеЗадания = НСтр("ru='Выполняется распределение расчетов с клиентами';uk='Виконується розподіл розрахунків з клієнтами'");
	
	ПараметрыЭкспортнойПроцедуры = Новый Массив();
	ПараметрыЭкспортнойПроцедуры.Добавить(Период);
	ПараметрыЭкспортнойПроцедуры.Добавить(АналитикиКРасчету);
	
	ПараметрыЗадания = Новый Массив();
	ПараметрыЗадания.Добавить("РаспределениеВзаиморасчетов.РаспределитьВсеРасчетыСКлиентами");
	ПараметрыЗадания.Добавить(ПараметрыЭкспортнойПроцедуры);

	ФоновыеЗадания.Выполнить("РаботаВБезопасномРежиме.ВыполнитьМетодКонфигурации", ПараметрыЗадания, Ключ, НаименованиеЗадания);
	
КонецПроцедуры

#КонецОбласти // РасчетВФоне

#Область ГраницыАналитикКРасчету

Функция НачалоРасчета(ИмяРегистраЗаданий, ОкончаниеРасчета, АналитикиРасчета = Неопределено, НомерЗадания = 0, Операция = Неопределено)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МИНИМУМ(Задания.Месяц) КАК НачалоПериодаРасчета
	|ИЗ
	|	ИмяРегистраЗаданий КАК Задания
	|ГДЕ
	|	Задания.Месяц <= &ОкончаниеРасчета
	|	И (Задания.АналитикаУчетаПоПартнерам В (&АналитикиРасчета)
	|		ИЛИ Задания.АналитикаУчетаПоПартнерам = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|		ИЛИ &ПоВсемАналитикам)
	|	И (Задания.НомерЗадания <= &НомерЗадания
	|		ИЛИ &НеУчитыватьНомерЗадания)
	|ИМЕЮЩИЕ 
	|	НЕ (МИНИМУМ(Задания.Месяц) ЕСТЬ NULL)
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ИмяРегистраЗаданий", "РегистрСведений."+ИмяРегистраЗаданий);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ОкончаниеРасчета", ОкончаниеРасчета);
	Запрос.УстановитьПараметр("АналитикиРасчета", АналитикиРасчета);
	Запрос.УстановитьПараметр("ПоВсемАналитикам", АналитикиРасчета = Неопределено);
	Запрос.УстановитьПараметр("НомерЗадания", НомерЗадания);
	Запрос.УстановитьПараметр("НеУчитыватьНомерЗадания", НомерЗадания = 0);
	Запрос.УстановитьПараметр("Операция", Операция);
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат ?(Выборка.Следующий(), Выборка.НачалоПериодаРасчета, КонецМесяца(ОкончаниеРасчета) + 1);
КонецФункции

Функция ВсегоДокументовКРасчету(НачалоРасчета, КонецРасчета, АналитикиКРасчету, РасчетКВосстановлению)
	Если РасчетКВосстановлению = "РасчетыСКлиентами" Тогда
		КРасчетуСКлиентами = КоличествоДокументовКРасчету(НачалоРасчета, КонецРасчета, АналитикиКРасчету, "ЗаданияКРаспределениюРасчетовСКлиентами");
		КРасчетуСПоставщиками = 0;
	ИначеЕсли РасчетКВосстановлению = "РасчетыСПоставщиками" Тогда
		КРасчетуСПоставщиками = КоличествоДокументовКРасчету(НачалоРасчета, КонецРасчета, АналитикиКРасчету, "ЗаданияКРаспределениюРасчетовСПоставщиками");
		КРасчетуСКлиентами = 0;
	Иначе
		КРасчетуСКлиентами = КоличествоДокументовКРасчету(НачалоРасчета, КонецРасчета, АналитикиКРасчету, "ЗаданияКРаспределениюРасчетовСКлиентами");
		КРасчетуСПоставщиками = КоличествоДокументовКРасчету(НачалоРасчета, КонецРасчета, АналитикиКРасчету, "ЗаданияКРаспределениюРасчетовСПоставщиками");
	КонецЕсли;
	Возврат (КРасчетуСКлиентами + КРасчетуСПоставщиками);
КонецФункции

Функция КоличествоДокументовКРасчету(НачалоРасчета, КонецРасчета, АналитикиКРасчету, ИмяРегистраРасчета)
	Возврат РегистрыСведений[ИмяРегистраРасчета].КоличествоНеактуальныхДокументов(НачалоРасчета, КонецРасчета, АналитикиКРасчету);
КонецФункции

Функция АналитикиРасчета(ИмяОперативногоРегистра, ИмяРегистраЗаданий, Месяц, НомерЗадания, АналитикиКРасчету)
		
	ТекстЗапроса = "
	// Выборка аналитик по которым есть движения за рассчитываемый период.
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОперативныйРегистр.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ПОМЕСТИТЬ Движения
	|ИЗ
	|	ИмяОперативногоРегистра КАК ОперативныйРегистр
	|ГДЕ
	|	ОперативныйРегистр.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|;
	|/////////////////////////////////////////////////
	// Выборка неактуальных границ, относительно множества аналитик к расчету.
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Движения.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ИЗ
	|	Движения КАК Движения
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИмяРегистраЗаданий КАК Задания
	|	ПО Движения.АналитикаУчетаПоПартнерам = Задания.АналитикаУчетаПоПартнерам
	|		ИЛИ Задания.АналитикаУчетаПоПартнерам = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|ГДЕ
	|	(Движения.АналитикаУчетаПоПартнерам В (&МассивАналитик) ИЛИ &ПоВсемАналитикам)
	|	И Задания.Месяц МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И Задания.НомерЗадания <= &НомерЗадания
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Дополнительно выбираются удаленные аналитики, которых может уже не быть в оперативном регистре,
	// но в регистре заданий еще присутствуют.
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Задания.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ИЗ
	|	ИмяРегистраЗаданий КАК Задания
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Движения КАК Движения
	|	ПО Движения.АналитикаУчетаПоПартнерам = Задания.АналитикаУчетаПоПартнерам
	|ГДЕ
	|	Движения.АналитикаУчетаПоПартнерам ЕСТЬ NULL
	|	И Задания.Месяц МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И Задания.НомерЗадания <= &НомерЗадания
	|	И (Задания.АналитикаУчетаПоПартнерам В (&МассивАналитик)
	|			ИЛИ &ПоВсемАналитикам)
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ИмяОперативногоРегистра", "РегистрНакопления."+ИмяОперативногоРегистра);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ИмяРегистраЗаданий", "РегистрСведений."+ИмяРегистраЗаданий);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Месяц));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Месяц));
	Запрос.УстановитьПараметр("МассивАналитик", АналитикиКРасчету);
	Запрос.УстановитьПараметр("ПоВсемАналитикам", АналитикиКРасчету = Неопределено);
	Запрос.УстановитьПараметр("НомерЗадания", НомерЗадания);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат.ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
КонецФункции

#КонецОбласти // ДокументыКРасчету

#Область ЭтапыРасчета

// Этап 1Х: распределение расчетов с клиентами
Процедура РассчитатьРасчетыСКлиентами(НачалоПериода, ОкончаниеПериода, АналитикиРасчета, РасчетныеРасчетыСКлиентами = Неопределено)
	Контекст = "РасчетыСКлиентами";
	ВременныеТаблицы = Новый МенеджерВременныхТаблиц;
	// ФАЗА 10: Выборка исходных данных для распределения расчетов с клиентами
	ДанныеДляРасчетовСКлиентами = ДанныеДляРасчетовСКлиентами(НачалоПериода, ОкончаниеПериода, АналитикиРасчета, ВременныеТаблицы);
	// ФАЗА 12: Распределение расчетов с клиентами по исходным данным
	ИсключаемыеТипыРегистраторов = ИсключаемыеТипы(); // данные типы регистраторов не проверятся и не возвращаются методом Регистраторы()
	Регистраторы = Регистраторы(НачалоПериода, ОкончаниеПериода, АналитикиРасчета, "РасчетыСКлиентами", ИсключаемыеТипыРегистраторов);
	РасчетныеРасчетыСКлиентами = РасчетныеРасчетыСКлиентами(ДанныеДляРасчетовСКлиентами, Регистраторы);
	// ФАЗА 14: Запись регистра РасчетыСКлиентамиПоДокументам
	ЗаписатьРасчетныеПартии(РегистрыНакопления.РасчетыСКлиентамиПоДокументам, РасчетныеРасчетыСКлиентами, Регистраторы);
	ОчиститьРегистрНаСервере(Регистраторы, "РасчетыСКлиентамиПоДокументам");
	РегистраторыКОтражению = РасчетныеРасчетыСКлиентами.ВыгрузитьКолонку("Регистратор");
	СформироватьДвиженияПоНДС(Контекст, РегистраторыКОтражению);
	СформироватьДвиженияПоУправленческомуУчету(Контекст, РегистраторыКОтражению);
	СуммыДокументовВВалютеРегл(Контекст, РегистраторыКОтражению);
КонецПроцедуры

// Этап 2Х: распределение расчетов с поставщиками
Процедура РассчитатьРасчетыСПоставщиками(НачалоПериода, ОкончаниеПериода, АналитикиРасчета, РасчетныеРасчетыСПоставщиками = Неопределено)
	Контекст = "РасчетыСПоставщиками";
	ВременныеТаблицы = Новый МенеджерВременныхТаблиц;
	// ФАЗА 10: Выборка исходных данных для распределения расчетов с поставщиками
	ДанныеДляРасчетовСПоставщиками = ДанныеДляРасчетовСПоставщиками(НачалоПериода, ОкончаниеПериода, АналитикиРасчета, ВременныеТаблицы);
	// ФАЗА 12: Распределение расчетов с поставщиками по исходным данным
	ИсключаемыеТипыРегистраторов = ИсключаемыеТипы(); // данные типы регистраторов не проверятся и не возвращаются методом Регистраторы()
	Регистраторы = Регистраторы(НачалоПериода, ОкончаниеПериода, АналитикиРасчета, "РасчетыСПоставщиками", ИсключаемыеТипыРегистраторов);
	РасчетныеРасчетыСПоставщиками = РасчетныеРасчетыСПоставщиками(ДанныеДляРасчетовСПоставщиками, Регистраторы);
	// ФАЗА 14: Запись регистра РасчетыСПоставщикамиПоДокументам
	ЗаписатьРасчетныеПартии(РегистрыНакопления.РасчетыСПоставщикамиПоДокументам, РасчетныеРасчетыСПоставщиками, Регистраторы);
	ОчиститьРегистрНаСервере(Регистраторы, "РасчетыСПоставщикамиПоДокументам");
	РегистраторыКОтражению = РасчетныеРасчетыСПоставщиками.ВыгрузитьКолонку("Регистратор");
	СформироватьДвиженияПоНДС(Контекст, РегистраторыКОтражению);
	СформироватьДвиженияПоУправленческомуУчету(Контекст, РегистраторыКОтражению);
	СуммыДокументовВВалютеРегл(Контекст, РегистраторыКОтражению);
КонецПроцедуры

// Этап 4Х: Запись новых границ расчетов
Процедура УстановитьГраницыРасчетов(РассчитаныйПериод, ОкончаниеПериодаРасчета, ИмяРегистраЗаданий, ИмяОперативногоРегистра, ИмяКонстантыЗаданий, АналитикиРасчета, НомерЗаданияДоРасчета)
	УстановитьПривилегированныйРежим(Истина);
	
	Если НачалоРасчета(ИмяРегистраЗаданий, ОкончаниеПериодаРасчета, АналитикиРасчета, НомерЗаданияДоРасчета) < РассчитаныйПериод Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = "
	// Выборка аналитик по которым есть движения за рассчитываемый период.
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОперативныйРегистр.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ПОМЕСТИТЬ Движения
	|ИЗ
	|	ИмяОперативногоРегистра КАК ОперативныйРегистр
	|ГДЕ
	|	ОперативныйРегистр.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|;
	|/////////////////////////////////////////////////////////////////////////////
	// Выборка аналитик по которым есть движения с начала рассчитываемого периода.
	|ВЫБРАТЬ
	|	ОперативныйРегистр.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	МИНИМУМ(ОперативныйРегистр.Период) КАК Период
	|ПОМЕСТИТЬ ВсеАналитики
	|ИЗ
	|	ИмяОперативногоРегистра КАК ОперативныйРегистр
	|ГДЕ
	|	ОперативныйРегистр.Период >= &НачалоПериода
	|СГРУППИРОВАТЬ ПО
	|	ОперативныйРегистр.АналитикаУчетаПоПартнерам
	|;
	|/////////////////////////////////////////////////////////////////////////////
	// По рассчитанным аналитикам удаляются старые движения в заданиях
	// если номер задания записей < текущего номера задания
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Месяц,
	|	ДД.АналитикаУчетаПоПартнерам
	|ИЗ
	|	ИмяРегистраЗаданий КАК ДД
	|ГДЕ
	|	ДД.Месяц МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И ДД.НомерЗадания < &НомерЗаданияДоРасчета + 1
	|	И (ДД.АналитикаУчетаПоПартнерам В (&АналитикиРасчета)
	|		ИЛИ ДД.АналитикаУчетаПоПартнерам  = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|		ИЛИ &ПоВсемАналитикам)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Задания.Месяц КАК Месяц,
	|	Задания.НомерЗадания КАК НомерЗадания,
	|	Задания.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Задания.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Задания.Документ КАК Документ
	|ИЗ
	|	(
		// Задания, которые появились уже после начала процесса расчета, будут пересчитаны позже.
	|	ВЫБРАТЬ
	|		ДД.Месяц,
	|		ДД.НомерЗадания,
	|		ДД.АналитикаУчетаПоПартнерам,
	|		ДД.ОбъектРасчетов,
	|		ДД.Документ
	|	ИЗ
	|		ИмяРегистраЗаданий КАК ДД
	|	ГДЕ
	|		((ДД.Месяц МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|				И ДД.НомерЗадания >= &НомерЗаданияДоРасчета + 1)
	|			ИЛИ ДД.Месяц > &ОкончаниеПериода
	|			ИЛИ ДД.Месяц < &НачалоПериода)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
		// Сохранее более ранних границ, по не рассчитываемым аналитикам
	|	ВЫБРАТЬ
	|		ДД.Месяц,
	|		ДД.НомерЗадания,
	|		ДД.АналитикаУчетаПоПартнерам,
	|		ДД.ОбъектРасчетов,
	|		ДД.Документ
	|	ИЗ
	|		ИмяРегистраЗаданий КАК ДД
	|	ГДЕ
	|		ДД.Месяц МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И НЕ ДД.АналитикаУчетаПоПартнерам В (&АналитикиРасчета)
	|		И НЕ &ПоВсемАналитикам
	|		И ДД.АналитикаУчетаПоПартнерам <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
		// В регистре заданий аналитика учета по партнерам не задана.
		// Сдвиг границ по рассчитанным аналитикам, сохранение нерассчитанных аналитик в прошлом периоде
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(Движения.Период, МЕСЯЦ),
	|		ДД.НомерЗадания,
	|		Движения.АналитикаУчетаПоПартнерам,
	|		ДД.ОбъектРасчетов,
	|		ДД.Документ
	|ИЗ
	|		ВсеАналитики КАК Движения
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИмяРегистраЗаданий КАК ДД
	|		ПО ДД.АналитикаУчетаПоПартнерам = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|
	|	ГДЕ
	|		НЕ Движения.АналитикаУчетаПоПартнерам В (&АналитикиРасчета)
	|		И НЕ &ПоВсемАналитикам
	|		И ДД.Месяц МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И ДД.НомерЗадания < &НомерЗаданияДоРасчета + 1
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
		// Перенос границ расчета
	|	ВЫБРАТЬ
	|		&СледующийМесяц,
	|		ДД.НомерЗадания,
	|		КРасчету.АналитикаУчетаПоПартнерам,
	|		ДД.ОбъектРасчетов,
	|		НЕОПРЕДЕЛЕНО КАК Документ
	|	ИЗ
	|		ВсеАналитики КАК КРасчету
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИмяРегистраЗаданий КАК ДД
	|		ПО ДД.АналитикаУчетаПоПартнерам = КРасчету.АналитикаУчетаПоПартнерам
	|				ИЛИ ДД.АналитикаУчетаПоПартнерам = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|
	|	ГДЕ
	|		(КРасчету.АналитикаУчетаПоПартнерам В (&АналитикиРасчета)
	|				ИЛИ &ПоВсемАналитикам)
	|		И ДД.Месяц МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И ДД.НомерЗадания < &НомерЗаданияДоРасчета + 1
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
		// Дополнительно выбираются удаленные аналитики, которых может уже не быть в оперативном регистре,
		// но в регистре заданий еще присутствуют.
	|	ВЫБРАТЬ
	|		&СледующийМесяц,
	|		ДД.НомерЗадания,
	|		ДД.АналитикаУчетаПоПартнерам,
	|		ДД.ОбъектРасчетов,
	|		НЕОПРЕДЕЛЕНО КАК Документ
	|	ИЗ
	|		ИмяРегистраЗаданий КАК ДД
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Движения КАК Движения
	|		ПО (Движения.АналитикаУчетаПоПартнерам = ДД.АналитикаУчетаПоПартнерам)
	|	ГДЕ
	|		(ДД.АналитикаУчетаПоПартнерам В (&АналитикиРасчета)
	|			ИЛИ &ПоВсемАналитикам)
	|		И Движения.АналитикаУчетаПоПартнерам ЕСТЬ NULL 
	|		И ДД.Месяц МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И ДД.НомерЗадания < &НомерЗаданияДоРасчета + 1
	|
	|	) КАК Задания
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяРегистраЗаданий", "РегистрСведений." + ИмяРегистраЗаданий);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяОперативногоРегистра", "РегистрНакопления." + ИмяОперативногоРегистра);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяКонстантыЗаданий", "Константа." + ИмяКонстантыЗаданий);
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(РассчитаныйПериод));
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца(РассчитаныйПериод));
	Запрос.УстановитьПараметр("СледующийМесяц", КонецМесяца(РассчитаныйПериод) + 1);
	Запрос.УстановитьПараметр("АналитикиРасчета", АналитикиРасчета);
	Запрос.УстановитьПараметр("ПоВсемАналитикам", НЕ ЗначениеЗаполнено(АналитикиРасчета));
	Запрос.УстановитьПараметр("НомерЗаданияДоРасчета", НомерЗаданияДоРасчета);
	Результаты = Запрос.ВыполнитьПакет();
	
	// Очищаются движения по рассчитанным аналитикам
	Выборка = Результаты[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		Набор = РегистрыСведений[ИмяРегистраЗаданий].СоздатьНаборЗаписей();
		Набор.Отбор.Месяц.Установить(Выборка.Месяц);
		Набор.Отбор.АналитикаУчетаПоПартнерам.Установить(Выборка.АналитикаУчетаПоПартнерам);
		Набор.Записать();
	КонецЦикла;
	
	// Сохранения движений, которые в текущей итерации не были рассчитаны;
	// перенос рассчитанных границ.
	Задания = Результаты[3].Выгрузить();
	Если Задания.Количество() > 0 Тогда
		Набор = РегистрыСведений[ИмяРегистраЗаданий].СоздатьНаборЗаписей();
		Набор.Загрузить(Задания);
		Набор.Записать();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область РасчетыСКлиентами

// Текст запроса исходных движений для распределения расчетов с клиентами
Функция ДанныеДляРасчетовСКлиентами(НачалоПериода, ОкончаниеПериода, АналитикиРасчета, ВременныеТаблицы)
	ТекстСортировка = "
		|УПОРЯДОЧИТЬ ПО
		|	Период, Приоритет, Регистратор
		|";
	
	ИсходныйТекстЗапроса =
		ТекстРасчетыСКлиентамиИнициализация() + ";"
		+ ТекстКорректировкиРеализаций() + ";"
		+ ТекстОписаниеРасчетыСКлиентами()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстОстаткиРасчетовСКлиентами()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстОтгрузкиПрошлыхПериодов()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстДвиженияРасчетовСКлиентами()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстАктуальныеЗаписиРасчетовСКлиентами()
		+ ТекстСортировка;
	ИсходныйЗапрос = Новый Запрос(ИсходныйТекстЗапроса);
	ИсходныйЗапрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	ИсходныйЗапрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	ИсходныйЗапрос.УстановитьПараметр("АналитикиРасчета", АналитикиРасчета);
	ИсходныйЗапрос.УстановитьПараметр("ПоВсемАналитикам", АналитикиРасчета = Неопределено);
	ИсходныйЗапрос.УстановитьПараметр("ВалютаРеглУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	ИсходныйЗапрос.УстановитьПараметр("ВалютаУпрУчета", Константы.ВалютаУправленческогоУчета.Получить());
	ИсходныйЗапрос.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ДанныеДляРасчета = ИсходныйЗапрос.Выполнить();

	Возврат ДанныеДляРасчета;
КонецФункции

Функция РасчетныеРасчетыСКлиентами(ДанныеДляРасчета, Регистраторы, ЦепочкиДвижений = Неопределено, Тест = Ложь) // ДанныеДляРасчета будут изменены
	// Шаг 1: создаем буфер накопления рассчитанных партий
	Запрос = Новый Запрос(ТекстОписаниеРасчетыСКлиентами());
	РасчетныеПартии = Запрос.Выполнить().Выгрузить();
	
	// Шаг 2: готовим описание обсчета
	КонтекстЦепочек = ОписаниеЦепочек("ОстатокОплат, ОстатокОтгрузок, ПрошлаяОтгрузка, Оплата, СторноОплаты, СторноОтгрузки, Отгрузка, ВозвратТоваров, Актуальные");
	
	ПоляПотреблений = "АналитикаУчетаПоПартнерам, ЗаказКлиента, Валюта";
	КонтекстДвижений = ОписаниеДвижений(
		"РасчетыСКлиентами", ПереченьПолей(РасчетныеПартии.Колонки),
		ПоляПотреблений,
		"КВозврату, Долг, ДолгУпр, ДолгРегл, Предоплата, ПредоплатаУпр, ПредоплатаРегл, ЗалогЗаТару, ЗалогЗаТаруРегл, Сумма",
		"Сумма", "Сумма", "РасчетныйДокумент", "Период", "Период, ПриоритетРасчетногоДокумента УБЫВ, Приоритет ВОЗР, Регистратор");
		
	// Шаг 3: обсчитываем потребления из партий
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Оплата", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Оплата", "ОстатокОтгрузок", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Оплата", "Отгрузка", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Оплата", "СторноОплаты", ПоляПотреблений);
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Отгрузка", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Отгрузка", "ОстатокОплат", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Отгрузка", "Оплата", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Отгрузка", "СторноОтгрузки", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Отгрузка", "ВозвратТоваров", ПоляПотреблений);
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "СторноОплаты", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "СторноОплаты", "ОстатокОплат", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "СторноОплаты", "Оплата", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "СторноОплаты", "ВозвратТоваров", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "СторноОплаты", "СторноОтгрузки", ПоляПотреблений);
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "ВозвратТоваров", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ВозвратТоваров", "ОстатокОтгрузок", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ВозвратТоваров", "Отгрузка", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ВозвратТоваров", "СторноОплаты", ПоляПотреблений);
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "СторноОтгрузки", "АналитикаУчетаПоПартнерам, ЗаказКлиента, Валюта, ДокументОтгрузки");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "СторноОтгрузки", "ОстатокОтгрузок", "АналитикаУчетаПоПартнерам, ЗаказКлиента, Валюта, ДокументОтгрузки");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "СторноОтгрузки", "ПрошлаяОтгрузка", "АналитикаУчетаПоПартнерам, ЗаказКлиента, Валюта, ДокументОтгрузки");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "СторноОтгрузки", "Отгрузка", "АналитикаУчетаПоПартнерам, ЗаказКлиента, Валюта, ДокументОтгрузки");
	
	ЦепочкиДвижений = ЦепочкиДвиженийИзВыборки(КонтекстЦепочек, ДанныеДляРасчета);
	РассчитатьПартииПоЦепочкамИзВыборки(КонтекстДвижений, ДанныеДляРасчета, РасчетныеПартии, ЦепочкиДвижений, Регистраторы, Тест);
	
	// Шаг 4: Удаляем строки, не требующие записи в базу.
	УдалитьНезаписываемыеСтроки(КонтекстДвижений.Контекст, РасчетныеПартии);
	
	// Шаг 5: Сортировка партий для дальнейшей записи
	РасчетныеПартии.Сортировать("Регистратор, Приоритет", Новый СравнениеЗначений);
	Возврат РасчетныеПартии;
	
КонецФункции

Функция ТекстРасчетыСКлиентамиИнициализация()
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Аналитики.КлючАналитики
		|ПОМЕСТИТЬ АналитикиРасчета
		|ИЗ
		|	РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитики
		|ГДЕ
		|	(Аналитики.КлючАналитики В (&АналитикиРасчета)
		|		ИЛИ &ПоВсемАналитикам)
		|;
		|///////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.Регистратор                             КАК Регистратор,
		|	Расчеты.АналитикаУчетаПоПартнерам               КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ЗаказКлиента                            КАК ЗаказКлиента,
		|	Расчеты.Валюта                                  КАК Валюта,
		|	ЕСТЬNULL(Суммы.Валюта, &ВалютаРеглУчета)        КАК ВалютаДокумента
		|ПОМЕСТИТЬ РегистраторыКРасчету
		|ИЗ 
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		Расчеты.Регистратор               КАК Регистратор,
		|		Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|		Расчеты.ЗаказКлиента              КАК ЗаказКлиента,
		|		Расчеты.Валюта                    КАК Валюта
		|	ИЗ
		|		РегистрНакопления.РасчетыСКлиентами КАК Расчеты 
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасчета КАК Аналитики
		|				ПО Расчеты.АналитикаУчетаПоПартнерам = Аналитики.КлючАналитики
		|	ГДЕ
		|		Расчеты.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И Расчеты.Сумма <> 0
		|
		|	ОБЪЕДИНИТЬ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		РасчетыПоДокументам.Регистратор               КАК Регистратор,
		|		РасчетыПоДокументам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|		РасчетыПоДокументам.ЗаказКлиента              КАК ЗаказКлиента,
		|		РасчетыПоДокументам.Валюта                    КАК Валюта
		|	ИЗ
		|		РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК РасчетыПоДокументам 
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасчета КАК Аналитики
		|				ПО РасчетыПоДокументам.АналитикаУчетаПоПартнерам = Аналитики.КлючАналитики
		|	ГДЕ
		|		РасчетыПоДокументам.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И (РасчетыПоДокументам.Долг <> 0 ИЛИ РасчетыПоДокументам.Предоплата <> 0)
		|	) КАК Расчеты
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК Суммы
		|	ПО Расчеты.Регистратор = Суммы.Регистратор
		|		И Расчеты.АналитикаУчетаПоПартнерам = Суммы.АналитикаУчетаПоПартнерам
		|		И Суммы.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
		|;
		|///////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.Регистратор.ДокументОснование КАК Регистратор
		|ПОМЕСТИТЬ ДокументыОтгрузки
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК Расчеты
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасчета КАК Аналитики
		|	ПО Расчеты.АналитикаУчетаПоПартнерам = Аналитики.КлючАналитики
		|ГДЕ
		|	Расчеты.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И Расчеты.Сумма <> 0
		|	И Расчеты.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|;
		|////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.РасчетныйДокумент КАК Ссылка,
		|	МАКСИМУМ(Периодика.Регистратор) КАК Регистратор,
		|	МАКСИМУМ(Периодика.Период) КАК Период
		|ПОМЕСТИТЬ ОстаткиПоЛИФО
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоДокументам.Остатки(&НачалоПериода) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Периодика
		|		ПО Периодика.Период < &НачалоПериода
		|		И Периодика.РасчетныйДокумент = ДД.РасчетныйДокумент
		|ГДЕ
		|	ДД.РасчетныйДокумент <> НЕОПРЕДЕЛЕНО
		|	И ((Периодика.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И Периодика.Долг > 0)
		|		ИЛИ (Периодика.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И Периодика.Предоплата > 0)
		|	)
		|СГРУППИРОВАТЬ ПО
		|	ДД.РасчетныйДокумент
		|;
		|///////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.Регистратор КАК Регистратор,
		|	Расчеты.РасчетныйДокумент КАК РасчетныйДокумент,
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ДокументыОтгрузки.Регистратор КАК ДокументОтгрузки
		|ПОМЕСТИТЬ ПрошлыеОтгрузки
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Расчеты
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыОтгрузки КАК ДокументыОтгрузки
		|	ПО Расчеты.Регистратор = ДокументыОтгрузки.Регистратор
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасчета КАК ОтборПоАналитикам
		|	ПО Расчеты.АналитикаУчетаПоПартнерам = ОтборПоАналитикам.КлючАналитики
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПоЛИФО КАК ОстаткиПоЛИФО
		|	ПО Расчеты.РасчетныйДокумент = ОстаткиПоЛИФО.Ссылка
		|ГДЕ
		|	Расчеты.Период < &НачалоПериода
		|	И ОстаткиПоЛИФО.Ссылка ЕСТЬ NULL
		|	И Расчеты.РасчетныйДокумент <> ДокументыОтгрузки.Регистратор
		|;
		|////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.Регистратор               КАК Регистратор,
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ЗаказКлиента              КАК ЗаказКлиента,
		|	Расчеты.Валюта                    КАК Валюта
		|ПОМЕСТИТЬ АктуальныеАналитики
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК Расчеты
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистраторыКРасчету КАК Регистраторы
		|	ПО Расчеты.Регистратор = Регистраторы.Регистратор
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ АналитикиРасчета КАК АналитикиКРасчету
		|	ПО Расчеты.АналитикаУчетаПоПартнерам = АналитикиКРасчету.КлючАналитики
		|ГДЕ
		|	АналитикиКРасчету.КлючАналитики ЕСТЬ NULL
		|	И НЕ Расчеты.Регистратор ССЫЛКА Документ.КорректировкаРегистров
		|";
КонецФункции

Функция ТекстКорректировкиРеализаций()
	Возврат "
		|ВЫБРАТЬ
		|	ДД.Период КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ДД.ЗаказКлиента КАК ЗаказКлиента,
		|	ДД.Валюта КАК Валюта,
		|
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ТОГДА - ДД.Сумма
		|		ИНАЧЕ ДД.Сумма
		|	КОНЕЦ) КАК Сумма,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ТОГДА - ДД.СуммаУпр
		|		ИНАЧЕ ДД.СуммаУпр
		|	КОНЕЦ) КАК СуммаУпр,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ТОГДА - ДД.СуммаРегл
		|		ИНАЧЕ ДД.СуммаРегл
		|	КОНЕЦ) КАК СуммаРегл,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ТОГДА - ДД.ЗалогЗаТару
		|		ИНАЧЕ ДД.ЗалогЗаТару
		|	КОНЕЦ) КАК ЗалогЗаТару,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ТОГДА - ДД.КОплате
		|		ИНАЧЕ ДД.КОплате
		|	КОНЕЦ) КАК КОплате,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ТОГДА - ДД.Оплачивается
		|		ИНАЧЕ ДД.Оплачивается
		|	КОНЕЦ) КАК Оплачивается,
		|
		|	ДД.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДД.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ДД.ДатаПлатежа КАК ДатаПлатежа,
		|	ВЫБОР КОГДА ДД.Валюта = &ВалютаРеглУчета
		|		ИЛИ КРасчету.ВалютаДокумента = &ВалютаРеглУчета
		|		ИЛИ КРасчету.Валюта = &ВалютаУпрУчета
		|		ИЛИ КРасчету.ВалютаДокумента = &ВалютаУпрУчета
		|		ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РасчетПоКурсуДокумента
		|ПОМЕСТИТЬ ВтКорректировки
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК ДД
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистраторыКРасчету КАК КРасчету
		|	ПО ДД.Регистратор = КРасчету.Регистратор
		|		И ДД.АналитикаУчетаПоПартнерам = КРасчету.АналитикаУчетаПоПартнерам
		|		И ДД.ЗаказКлиента = КРасчету.ЗаказКлиента
		|		И ДД.Валюта = КРасчету.Валюта
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Сумма <> 0
		|	И ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ЗНАЧЕНИЕ(Документ.КорректировкаРеализации)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.АналитикаУчетаПоПартнерам,
		|	ДД.ЗаказКлиента,
		|	ДД.Валюта,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.СтатьяДвиженияДенежныхСредств,
		|	ДД.ДатаПлатежа,
		|	ВЫБОР КОГДА ДД.Валюта = &ВалютаРеглУчета
		|		ИЛИ КРасчету.ВалютаДокумента = &ВалютаРеглУчета
		|		ИЛИ КРасчету.Валюта = &ВалютаУпрУчета
		|		ИЛИ КРасчету.ВалютаДокумента = &ВалютаУпрУчета
		|		ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|;
		|/////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДД.Период КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	ВЫБОР КОГДА ДД.Сумма < 0 
		|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	КОНЕЦ КАК ВидДвижения,
		|	ДД.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ДД.ЗаказКлиента КАК ЗаказКлиента,
		|	ДД.Валюта КАК Валюта,
		|
		|	ВЫБОР КОГДА ДД.Сумма < 0
		|		ТОГДА 0 - ДД.Сумма
		|		ИНАЧЕ ДД.Сумма
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР КОГДА ДД.СуммаУпр < 0
		|		ТОГДА 0 - ДД.СуммаУпр
		|		ИНАЧЕ ДД.СуммаУпр
		|	КОНЕЦ КАК СуммаУпр,
		|	ВЫБОР КОГДА ДД.СуммаРегл < 0
		|		ТОГДА 0 - ДД.СуммаРегл
		|		ИНАЧЕ ДД.СуммаРегл
		|	КОНЕЦ КАК СуммаРегл,
		|
		|	ВЫБОР КОГДА ДД.ЗалогЗаТару < 0
		|		ТОГДА 0 - ДД.ЗалогЗаТару
		|		ИНАЧЕ ДД.ЗалогЗаТару
		|	КОНЕЦ КАК ЗалогЗаТару,
		|	ВЫБОР КОГДА ДД.КОплате < 0
		|		ТОГДА 0 - ДД.КОплате
		|		ИНАЧЕ ДД.КОплате
		|	КОНЕЦ КАК КОплате,
		|	ВЫБОР КОГДА ДД.Оплачивается < 0
		|		ТОГДА 0 - ДД.Оплачивается
		|		ИНАЧЕ ДД.Оплачивается
		|	КОНЕЦ КАК Оплачивается,
		|
		|	ДД.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДД.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ДД.ДатаПлатежа КАК ДатаПлатежа,
		|	ДД.РасчетПоКурсуДокумента КАК РасчетПоКурсуДокумента
		|ПОМЕСТИТЬ Корректировки
		|ИЗ ВтКорректировки КАК ДД
		|;
		|////////////////////////////////////
		|УНИЧТОЖИТЬ ВтКорректировки
		|";
КонецФункции

Функция ТекстОписаниеРасчетыСКлиентами()
	Возврат "
	|ВЫБРАТЬ ПЕРВЫЕ 0
	|	0 КАК Приоритет,
	|	""ХХХХХХХХХХХХХХХХХХ"" КАК ТипЗаписи,
	|	ЛОЖЬ КАК РасчетЗавершен,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК Период,
	|	ДД.Регистратор КАК Регистратор,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка) КАК АналитикаУчетаПоПартнерам,
	|	ДД.ЗаказКлиента КАК ЗаказКлиента,
	|	ДД.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК Валюта,
	|	0. КАК Сумма,
	|	0. КАК КВозврату,
	|	0. КАК Долг,
	|	0. КАК ДолгУпр,
	|	0. КАК ДолгРегл,
	|	0. КАК Предоплата,
	|	0. КАК ПредоплатаУпр,
	|	0. КАК ПредоплатаРегл,
	|	0. КАК ЗалогЗаТару,
	|	0. КАК ЗалогЗаТаруРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка) КАК СтатьяДвиженияДенежныхСредств,
	|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК УдалитьДатаПлатежа,
	|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаПлатежа,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОтгрузки,
	|	НЕОПРЕДЕЛЕНО КАК РасчетПоКурсуДокумента,
	|	1 КАК ИндексДвиженияВзаимозачета,
	|	0 КАК ПриоритетРасчетногоДокумента
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК ДД
	|";
КонецФункции
	
Функция ТекстОстаткиРасчетовСКлиентами()
	Возврат "
		|ВЫБРАТЬ
		|	10 КАК Приоритет,
		|	ВЫБОР КОГДА ДД.ПредоплатаОстаток <> 0 
		|		ТОГДА ""ОстатокОплат""
		|		ИНАЧЕ ""ОстатокОтгрузок""
		|	КОНЕЦ КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЕСТЬNULL(Периодика.Период, &НачалоПериода) КАК Период,
		|	ЕСТЬNULL(Периодика.Регистратор, ДД.РасчетныйДокумент) КАК Регистратор,
		|	ДД.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ВЫБОР КОГДА ДД.ЗаказКлиента = НЕОПРЕДЕЛЕНО
		|		ТОГДА NULL
		|		ИНАЧЕ ДД.ЗаказКлиента
		|	КОНЕЦ КАК ЗаказКлиента,
		|	ДД.РасчетныйДокумент КАК РасчетныйДокумент,
		|	ДД.Валюта КАК Валюта,
		|	ВЫБОР КОГДА (ДД.ДолгОстаток + ЕСТЬNULL(Корректировки.Долг,0) + ДД.ПредоплатаОстаток + ЕСТЬNULL(Корректировки.Предоплата,0)) < 0
		|		ТОГДА 0 - (ДД.ДолгОстаток + ЕСТЬNULL(Корректировки.Долг,0) + ДД.ПредоплатаОстаток + ЕСТЬNULL(Корректировки.Предоплата,0))
		|		ИНАЧЕ (ДД.ДолгОстаток + ЕСТЬNULL(Корректировки.Долг,0)  + ДД.ПредоплатаОстаток + ЕСТЬNULL(Корректировки.Предоплата,0))
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР КОГДА ДД.КВозвратуОстаток + ЕСТЬNULL(Корректировки.КВозврату,0) < 0
		|		ТОГДА 0 - (ДД.КВозвратуОстаток + ЕСТЬNULL(Корректировки.КВозврату,0))
		|		ИНАЧЕ ДД.КВозвратуОстаток + ЕСТЬNULL(Корректировки.КВозврату,0)
		|	КОНЕЦ КАК КВозврату,
		|	ВЫБОР КОГДА ДД.ДолгОстаток + ЕСТЬNULL(Корректировки.Долг,0) < 0
		|		ТОГДА 0 - (ДД.ДолгОстаток + ЕСТЬNULL(Корректировки.Долг,0))
		|		ИНАЧЕ ДД.ДолгОстаток + ЕСТЬNULL(Корректировки.Долг,0)
		|	КОНЕЦ КАК Долг,
		|	ВЫБОР КОГДА ДД.ДолгУпрОстаток + ЕСТЬNULL(Корректировки.ДолгУпр,0) < 0
		|		ТОГДА 0 - (ДД.ДолгУпрОстаток + ЕСТЬNULL(Корректировки.ДолгУпр,0))
		|		ИНАЧЕ ДД.ДолгУпрОстаток + ЕСТЬNULL(Корректировки.ДолгУпр,0)
		|	КОНЕЦ КАК ДолгУпр,
		|	ВЫБОР КОГДА ДД.ДолгРеглОстаток + ЕСТЬNULL(Корректировки.ДолгРегл,0) < 0
		|		ТОГДА 0 - (ДД.ДолгРеглОстаток + ЕСТЬNULL(Корректировки.ДолгРегл,0))
		|		ИНАЧЕ ДД.ДолгРеглОстаток + ЕСТЬNULL(Корректировки.ДолгРегл,0)
		|	КОНЕЦ КАК ДолгРегл,
		|	ВЫБОР КОГДА ДД.ПредоплатаОстаток + ЕСТЬNULL(Корректировки.Предоплата,0) < 0
		|		ТОГДА 0 - (ДД.ПредоплатаОстаток + ЕСТЬNULL(Корректировки.Предоплата,0))
		|		ИНАЧЕ ДД.ПредоплатаОстаток + ЕСТЬNULL(Корректировки.Предоплата,0)
		|	КОНЕЦ КАК Предоплата,
		|	ВЫБОР КОГДА ДД.ПредоплатаУпрОстаток + ЕСТЬNULL(Корректировки.ПредоплатаУпр,0) < 0
		|		ТОГДА 0 - (ДД.ПредоплатаУпрОстаток + ЕСТЬNULL(Корректировки.ПредоплатаУпр,0))
		|		ИНАЧЕ ДД.ПредоплатаУпрОстаток + ЕСТЬNULL(Корректировки.ПредоплатаУпр,0)
		|	КОНЕЦ КАК ПредоплатаУпр,
		|	ВЫБОР КОГДА ДД.ПредоплатаРеглОстаток + ЕСТЬNULL(Корректировки.ПредоплатаРегл,0) < 0
		|		ТОГДА 0 - (ДД.ПредоплатаРеглОстаток + ЕСТЬNULL(Корректировки.ПредоплатаРегл,0))
		|		ИНАЧЕ ДД.ПредоплатаРеглОстаток + ЕСТЬNULL(Корректировки.ПредоплатаРегл,0)
		|	КОНЕЦ КАК ПредоплатаРегл,
		|	ВЫБОР КОГДА ДД.ЗалогЗаТаруОстаток + ЕСТЬNULL(Корректировки.ЗалогЗаТару,0) < 0
		|		ТОГДА 0 - (ДД.ЗалогЗаТаруОстаток + ЕСТЬNULL(Корректировки.ЗалогЗаТару,0))
		|		ИНАЧЕ ДД.ЗалогЗаТаруОстаток + ЕСТЬNULL(Корректировки.ЗалогЗаТару,0)
		|	КОНЕЦ КАК ЗалогЗаТару,
		|	ВЫБОР КОГДА ДД.ЗалогЗаТаруРеглОстаток + ЕСТЬNULL(Корректировки.ЗалогЗаТаруРегл,0)< 0
		|		ТОГДА 0 - (ДД.ЗалогЗаТаруРеглОстаток + ЕСТЬNULL(Корректировки.ЗалогЗаТаруРегл,0))
		|		ИНАЧЕ ДД.ЗалогЗаТаруРеглОстаток + ЕСТЬNULL(Корректировки.ЗалогЗаТаруРегл,0)
		|	КОНЕЦ КАК ЗалогЗаТаруРегл,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка) КАК СтатьяДвиженияДенежныхСредств,
		|	ДД.УдалитьДатаПлатежа КАК УдалитьДатаПлатежа,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаПлатежа,
		|	ЕСТЬNULL(Периодика.Регистратор, ДД.РасчетныйДокумент) КАК ДокументОтгрузки,
		|	НЕОПРЕДЕЛЕНО КАК РасчетПоКурсуДокумента,
		|	1 КАК ИндексДвиженияВзаимозачета,
		|	ВЫБОР КОГДА ЕСТЬNULL(Периодика.Регистратор, 0) = ДД.РасчетныйДокумент 
		|		ТОГДА 1 
		|			ИНАЧЕ 0 
		|	КОНЕЦ КАК ПриоритетРасчетногоДокумента
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоДокументам.Остатки(&НачалоПериода) КАК ДД
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасчета КАК Аналитики
		|	ПО ДД.АналитикаУчетаПоПартнерам = Аналитики.КлючАналитики
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПоЛИФО КАК Периодика
		|		ПО Периодика.Ссылка = ДД.РасчетныйДокумент
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ 
		|							Корректировки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|							Корректировки.ЗаказКлиента              КАК ЗаказКлиента,
		|							Корректировки.РасчетныйДокумент         КАК РасчетныйДокумент,
		|							Корректировки.Валюта                    КАК Валюта,
		|							СУММА(Корректировки.КВозврату)          КАК КВозврату,
		|							СУММА(Корректировки.Долг)               КАК Долг,
		|							СУММА(Корректировки.ДолгУпр)            КАК ДолгУпр,
		|							СУММА(Корректировки.ДолгРегл)           КАК ДолгРегл,
		|							СУММА(Корректировки.Предоплата)         КАК Предоплата,
		|							СУММА(Корректировки.ПредоплатаУпр)      КАК ПредоплатаУпр,
		|							СУММА(Корректировки.ПредоплатаРегл)     КАК ПредоплатаРегл,
		|							СУММА(Корректировки.ЗалогЗаТару)        КАК ЗалогЗаТару,
		|							СУММА(Корректировки.ЗалогЗаТаруРегл)    КАК ЗалогЗаТаруРегл
		|						ИЗ	
		|							РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Корректировки
		|						ГДЕ
		|							Корректировки.Регистратор ССЫЛКА Документ.КорректировкаРегистров
		|							И Корректировки.Период МЕЖДУ  &НачалоПериода И &ОкончаниеПериода
		|						СГРУППИРОВАТЬ ПО 
		|							Корректировки.АналитикаУчетаПоПартнерам,
		|							Корректировки.ЗаказКлиента,
		|							Корректировки.РасчетныйДокумент,
		|							Корректировки.Валюта) КАК Корректировки
		|		ПО ДД.АналитикаУчетаПоПартнерам = Корректировки.АналитикаУчетаПоПартнерам
		|			И ДД.ЗаказКлиента           = Корректировки.ЗаказКлиента
		|			И ДД.РасчетныйДокумент         = Корректировки.РасчетныйДокумент
		|			И ДД.Валюта                    = Корректировки.Валюта
		|";
КонецФункции
	
Функция ТекстОтгрузкиПрошлыхПериодов()
	Возврат "
		|ВЫБРАТЬ
		|	ВЫБОР КОГДА ДД.Регистратор <> ДД.РасчетныйДокумент
		|		ТОГДА 9
		|		ИНАЧЕ 10
		|	КОНЕЦ КАК Приоритет,
		|	""ПрошлаяОтгрузка"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.ВидДвижения КАК ВидДвижения,
		|	ДД.Период КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ВЫБОР КОГДА ДД.ЗаказКлиента = НЕОПРЕДЕЛЕНО
		|		ТОГДА NULL
		|		ИНАЧЕ ДД.ЗаказКлиента
		|	КОНЕЦ КАК ЗаказКлиента,
		|	ВЫБОР КОГДА ДД.РасчетныйДокумент = ПрошлыеОтгрузки.ДокументОтгрузки
		|		ТОГДА ДД.Регистратор
		|		ИНАЧЕ ДД.РасчетныйДокумент
		|	КОНЕЦ КАК РасчетныйДокумент,
		|	ДД.Валюта КАК Валюта,
		|	СУММА(ВЫБОР КОГДА (ДД.Долг + ДД.Предоплата) < 0
		|		ТОГДА 0 - (ДД.Долг + ДД.Предоплата)
		|		ИНАЧЕ (ДД.Долг + ДД.Предоплата)
		|	КОНЕЦ) КАК Сумма,
		|	СУММА(ДД.КВозврату) КАК КВозврату,
		|	СУММА(ВЫБОР КОГДА ДД.РасчетныйДокумент = ДД.Регистратор
		|			ТОГДА ДД.Долг
		|			ИНАЧЕ 0
		|	КОНЕЦ) КАК Долг,
		|	СУММА(ВЫБОР КОГДА ДД.РасчетныйДокумент = ДД.Регистратор
		|			ТОГДА ДД.ДолгУпр
		|			ИНАЧЕ 0
		|	КОНЕЦ) КАК ДолгУпр,
		|	СУММА(ВЫБОР КОГДА ДД.РасчетныйДокумент = ДД.Регистратор
		|			ТОГДА ДД.ДолгРегл
		|			ИНАЧЕ 0
		|	КОНЕЦ) КАК ДолгРегл,
		|	СУММА(ВЫБОР КОГДА ДД.РасчетныйДокумент <> ДД.Регистратор И ДД.Долг <> 0
		|			ТОГДА ДД.Долг
		|			ИНАЧЕ ДД.Предоплата
		|	КОНЕЦ) КАК Предоплата,
		|	СУММА(ВЫБОР КОГДА ДД.РасчетныйДокумент <> ДД.Регистратор И ДД.ДолгУпр <> 0
		|			ТОГДА ДД.ДолгУпр
		|			ИНАЧЕ ДД.ПредоплатаУпр
		|	КОНЕЦ) КАК ПредоплатаУпр,
		|	СУММА(ВЫБОР КОГДА ДД.РасчетныйДокумент <> ДД.Регистратор И ДД.ДолгРегл <> 0
		|			ТОГДА ДД.ДолгРегл
		|			ИНАЧЕ ДД.ПредоплатаРегл
		|	КОНЕЦ) КАК ПредоплатаРегл,
		|	СУММА(ДД.ЗалогЗаТару) КАК ЗалогЗаТару,
		|	СУММА(ДД.ЗалогЗаТаруРегл) КАК ЗалогЗаТаруРегл,
		|	ДД.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДД.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК УдалитьДатаПлатежа,
		|	ДД.ДатаПлатежа КАК ДатаПлатежа,
		|	ПрошлыеОтгрузки.ДокументОтгрузки КАК ДокументОтгрузки,
		|	НЕОПРЕДЕЛЕНО КАК РасчетПоКурсуДокумента,
		|	1 КАК ИндексДвиженияВзаимозачета,
		|	ВЫБОР КОГДА ДД.Регистратор = ДД.РасчетныйДокумент 
		|		ТОГДА 1
		|			ИНАЧЕ 0 
		|	КОНЕЦ КАК ПриоритетРасчетногоДокумента
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК ДД
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрошлыеОтгрузки КАК ПрошлыеОтгрузки
		|	ПО ДД.Регистратор = ПрошлыеОтгрузки.Регистратор
		|		И ДД.РасчетныйДокумент = ПрошлыеОтгрузки.РасчетныйДокумент
		|		И ДД.АналитикаУчетаПоПартнерам = ПрошлыеОтгрузки.АналитикаУчетаПоПартнерам
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР КОГДА ДД.Регистратор <> ДД.РасчетныйДокумент
		|		ТОГДА 9
		|		ИНАЧЕ 10
		|	КОНЕЦ,
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.АналитикаУчетаПоПартнерам,
		|	ДД.ЗаказКлиента,
		|	ДД.РасчетныйДокумент,
		|	ДД.Валюта,
		|	ДД.ДатаПлатежа,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.СтатьяДвиженияДенежныхСредств,
		|	ДД.ДатаПлатежа,
		|	ПрошлыеОтгрузки.ДокументОтгрузки,
		|	ВЫБОР КОГДА ДД.Регистратор = ДД.РасчетныйДокумент 
		|		ТОГДА 1
		|			ИНАЧЕ 0 
		|	КОНЕЦ
		|";
КонецФункции

Функция ТекстДвиженияРасчетовСКлиентами()
	Возврат "
		| // Движения по РН ""Расчеты с клиентами"" без корректировок реализаций
		|ВЫБРАТЬ
		|	ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0)
		|			ТОГДА 12
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|		ТОГДА 14
		|		ИНАЧЕ 13
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Оплачивается > 0)
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.КОплате > 0)
		|				И ДД.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента)
		|			ТОГДА ""Оплата""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ""Отгрузка""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0 И ДД.КОплате <> 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента)
		|			ТОГДА ""ВозвратТоваров""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|			ТОГДА ""СторноОтгрузки""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Оплачивается = 0
		|			ТОГДА ""СторноОплаты""
		|	КОНЕЦ КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения КАК ВидДвижения,
		|	ДД.Период КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ВЫБОР КОГДА ДД.ЗаказКлиента = НЕОПРЕДЕЛЕНО
		|		ТОГДА NULL
		|		ИНАЧЕ ДД.ЗаказКлиента
		|	КОНЕЦ КАК ЗаказКлиента,
		|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.ВводОстатков)
		|		И ТИПЗНАЧЕНИЯ(ДД.ЗаказКлиента) <> ТИП(Справочник.ДоговорыКонтрагентов)
		|		И ДД.ЗаказКлиента <> НЕОПРЕДЕЛЕНО
		|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ТОГДА ДД.ЗаказКлиента
		|		ИНАЧЕ ДД.Регистратор
		|	КОНЕЦ КАК РасчетныйДокумент,
		|	ДД.Валюта КАК Валюта,
		|	СУММА(ДД.Сумма) КАК Сумма,
		|	0. КАК КВозврату,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0)
		|			ТОГДА ДД.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК Долг,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0)
		|			ТОГДА ДД.СуммаУпр
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК ДолгУпр,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0)
		|			ТОГДА ДД.СуммаРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК ДолгРегл,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			ТОГДА ДД.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК Предоплата,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			ТОГДА ДД.СуммаУпр
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК ПредоплатаУпр,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			ТОГДА ДД.СуммаРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК ПредоплатаРегл,
		|	СУММА(ДД.ЗалогЗаТару) КАК ЗалогЗаТару,
		|	0. КАК ЗалогЗаТаруРегл,
		|	ДД.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДД.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК УдалитьДатаПлатежа,
		|	ДД.ДатаПлатежа КАК ДатаПлатежа,
		|	ВЫБОР КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|		ТОГДА ДД.Регистратор.ДокументОснование
		|		ИНАЧЕ ДД.Регистратор
		|	КОНЕЦ КАК ДокументОтгрузки,
		|	ВЫБОР КОГДА ДД.Валюта = &ВалютаРеглУчета
		|		ИЛИ КРасчету.ВалютаДокумента = &ВалютаРеглУчета
		|		ИЛИ КРасчету.Валюта = &ВалютаУпрУчета
		|		ИЛИ КРасчету.ВалютаДокумента = &ВалютаУпрУчета
		|		ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РасчетПоКурсуДокумента,
		|
		|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.ВзаимозачетЗадолженности)
		|			И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|		ТОГДА 0
		|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.ВзаимозачетЗадолженности)
		|			И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0
		|		ТОГДА -1
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ИндексДвиженияВзаимозачета,
		|	0 КАК ПриоритетРасчетногоДокумента
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК ДД
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистраторыКРасчету КАК КРасчету
		|	ПО ДД.Регистратор = КРасчету.Регистратор
		|		И ДД.АналитикаУчетаПоПартнерам = КРасчету.АналитикаУчетаПоПартнерам
		|		И ДД.ЗаказКлиента = КРасчету.ЗаказКлиента
		|		И ДД.Валюта = КРасчету.Валюта
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Сумма <> 0
		|	И ТИПЗНАЧЕНИЯ(ДД.Регистратор) <> ЗНАЧЕНИЕ(Документ.КорректировкаРеализации)
		|	И НЕ ДД.Регистратор ССЫЛКА Документ.КорректировкаРегистров
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0)
		|			ТОГДА 12
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|		ТОГДА 14
		|		ИНАЧЕ 13
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Оплачивается > 0)
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.КОплате > 0)
		|				И ДД.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента)
		|			ТОГДА ""Оплата""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ""Отгрузка""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0 И ДД.КОплате <> 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента)
		|			ТОГДА ""ВозвратТоваров""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|			ТОГДА ""СторноОтгрузки""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Оплачивается = 0
		|			ТОГДА ""СторноОплаты""
		|	КОНЕЦ,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.ВидДвижения,
		|	ДД.АналитикаУчетаПоПартнерам,
		|	ДД.ЗаказКлиента,
		|	ДД.Валюта,
		|	ДД.ДатаПлатежа,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.СтатьяДвиженияДенежныхСредств,
		|	ДД.ДатаПлатежа,
		|	ВЫБОР КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|		ТОГДА ДД.Регистратор.ДокументОснование
		|		ИНАЧЕ ДД.Регистратор
		|	КОНЕЦ,
		|	ВЫБОР КОГДА ДД.Валюта = &ВалютаРеглУчета
		|		ИЛИ КРасчету.ВалютаДокумента = &ВалютаРеглУчета
		|		ИЛИ КРасчету.Валюта = &ВалютаУпрУчета
		|		ИЛИ КРасчету.ВалютаДокумента = &ВалютаУпрУчета
		|		ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.ВзаимозачетЗадолженности)
		|			И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|		ТОГДА 0
		|		КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.ВзаимозачетЗадолженности)
		|			И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0
		|		ТОГДА -1
		|		ИНАЧЕ 1
		|	КОНЕЦ,
		|	0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		| // Движения по РН ""Расчеты с клиентами"" документа ""Корректировка реализации""
		|ВЫБРАТЬ
		|	ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0)
		|			ТОГДА 12
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|		ТОГДА 14
		|		ИНАЧЕ 13
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Оплачивается > 0)
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.КОплате > 0)
		|				И ДД.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента)
		|			ТОГДА ""Оплата""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ""Отгрузка""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0 И ДД.КОплате <> 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента)
		|			ТОГДА ""ВозвратТоваров""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|			ТОГДА ""СторноОтгрузки""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Оплачивается = 0
		|			ТОГДА ""СторноОплаты""
		|	КОНЕЦ КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ДД.ВидДвижения КАК ВидДвижения,
		|	ДД.Период КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ВЫБОР КОГДА ДД.ЗаказКлиента = НЕОПРЕДЕЛЕНО
		|		ТОГДА NULL
		|		ИНАЧЕ ДД.ЗаказКлиента
		|	КОНЕЦ КАК ЗаказКлиента,
		|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.ВводОстатков)
		|		И ТИПЗНАЧЕНИЯ(ДД.ЗаказКлиента) <> ТИП(Справочник.ДоговорыКонтрагентов)
		|		И ДД.ЗаказКлиента <> НЕОПРЕДЕЛЕНО
		|		И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ТОГДА ДД.ЗаказКлиента
		|		ИНАЧЕ ДД.Регистратор
		|	КОНЕЦ КАК РасчетныйДокумент,
		|	ДД.Валюта КАК Валюта,
		|	СУММА(ДД.Сумма) КАК Сумма,
		|	0. КАК КВозврату,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0)
		|			ТОГДА ДД.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК Долг,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0)
		|			ТОГДА ДД.СуммаУпр
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК ДолгУпр,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0)
		|			ТОГДА ДД.СуммаРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК ДолгРегл,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			ТОГДА ДД.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК Предоплата,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			ТОГДА ДД.СуммаУпр
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК ПредоплатаУпр,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			ТОГДА ДД.СуммаРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК ПредоплатаРегл,
		|	СУММА(ДД.ЗалогЗаТару) КАК ЗалогЗаТару,
		|	0. КАК ЗалогЗаТаруРегл,
		|	ДД.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДД.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК УдалитьДатаПлатежа,
		|	ДД.ДатаПлатежа КАК ДатаПлатежа,
		|	ВЫБОР КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|		ТОГДА ДД.Регистратор.ДокументОснование
		|		ИНАЧЕ ДД.Регистратор
		|	КОНЕЦ КАК ДокументОтгрузки,
		|	ДД.РасчетПоКурсуДокумента КАК РасчетПоКурсуДокумента,
		|	1 КАК ИндексДвиженияВзаимозачета,
		|	0 КАК ПриоритетРасчетногоДокумента
		|ИЗ
		|	Корректировки КАК ДД
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0)
		|			ТОГДА 12
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|		ТОГДА 14
		|		ИНАЧЕ 13
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Оплачивается > 0)
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.КОплате > 0)
		|				И ДД.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента)
		|			ТОГДА ""Оплата""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ""Отгрузка""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0 И ДД.КОплате <> 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента)
		|			ТОГДА ""ВозвратТоваров""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|			ТОГДА ""СторноОтгрузки""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Оплачивается = 0
		|			ТОГДА ""СторноОплаты""
		|	КОНЕЦ,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.ВидДвижения,
		|	ДД.АналитикаУчетаПоПартнерам,
		|	ДД.ЗаказКлиента,
		|	ДД.Валюта,
		|	ДД.ДатаПлатежа,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.СтатьяДвиженияДенежныхСредств,
		|	ДД.ДатаПлатежа,
		|	ВЫБОР КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|		ТОГДА ДД.Регистратор.ДокументОснование
		|		ИНАЧЕ ДД.Регистратор
		|	КОНЕЦ,
		|	ДД.РасчетПоКурсуДокумента,
		|	0
		|";
КонецФункции

Функция ТекстАктуальныеЗаписиРасчетовСКлиентами()
	Возврат "
		|ВЫБРАТЬ
		|	11 КАК Приоритет,
		|	""Актуальные"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.ВидДвижения КАК ВидДвижения,
		|	ДД.Период КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ДД.ЗаказКлиента КАК ЗаказКлиента,
		|	ДД.РасчетныйДокумент КАК РасчетныйДокумент,
		|	ДД.Валюта КАК Валюта,
		|	0. КАК Сумма,
		|	ДД.КВозврату КАК КВозврату,
		|	ДД.Долг КАК Долг,
		|	ДД.ДолгУпр КАК ДолгУпр,
		|	ДД.ДолгРегл КАК ДолгРегл,
		|	ДД.Предоплата КАК Предоплата,
		|	ДД.ПредоплатаУпр КАК ПредоплатаУпр,
		|	ДД.ПредоплатаРегл КАК ПредоплатаРегл,
		|	ДД.ЗалогЗаТару КАК ЗалогЗаТару,
		|	ДД.ЗалогЗаТаруРегл КАК ЗалогЗаТаруРегл,
		|	ДД.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДД.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК УдалитьДатаПлатежа,
		|	ДД.ДатаПлатежа КАК ДатаПлатежа,
		|	НЕОПРЕДЕЛЕНО КАК ДокументОтгрузки,
		|	ЛОЖЬ КАК РасчетПоКурсуДокумента,
		|	1 КАК ИндексДвиженияВзаимозачета,
		|	0 КАК ПриоритетРасчетногоДокумента
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК ДД
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АктуальныеАналитики КАК АктуальныеАналитики
		|	ПО ДД.Регистратор = АктуальныеАналитики.Регистратор
		|		И ДД.АналитикаУчетаПоПартнерам = АктуальныеАналитики.АналитикаУчетаПоПартнерам
		|		И ДД.ЗаказКлиента = АктуальныеАналитики.ЗаказКлиента
		|		И ДД.Валюта = АктуальныеАналитики.Валюта
		|";
КонецФункции

#КонецОбласти

#Область РасчетыСПоставщиками

// Текст запроса исходных движений для распределения расчетов с поставщиками
Функция ДанныеДляРасчетовСПоставщиками(НачалоПериода, ОкончаниеПериода, АналитикиРасчета, ВременныеТаблицы)
	// СОРТИРОВКА по аналитикам, приоритету и моменту движения - требование алгоритма обсчета расчетов
	ТекстСортировка = "
		|УПОРЯДОЧИТЬ ПО
		|	Период, Приоритет, Регистратор
		|";
	
	ИсходныйТекстЗапроса =
		ТекстРасчетыСПоставщикамиИнициализация() + ";"
		+  ТекстОписаниеРасчетыСПоставщиками()
		+ "ОБЪЕДИНИТЬ ВСЕ" +  ТекстОстаткиРасчетовСПоставщиками()
		+ "ОБЪЕДИНИТЬ ВСЕ" +  ТекстПоступленияПрошлыхПериодов()
		+ "ОБЪЕДИНИТЬ ВСЕ" +  ТекстДвиженияРасчетовСПоставщиками()
		+ ТекстСортировка;
	ИсходныйЗапрос = Новый Запрос(ИсходныйТекстЗапроса);
	ИсходныйЗапрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	ИсходныйЗапрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	ИсходныйЗапрос.УстановитьПараметр("АналитикиРасчета", АналитикиРасчета);
	ИсходныйЗапрос.УстановитьПараметр("ПоВсемАналитикам", АналитикиРасчета = Неопределено);
	ИсходныйЗапрос.УстановитьПараметр("ВалютаРеглУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	ИсходныйЗапрос.УстановитьПараметр("ВалютаУпрУчета", Константы.ВалютаУправленческогоУчета.Получить());
	ИсходныйЗапрос.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ДанныеДляРасчета = ИсходныйЗапрос.Выполнить();

	Возврат ДанныеДляРасчета;
КонецФункции

Функция РасчетныеРасчетыСПоставщиками(ДанныеДляРасчета, Регистраторы, ЦепочкиДвижений = Неопределено, Тест = Ложь) // ДанныеДляРасчета будут изменены
	// Шаг 1: создаем буфер накопления рассчитанных партий
	Запрос = Новый Запрос(ТекстОписаниеРасчетыСПоставщиками());
	РасчетныеПартии = Запрос.Выполнить().Выгрузить();
	
	// Шаг 2: готовим описание обсчета
	КонтекстЦепочек = ОписаниеЦепочек("ОстатокОплат, ОстатокПоступлений, ПрошлоеПоступление, Оплата, СторноПоступления, СторноОплаты, ПоступлениеТоваров, ВозвратТоваров, Актуальные");
	
	ПоляПотреблений = "АналитикаУчетаПоПартнерам, ЗаказПоставщику, Валюта";
	КонтекстДвижений = ОписаниеДвижений(
		"РасчетыСПоставщиками", ПереченьПолей(РасчетныеПартии.Колонки),
		ПоляПотреблений,
		"КВозврату, Долг, ДолгУпр, ДолгРегл, Предоплата, ПредоплатаУпр, ПредоплатаРегл, ЗалогЗаТару, ЗалогЗаТаруРегл, Сумма",
		"Сумма", "Сумма", "РасчетныйДокумент", "Период", "Период, ПриоритетРасчетногоДокумента УБЫВ, Приоритет ВОЗР");
		
	// Шаг 3: обсчитываем потребления из партий
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "Оплата", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Оплата", "ОстатокПоступлений", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Оплата", "ПоступлениеТоваров", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "Оплата", "СторноОплаты", ПоляПотреблений);
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "ПоступлениеТоваров", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ПоступлениеТоваров", "ОстатокОплат", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ПоступлениеТоваров", "Оплата", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ПоступлениеТоваров", "ВозвратТоваров", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ПоступлениеТоваров", "СторноПоступления", ПоляПотреблений);
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "СторноОплаты", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "СторноОплаты", "ОстатокОплат", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "СторноОплаты", "Оплата", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "СторноОплаты", "ВозвратТоваров", ПоляПотреблений);
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "ВозвратТоваров", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ВозвратТоваров", "ОстатокПоступлений", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ВозвратТоваров", "ПоступлениеТоваров", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ВозвратТоваров", "ОстатокПоступлений", ПоляПотреблений);
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "ВозвратТоваров", "СторноОплаты", ПоляПотреблений);
	
	ДобавитьОписаниеПриемника(КонтекстЦепочек, "СторноПоступления", "АналитикаУчетаПоПартнерам, ЗаказПоставщику, Валюта, ДокументПоступления");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "СторноПоступления", "ОстатокПоступлений", "АналитикаУчетаПоПартнерам, ЗаказПоставщику, Валюта, ДокументПоступления");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "СторноПоступления", "ПрошлоеПоступление", "АналитикаУчетаПоПартнерам, ЗаказПоставщику, Валюта, ДокументПоступления");
	ДобавитьОписаниеИсточника(КонтекстЦепочек, "СторноПоступления", "ПоступлениеТоваров", "АналитикаУчетаПоПартнерам, ЗаказПоставщику, Валюта, ДокументПоступления");
	
	ЦепочкиДвижений = ЦепочкиДвиженийИзВыборки(КонтекстЦепочек, ДанныеДляРасчета);
	РассчитатьПартииПоЦепочкамИзВыборки(КонтекстДвижений, ДанныеДляРасчета, РасчетныеПартии, ЦепочкиДвижений, Регистраторы, Тест);
	
	// Шаг 4: Удаляем строки, не требующие записи в базу.
	УдалитьНезаписываемыеСтроки(КонтекстДвижений.Контекст, РасчетныеПартии);
	
	// Шаг 5: Сортировка партий для дальнейшей записи
	РасчетныеПартии.Сортировать("Регистратор, Приоритет", Новый СравнениеЗначений);
	Возврат РасчетныеПартии;
КонецФункции

Функция ТекстРасчетыСПоставщикамиИнициализация()
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Аналитики.КлючАналитики КАК КлючАналитики,
		|	Аналитики.Организация   КАК Организация,
		|	Аналитики.Партнер       КАК Партнер,
		|	Аналитики.Контрагент    КАК Контрагент,
		|	Аналитики.Договор       КАК Договор
		|ПОМЕСТИТЬ АналитикиРасчета
		|ИЗ
		|	РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитики
		|ГДЕ
		|	(Аналитики.КлючАналитики В (&АналитикиРасчета)
		|		ИЛИ &ПоВсемАналитикам)
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.Регистратор                             КАК Регистратор,
		|	Расчеты.АналитикаУчетаПоПартнерам               КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ЗаказПоставщику                         КАК ЗаказПоставщику,
		|	Расчеты.Валюта                                  КАК Валюта,
		|	ЕСТЬNULL(Суммы.Валюта, &ВалютаРеглУчета)        КАК ВалютаДокумента
		|ПОМЕСТИТЬ РегистраторыКРасчету
		|ИЗ (ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		Расчеты.Регистратор               КАК Регистратор,
		|		Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|		Расчеты.ЗаказПоставщику           КАК ЗаказПоставщику,
		|		Расчеты.Валюта                    КАК Валюта
		|	ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасчета КАК Аналитики
		|			ПО Расчеты.АналитикаУчетаПоПартнерам = Аналитики.КлючАналитики
		|	ГДЕ
		|		Расчеты.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И Расчеты.Сумма <> 0
		|
		|	ОБЪЕДИНИТЬ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		РасчетыПоДокументам.Регистратор               КАК Регистратор,
		|		РасчетыПоДокументам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|		РасчетыПоДокументам.ЗаказПоставщику           КАК ЗаказПоставщику,
		|		РасчетыПоДокументам.Валюта                    КАК Валюта
		|	ИЗ
		|		РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК РасчетыПоДокументам 
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасчета КАК Аналитики
		|				ПО РасчетыПоДокументам.АналитикаУчетаПоПартнерам = Аналитики.КлючАналитики
		|	ГДЕ
		|		РасчетыПоДокументам.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И (РасчетыПоДокументам.Долг <> 0 ИЛИ РасчетыПоДокументам.Предоплата <> 0)
		|	) КАК Расчеты
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК Суммы
		|	ПО Расчеты.Регистратор = Суммы.Регистратор
		|		И Расчеты.АналитикаУчетаПоПартнерам = Суммы.АналитикаУчетаПоПартнерам
		|		И Суммы.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
		|;
		|///////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.Регистратор.ДокументОснование КАК Регистратор
		|ПОМЕСТИТЬ ДокументыПоступления
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасчета КАК Аналитики
		|	ПО Расчеты.АналитикаУчетаПоПартнерам = Аналитики.КлючАналитики
		|ГДЕ
		|	Расчеты.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И Расчеты.Сумма <> 0
		|	И Расчеты.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|;
		|///////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.Регистратор КАК Регистратор,
		|	Расчеты.РасчетныйДокумент КАК РасчетныйДокумент,
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ДокументыПоступления.Регистратор КАК ДокументПоступления
		|ПОМЕСТИТЬ ПрошлыеПоступления
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК Расчеты
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПоступления КАК ДокументыПоступления
		|	ПО Расчеты.Регистратор = ДокументыПоступления.Регистратор
		|		ИЛИ Расчеты.РасчетныйДокумент = ДокументыПоступления.Регистратор
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасчета КАК ОтборПоАналитикам
		|	ПО Расчеты.АналитикаУчетаПоПартнерам = ОтборПоАналитикам.КлючАналитики
		|ГДЕ
		|	Расчеты.Период < &НачалоПериода
		|	И Расчеты.РасчетныйДокумент <> ДокументыПоступления.Регистратор
		|;
		|////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.Регистратор               КАК Регистратор,
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ЗаказПоставщику           КАК ЗаказПоставщику,
		|	Расчеты.Валюта                    КАК Валюта
		|ПОМЕСТИТЬ АктуальныеАналитики
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистраторыКРасчету КАК Регистраторы
		|	ПО Расчеты.Регистратор = Регистраторы.Регистратор
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ АналитикиРасчета КАК АналитикиКРасчету
		|	ПО Расчеты.АналитикаУчетаПоПартнерам = АналитикиКРасчету.КлючАналитики
		|ГДЕ
		|	АналитикиКРасчету.КлючАналитики ЕСТЬ NULL
		|	И НЕ Расчеты.Регистратор ССЫЛКА Документ.КорректировкаРегистров
		|;
		|////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.РасчетныйДокумент КАК Ссылка,
		|	МАКСИМУМ(Периодика.Регистратор) КАК Регистратор,
		|	МАКСИМУМ(Периодика.Период) КАК Период
		|ПОМЕСТИТЬ ОстаткиПоЛИФО
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам.Остатки(&НачалоПериода) КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК Периодика
		|		ПО Периодика.Период < &НачалоПериода
		|		И Периодика.РасчетныйДокумент = ДД.РасчетныйДокумент
		|ГДЕ
		|	ДД.РасчетныйДокумент <> НЕОПРЕДЕЛЕНО
		|	И ((Периодика.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И Периодика.Долг > 0)
		|		ИЛИ (Периодика.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И Периодика.Предоплата > 0)
		|	)
		|СГРУППИРОВАТЬ ПО
		|	ДД.РасчетныйДокумент
		|";
КонецФункции

Функция ТекстОписаниеРасчетыСПоставщиками()
	Возврат "
		|ВЫБРАТЬ ПЕРВЫЕ 0
		|	0 КАК Приоритет,
		|	""ХХХХХХХХХХХХХХХХХХ"" КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка) КАК АналитикаУчетаПоПартнерам,
		|	ДД.ЗаказПоставщику КАК ЗаказПоставщику,
		|	ДД.РасчетныйДокумент КАК РасчетныйДокумент,
		|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК Валюта,
		|	0. КАК Сумма,
		|	0. КАК КВозврату,
		|	0. КАК Долг,
		|	0. КАК ДолгУпр,
		|	0. КАК ДолгРегл,
		|	0. КАК Предоплата,
		|	0. КАК ПредоплатаУпр,
		|	0. КАК ПредоплатаРегл,
		|	0. КАК ЗалогЗаТару,
		|	0. КАК ЗалогЗаТаруРегл,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка) КАК СтатьяДвиженияДенежныхСредств,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК УдалитьДатаПлатежа,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаПлатежа,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	НЕОПРЕДЕЛЕНО КАК РасчетПоКурсуДокумента,
		|	0 КАК ПриоритетРасчетногоДокумента
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК ДД
		|";
КонецФункции
	
Функция ТекстОстаткиРасчетовСПоставщиками()
	Возврат "
		|ВЫБРАТЬ
		|	10 КАК Приоритет,
		|	ВЫБОР КОГДА ДД.ДолгОстаток <> 0 
		|		ТОГДА ""ОстатокПоступлений""
		|		ИНАЧЕ ""ОстатокОплат""
		|	КОНЕЦ КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЕСТЬNULL(Периодика.Период, &НачалоПериода) КАК Период,
		|	ЕСТЬNULL(Периодика.Регистратор, ДД.РасчетныйДокумент) КАК Регистратор,
		|	ДД.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ВЫБОР КОГДА ДД.ЗаказПоставщику = НЕОПРЕДЕЛЕНО
		|		ТОГДА NULL
		|		ИНАЧЕ ДД.ЗаказПоставщику
		|	КОНЕЦ КАК ЗаказПоставщику,
		|	ДД.РасчетныйДокумент КАК РасчетныйДокумент,
		|	ДД.Валюта КАК Валюта,
		|	ВЫБОР КОГДА (ДД.ДолгОстаток + ЕСТЬNULL(Корректировки.Долг,0) + ДД.ПредоплатаОстаток + ЕСТЬNULL(Корректировки.Предоплата,0)) < 0
		|		ТОГДА 0 - (ДД.ДолгОстаток + ЕСТЬNULL(Корректировки.Долг,0) + ДД.ПредоплатаОстаток + ЕСТЬNULL(Корректировки.Предоплата,0))
		|		ИНАЧЕ (ДД.ДолгОстаток + ЕСТЬNULL(Корректировки.Долг,0)  + ДД.ПредоплатаОстаток + ЕСТЬNULL(Корректировки.Предоплата,0))
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР КОГДА ДД.КВозвратуОстаток + ЕСТЬNULL(Корректировки.КВозврату,0) < 0
		|		ТОГДА 0 - (ДД.КВозвратуОстаток + ЕСТЬNULL(Корректировки.КВозврату,0))
		|		ИНАЧЕ ДД.КВозвратуОстаток + ЕСТЬNULL(Корректировки.КВозврату,0)
		|	КОНЕЦ КАК КВозврату,
		|	ВЫБОР КОГДА ДД.ДолгОстаток + ЕСТЬNULL(Корректировки.Долг,0) < 0
		|		ТОГДА 0 - (ДД.ДолгОстаток + ЕСТЬNULL(Корректировки.Долг,0))
		|		ИНАЧЕ ДД.ДолгОстаток + ЕСТЬNULL(Корректировки.Долг,0)
		|	КОНЕЦ КАК Долг,
		|	ВЫБОР КОГДА ДД.ДолгУпрОстаток + ЕСТЬNULL(Корректировки.ДолгУпр,0) < 0
		|		ТОГДА 0 - (ДД.ДолгУпрОстаток + ЕСТЬNULL(Корректировки.ДолгУпр,0))
		|		ИНАЧЕ ДД.ДолгУпрОстаток + ЕСТЬNULL(Корректировки.ДолгУпр,0)
		|	КОНЕЦ КАК ДолгУпр,
		|	ВЫБОР КОГДА ДД.ДолгРеглОстаток + ЕСТЬNULL(Корректировки.ДолгРегл,0) < 0
		|		ТОГДА 0 - (ДД.ДолгРеглОстаток + ЕСТЬNULL(Корректировки.ДолгРегл,0))
		|		ИНАЧЕ ДД.ДолгРеглОстаток + ЕСТЬNULL(Корректировки.ДолгРегл,0)
		|	КОНЕЦ КАК ДолгРегл,
		|	ВЫБОР КОГДА ДД.ПредоплатаОстаток + ЕСТЬNULL(Корректировки.Предоплата,0) < 0
		|		ТОГДА 0 - (ДД.ПредоплатаОстаток + ЕСТЬNULL(Корректировки.Предоплата,0))
		|		ИНАЧЕ ДД.ПредоплатаОстаток + ЕСТЬNULL(Корректировки.Предоплата,0)
		|	КОНЕЦ КАК Предоплата,
		|	ВЫБОР КОГДА ДД.ПредоплатаУпрОстаток + ЕСТЬNULL(Корректировки.ПредоплатаУпр,0) < 0
		|		ТОГДА 0 - (ДД.ПредоплатаУпрОстаток + ЕСТЬNULL(Корректировки.ПредоплатаУпр,0))
		|		ИНАЧЕ ДД.ПредоплатаУпрОстаток + ЕСТЬNULL(Корректировки.ПредоплатаУпр,0)
		|	КОНЕЦ КАК ПредоплатаУпр,
		|	ВЫБОР КОГДА ДД.ПредоплатаРеглОстаток + ЕСТЬNULL(Корректировки.ПредоплатаРегл,0) < 0
		|		ТОГДА 0 - (ДД.ПредоплатаРеглОстаток + ЕСТЬNULL(Корректировки.ПредоплатаРегл,0))
		|		ИНАЧЕ ДД.ПредоплатаРеглОстаток + ЕСТЬNULL(Корректировки.ПредоплатаРегл,0)
		|	КОНЕЦ КАК ПредоплатаРегл,
		|	ВЫБОР КОГДА ДД.ЗалогЗаТаруОстаток + ЕСТЬNULL(Корректировки.ЗалогЗаТару,0) < 0
		|		ТОГДА 0 - (ДД.ЗалогЗаТаруОстаток + ЕСТЬNULL(Корректировки.ЗалогЗаТару,0))
		|		ИНАЧЕ ДД.ЗалогЗаТаруОстаток + ЕСТЬNULL(Корректировки.ЗалогЗаТару,0)
		|	КОНЕЦ КАК ЗалогЗаТару,
		|	ВЫБОР КОГДА ДД.ЗалогЗаТаруРеглОстаток + ЕСТЬNULL(Корректировки.ЗалогЗаТаруРегл,0)< 0
		|		ТОГДА 0 - (ДД.ЗалогЗаТаруРеглОстаток + ЕСТЬNULL(Корректировки.ЗалогЗаТаруРегл,0))
		|		ИНАЧЕ ДД.ЗалогЗаТаруРеглОстаток + ЕСТЬNULL(Корректировки.ЗалогЗаТаруРегл,0)
		|	КОНЕЦ КАК ЗалогЗаТаруРегл,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка) КАК СтатьяДвиженияДенежныхСредств,
		|	ДД.УдалитьДатаПлатежа КАК УдалитьДатаПлатежа,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаПлатежа,
		|	ЕСТЬNULL(Периодика.Регистратор, ДД.РасчетныйДокумент) КАК ДокументПоступления,
		|	НЕОПРЕДЕЛЕНО КАК РасчетПоКурсуДокумента,
		|	ВЫБОР КОГДА ЕСТЬNULL(Периодика.Регистратор, 0) = ДД.РасчетныйДокумент 
		|		ТОГДА 1 
		|			ИНАЧЕ 0 
		|	КОНЕЦ КАК ПриоритетРасчетногоДокумента
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам.Остатки(&НачалоПериода) КАК ДД
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиРасчета КАК Аналитики
		|	ПО ДД.АналитикаУчетаПоПартнерам = Аналитики.КлючАналитики
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПоЛИФО КАК Периодика
		|		ПО Периодика.Ссылка = ДД.РасчетныйДокумент
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ 
		|							Корректировки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|							Корректировки.ЗаказПоставщику           КАК ЗаказПоставщику,
		|							Корректировки.РасчетныйДокумент         КАК РасчетныйДокумент,
		|							Корректировки.Валюта                    КАК Валюта,
		|							СУММА(Корректировки.КВозврату)          КАК КВозврату,
		|							СУММА(Корректировки.Долг)               КАК Долг,
		|							СУММА(Корректировки.ДолгУпр)            КАК ДолгУпр,
		|							СУММА(Корректировки.ДолгРегл)           КАК ДолгРегл,
		|							СУММА(Корректировки.Предоплата)         КАК Предоплата,
		|							СУММА(Корректировки.ПредоплатаУпр)      КАК ПредоплатаУпр,
		|							СУММА(Корректировки.ПредоплатаРегл)     КАК ПредоплатаРегл,
		|							СУММА(Корректировки.ЗалогЗаТару)        КАК ЗалогЗаТару,
		|							СУММА(Корректировки.ЗалогЗаТаруРегл)    КАК ЗалогЗаТаруРегл
		|						ИЗ	
		|							РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК Корректировки
		|						ГДЕ
		|							Корректировки.Регистратор ССЫЛКА Документ.КорректировкаРегистров
		|							И Корректировки.Период МЕЖДУ  &НачалоПериода И &ОкончаниеПериода
		|						СГРУППИРОВАТЬ ПО 
		|							Корректировки.АналитикаУчетаПоПартнерам,
		|							Корректировки.ЗаказПоставщику,
		|							Корректировки.РасчетныйДокумент,
		|							Корректировки.Валюта) КАК Корректировки
		|		ПО ДД.АналитикаУчетаПоПартнерам = Корректировки.АналитикаУчетаПоПартнерам
		|			И ДД.ЗаказПоставщику           = Корректировки.ЗаказПоставщику
		|			И ДД.РасчетныйДокумент         = Корректировки.РасчетныйДокумент
		|			И ДД.Валюта                    = Корректировки.Валюта
		|";
КонецФункции
	
Функция ТекстПоступленияПрошлыхПериодов()
	Возврат "
		|ВЫБРАТЬ
		|	ВЫБОР КОГДА ДД.Регистратор <> ДД.РасчетныйДокумент
		|		ТОГДА 9
		|		ИНАЧЕ 10
		|	КОНЕЦ КАК Приоритет,
		|	""ПрошлоеПоступление"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.ВидДвижения КАК ВидДвижения,
		|	ДД.Период КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ВЫБОР КОГДА ДД.ЗаказПоставщику = НЕОПРЕДЕЛЕНО
		|		ТОГДА NULL
		|		ИНАЧЕ ДД.ЗаказПоставщику
		|	КОНЕЦ КАК ЗаказПоставщику,
		|	ВЫБОР КОГДА ДД.РасчетныйДокумент = ПрошлыеПоступления.ДокументПоступления
		|		ТОГДА ДД.Регистратор
		|		ИНАЧЕ ДД.РасчетныйДокумент
		|	КОНЕЦ КАК РасчетныйДокумент,
		|	ДД.Валюта КАК Валюта,
		|	СУММА(ВЫБОР КОГДА (ДД.Долг + ДД.Предоплата) < 0
		|		ТОГДА 0 - (ДД.Долг + ДД.Предоплата)
		|		ИНАЧЕ (ДД.Долг + ДД.Предоплата)
		|	КОНЕЦ) КАК Сумма,
		|	СУММА(ДД.КВозврату) КАК КВозврату,
		|	СУММА(ВЫБОР КОГДА ДД.РасчетныйДокумент = ДД.Регистратор
		|			ТОГДА ДД.Долг
		|			ИНАЧЕ 0
		|	КОНЕЦ) КАК Долг,
		|	СУММА(ВЫБОР КОГДА ДД.РасчетныйДокумент = ДД.Регистратор
		|			ТОГДА ДД.ДолгУпр
		|			ИНАЧЕ 0
		|	КОНЕЦ) КАК ДолгУпр,
		|	СУММА(ВЫБОР КОГДА ДД.РасчетныйДокумент = ДД.Регистратор
		|			ТОГДА ДД.ДолгРегл
		|			ИНАЧЕ 0
		|	КОНЕЦ) КАК ДолгРегл,
		|	СУММА(ВЫБОР КОГДА ДД.РасчетныйДокумент <> ДД.Регистратор И ДД.Долг <> 0
		|			ТОГДА ДД.Долг
		|			ИНАЧЕ ДД.Предоплата
		|	КОНЕЦ) КАК Предоплата,
		|	СУММА(ВЫБОР КОГДА ДД.РасчетныйДокумент <> ДД.Регистратор И ДД.ДолгУпр <> 0
		|			ТОГДА ДД.ДолгУпр
		|			ИНАЧЕ ДД.ПредоплатаУпр
		|	КОНЕЦ) КАК ПредоплатаУпр,
		|	СУММА(ВЫБОР КОГДА ДД.РасчетныйДокумент <> ДД.Регистратор И ДД.ДолгРегл <> 0
		|			ТОГДА ДД.ДолгРегл
		|			ИНАЧЕ ДД.ПредоплатаРегл
		|	КОНЕЦ) КАК ПредоплатаРегл,
		|	СУММА(ДД.ЗалогЗаТару) КАК ЗалогЗаТару,
		|	СУММА(ДД.ЗалогЗаТаруРегл) КАК ЗалогЗаТаруРегл,
		|	ДД.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДД.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК УдалитьДатаПлатежа,
		|	ДД.ДатаПлатежа КАК ДатаПлатежа,
		|	ПрошлыеПоступления.ДокументПоступления КАК ДокументПоступления,
		|	НЕОПРЕДЕЛЕНО КАК РасчетПоКурсуДокумента,
		|	ВЫБОР КОГДА ДД.Регистратор = ДД.РасчетныйДокумент 
		|		ТОГДА 1
		|			ИНАЧЕ 0 
		|	КОНЕЦ КАК ПриоритетРасчетногоДокумента
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК ДД
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрошлыеПоступления КАК ПрошлыеПоступления
		|	ПО ДД.Регистратор = ПрошлыеПоступления.Регистратор
		|		И ДД.РасчетныйДокумент = ПрошлыеПоступления.РасчетныйДокумент
		|		И ДД.АналитикаУчетаПоПартнерам = ПрошлыеПоступления.АналитикаУчетаПоПартнерам
		|СГРУППИРОВАТЬ ПО
		|	ДД.ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.АналитикаУчетаПоПартнерам,
		|	ДД.ЗаказПоставщику,
		|	ДД.РасчетныйДокумент,
		|	ДД.Валюта,
		|	ДД.ДатаПлатежа,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.СтатьяДвиженияДенежныхСредств,
		|	ДД.ДатаПлатежа,
		|	ПрошлыеПоступления.ДокументПоступления
		|";
КонецФункции

Функция ТекстДвиженияРасчетовСПоставщиками()
	Возврат "
		|ВЫБРАТЬ
		|	ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма >= 0 И ДД.КОплате >= 0 И ДД.Оплачивается >= 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|		ТОГДА 10 // это сторно поступления
		|	КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма < 0 И ДД.КОплате <= 0)
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0 И ДД.КОплате < 0
		|					И ДД.ХозяйственнаяОперация В (
		|						ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности),
		|						ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)))
		|		ТОГДА 10 // это сторно оплаты
		|	КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0 // это долг
		|		ТОГДА 11
		|	КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма < 0 // это долг
		|					И (ДД.КПоступлению < 0 ИЛИ ДД.ХозяйственнаяОперация В (
		|							ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности),
		|							ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеДебиторскойЗадолженности)))
		|		ТОГДА 11
		|		ИНАЧЕ 12
		|	КОНЕЦ КАК Приоритет,
		|	ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0 И ДД.КПоступлению >= 0
		|			ТОГДА ""ПоступлениеТоваров""
		|		КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма >= 0 И ДД.КОплате >= 0 И ДД.Оплачивается >= 0)
		|				И НЕ ДД.ХозяйственнаяОперация В (
		|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику),
		|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности))
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0 И ДД.КПоступлению < 0)
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма > 0 И ДД.КОплате = 0 И ДД.Оплачивается = 0 
		|					И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику))
		|			ТОГДА ""Оплата""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма >= 0 И ДД.КОплате >= 0 И ДД.Оплачивается >= 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|			ТОГДА ""СторноПоступления""
		|		КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма > 0 И ДД.КОплате > 0
		|					И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику)
		|				) ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0 И ДД.КОплате = 0)
		|			ТОГДА ""ВозвратТоваров""
		|		КОГДА ((ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма < 0 И ДД.КОплате <= 0)
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0 И ДД.КОплате < 0
		|					И ДД.ХозяйственнаяОперация В (
		|						ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности),
		|						ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка))
		|					))
		|			ТОГДА ""СторноОплаты""
		|	КОНЕЦ КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ВЫБОР КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0
		|				И (ДД.КПоступлению < 0 ИЛИ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)))
		|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ДД.ВидДвижения
		|	КОНЕЦ КАК ВидДвижения,
		|	ДД.Период КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ВЫБОР КОГДА ДД.ЗаказПоставщику = НЕОПРЕДЕЛЕНО
		|		ТОГДА NULL
		|		ИНАЧЕ ДД.ЗаказПоставщику
		|	КОНЕЦ КАК ЗаказПоставщику,
		|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.ВводОстатков)
		|			И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			И ТИПЗНАЧЕНИЯ(ДД.ЗаказПоставщику) <> ТИП(Справочник.ДоговорыКонтрагентов)
		|			И ДД.ЗаказПоставщику <> НЕОПРЕДЕЛЕНО
		|		ТОГДА ДД.ЗаказПоставщику
		|		ИНАЧЕ ДД.Регистратор
		|	КОНЕЦ КАК РасчетныйДокумент,
		|	ДД.Валюта КАК Валюта,
		|	СУММА(
		|		ВЫБОР 
		|			КОГДА ДД.Сумма < 0 И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|			ТОГДА 0 - ДД.Сумма
		|			ИНАЧЕ ДД.Сумма
		|		КОНЕЦ
		|	) КАК Сумма,
		|	0. КАК КВозврату,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			ТОГДА ДД.Сумма
		|		КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма < 0
		|					И (ДД.КПоступлению < 0 ИЛИ ДД.ХозяйственнаяОперация В (
		|							ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности),
		|							ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеДебиторскойЗадолженности))
		|				))
		|			ТОГДА ДД.Сумма
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма < 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации)
		|			ТОГДА 0 - ДД.Сумма
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма < 0 И ДД.КОплате < 0
		|				И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика)
		|			ТОГДА ДД.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК Долг,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			ТОГДА ДД.СуммаУпр
		|		КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма < 0
		|					И (ДД.КПоступлению < 0 ИЛИ ДД.ХозяйственнаяОперация В (
		|							ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности),
		|							ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеДебиторскойЗадолженности))
		|				))
		|			ТОГДА ДД.СуммаУпр
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма < 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации)
		|			ТОГДА 0 - ДД.СуммаУпр
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма < 0 И ДД.КОплате < 0
		|				И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика)
		|			ТОГДА ДД.СуммаУпр
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК ДолгУпр,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|			ТОГДА ДД.СуммаРегл
		|		КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма < 0
		|					И (ДД.КПоступлению < 0 ИЛИ ДД.ХозяйственнаяОперация В (
		|							ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности),
		|							ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеДебиторскойЗадолженности))
		|				))
		|			ТОГДА ДД.СуммаРегл
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма < 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации)
		|			ТОГДА 0 - ДД.СуммаРегл
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма < 0 И ДД.КОплате < 0
		|				И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика)
		|			ТОГДА ДД.СуммаРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК ДолгРегл,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма > 0
		|				ТОГДА ДД.Сумма
		|			КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0
		|					И (ДД.КПоступлению < 0 ИЛИ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)))
		|				ТОГДА 0 - ДД.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК Предоплата,
		|	СУММА(
		|		ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма > 0
		|				ТОГДА ДД.СуммаУпр
		|			КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0
		|					И (ДД.КПоступлению < 0 ИЛИ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)))
		|				ТОГДА 0 - ДД.СуммаУпр
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК ПредоплатаУпр,
		|	СУММА(ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма > 0
		|				ТОГДА ДД.СуммаРегл
		|			КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0
		|					И (ДД.КПоступлению < 0 ИЛИ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)))
		|				ТОГДА 0 - ДД.СуммаРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	) КАК ПредоплатаРегл,
		|	СУММА(ДД.ЗалогЗаТару) КАК ЗалогЗаТару,
		|	0. КАК ЗалогЗаТаруРегл,
		|	ДД.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДД.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК УдалитьДатаПлатежа,
		|	ДД.ДатаПлатежа КАК ДатаПлатежа,
		|	ВЫБОР КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|		ТОГДА ДД.Регистратор.ДокументОснование
		|		ИНАЧЕ ДД.Регистратор
		|	КОНЕЦ КАК ДокументПоступления,
		|	ВЫБОР КОГДА ДД.Валюта = &ВалютаРеглУчета
		|		ИЛИ КРасчету.ВалютаДокумента = &ВалютаРеглУчета
		|		ИЛИ КРасчету.Валюта = &ВалютаУпрУчета
		|		ИЛИ КРасчету.ВалютаДокумента = &ВалютаУпрУчета
		|		ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РасчетПоКурсуДокумента,
		|	0 КАК ПриоритетРасчетногоДокумента
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками КАК ДД
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистраторыКРасчету КАК КРасчету
		|	ПО ДД.Регистратор = КРасчету.Регистратор
		|		И ДД.АналитикаУчетаПоПартнерам = КРасчету.АналитикаУчетаПоПартнерам
		|		И ДД.ЗаказПоставщику = КРасчету.ЗаказПоставщику
		|		И ДД.Валюта = КРасчету.Валюта
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Сумма <> 0 
		|	И НЕ ДД.Регистратор ССЫЛКА Документ.КорректировкаРегистров
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма >= 0 И ДД.КОплате >= 0 И ДД.Оплачивается >= 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|		ТОГДА 10
		|	КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма < 0 И ДД.КОплате <= 0)
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0 И ДД.КОплате < 0
		|					И ДД.ХозяйственнаяОперация В (
		|						ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности),
		|						ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)))
		|		ТОГДА 10
		|	КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0
		|		ТОГДА 11
		|	КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма < 0
		|					И (ДД.КПоступлению < 0 ИЛИ ДД.ХозяйственнаяОперация В (
		|							ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности),
		|							ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеДебиторскойЗадолженности)))
		|		ТОГДА 11
		|		ИНАЧЕ 12
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма > 0 И ДД.КПоступлению >= 0
		|			ТОГДА ""ПоступлениеТоваров""
		|		КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма >= 0 И ДД.КОплате >= 0 И ДД.Оплачивается >= 0)
		|				И НЕ ДД.ХозяйственнаяОперация В (
		|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику),
		|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности))
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0 И ДД.КПоступлению < 0)
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма > 0 И ДД.КОплате = 0 И ДД.Оплачивается = 0 
		|					И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику))
		|			ТОГДА ""Оплата""
		|		КОГДА ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма >= 0 И ДД.КОплате >= 0 И ДД.Оплачивается >= 0
		|			И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|			ТОГДА ""СторноПоступления""
		|		КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма > 0 И ДД.КОплате > 0
		|					И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику)
		|				) ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0 И ДД.КОплате = 0)
		|			ТОГДА ""ВозвратТоваров""
		|		КОГДА ((ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И ДД.Сумма < 0 И ДД.КОплате <= 0)
		|				ИЛИ (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0 И ДД.КОплате < 0
		|					И ДД.ХозяйственнаяОперация В (
		|						ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности),
		|						ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка))
		|					))
		|			ТОГДА ""СторноОплаты""
		|	КОНЕЦ,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДД.Регистратор) = ТИП(Документ.ВводОстатков)
		|			И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			И ТИПЗНАЧЕНИЯ(ДД.ЗаказПоставщику) <> ТИП(Справочник.ДоговорыКонтрагентов)
		|			И ДД.ЗаказПоставщику <> НЕОПРЕДЕЛЕНО
		|		ТОГДА ДД.ЗаказПоставщику
		|		ИНАЧЕ ДД.Регистратор
		|	КОНЕЦ,
		|	ВЫБОР КОГДА (ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И ДД.Сумма < 0
		|				И (ДД.КПоступлению < 0 ИЛИ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)))
		|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ДД.ВидДвижения
		|	КОНЕЦ,
		|	ДД.АналитикаУчетаПоПартнерам,
		|	ДД.ЗаказПоставщику,
		|	ДД.Валюта,
		|	ДД.ДатаПлатежа,
		|	ДД.ХозяйственнаяОперация,
		|	ДД.СтатьяДвиженияДенежныхСредств,
		|	ДД.ДатаПлатежа,
		|	ВЫБОР КОГДА ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
		|		ТОГДА ДД.Регистратор.ДокументОснование
		|		ИНАЧЕ ДД.Регистратор
		|	КОНЕЦ,
		|	ВЫБОР КОГДА ДД.Валюта = &ВалютаРеглУчета
		|		ИЛИ КРасчету.ВалютаДокумента = &ВалютаРеглУчета
		|		ИЛИ КРасчету.Валюта = &ВалютаУпрУчета
		|		ИЛИ КРасчету.ВалютаДокумента = &ВалютаУпрУчета
		|		ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	10 КАК Приоритет,
		|	""Актуальные"" КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ДД.ВидДвижения КАК ВидДвижения,
		|	ДД.Период КАК Период,
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ДД.ЗаказПоставщику КАК ЗаказПоставщику,
		|	ДД.РасчетныйДокумент КАК РасчетныйДокумент,
		|	ДД.Валюта КАК Валюта,
		|	0. КАК Сумма,
		|	ДД.КВозврату КАК КВозврату,
		|	ДД.Долг КАК Долг,
		|	ДД.ДолгУпр КАК ДолгУпр,
		|	ДД.ДолгРегл КАК ДолгРегл,
		|	ДД.Предоплата КАК Предоплата,
		|	ДД.ПредоплатаУпр КАК ПредоплатаУпр,
		|	ДД.ПредоплатаРегл КАК ПредоплатаРегл,
		|	ДД.ЗалогЗаТару КАК ЗалогЗаТару,
		|	ДД.ЗалогЗаТаруРегл КАК ЗалогЗаТаруРегл,
		|	ДД.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДД.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК УдалитьДатаПлатежа,
		|	ДД.ДатаПлатежа КАК ДатаПлатежа,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	ЛОЖЬ КАК РасчетПоКурсуДокумента,
		|	0 КАК ПриоритетРасчетногоДокумента
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК ДД
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АктуальныеАналитики КАК АктуальныеАналитики
		|	ПО ДД.Регистратор = АктуальныеАналитики.Регистратор
		|		И ДД.АналитикаУчетаПоПартнерам = АктуальныеАналитики.АналитикаУчетаПоПартнерам
		|		И ДД.ЗаказПоставщику = АктуальныеАналитики.ЗаказПоставщику
		|		И ДД.Валюта = АктуальныеАналитики.Валюта
		|";
КонецФункции
#КонецОбласти

// Контекстные методы заполнения документа расчетов по строке расхода и строке прихода
#Область ЗаполнитьДокументРасчетов

// Используется для ВСЕХ вызовов заполнения расчетной партии
Процедура ЗаполнитьРасчетнуюПартию(Контекст, РасчетнаяПартия, Расход, Приход, Валюты)
	Если Контекст = "РасчетыСКлиентами" Тогда
		ЗаполнитьРасчетыСКлиентами(РасчетнаяПартия, Расход, Приход, Валюты);
	ИначеЕсли Контекст = "РасчетыСПоставщиками" Тогда
		ЗаполнитьРасчетысПоставщиками(РасчетнаяПартия, Расход, Приход, Валюты);
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьРасчетыСКлиентами(РасчетнаяПартия, Расход, Приход, Валюты) // РасчетыСКлиентамиПоДокументам
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	Если Неопределено = Приход // остатки по оплате и отгрузке не должны "закрываться" друг на друга
		ИЛИ (ТипЗнч(Расход.Регистратор) = Тип("ДокументСсылка.ВводОстатков") 
				И ТипЗнч(Приход.Регистратор) = Тип("ДокументСсылка.ВводОстатков"))
		ИЛИ (Расход.ТипЗаписи = "Отгрузка" И Приход.Долг <> 0) // Отгрузка закрывается только на предплату
		ИЛИ (Расход.ТипЗаписи = "Оплата" И Приход.Предоплата <> 0) // Оплата закрывается только на долг
	Тогда
		Возврат;
	КонецЕсли;
	
	СуммаСписания = Мин(Расход.Сумма, Приход.Сумма);
	
	РасходБазис = ?(Расход.Сумма <> 0, СуммаСписания / Расход.Сумма, 0);
	ПриходБазис = ?(Приход.Сумма <> 0, СуммаСписания / Приход.Сумма, 0);
	
	КурсУпрДолгаРасходнойПартии  = ?(Расход.Долг <> 0, Расход.ДолгУпр / Расход.Долг, 0);
	КурсРеглДолгаРасходнойПартии = ?(Расход.Долг <> 0, Расход.ДолгРегл / Расход.Долг, 0);
	
	// заполняем показатели расчетной партии
	РасчетнаяПартия.Сумма = Окр(ПриходБазис * Приход.Сумма, 2);
	РасчетнаяПартия.КВозврату = Окр(ПриходБазис * Приход.КВозврату, 2);
	Если Расход.ТипЗаписи = "Отгрузка"
		ИЛИ Расход.ТипЗаписи = "СторноОтгрузки"
		ИЛИ Расход.ТипЗаписи = "ВозвратТоваров"
	Тогда
		РасчетнаяПартия.ЗалогЗаТару = Окр(ПриходБазис * Приход.ЗалогЗаТару, 2);
	Иначе
		РасчетнаяПартия.ЗалогЗаТару = 0;
	КонецЕсли;
	РасчетнаяПартия.Предоплата = Окр(ПриходБазис * Приход.Предоплата, 2);
	Если РасчетнаяПартия.Предоплата = 0 Тогда
		РасчетнаяПартия.ПредоплатаУпр = 0;
		РасчетнаяПартия.ПредоплатаРегл = 0;
	ИначеЕсли Расход.РасчетПоКурсуДокумента И Расход.Долг <> 0 И ТипЗнч(Расход.Регистратор) = Тип("ДокументСсылка.ВзаимозачетЗадолженности") Тогда
		РасчетнаяПартия.ПредоплатаУпр = Окр(РасходБазис * Расход.ДолгУпр, 2);
		РасчетнаяПартия.ПредоплатаРегл = Окр(РасходБазис * Расход.ДолгРегл, 2);
	Иначе
		РасчетнаяПартия.ПредоплатаУпр = Окр(ПриходБазис * Приход.ПредоплатаУпр, 2);
		РасчетнаяПартия.ПредоплатаРегл = Окр(ПриходБазис * Приход.ПредоплатаРегл, 2);
	КонецЕсли;
	РасчетнаяПартия.Долг = Окр(ПриходБазис * Приход.Долг, 2);
	Если РасчетнаяПартия.Долг <> 0 Тогда
		Если Расход.ТипЗаписи = "Оплата"
			ИЛИ Расход.ТипЗаписи = "СторноОтгрузки"
		Тогда // суммы Упр и Регл считаем по курсу оплаты
			РасчетнаяПартия.ДолгУпр = Окр(РасходБазис * Расход.ПредоплатаУпр, 2);
			РасчетнаяПартия.ДолгРегл = Окр(РасходБазис * Расход.ПредоплатаРегл, 2);
		Иначе
			РасчетнаяПартия.ДолгУпр = Окр(ПриходБазис * Приход.ДолгУпр, 2);
			РасчетнаяПартия.ДолгРегл = Окр(ПриходБазис * Приход.ДолгРегл, 2);
		КонецЕсли;
		РасчетнаяПартия.Приоритет = 12;
	Иначе
		РасчетнаяПартия.ДолгУпр = 0;
		РасчетнаяПартия.ДолгРегл = 0;
		РасчетнаяПартия.Приоритет = 13;
	КонецЕсли;
	
	// корректируем базу расчета в потреблении
	Расход.Сумма = Расход.Сумма - Окр(РасходБазис * Расход.Сумма, 2);
	Расход.КВозврату = Расход.КВозврату - Окр(РасходБазис * Расход.КВозврату, 2);
	Расход.ЗалогЗаТару = Расход.ЗалогЗаТару - Окр(РасходБазис * Расход.ЗалогЗаТару, 2);
	Расход.Предоплата = Расход.Предоплата - Окр(РасходБазис * Расход.Предоплата, 2);
	Если Расход.ТипЗаписи = "Оплата"
		ИЛИ Расход.ТипЗаписи = "СторноОтгрузки"
	Тогда
		Если РасчетнаяПартия.Долг <> 0 Тогда
			Расход.ПредоплатаУпр = Расход.ПредоплатаУпр - РасчетнаяПартия.ДолгУпр;
			Расход.ПредоплатаРегл = Расход.ПредоплатаРегл - РасчетнаяПартия.ДолгРегл;
		ИначеЕсли РасчетнаяПартия.Предоплата <> 0 Тогда
			Расход.ПредоплатаУпр = Расход.ПредоплатаУпр - РасчетнаяПартия.ПредоплатаУпр;
			Расход.ПредоплатаРегл = Расход.ПредоплатаРегл - РасчетнаяПартия.ПредоплатаРегл;
		КонецЕсли;
	Иначе
		Расход.ПредоплатаУпр = Расход.ПредоплатаУпр - Окр(РасходБазис * Расход.ПредоплатаУпр, 2);
		Расход.ПредоплатаРегл = Расход.ПредоплатаРегл - Окр(РасходБазис * Расход.ПредоплатаРегл, 2);
	КонецЕсли;
	Расход.Долг = Расход.Долг - Окр(РасходБазис * Расход.Долг, 2);
	Если Расход.РасчетПоКурсуДокумента И Расход.Долг <> 0
		И РасчетнаяПартия.Предоплата <> 0 Тогда
		Расход.ДолгУпр = Расход.Долг * КурсУпрДолгаРасходнойПартии; 
		Расход.ДолгРегл = Расход.Долг * КурсРеглДолгаРасходнойПартии; 
	Иначе
		Расход.ДолгУпр = Расход.ДолгУпр - Окр(РасходБазис * Расход.ДолгУпр, 2);
		Расход.ДолгРегл = Расход.ДолгРегл - Окр(РасходБазис * Расход.ДолгРегл, 2);
	КонецЕсли;
	
	Приход.Сумма = Приход.Сумма - РасчетнаяПартия.Сумма;
	Приход.КВозврату = Приход.КВозврату - РасчетнаяПартия.КВозврату;
	Приход.ЗалогЗаТару = Приход.ЗалогЗаТару - РасчетнаяПартия.ЗалогЗаТару;
	Если Расход.ТипЗаписи = "Оплата"
		ИЛИ Расход.ТипЗаписи = "СторноОтгрузки"
	Тогда
		Приход.Долг = Приход.Долг - Окр(ПриходБазис * Приход.Долг, 2);
		Приход.ДолгУпр = Приход.ДолгУпр - Окр(ПриходБазис * Приход.ДолгУпр, 2);
		Приход.ДолгРегл = Приход.ДолгРегл - Окр(ПриходБазис * Приход.ДолгРегл, 2);
	Иначе
		Приход.Долг = Приход.Долг - РасчетнаяПартия.Долг;
		Приход.ДолгУпр = Приход.ДолгУпр - РасчетнаяПартия.ДолгУпр;
		Приход.ДолгРегл = Приход.ДолгРегл - РасчетнаяПартия.ДолгРегл;
	КонецЕсли;
	Приход.Предоплата = Приход.Предоплата - РасчетнаяПартия.Предоплата;
	Приход.ПредоплатаУпр = Приход.ПредоплатаУпр - РасчетнаяПартия.ПредоплатаУпр;
	Приход.ПредоплатаРегл = Приход.ПредоплатаРегл - РасчетнаяПартия.ПредоплатаРегл;
	
	РасчетнаяПартия.РасчетЗавершен = Истина;
	РасчетнаяПартия.Приоритет = Приход.Приоритет;
	РасчетнаяПартия.УдалитьДатаПлатежа = Приход.УдалитьДатаПлатежа;
	ЭтоКорректировка = ?(ТипЗнч(Расход.Регистратор) = Тип("ДокументСсылка.КорректировкаРеализации"), Истина, Ложь);
	Если (ЭтоКорректировка И РасчетнаяПартия.Долг <> 0 И Расход.ВидДвижения = ВидДвиженияНакопления.Приход)
		ИЛИ (ЭтоКорректировка И РасчетнаяПартия.Предоплата <> 0 И Расход.ВидДвижения = ВидДвиженияНакопления.Расход
			И Приход.Регистратор = Приход.ДокументОтгрузки) Тогда
		РасчетнаяПартия.РасчетныйДокумент = Расход.РасчетныйДокумент; // для аванса расчетным документом будет корректировка
	Иначе
		ЭтоСторно = ЭтоСторно(Расход.ТипЗаписи);
		ПриходЭтоСторно = ЭтоСторно(Приход.ТипЗаписи);
		Если Приход.Регистратор = Неопределено ИЛИ Приход.ТипЗаписи = "ОстатокОплат" ИЛИ Приход.ТипЗаписи = "ОстатокОтгрузок" 
			ИЛИ ТипЗнч(Приход.Регистратор) = Тип("ДокументСсылка.ВводОстатков")
			ИЛИ ЭтоСторно
			ИЛИ ПриходЭтоСторно
			ИЛИ ЭтоКорректировка Тогда
			РасчетнаяПартия.РасчетныйДокумент = Приход.РасчетныйДокумент;
		Иначе
			РасчетнаяПартия.РасчетныйДокумент = Приход.Регистратор; // у долга расчетный документ сохраняем
		КонецЕсли;
	КонецЕсли;
	
	Если РасчетнаяПартия.ЗалогЗаТару <> 0 Тогда
		ЗаполнитьЗалогЗаТаруРегл(Валюты, РасчетнаяПартия);
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьРасчетыСПоставщиками(РасчетнаяПартия, Расход, Приход, Валюты) // РасчетысПоставщикамиПоДокументам
	
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	Если Неопределено = Приход 
		ИЛИ (ТипЗнч(Расход.Регистратор) = Тип("ДокументСсылка.ВводОстатков") // остатки по оплате и отгрузке не должны "закрываться" друг на друга
				И ТипЗнч(Приход.Регистратор) = Тип("ДокументСсылка.ВводОстатков"))
		ИЛИ (Расход.ТипЗаписи = "Оплата" И Приход.Предоплата <> 0) // Оплата закрывается только на долг
		ИЛИ (Расход.ТипЗаписи = "СторноОплаты" И Приход.Предоплата = 0) // Сторно оплаты закрывается только на предоплату
		ИЛИ (Расход.ТипЗаписи = "ПоступлениеТоваров" И Приход.Долг <> 0) // Поступление закрывается только на предплату
		ИЛИ (Расход.ТипЗаписи = "ПоступлениеТоваров" И Приход.ТипЗаписи = "СторноПоступления" 
			И Расход.ХозяйственнаяОперация = Приход.ХозяйственнаяОперация И Расход.Регистратор <> Приход.Регистратор)
	Тогда
		Возврат;
	КонецЕсли;
	
	СуммаСписания = Мин(Расход.Сумма, Приход.Сумма);
	
	РасходБазис = ?(Расход.Сумма <> 0, СуммаСписания / Расход.Сумма, 0);
	ПриходБазис = ?(Приход.Сумма <> 0, СуммаСписания / Приход.Сумма, 0);
	
	КурсУпрДолгаРасходнойПартии  = ?(Расход.Долг <> 0, Расход.ДолгУпр / Расход.Долг, 0);
	КурсРеглДолгаРасходнойПартии = ?(Расход.Долг <> 0, Расход.ДолгРегл / Расход.Долг, 0);
	
	// заполняем показатели расчетной партии
	РасчетнаяПартия.Сумма = Окр(ПриходБазис * Приход.Сумма, 2);
	РасчетнаяПартия.КВозврату = Окр(ПриходБазис * Приход.КВозврату, 2);
	Если Расход.ТипЗаписи = "Поступление"
		ИЛИ Расход.ТипЗаписи = "СторноПоступления"
		ИЛИ Расход.ТипЗаписи = "ВозвратТоваров"
	Тогда
		РасчетнаяПартия.ЗалогЗаТару = Окр(ПриходБазис * Приход.ЗалогЗаТару, 2);
	Иначе
		РасчетнаяПартия.ЗалогЗаТару = 0;
	КонецЕсли;
	РасчетнаяПартия.Долг = Окр(ПриходБазис * Приход.Долг, 2);
	РасчетнаяПартия.Предоплата = Окр(ПриходБазис * Приход.Предоплата, 2);
	Если РасчетнаяПартия.Предоплата = 0 Тогда
		РасчетнаяПартия.ПредоплатаУпр = 0;
		РасчетнаяПартия.ПредоплатаРегл = 0;
		РасчетнаяПартия.Приоритет = 12;
	ИначеЕсли Расход.РасчетПоКурсуДокумента И Расход.Долг <> 0 И ТипЗнч(Расход.Регистратор) = Тип("ДокументСсылка.ВзаимозачетЗадолженности") Тогда
		РасчетнаяПартия.ПредоплатаУпр = Окр(РасходБазис * Расход.ДолгУпр, 2);
		РасчетнаяПартия.ПредоплатаРегл = Окр(РасходБазис * Расход.ДолгРегл, 2);
		РасчетнаяПартия.Приоритет = 13;
	Иначе
		РасчетнаяПартия.ПредоплатаУпр = Окр(ПриходБазис * Приход.ПредоплатаУпр, 2);
		РасчетнаяПартия.ПредоплатаРегл = Окр(ПриходБазис * Приход.ПредоплатаРегл, 2);
		РасчетнаяПартия.Приоритет = 13;
	КонецЕсли;
	Если Расход.ТипЗаписи = "Оплата" Тогда // суммы Упр и Регл считаем по курсу оплаты
		Если РасчетнаяПартия.Долг = 0 Тогда
			РасчетнаяПартия.ДолгУпр = 0;
			РасчетнаяПартия.ДолгРегл = 0;
		Иначе
			РасчетнаяПартия.ДолгУпр = Окр(РасходБазис * Расход.ПредоплатаУпр, 2);
			РасчетнаяПартия.ДолгРегл = Окр(РасходБазис * Расход.ПредоплатаРегл, 2);
		КонецЕсли;
	Иначе
		РасчетнаяПартия.ДолгУпр = Окр(ПриходБазис * Приход.ДолгУпр, 2);
		РасчетнаяПартия.ДолгРегл = Окр(ПриходБазис * Приход.ДолгРегл, 2);
	КонецЕсли;
	
	// корректируем базу расчета в потреблении
	Расход.Сумма = Расход.Сумма - Окр(РасходБазис * Расход.Сумма, 2);
	Расход.КВозврату = Расход.КВозврату - Окр(РасходБазис * Расход.КВозврату, 2);
	Расход.ЗалогЗатару = Расход.ЗалогЗатару - Окр(РасходБазис * Расход.ЗалогЗатару, 2);
	Расход.Долг = Расход.Долг - Окр(РасходБазис * Расход.Долг, 2);
	Если Расход.РасчетПоКурсуДокумента И Расход.Долг <> 0 
		И РасчетнаяПартия.Предоплата <> 0 Тогда
		Расход.ДолгУпр = Расход.Долг * КурсУпрДолгаРасходнойПартии; 
		Расход.ДолгРегл = Расход.Долг * КурсРеглДолгаРасходнойПартии; 
	ИначеЕсли Расход.РасчетПоКурсуДокумента И Расход.Долг <> 0 Тогда
		Расход.ДолгУпр = Расход.ДолгУпр - РасчетнаяПартия.ПредоплатаУпр; 
		Расход.ДолгРегл = Расход.ДолгРегл - РасчетнаяПартия.ПредоплатаРегл; 
	Иначе
		Расход.ДолгУпр = Расход.ДолгУпр - Окр(РасходБазис * Расход.ДолгУпр, 2);
		Расход.ДолгРегл = Расход.ДолгРегл - Окр(РасходБазис * Расход.ДолгРегл, 2);
	КонецЕсли;
	Расход.Предоплата = Расход.Предоплата - Окр(РасходБазис * Расход.Предоплата, 2);
	Если Расход.ТипЗаписи = "Оплата" Тогда
		Расход.ПредоплатаУпр = Расход.ПредоплатаУпр - РасчетнаяПартия.ДолгУпр;
		Расход.ПредоплатаРегл = Расход.ПредоплатаРегл - РасчетнаяПартия.ДолгРегл;
	Иначе
		Расход.ПредоплатаУпр = Расход.ПредоплатаУпр - Окр(РасходБазис * Расход.ПредоплатаУпр, 2);
		Расход.ПредоплатаРегл = Расход.ПредоплатаРегл - Окр(РасходБазис * Расход.ПредоплатаРегл, 2);
	КонецЕсли;
	
	Приход.Сумма = Приход.Сумма - РасчетнаяПартия.Сумма;
	Приход.КВозврату = Приход.КВозврату - РасчетнаяПартия.КВозврату;
	Приход.ЗалогЗаТару = Приход.ЗалогЗаТару - РасчетнаяПартия.ЗалогЗаТару;
	Если Расход.ТипЗаписи = "Оплата" Тогда
		Приход.Долг = Приход.Долг - Окр(ПриходБазис * Приход.Долг, 2);
		Приход.ДолгУпр = Приход.ДолгУпр - Окр(ПриходБазис * Приход.ДолгУпр, 2);
		Приход.ДолгРегл = Приход.ДолгРегл - Окр(ПриходБазис * Приход.ДолгРегл, 2);
	Иначе
		Приход.Долг = Приход.Долг - РасчетнаяПартия.Долг;
		Приход.ДолгУпр = Приход.ДолгУпр - РасчетнаяПартия.ДолгУпр;
		Приход.ДолгРегл = Приход.ДолгРегл - РасчетнаяПартия.ДолгРегл;
	КонецЕсли;
	Приход.Предоплата = Приход.Предоплата - РасчетнаяПартия.Предоплата;
	Приход.ПредоплатаУпр = Приход.ПредоплатаУпр - РасчетнаяПартия.ПредоплатаУпр;
	Приход.ПредоплатаРегл = Приход.ПредоплатаРегл - РасчетнаяПартия.ПредоплатаРегл;
	
	ЭтоСторно = ЭтоСторно(Расход.ТипЗаписи);
	Если Приход.Регистратор = Неопределено
		ИЛИ ЭтоСторно
		ИЛИ Расход.ХозяйственнаяОперация = Приход.ХозяйственнаяОперация
		ИЛИ Приход.ТипЗаписи = "СторноПоступления"
		ИЛИ Приход.ТипЗаписи = "ОстатокПоступлений" И (Приход.РасчетныйДокумент = Приход.ЗаказПоставщику) Тогда
		РасчетнаяПартия.РасчетныйДокумент = Приход.РасчетныйДокумент;
	Иначе
		РасчетнаяПартия.РасчетныйДокумент = Приход.Регистратор;
	КонецЕсли;
	
	РасчетнаяПартия.РасчетЗавершен = Истина;
	РасчетнаяПартия.Приоритет = Приход.Приоритет;
	РасчетнаяПартия.УдалитьДатаПлатежа = Приход.УдалитьДатаПлатежа;
	Если РасчетнаяПартия.ЗалогЗаТару <> 0 Тогда
		ЗаполнитьЗалогЗаТаруРегл(Валюты, РасчетнаяПартия);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

// Общие алгоритмы расчета партий
#Область ОбщиеРасчетныеМетоды

Процедура УдалитьНезаписываемыеСтроки(Контекст, РасчетныеПартии)
	
	УдаляемыеТипы = Новый Структура;
	УдаляемыеРазделы = Новый Соответствие;
	Если Контекст = "РасчетыСКлиентами" Тогда
		УдаляемыеТипы.Вставить("ОстатокОплат");
		УдаляемыеТипы.Вставить("ОстатокОтгрузок");
		УдаляемыеТипы.Вставить("ПрошлаяОтгрузка");
		УдалятьНезавершенные = Ложь;
	ИначеЕсли Контекст = "РасчетыСПоставщиками" Тогда
		УдаляемыеТипы.Вставить("ОстатокОплат");
		УдаляемыеТипы.Вставить("ОстатокПоступлений");
		УдаляемыеТипы.Вставить("ПрошлоеПоступление");
		УдалятьНезавершенные = Ложь;
	КонецЕсли;
	
	МинимальныйИндекс = 1 - РасчетныеПартии.Количество();
	Для Индекс = МинимальныйИндекс По 0 Цикл
		РасчетнаяПартия = РасчетныеПартии[-Индекс];
		Если УдалятьНезавершенные И РасчетнаяПартия.РасчетЗавершен <> Истина
			Или УдаляемыеТипы.Свойство(РасчетнаяПартия.ТипЗаписи)
		Тогда
			РасчетныеПартии.Удалить(-Индекс);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция Регистраторы(НачалоПериода, ОкончаниеПериода, АналитикиРасчета, ИмяРегистра, МассивИсключаемыхТипов = Неопределено)
	ШаблонЗапроса = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор КАК Регистратор
		|ИЗ
		|	&ШаблонРегистраРасчетов КАК ДД
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И ДД.Сумма <> 0
		|	И НЕ ТипЗначения(ДД.Регистратор) В (&МассивИсключаемыхТипов)
		|	И (ДД.АналитикаУчетаПоПартнерам В (&АналитикиРасчета) ИЛИ &ПоВсемАналитикам)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор КАК Регистратор
		|ИЗ
		|	&ШаблонРегистраПоДокументам КАК ДД
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|	И (ДД.Долг <> 0 ИЛИ ДД.Предоплата <> 0) 
		|	И НЕ ТипЗначения(ДД.Регистратор) В (&МассивИсключаемыхТипов)
		|	И (ДД.АналитикаУчетаПоПартнерам В (&АналитикиРасчета) ИЛИ &ПоВсемАналитикам)
		|";
	ШаблонРегистра = "РегистрНакопления.";
	ТекстЗапроса = СтрЗаменить(ШаблонЗапроса, "&ШаблонРегистраРасчетов", ШаблонРегистра + ИмяРегистра);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ШаблонРегистраПоДокументам", ШаблонРегистра + ИмяРегистра + "ПоДокументам");
	Регистраторы = Новый Соответствие;
	Если МассивИсключаемыхТипов = Неопределено Тогда
		МассивИсключаемыхТипов = Новый Массив;
	КонецЕсли;
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивИсключаемыхТипов", МассивИсключаемыхТипов);
	Запрос.УстановитьПараметр("АналитикиРасчета", АналитикиРасчета);
	Запрос.УстановитьПараметр("ПоВсемАналитикам", АналитикиРасчета = Неопределено);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Регистраторы.Вставить(Выборка.Регистратор, Истина);
	КонецЦикла;
	Возврат Регистраторы;
КонецФункции

Процедура ЗаписатьРасчетныеПартии(МенеджерРегистра, РасчетныеПартии, Регистраторы)
	РасчетныеПартии.Сортировать("Регистратор", Новый СравнениеЗначений);
	
	Движения = МенеджерРегистра.СоздатьНаборЗаписей();
	
	Регистратор = Неопределено;
	Замещать = Истина;
	Счетчик = 0;
	МаксимумСчетчика = РасчетныеПартии.Количество() - 1;
	Пока Счетчик <= МаксимумСчетчика Цикл
		РасчетнаяПартия = РасчетныеПартии[Счетчик];
		Если Регистратор <> РасчетнаяПартия.Регистратор Тогда
			Регистратор = РасчетнаяПартия.Регистратор;
			
			Движения.Очистить();
			Движения.Отбор.Регистратор.Установить(Регистратор);
			
			НачатьТранзакцию();
			Если Регистраторы <> Неопределено Тогда
				Если Регистраторы[Регистратор] <> Неопределено Тогда
					Замещать = Истина;
					Регистраторы.Удалить(Регистратор);
				Иначе
					Замещать = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		Движение = Движения.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, РасчетнаяПартия);
		Движение.Активность = Истина;
		
		Счетчик = Счетчик + 1;
		Если Счетчик > МаксимумСчетчика Или (Регистратор <> РасчетныеПартии[Счетчик].Регистратор) Тогда
			Движения.Записать(Замещать);
			ЗафиксироватьТранзакцию();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура СформироватьДвиженияПоУправленческомуУчету(Контекст, РегистраторыКОтражению)
	Если Контекст = "РасчетыСКлиентами" Тогда
		УправленческийУчетПроведениеСервер.СформироватьДвиженияДенежныеСредстваКонтрагентПоКлиентам(РегистраторыКОтражению);
	Иначе
		УправленческийУчетПроведениеСервер.СформироватьДвиженияДенежныеСредстваКонтрагентПоПоставщикам(РегистраторыКОтражению);
	КонецЕсли;
	УправленческийУчетПроведениеСервер.СформироватьДвиженияКонтрагентКонтрагент(РегистраторыКОтражению);
КонецПроцедуры

Процедура ЗаполнитьЗалогЗаТаруРегл(Валюты, РасчетнаяПартия)
	
	КоэффициентВалютыРегл = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(
		Валюты.ВалютаРегламентированногоУчета,
		РасчетнаяПартия.Период,
		Валюты.КэшКурсовВалют);
		
	КоэффициентВалютыВзаиморасчетов = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(
		РасчетнаяПартия.Валюта,
		РасчетнаяПартия.Период,
		Валюты.КэшКурсовВалют);
		
	Если КоэффициентВалютыРегл <> 0 Тогда
		РасчетнаяПартия.ЗалогЗаТаруРегл = РасчетнаяПартия.ЗалогЗаТару * КоэффициентВалютыВзаиморасчетов / КоэффициентВалютыРегл;
	КонецЕсли;
КонецПроцедуры

Функция СуммыКлиентовВВалютеРегл()
	Возврат "
	|ВЫБРАТЬ
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.Валюта КАК Валюта,
	|	
	|	СУММА(ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
	|			ТОГДА ДанныеРегистра.ПредоплатаРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	) КАК ПредоплатаРегл,
	|
	|	СУММА(ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
	|			ТОГДА ДанныеРегистра.Долг
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	) КАК Долг
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументов КАК ТаблицаДокументов
	|	ПО ДанныеРегистра.Регистратор = ТаблицаДокументов.Регистратор
	|		И (ДанныеРегистра.АналитикаУчетаПоПартнерам = ТаблицаДокументов.АналитикаУчетаПоПартнерам
	|			ИЛИ ТаблицаДокументов.АналитикаУчетаПоПартнерам = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка))
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Регистратор,
	|	ДанныеРегистра.Валюта
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
КонецФункции

Функция СуммыПоставщиковВВалютеРегл()
	Возврат "
	|ВЫБРАТЬ
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.Валюта КАК Валюта,
	|	
	|	СУММА(ВЫБОР КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|		 		И Поступление.Организация = Аналитика.Организация 
	|			ТОГДА 
	|				ВЫБОР КОГДА ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|					ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту)
	|					ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
	|				ТОГДА ДанныеРегистра.ПредоплатаРегл
	|				ИНАЧЕ 0
	|				КОНЕЦ
	|		КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ТОГДА ДанныеРегистра.ПредоплатаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|	) КАК ПредоплатаРегл,
	|	
	|	СУММА(ВЫБОР КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|			И Поступление.Организация = Аналитика.Организация
	|			ТОГДА
	|				ВЫБОР КОГДА ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|			 		ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту)
	|			 		ИЛИ ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
	|				ТОГДА ДанныеРегистра.Долг
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|		КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ТОГДА ДанныеРегистра.Долг
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|	) КАК Долг
	|
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументов КАК ТаблицаДокументов
	|	ПО ДанныеРегистра.Регистратор = ТаблицаДокументов.Регистратор
	|		И (ДанныеРегистра.АналитикаУчетаПоПартнерам = ТаблицаДокументов.АналитикаУчетаПоПартнерам
	|			ИЛИ ТаблицаДокументов.АналитикаУчетаПоПартнерам = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка))
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
	|	ПО Аналитика.Ссылка = ДанныеРегистра.АналитикаУчетаПоПартнерам
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК Поступление
	|	ПО Поступление.Ссылка = ДанныеРегистра.Регистратор
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Регистратор,
	|	ДанныеРегистра.Валюта
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
КонецФункции

Процедура СуммыДокументовВВалютеРегл(ВариантРасчета, МассивДокументов = Неопределено)
	
	Валюты = Новый Структура("ВалютаРегламентированногоУчета, ВалютаУправленческогоУчета, КэшКурсовВалют",
		Константы.ВалютаРегламентированногоУчета.Получить(),
		Константы.ВалютаУправленческогоУчета.Получить(),
		РаботаСКурсамиВалютУТ.ИнициализироватьКэшКурсовВалют());
		
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|
	|ПОМЕСТИТЬ ТаблицаДокументов
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютеРегл КАК ДанныеРегистра
	|
	|ГДЕ
	|	ДанныеРегистра.ТипРасчетов = &ТипРасчета
	|	И (ДанныеРегистра.Регистратор В (&МассивДокументов)
	|		ИЛИ (&ПоВсемДокументам 
	|			И ((ДанныеРегистра.СуммаБезНДС <> 0 И ДанныеРегистра.СуммаБезНДСРегл = 0)
	|				ИЛИ (ДанныеРегистра.СуммаНДС <> 0 И ДанныеРегистра.СуммаНДСРегл = 0))
	|		)
	|	)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Если ВариантРасчета = "РасчетыСКлиентами" Тогда
		ТипРасчета = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
		ТекстРасчета = СуммыКлиентовВВалютеРегл();
	Иначе
		ТипРасчета = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
		ТекстРасчета = СуммыПоставщиковВВалютеРегл();
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса
	+ ТекстРасчета
	+ "
	|ВЫБРАТЬ
	|	ДанныеРегистра.Регистратор КАК Ссылка,
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ДанныеРегистра.СуммаБезНДС КАК СуммаБезНДС,
	|	ДанныеРегистра.СуммаНДС КАК СуммаНДС,
	|	ДанныеРегистра.СуммаБезНДСРегл КАК СуммаБезНДСРегл,
	|	ДанныеРегистра.СуммаНДСРегл КАК СуммаНДСРегл,
	|	ДанныеРегистра.Валюта КАК Валюта,
	|	ДанныеРегистра.СтавкаНДС КАК СтавкаНДС,
	|	ДанныеРегистра.ТипРасчетов КАК ТипРасчетов,
	|	ДанныеРегистра.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	
	|	ВЫБОР КОГДА ДанныеРегистра.ТипРасчетов = &ТипРасчета
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПересчитатьСуммыРегл,
	|	
	|	ВЫБОР КОГДА ДанныеРегистра.ТипРасчетов = &ТипРасчета
	|		ТОГДА ДанныеРегистра.СуммаБезНДС + ДанныеРегистра.СуммаНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСНДС,
	|
	|	ВЫБОР КОГДА ДанныеРегистра.СуммаБезНДС + ДанныеРегистра.СуммаНДС < 0
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоСторно
	|
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютеРегл КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументов КАК ТаблицаДокументов
	|	ПО ДанныеРегистра.Регистратор = ТаблицаДокументов.Регистратор
	|ИТОГИ
	|	МАКСИМУМ(Период),
	|	СУММА(СуммаСНДС)
	|ПО
	|	ДанныеРегистра.Регистратор,
	|	ЭтоСторно
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.УстановитьПараметр("ПоВсемДокументам", Не ЗначениеЗаполнено(МассивДокументов));
	Запрос.УстановитьПараметр("ТипРасчета", ТипРасчета);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	РезультатПоРасчетам   = МассивРезультатов[1];
	РезультатПоДокументам = МассивРезультатов[2];
	
	ВыборкаПоРасчетам = РезультатПоРасчетам.Выбрать();
	
	ЗатронутыеРегистраторы = Новый Массив;
	ВыборкаПоДокументу = РезультатПоДокументам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоДокументу.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.СуммыДокументовВВалютеРегл.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаПоДокументу.Ссылка);
		
		ВыборкаПоТипуЗаписи = ВыборкаПоДокументу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоТипуЗаписи.Следующий() Цикл
			СуммаДокументаРегл = 0;
			СуммаДокумента     = ВыборкаПоТипуЗаписи.СуммаСНДС;
			
			Пока ВыборкаПоРасчетам.НайтиСледующий(Новый Структура("Регистратор", ВыборкаПоДокументу.Ссылка)) Цикл
				
				КурсВалютыВзаиморасчетов = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(
					ВыборкаПоРасчетам.Валюта,
					ВыборкаПоДокументу.Период,
					Валюты.КэшКурсовВалют);
				КурсВалютыРегл = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(
					Валюты.ВалютаРегламентированногоУчета,
					ВыборкаПоДокументу.Период,
					Валюты.КэшКурсовВалют);
				
				ДолгРегл = ВыборкаПоРасчетам.Долг * КурсВалютыВзаиморасчетов / КурсВалютыРегл;
				
				СуммаДокументаРегл = СуммаДокументаРегл + ВыборкаПоРасчетам.ПредоплатаРегл + ДолгРегл;
			КонецЦикла;
		
			ВыборкаПоРасчетам.Сбросить();
			
			УчтеноБазыРаспределения = 0;
			УжеРаспределено = 0;
			
			Выборка = ВыборкаПоТипуЗаписи.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				НоваяЗапись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
				
				Если Выборка.ПересчитатьСуммыРегл И СуммаДокумента <> 0 Тогда
					
					СуммаСНДС = Выборка.СуммаБезНДС + Выборка.СуммаНДС;
					
					СуммаСНДСРегл = Окр(СуммаДокументаРегл * (УчтеноБазыРаспределения + СуммаСНДС) / СуммаДокумента, 2) - УжеРаспределено;
					
					УчтеноБазыРаспределения = УчтеноБазыРаспределения + СуммаСНДС;
					УжеРаспределено         = УжеРаспределено + СуммаСНДСРегл;
					
					ТекПроцентНДС = ЦенообразованиеКлиентСервер.ПолучитьСтавкуНДСЧислом(Выборка.СтавкаНДС);
					
					НоваяЗапись.СуммаНДСРегл    = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(СуммаСНДСРегл, ТекПроцентНДС);
					НоваяЗапись.СуммаБезНДСРегл = СуммаСНДСРегл - НоваяЗапись.СуммаНДСРегл;
					
					Если Выборка.ЭтоСторно Тогда
						НоваяЗапись.СуммаНДСРегл = -НоваяЗапись.СуммаНДСРегл;
						НоваяЗапись.СуммаБезНДСРегл = -НоваяЗапись.СуммаБезНДСРегл;
					КонецЕсли;
					
				КонецЕсли;
						
			КонецЦикла;
		КонецЦикла;
		
		НаборЗаписей.Записывать = Истина;
		НаборЗаписей.Записать();
		
		ЗатронутыеРегистраторы.Добавить(ВыборкаПоДокументу.Ссылка);
		
	КонецЦикла;
	
	РегистрыСведений.СуммыДокументовВВалютеРегл.ОбновитьДвиженияДокументов(ЗатронутыеРегистраторы);
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Распределение приходов на расходы

Функция ОписаниеДвижений(Контекст, ПоляРасчета, КлючиСравнения, Показатели, БазисПрихода, БазисРасхода, КлючРасхода, ПолеПорядка, ПоляСортировки = "", СортировкаПоУсловию = "")
	Возврат
		Новый Структура(
			"Контекст, ПоляРасчета, КлючиСравнения, Показатели, БазисПрихода, БазисРасхода, КлючРасхода, ПолеПорядка, ПоляСортировки, СортировкаПоУсловию",
			Контекст, ПоляРасчета, КлючиСравнения, Показатели, БазисПрихода, БазисРасхода, КлючРасхода, ПолеПорядка, ПоляСортировки, СортировкаПоУсловию);
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Построение цепочек движений для проброса документов расчета

// Граф сопоставлений индексов строк расхода и индексов строк с источниками расчетов
Функция ЦепочкиДвиженийИзВыборки(ОписаниеЦепочек, РасчетныеПартии)
	ИндексРасчета = Новый Соответствие;
	ТипыЗаписей = Новый Соответствие;
	
	Движение = РасчетныеПартии.Выбрать();
	
	КлючиДвижения = Новый Массив;
	// строим индекс нерасчитанных потребителей
	НомерСтроки = -1;
	Пока Движение.Следующий() Цикл
		НомерСтроки = НомерСтроки + 1;
		Если Движение.РасчетЗавершен Тогда
			Продолжить;
		КонецЕсли;
		
		КлючиДвижения.Очистить();
		КлючиДвижения.Добавить(Движение.ТипЗаписи);
		Для Каждого ПолеСвязи Из ОписаниеЦепочек[Движение.ТипЗаписи].ПоляСвязи Цикл
			КлючиДвижения.Добавить(Движение[ПолеСвязи]);
		КонецЦикла;
		КлючиДвижения.Добавить(НомерСтроки);
		ТипыЗаписей.Вставить(НомерСтроки, Движение.ТипЗаписи);
		
		ДобавитьУзелИндекса(ИндексРасчета, КлючиДвижения, НомерСтроки);
	КонецЦикла;
	
	ЦепочкиДвижений = Новый Соответствие;
	
	// ищем элементы индекса, которые могут являться приемниками данных текущего движения
	Движение.Сбросить();
	НомерСтроки = -1;
	Пока Движение.Следующий() Цикл
		НомерСтроки = НомерСтроки + 1;
		ОписаниеЦепочки = Неопределено;
		Если Не ОписаниеЦепочек.Свойство(Движение.ТипЗаписи, ОписаниеЦепочки) Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ОписаниеТипаПриемника Из ОписаниеЦепочки.ТипыПриемников Цикл
			Узлы = ИндексРасчета[ОписаниеТипаПриемника.Ключ];
			Если Узлы = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Для Каждого ПолеИсточника Из ОписаниеТипаПриемника.Значение Цикл
				Узлы = Узлы[Движение[ПолеИсточника]];
				Если Узлы = Неопределено Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если Узлы = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Для Каждого Узел Из Узлы Цикл
				Если ОписаниеТипаПриемника.Ключ = ТипыЗаписей[Узел.Ключ] Тогда // Сравниваем тип записи
					СвязатьУзлыЦепочки(ЦепочкиДвижений, Узел.Ключ, НомерСтроки);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

	Возврат ЦепочкиДвижений;
КонецФункции

Функция ОписаниеЦепочек(ПереченьТиповЗаписей)
	ОписаниеЦепочек = Новый Структура(ПереченьТиповЗаписей);
	Для Каждого Описание Из ОписаниеЦепочек Цикл
		ОписаниеЦепочек[Описание.Ключ] = Новый Структура("ПоляСвязи, ТипыПриемников");
		ОписаниеЦепочек[Описание.Ключ].ПоляСвязи = Новый Массив;
		ОписаниеЦепочек[Описание.Ключ].ТипыПриемников = Новый Соответствие;
	КонецЦикла;
	Возврат ОписаниеЦепочек;
КонецФункции

Процедура ДобавитьОписаниеПриемника(ОписаниеЦепочек, ТипПриемника, ПоляПриемника)
	Для Каждого ОписаниеПоля Из Новый Структура(ПоляПриемника) Цикл
		ОписаниеЦепочек[ТипПриемника].ПоляСвязи.Добавить(ОписаниеПоля.Ключ);
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьОписаниеИсточника(ОписаниеЦепочек, ТипПриемника, ТипИсточника, ПоляИсточника)
	ПоляСвязи = Новый Массив;
	Для Каждого ОписаниеПоля Из Новый Структура(ПоляИсточника) Цикл
		ПоляСвязи.Добавить(ОписаниеПоля.Ключ);
	КонецЦикла;
	ОписаниеЦепочек[ТипИсточника].ТипыПриемников.Вставить(ТипПриемника, ПоляСвязи);
КонецПроцедуры

// В источники приемника добавляется источник, в приемники источника добавляется приемник
Процедура СвязатьУзлыЦепочки(ЦепочкиДвижений, Приемник, Источник, ПослеИсточника = Неопределено)
	// связываем только различные строки
	Если Источник = Приемник Тогда
		Возврат;
	КонецЕсли;
	
	// приемник добавляется в сопоставление, для приемника заполняются источники (строка с ложной партией)
	Если Неопределено = ЦепочкиДвижений[Приемник] Тогда
		ЦепочкиДвижений.Вставить(Приемник, Новый Структура("Источники, Приемники", Новый Массив, Новый Массив));
	КонецЕсли;
	Источники = ЦепочкиДвижений[Приемник].Источники;
	Если ПослеИсточника <> Неопределено Тогда
		Индекс = Источники.Найти(ПослеИсточника);
		Если Индекс <> Неопределено Тогда
			Источники.Вставить(Индекс + 1, Источник);
		КонецЕсли;
	Иначе
		Источники.Добавить(Источник);
	КонецЕсли;
	
	// источник добавляется в сопоставление, для источника заполняются приемники (строка ложной партии)
	Если Неопределено = ЦепочкиДвижений[Источник] Тогда
		ЦепочкиДвижений.Вставить(Источник, Новый Структура("Источники, Приемники", Новый Массив, Новый Массив));
	КонецЕсли;
	ЦепочкиДвижений[Источник].Приемники.Добавить(Приемник);
КонецПроцедуры

// ждем, что источников будем менее десятка и они будут почти всегда уже упорядочены, поэтому делаем сортировку вставками
Процедура СортироватьИсточники(Источники, ПолеПорядка, РасчетныеПартии)
	Для У = 1 По Источники.ВГраница() Цикл
		Если Неопределено = РасчетныеПартии[Источники[У]] Тогда
			Продолжить;
		КонецЕсли;
		Элемент = Новый Структура("Порядок, Источник", РасчетныеПартии[Источники[У]][ПолеПорядка], Источники[У]);
		Х = У - 1;
		Пока Х >= 0 Цикл
			Если Неопределено = РасчетныеПартии[Источники[Х]] Тогда
				Х = Х - 1;
				Продолжить;
			КонецЕсли;
			Если РасчетныеПартии[Источники[Х]][ПолеПорядка] <= Элемент.Порядок Тогда
				Прервать;
			КонецЕсли;
			Источники[Х + 1] = Источники[Х];
			Х = Х - 1;
		КонецЦикла;
		Источники[Х + 1] = Элемент.Источник;
	КонецЦикла;
КонецПроцедуры

Процедура СортироватьИсточникиПоПолямСортировки(Источники, ПоляСортировки, Приходы, НаправлениеСортировки = "Убыв")
	ТаблицаСортировки = Новый ТаблицаЗначений;
	ТаблицаСортировки.Колонки.Добавить("Ключ");
	УсловиеСортировки = "";
	Для Каждого ПолеСортировки Из ПоляСортировки Цикл
		ПараметрыПоляСортировки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(ПолеСортировки, " ");
		ТаблицаСортировки.Колонки.Добавить(ПараметрыПоляСортировки[0]);
		Если ЗначениеЗаполнено(УсловиеСортировки) Тогда
			УсловиеСортировки = УсловиеСортировки + ", ";
		КонецЕсли;
		УсловиеСортировки = УсловиеСортировки + ПараметрыПоляСортировки[0] + " " + ?(ПараметрыПоляСортировки.Количество() > 1, ПараметрыПоляСортировки[1], НаправлениеСортировки);
	КонецЦикла;
	Для Каждого Источник Из Источники Цикл
		СтрокаПрихода = Приходы.Получить(Источник);
		Если СтрокаПрихода = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СтрокаСортировки = ТаблицаСортировки.Добавить();
		СтрокаСортировки.Ключ = Источник;
		Для Каждого ПолеСортировки Из ПоляСортировки Цикл
			ПараметрыПоляСортировки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(ПолеСортировки, " ");
			СтрокаСортировки[ПараметрыПоляСортировки[0]] = СтрокаПрихода[ПараметрыПоляСортировки[0]];
		КонецЦикла;
	КонецЦикла;
	ТаблицаСортировки.Сортировать(УсловиеСортировки);
	Источники = ТаблицаСортировки.ВыгрузитьКолонку("Ключ");
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Расчет по цепочкам движений и выборки из результата запроса

Процедура РассчитатьПартииПоЦепочкамИзВыборки(ОписаниеДвижений, РезультатЗапроса, РасчетныеПартии, ЦепочкиДвижений, Регистраторы,  Тест = Ложь)
	Приходы = Новый Соответствие; // буфер копий партий для покрытия расходов
	СтрокиПартий = Новый Соответствие; // буфер нерассчитанных партий для расчета на следующих итерациях
	ПройденныйПуть = Новый Соответствие; // используется для прерывания циклов
	
	Валюты = Новый Структура;
	Валюты.Вставить("ВалютаРегламентированногоУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Валюты.Вставить("КэшКурсовВалют", РаботаСКурсамиВалютУТ.ИнициализироватьКэшКурсовВалют());
	
	ИндексРасхода = -1;
	Выборка = РезультатЗапроса.Выбрать();
	МаксИндекс = Выборка.Количество() + 1;
	Пока Выборка.Следующий() Цикл
		ИндексРасхода = ИндексРасхода + 1;
		Если ЦепочкиДвижений[ИндексРасхода] = Неопределено Тогда
			РасчетнаяПартия = РасчетныеПартии.Добавить();
			ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Выборка);
			ЗаполнитьЗалогЗаТаруРегл(Валюты, РасчетнаяПартия);;
			Продолжить; // строка к обсчету НЕ зарегистрирована
		ИначеЕсли Выборка.РасчетЗавершен Тогда
			Если ЦепочкиДвижений[ИндексРасхода].Приемники.Количество() > 0 Тогда
				Приход = КопияЗаписиСтруктурой(Выборка, ОписаниеДвижений.ПоляРасчета);
				ЭтоСторно = ЭтоСторно(Приход.ТипЗаписи);
				ИнвертироватьПоказатели(Приход, ОписаниеДвижений.Показатели, ЭтоСторно);
				Приходы.Вставить(ИндексРасхода, Приход);
			КонецЕсли;
			РасчетнаяПартия = РасчетныеПартии.Добавить();
			ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Выборка);
			ЗаполнитьЗалогЗаТаруРегл(Валюты, РасчетнаяПартия);
			Продолжить; // строка является исходной партией
		КонецЕсли;
		ПройденныйПуть.Очистить();
		РасчетнаяПартия = КопияЗаписиСтруктурой(Выборка, ОписаниеДвижений.ПоляРасчета);
		РассчитатьПартиюПоЦепочкеИзВыборки(ОписаниеДвижений, РасчетнаяПартия, РасчетныеПартии, ЦепочкиДвижений, Приходы, ИндексРасхода, ПройденныйПуть, МаксИндекс, Ложь, СтрокиПартий, Валюты);
		
		// Запишем порцию движений в регистр.
		Если РасчетныеПартии.Количество() > 1000 Тогда
			УдалитьНезаписываемыеСтроки(ОписаниеДвижений.Контекст, РасчетныеПартии);
			Если ОписаниеДвижений.Контекст = "РасчетыСКлиентами" Тогда
				ЗаписатьРасчетныеПартии(РегистрыНакопления.РасчетыСКлиентамиПоДокументам, РасчетныеПартии, Регистраторы);
			ИначеЕсли ОписаниеДвижений.Контекст = "РасчетыСПоставщиками" Тогда
				ЗаписатьРасчетныеПартии(РегистрыНакопления.РасчетыСПоставщикамиПоДокументам, РасчетныеПартии, Регистраторы);
			КонецЕсли;
			РегистраторыКОтражению = РасчетныеПартии.ВыгрузитьКолонку("Регистратор");
			СформироватьДвиженияПоНДС(ОписаниеДвижений.Контекст, РегистраторыКОтражению);
			СформироватьДвиженияПоУправленческомуУчету(ОписаниеДвижений.Контекст,РегистраторыКОтражению);
			СуммыДокументовВВалютеРегл(ОписаниеДвижений.Контекст, РегистраторыКОтражению);
			РасчетныеПартии.Очистить();
		КонецЕсли;
	КонецЦикла;
	
	Счетчик = 0;
	Пока Счетчик < МаксИндекс Цикл
		ИндексРасхода = Счетчик;
		Счетчик = Счетчик + 1;
		Если СтрокиПартий[ИндексРасхода] = Неопределено Тогда
			Продолжить; // строка к обсчету НЕ зарегистрирована
		КонецЕсли;
		ПройденныйПуть.Очистить();
		РасчетнаяПартия = КопияЗаписиСтруктурой(СтрокиПартий[ИндексРасхода], ОписаниеДвижений.ПоляРасчета);
		РассчитатьПартиюПоЦепочкеИзВыборки(ОписаниеДвижений, РасчетнаяПартия, РасчетныеПартии, ЦепочкиДвижений, Приходы, ИндексРасхода, ПройденныйПуть, МаксИндекс, Истина, СтрокиПартий, Валюты);
		
		// Запишем порцию движений в регистр.
		Если РасчетныеПартии.Количество() > 1000 Тогда
			УдалитьНезаписываемыеСтроки(ОписаниеДвижений.Контекст, РасчетныеПартии);
			Если ОписаниеДвижений.Контекст = "РасчетыСКлиентами" Тогда
				ЗаписатьРасчетныеПартии(РегистрыНакопления.РасчетыСКлиентамиПоДокументам, РасчетныеПартии, Регистраторы);
			ИначеЕсли ОписаниеДвижений.Контекст = "РасчетыСПоставщиками" Тогда
				ЗаписатьРасчетныеПартии(РегистрыНакопления.РасчетыСПоставщикамиПоДокументам, РасчетныеПартии, Регистраторы);
			КонецЕсли;
			РегистраторыКОтражению = РасчетныеПартии.ВыгрузитьКолонку("Регистратор");
			СформироватьДвиженияПоНДС(ОписаниеДвижений.Контекст, РегистраторыКОтражению);
			СформироватьДвиженияПоУправленческомуУчету(ОписаниеДвижений.Контекст, РегистраторыКОтражению);
			СуммыДокументовВВалютеРегл(ОписаниеДвижений.Контекст, РегистраторыКОтражению);
			РасчетныеПартии.Очистить();
		КонецЕсли;
	КонецЦикла;
	Для Каждого Строка Из СтрокиПартий Цикл
		РасчетнаяПартия = РасчетныеПартии.Добавить();
		ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Строка.Значение);
		ЗаполнитьЗалогЗаТаруРегл(Валюты, РасчетнаяПартия);
		Если РасчетныеПартии.Количество() > 1000 Тогда
			УдалитьНезаписываемыеСтроки(ОписаниеДвижений.Контекст, РасчетныеПартии);
			Если ОписаниеДвижений.Контекст = "РасчетыСКлиентами" Тогда
				ЗаписатьРасчетныеПартии(РегистрыНакопления.РасчетыСКлиентамиПоДокументам, РасчетныеПартии, Регистраторы);
			ИначеЕсли ОписаниеДвижений.Контекст = "РасчетыСПоставщиками" Тогда
				ЗаписатьРасчетныеПартии(РегистрыНакопления.РасчетыСПоставщикамиПоДокументам, РасчетныеПартии, Регистраторы);
			КонецЕсли;
			РегистраторыКОтражению = РасчетныеПартии.ВыгрузитьКолонку("Регистратор");
			СформироватьДвиженияПоНДС(ОписаниеДвижений.Контекст, РегистраторыКОтражению);
			СформироватьДвиженияПоУправленческомуУчету(ОписаниеДвижений.Контекст, РегистраторыКОтражению);
			СуммыДокументовВВалютеРегл(ОписаниеДвижений.Контекст, РегистраторыКОтражению);
			РасчетныеПартии.Очистить();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура РассчитатьПартиюПоЦепочкеИзВыборки(ОписаниеДвижений, РасчетнаяПартия, РасчетныеПартии, ЦепочкиДвижений, Приходы, ИндексРасхода, ПройденныйПуть, МаксИндекс, Итерация, СтрокиПартий, Валюты)
	Если ЦепочкиДвижений[ИндексРасхода] = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ИнвертировалиПоказатели = Ложь;
	Источники = ЦепочкиДвижений[ИндексРасхода].Источники;
	Расход = КопияЗаписиСтруктурой(РасчетнаяПартия, ОписаниеДвижений.ПоляРасчета);
	Если ИндексРасхода = ПройденныйПуть[ИндексРасхода] Тогда
		Возврат; //мы тут уже были или источников для данной строки не зарегистрировано
	КонецЕсли;
	Если Источники.Количество() = 0 Тогда
		Расход.РасчетЗавершен = Истина;
		Расход.РасчетныйДокумент = Расход.Регистратор;
		Расход.ПриоритетРасчетногоДокумента = 1;
		НоваяПартия = РасчетныеПартии.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПартия, Расход);
		ЗаполнитьЗалогЗаТаруРегл(Валюты, НоваяПартия);
		
		Приход = КопияЗаписиСтруктурой(НоваяПартия, ОписаниеДвижений.ПоляРасчета);
		ЭтоСторно = ЭтоСторно(Приход.ТипЗаписи);
		ИнвертироватьПоказатели(Приход, ОписаниеДвижений.Показатели, ЭтоСторно);
		Приходы.Вставить(ИндексРасхода, Приход);
		Возврат;
	КонецЕсли;
	
	ПройденныйПуть.Вставить(ИндексРасхода, ИндексРасхода);
	ЭтоСторно = ЭтоСторно(Расход.ТипЗаписи);
	
	ЗначенияПолей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(ОписаниеДвижений.ПоляСортировки, ",");
	Если ЭтоСторно И ТребуетсяСортировка(Расход, ЭтоСторно) Тогда
		СортироватьИсточникиПоПолямСортировки(Источники, ЗначенияПолей, Приходы);
	ИначеЕсли ЭтоСторно Тогда
		СортироватьИсточникиПоПолямСортировки(Источники, ЗначенияПолей, Приходы, "Возр");
	КонецЕсли;
	
	Если ЭтоСторно И Итерация Тогда
		Для Каждого ИндексИсточника Из Источники Цикл
			Если Неопределено = Приходы[ИндексИсточника] И СтрокиПартий[ИндексИсточника] <> Неопределено Тогда // данный источник еще не рассчитан
				РассчитатьПартиюПоЦепочкеИзВыборки(ОписаниеДвижений, СтрокиПартий[ИндексИсточника], РасчетныеПартии, ЦепочкиДвижений, Приходы, ИндексИсточника, ПройденныйПуть, МаксИндекс, Итерация, СтрокиПартий, Валюты);
			КонецЕсли;
		КонецЦикла;
		СортироватьИсточники(Источники, ОписаниеДвижений.ПолеПорядка, Приходы);
	КонецЕсли;
	
	Счетчик = 0;
	ВГраница = Источники.ВГраница();
	ДоступныеИсточники = Новый Массив;
	РасчетЗавершен = Новый Массив;
	
	ИндексРасчетнойПартии = -1;
	Если ЭтоСторно И Расход[ОписаниеДвижений.БазисРасхода] < 0 Тогда
		ИнвертироватьПоказатели(Расход, ОписаниеДвижений.Показатели, ЭтоСторно);
		ИнвертировалиПоказатели = Истина;
	КонецЕсли;
	Пока Счетчик <= ВГраница Цикл
		ИндексИсточника = Источники[Счетчик];
		Счетчик = Счетчик + 1;
		
		Если Не ЭтоСторно И ИндексИсточника = ПройденныйПуть[ИндексИсточника] Тогда
			ДоступныеИсточники.Добавить(ИндексИсточника);
			Продолжить; // данный источник еще рассчитан не до конца
		КонецЕсли;
		
		Приход = Приходы[ИндексИсточника];
		Если Неопределено = Приход Тогда
			Если Не Итерация ИЛИ Неопределено = СтрокиПартий[ИндексИсточника] Тогда
				ДоступныеИсточники.Добавить(ИндексИсточника);
				Продолжить;
			КонецЕсли;
			Источник = СтрокиПартий[ИндексИсточника];
			Если Не Источник.РасчетЗавершен Тогда
				РассчитатьПартиюПоЦепочкеИзВыборки(ОписаниеДвижений, Источник, РасчетныеПартии, ЦепочкиДвижений, Приходы, ИндексИсточника, ПройденныйПуть, МаксИндекс, Итерация, СтрокиПартий, Валюты);
				ВГраница = Источники.ВГраница();
				Приход = Приходы[ИндексИсточника];
				Если Неопределено = Приход Тогда
					ДоступныеИсточники.Добавить(ИндексИсточника);
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ПриходЭтоСторно = ЭтоСторно(Приход.ТипЗаписи);
		Если Приход[ОписаниеДвижений.БазисПрихода] < 0. Тогда
			ИнвертироватьПоказатели(Приход, ОписаниеДвижений.Показатели, ПриходЭтоСторно);
		КонецЕсли;
		Если Приход[ОписаниеДвижений.БазисПрихода] = 0. Тогда
			Приходы.Удалить(ИндексИсточника);
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьРасчетнуюПартию(ОписаниеДвижений.Контекст, РасчетнаяПартия, Расход, Приход, Валюты);
		Если ИнвертировалиПоказатели Тогда
			ИнвертироватьПоказатели(РасчетнаяПартия, ОписаниеДвижений.Показатели, ЭтоСторно);
		КонецЕсли;
		
		Если Приход[ОписаниеДвижений.БазисПрихода] <= 0. Тогда
			Приходы.Удалить(ИндексИсточника);
		КонецЕсли;
		Если РасчетнаяПартия.РасчетЗавершен Тогда
			НоваяПартия = РасчетныеПартии.Добавить();
			Если РасчетнаяПартия.Регистратор = РасчетнаяПартия.РасчетныйДокумент Тогда
				РасчетнаяПартия.ПриоритетРасчетногоДокумента = 1;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(НоваяПартия, РасчетнаяПартия);
			ЗаполнитьЗалогЗаТаруРегл(Валюты, НоваяПартия);
			
			ТекущийИндекс = ?(ИндексРасчетнойПартии >= 0, ИндексРасчетнойПартии, ИндексРасхода);
			РасчетЗавершен.Добавить(ТекущийИндекс);
			Если СтрокиПартий[ТекущийИндекс] <> Неопределено Тогда
				СтрокиПартий.Удалить(ТекущийИндекс);
			КонецЕсли;
			Приход = КопияЗаписиСтруктурой(НоваяПартия, ОписаниеДвижений.ПоляРасчета);
			ЭтоСторно = ЭтоСторно(Приход.ТипЗаписи);
			ИнвертироватьПоказатели(Приход, ОписаниеДвижений.Показатели, ЭтоСторно);
			Приходы.Вставить(ТекущийИндекс, Приход);
			// Добавим новую строку партии.
			Если Расход[ОписаниеДвижений.БазисРасхода] > 0. Тогда
				РасчетнаяПартия = КопияЗаписиСтруктурой(Расход, ОписаниеДвижений.ПоляРасчета);
				РасчетнаяПартия.РасчетЗавершен = Ложь;
				РасчетнаяПартия[ОписаниеДвижений.ПолеПорядка] = Дата(1,1,1);
				ИндексРасчетнойПартии = МаксИндекс;
				ПройденныйПуть.Вставить(ИндексРасчетнойПартии, ИндексРасчетнойПартии);
				МаксИндекс = МаксИндекс + 1;
				// текущий источник будет также источником для новой расчетной партии
				Для Каждого ИндексПриемника Из ЦепочкиДвижений[ИндексРасхода].Приемники Цикл
					Если Не ЦепочкиДвижений[ИндексПриемника].Свойство("РасчетЗавершен") Тогда
						СвязатьУзлыЦепочки(ЦепочкиДвижений, ИндексПриемника, ИндексРасчетнойПартии, ИндексРасхода);
					КонецЕсли;
				КонецЦикла; // приемники текущего расхода получили новую партию в источниках
			КонецЕсли;
		КонецЕсли;
		Если Расход[ОписаниеДвижений.БазисРасхода] <= 0. Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если СтрокиПартий[ИндексРасхода] <> Неопределено Тогда
		СтрокиПартий.Удалить(ИндексРасхода);
	КонецЕсли;
	Если Расход[ОписаниеДвижений.БазисРасхода] <> 0. Тогда
		Расход.РасчетЗавершен = Истина;
		НоваяПартия = РасчетныеПартии.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПартия, Расход);
		
		Если НоваяПартия.Регистратор = НоваяПартия.РасчетныйДокумент Тогда
			НоваяПартия.ПриоритетРасчетногоДокумента = 1;
		КонецЕсли;
		
		Если Расход.ТипЗаписи <> "Отгрузка" Тогда
			НоваяПартия.ЗалогЗаТару = 0;
			НоваяПартия.ЗалогЗаТаруРегл = 0;
		Иначе
			ЗаполнитьЗалогЗаТаруРегл(Валюты, НоваяПартия);
		КонецЕсли;
		
		Если ИнвертировалиПоказатели Тогда
			ИнвертироватьПоказатели(НоваяПартия, ОписаниеДвижений.Показатели, ЭтоСторно);
		КонецЕсли;
		
		ТекущийИндекс = ?(ИндексРасчетнойПартии >= 0, ИндексРасчетнойПартии, ИндексРасхода);
		
		Приход = КопияЗаписиСтруктурой(НоваяПартия, ОписаниеДвижений.ПоляРасчета);
		ЭтоСторно = ЭтоСторно(Приход.ТипЗаписи);
		ИнвертироватьПоказатели(Приход, ОписаниеДвижений.Показатели, ЭтоСторно);
		Приходы.Вставить(ТекущийИндекс, Приход);
		
		Если ИндексРасчетнойПартии >= 0 И ДоступныеИсточники.Количество() > 0 Тогда
			Для Каждого ИндексИсточника Из ДоступныеИсточники Цикл
				СвязатьУзлыЦепочки(ЦепочкиДвижений, ТекущийИндекс, ИндексИсточника);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	Для Каждого Индекс Из РасчетЗавершен Цикл
		Если ЦепочкиДвижений[Индекс] <> Неопределено Тогда
			ЦепочкиДвижений[Индекс].Вставить("РасчетЗавершен", Истина);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

// Вспомогательные универсальные методы
#Область СлужебныеПроцедурыИФункции

// Выполняет проверку на активное задание распределения взаиморасчетов
Функция ФоновоеЗаданиеАктивно(КлючЗадания)
	Отбор = Новый Структура();
	Отбор.Вставить("Ключ", КлючЗадания);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	АктивныеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	
	Если АктивныеЗадания.Количество() > 0 Тогда
		ФоновоеЗаданиеАктивно = Истина;
	Иначе
		ФоновоеЗаданиеАктивно = Ложь;
	КонецЕсли;
	Возврат ФоновоеЗаданиеАктивно;
КонецФункции

Функция КопияЗаписиСтруктурой(Запись, ПереченьПолей)
	КопияСтрокиСтруктурой = Новый Структура(ПереченьПолей);
	ЗаполнитьЗначенияСвойств(КопияСтрокиСтруктурой, Запись);
	Возврат КопияСтрокиСтруктурой;
КонецФункции

Функция ЭтоСторно(ТипЗаписи)
	Если ТипЗаписи = "СторноОплаты" ИЛИ ТипЗаписи = "СторноОтгрузки"
		ИЛИ ТипЗаписи = "СторноПоступления" ИЛИ ТипЗаписи = "ВозвратТоваров"
	Тогда
		ЭтоСторно = Истина;
	Иначе
		ЭтоСторно = Ложь;
	КонецЕсли;
	Возврат ЭтоСторно;
КонецФункции

Функция ТребуетсяСортировка(Запись, ЭтоСторно)
	Если (ЭтоСторно И Запись.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ВзаимозачетЗадолженности) Тогда
		ТребуетсяСортировка = Истина;
	Иначе
		ТребуетсяСортировка = Ложь;
	КонецЕсли;
	Возврат ТребуетсяСортировка;
КонецФункции

Процедура ИнвертироватьПоказатели(Запись, ПереченьПоказателей, Инвертировать)
	Если Не Инвертировать Тогда
		Возврат;
	КонецЕсли;
	Для Каждого Поле Из Новый Структура(ПереченьПоказателей) Цикл
		Запись[Поле.Ключ] = -Запись[Поле.Ключ];
	КонецЦикла;
КонецПроцедуры

Функция ПереченьПолей(КоллекцияКолонок)
	ПереченьПолей = "";
	Для Каждого Колонка Из КоллекцияКолонок Цикл
		ПереченьПолей = ПереченьПолей + ?(ЗначениеЗаполнено(ПереченьПолей), ", ", "") + Колонка.Имя;
	КонецЦикла;
	Возврат ПереченьПолей;
КонецФункции

Функция ДобавитьУзелИндекса(Индекс, КлючиИндекса, Значение)
	ВГраница = КлючиИндекса.ВГраница();
	Если ВГраница < 0 Или КлючиИндекса[0] = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	УзелИндекса = Индекс;
	Счетчик = 0;
	Пока Счетчик < ВГраница Цикл
		ПодузелИндекса = УзелИндекса[КлючиИндекса[Счетчик]];
		Если ПодузелИндекса = Неопределено Тогда
			ПодузелИндекса = Новый Соответствие;
			УзелИндекса.Вставить(КлючиИндекса[Счетчик], ПодузелИндекса);
		КонецЕсли;
		УзелИндекса = ПодузелИндекса;
		Счетчик = Счетчик + 1;
		Если КлючиИндекса[Счетчик] = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла;
	УзелИндекса.Вставить(КлючиИндекса[Счетчик], Значение);
	Возврат Значение;
КонецФункции

Функция УвеличитьНомерЗадания(ИмяКонстанты)
	НачатьТранзакцию();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Константа."+ИмяКонстанты);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	НомерДоРасчета = Константы[ИмяКонстанты].Получить();
	Константы[ИмяКонстанты].Установить(НомерДоРасчета + 1);
	ЗафиксироватьТранзакцию();
	
	Возврат НомерДоРасчета;
КонецФункции

Функция СписокОрганизаций(АналитикиРасчета)
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Ключи.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПоПартнерам КАК Ключи
	|ГДЕ
	|	Ключи.КлючАналитики В (&АналитикиРасчета)
	|	И НЕ &ПоВсемОрганизациям
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	&ПоВсемОрганизациям
	|");
	
	Запрос.УстановитьПараметр("АналитикиРасчета", АналитикиРасчета);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", АналитикиРасчета = Неопределено);
	
	Результат = Запрос.Выполнить().Выгрузить();
	Возврат Результат.ВыгрузитьКолонку("Организация");
КонецФункции


Функция СформироватьДвиженияПоНДС(Контекст, РегистраторыКОтражению)
КонецФункции


#КонецОбласти

#Область ОчисткаДвиженийРегистров

Процедура ОчиститьРегистрНаСервере(Регистраторы, ИмяРегистра)
	Для Каждого Элемент Из Регистраторы Цикл 
		НачатьТранзакцию();
		Движения = РегистрыНакопления[ИмяРегистра].СоздатьНаборЗаписей();
		Движения.Отбор.Регистратор.Установить(Элемент.Ключ);
		Попытка
			Движения.Записать();
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не удалось очистить регистр: %1 у документа: %2 по причине: %3';uk='Не вдалося очистити регістр: %1 у документа: %2 з причини: %3'"),
				ИмяРегистра,
				Элемент.Ключ,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				Элемент.Ключ.Метаданные(),
				Элемент.Ключ,
				ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

Функция ИсключаемыеТипы()
	ИсключаемыеТипы = Новый Массив;
	ИсключаемыеТипы.Добавить(Тип("ДокументСсылка.КорректировкаРегистров"));
	Возврат ИсключаемыеТипы;
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ОчиститьЗаказыВДвиженияхРасчетовСПоставщиками() Экспорт
	
	ЗаписьЖурналаРегистрации("Взаиморасчеты", УровеньЖурналаРегистрации.Информация, , ,
			НСтр("ru='Запуск задания очистки заказов в движениях расчетов с поставщиками.';uk='Запуск завдання очищення замовлень в рухах розрахунків з постачальниками.'"));

	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК Расчеты
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Расчеты.РасчетныйДокумент) = ТИП(Документ.ЗаказПоставщику)
	|");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	Расчеты.Период КАК Период,
		|	Расчеты.Регистратор КАК Регистратор,
		|	Расчеты.Активность КАК Активность,
		|	Расчеты.ВидДвижения КАК ВидДвижения,
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ЗаказПоставщику КАК ЗаказПоставщику,
		|	Расчеты.РасчетныйДокумент КАК РасчетныйДокумент,
		|	Расчеты.Валюта КАК Валюта,
		|	Расчеты.КВозврату КАК КВозврату,
		|	Расчеты.Долг КАК Долг,
		|	Расчеты.ДолгУпр КАК ДолгУпр,
		|	Расчеты.ДолгРегл КАК ДолгРегл,
		|	Расчеты.Предоплата КАК Предоплата,
		|	Расчеты.ПредоплатаУпр КАК ПредоплатаУпр,
		|	Расчеты.ПредоплатаРегл КАК ПредоплатаРегл,
		|	Расчеты.ЗалогЗаТару КАК ЗалогЗаТару,
		|	Расчеты.ЗалогЗаТаруРегл КАК ЗалогЗаТаруРегл,
		|	Расчеты.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	Расчеты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК Расчеты
		|ГДЕ
		|	Расчеты.Регистратор = &Регистратор
		|	И ТИПЗНАЧЕНИЯ(Расчеты.РасчетныйДокумент) <> ТИП(Документ.ЗаказПоставщику)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 0
		|	Расчеты.Период КАК Период,
		|	Расчеты.Регистратор КАК Регистратор,
		|	Расчеты.Активность КАК Активность,
		|	Расчеты.ВидДвижения КАК ВидДвижения,
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ЗаказПоставщику КАК ЗаказПоставщику,
		|	Расчеты.РасчетныйДокумент КАК РасчетныйДокумент,
		|	Расчеты.Валюта КАК Валюта,
		|	Расчеты.КВозврату КАК КВозврату,
		|	Расчеты.Долг КАК Долг,
		|	Расчеты.ДолгУпр КАК ДолгУпр,
		|	Расчеты.ДолгРегл КАК ДолгРегл,
		|	Расчеты.Предоплата КАК Предоплата,
		|	Расчеты.ПредоплатаУпр КАК ПредоплатаУпр,
		|	Расчеты.ПредоплатаРегл КАК ПредоплатаРегл,
		|	Расчеты.ЗалогЗаТару КАК ЗалогЗаТару,
		|	Расчеты.ЗалогЗаТаруРегл КАК ЗалогЗаТаруРегл,
		|	Расчеты.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	Расчеты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК Расчеты
		|ГДЕ
		|	Расчеты.Регистратор = &Регистратор
		|	И ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) = ТИП(Документ.ЗаказПоставщику)
		|";
		
		Запрос.УстановитьПараметр("Регистратор", Выборка.Регистратор);
		
		Набор = РегистрыНакопления.РасчетыСПоставщикамиПоДокументам.СоздатьНаборЗаписей();
		Набор.Отбор.Регистратор.Установить(Выборка.Регистратор);
		Набор.Очистить();
		Набор.Загрузить(Запрос.Выполнить().Выгрузить());
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации("Взаиморасчеты", УровеньЖурналаРегистрации.Информация, , ,
			НСтр("ru='Задание очистки заказов в движениях расчетов с поставщиками завершено.';uk='Завдання очищення замовлень в рухах розрахунків з постачальниками завершено.'"));
	
КонецПроцедуры

Процедура ОчиститьЗаказыВДвиженияхРасчетовСКлиентами() Экспорт
	
	ЗаписьЖурналаРегистрации("Взаиморасчеты", УровеньЖурналаРегистрации.Информация, , ,
			НСтр("ru='Запуск задания очистки заказов в движениях расчетов с клиентами.';uk='Запуск завдання очищення замовлень в рухах розрахунків з клієнтами.'"));

	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Расчеты
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Расчеты.РасчетныйДокумент) = ТИП(Документ.ЗаказКлиента)
	|	ИЛИ (Расчеты.КВозврату = 0 И Расчеты.Долг = 0 
	|			И Расчеты.Предоплата = 0 И Расчеты.ЗалогЗаТару = 0)
	|");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	Расчеты.Период КАК Период,
		|	Расчеты.Регистратор КАК Регистратор,
		|	Расчеты.ВидДвижения КАК ВидДвижения,
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ЗаказКлиента КАК ЗаказКлиента,
		|	Расчеты.РасчетныйДокумент КАК РасчетныйДокумент,
		|	Расчеты.Валюта КАК Валюта,
		|	Расчеты.КВозврату КАК КВозврату,
		|	Расчеты.Долг КАК Долг,
		|	Расчеты.ДолгУпр КАК ДолгУпр,
		|	Расчеты.ДолгРегл КАК ДолгРегл,
		|	Расчеты.Предоплата КАК Предоплата,
		|	Расчеты.ПредоплатаУпр КАК ПредоплатаУпр,
		|	Расчеты.ПредоплатаРегл КАК ПредоплатаРегл,
		|	Расчеты.ЗалогЗаТару КАК ЗалогЗаТару,
		|	Расчеты.ЗалогЗаТаруРегл КАК ЗалогЗаТаруРегл,
		|	Расчеты.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	Расчеты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Расчеты
		|ГДЕ
		|	Расчеты.Регистратор = &Регистратор
		|	И ТИПЗНАЧЕНИЯ(Расчеты.РасчетныйДокумент) <> ТИП(Документ.ЗаказКлиента)
		|	И (Расчеты.КВозврату <> 0 ИЛИ Расчеты.Долг <> 0 
		|			ИЛИ Расчеты.Предоплата <> 0 ИЛИ Расчеты.ЗалогЗаТару <> 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 0
		|	Расчеты.Период КАК Период,
		|	Расчеты.Регистратор КАК Регистратор,
		|	Расчеты.ВидДвижения КАК ВидДвижения,
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ЗаказКлиента КАК ЗаказКлиента,
		|	Расчеты.РасчетныйДокумент КАК РасчетныйДокумент,
		|	Расчеты.Валюта КАК Валюта,
		|	Расчеты.КВозврату КАК КВозврату,
		|	Расчеты.Долг КАК Долг,
		|	Расчеты.ДолгУпр КАК ДолгУпр,
		|	Расчеты.ДолгРегл КАК ДолгРегл,
		|	Расчеты.Предоплата КАК Предоплата,
		|	Расчеты.ПредоплатаУпр КАК ПредоплатаУпр,
		|	Расчеты.ПредоплатаРегл КАК ПредоплатаРегл,
		|	Расчеты.ЗалогЗаТару КАК ЗалогЗаТару,
		|	Расчеты.ЗалогЗаТаруРегл КАК ЗалогЗаТаруРегл,
		|	Расчеты.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	Расчеты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Расчеты
		|ГДЕ
		|	Расчеты.Регистратор = &Регистратор
		|	И ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) = ТИП(Документ.ЗаказКлиента)
		|";
		
		Запрос.УстановитьПараметр("Регистратор", Выборка.Регистратор);
		
		Набор = РегистрыНакопления.РасчетыСКлиентамиПоДокументам.СоздатьНаборЗаписей();
		Набор.Отбор.Регистратор.Установить(Выборка.Регистратор);
		Набор.Очистить();
		Набор.Загрузить(Запрос.Выполнить().Выгрузить());
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации("Взаиморасчеты", УровеньЖурналаРегистрации.Информация, , ,
			НСтр("ru='Задание очистки заказов в движениях расчетов с клиентами завершено.';uk='Завдання очищення замовлень в рухах розрахунків з клієнтами завершено.'"));
	
КонецПроцедуры

Процедура ЗаполнитьПорядокРасчетов() Экспорт
	
	ИспользоватьЗаказыКлиентов = ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыКлиентов");
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеСоглашения.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СоглашенияСКлиентами КАК ДанныеСоглашения
	|ГДЕ
	|	НЕ ДанныеСоглашения.ИспользуютсяДоговорыКонтрагентов
	|	И ДанныеСоглашения.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)
	|");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если ИспользоватьЗаказыКлиентов Тогда
			СправочникОбъект.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказамНакладным;
		Иначе
			СправочникОбъект.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоНакладным;
		КонецЕсли;
		
		СправочникОбъект.ОбменДанными.Загрузка = Истина;
		СправочникОбъект.Записать();
		
	КонецЦикла;
	
	ИспользоватьЗаказыПоставщикам = ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыПоставщикам");
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеСоглашения.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СоглашенияСПоставщиками КАК ДанныеСоглашения
	|ГДЕ
	|	НЕ ДанныеСоглашения.ИспользуютсяДоговорыКонтрагентов
	|	И ДанныеСоглашения.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)
	|");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если ИспользоватьЗаказыПоставщикам Тогда
			СправочникОбъект.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказамНакладным;
		Иначе
			СправочникОбъект.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоНакладным;
		КонецЕсли;
		
		СправочникОбъект.ОбменДанными.Загрузка = Истина;
		СправочникОбъект.Записать();
		
	КонецЦикла;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|
	|	ВЫБОР КОГДА ДанныеСоглашения.ИспользуютсяДоговорыКонтрагентов ИЛИ ДанныеСоглашения.Ссылка ЕСТЬ NULL ТОГДА
	|		ЕСТЬNULL(ДанныеДоговора.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДанныеСоглашения.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	КОНЕЦ КАК ПорядокРасчетов
	|
	|ИЗ
	|	Документ.АктВыполненныхРабот КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СоглашенияСКлиентами КАК ДанныеСоглашения
	|	ПО
	|		ДанныеДокумента.Соглашение = ДанныеСоглашения.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ДоговорыКонтрагентов КАК ДанныеДоговора
	|	ПО
	|		ДанныеДокумента.Договор = ДанныеДоговора.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|
	|	ВЫБОР КОГДА ДанныеСоглашения.ИспользуютсяДоговорыКонтрагентов ИЛИ ДанныеСоглашения.Ссылка ЕСТЬ NULL ТОГДА
	|		ЕСТЬNULL(ДанныеДоговора.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДанныеСоглашения.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	КОНЕЦ КАК ПорядокРасчетов
	|
	|ИЗ
	|	Документ.КорректировкаПоступления КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СоглашенияСПоставщиками КАК ДанныеСоглашения
	|	ПО
	|		ДанныеДокумента.Соглашение = ДанныеСоглашения.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ДоговорыКонтрагентов КАК ДанныеДоговора
	|	ПО
	|		ДанныеДокумента.Договор = ДанныеДоговора.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|
	|	ВЫБОР КОГДА ДанныеСоглашения.ИспользуютсяДоговорыКонтрагентов ИЛИ ДанныеСоглашения.Ссылка ЕСТЬ NULL ТОГДА
	|		ЕСТЬNULL(ДанныеДоговора.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДанныеСоглашения.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	КОНЕЦ КАК ПорядокРасчетов
	|
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СоглашенияСКлиентами КАК ДанныеСоглашения
	|	ПО
	|		ДанныеДокумента.Соглашение = ДанныеСоглашения.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ДоговорыКонтрагентов КАК ДанныеДоговора
	|	ПО
	|		ДанныеДокумента.Договор = ДанныеДоговора.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|
	|	ВЫБОР КОГДА ДанныеСоглашения.ИспользуютсяДоговорыКонтрагентов ИЛИ ДанныеСоглашения.Ссылка ЕСТЬ NULL ТОГДА
	|		ЕСТЬNULL(ДанныеДоговора.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДанныеСоглашения.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	КОНЕЦ КАК ПорядокРасчетов
	|
	|ИЗ
	|	Документ.ОтчетКомиссионера КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СоглашенияСКлиентами КАК ДанныеСоглашения
	|	ПО
	|		ДанныеДокумента.Соглашение = ДанныеСоглашения.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ДоговорыКонтрагентов КАК ДанныеДоговора
	|	ПО
	|		ДанныеДокумента.Договор = ДанныеДоговора.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|
	|	ВЫБОР КОГДА ДанныеСоглашения.ИспользуютсяДоговорыКонтрагентов ИЛИ ДанныеСоглашения.Ссылка ЕСТЬ NULL ТОГДА
	|		ЕСТЬNULL(ДанныеДоговора.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДанныеСоглашения.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	КОНЕЦ КАК ПорядокРасчетов
	|
	|ИЗ
	|	Документ.ОтчетКомиссионераОСписании КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СоглашенияСКлиентами КАК ДанныеСоглашения
	|	ПО
	|		ДанныеДокумента.Соглашение = ДанныеСоглашения.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ДоговорыКонтрагентов КАК ДанныеДоговора
	|	ПО
	|		ДанныеДокумента.Договор = ДанныеДоговора.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|
	|	ВЫБОР КОГДА ДанныеСоглашения.ИспользуютсяДоговорыКонтрагентов ИЛИ ДанныеСоглашения.Ссылка ЕСТЬ NULL ТОГДА
	|		ЕСТЬNULL(ДанныеДоговора.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДанныеСоглашения.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	КОНЕЦ КАК ПорядокРасчетов
	|
	|ИЗ
	|	Документ.ОтчетКомитенту КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СоглашенияСПоставщиками КАК ДанныеСоглашения
	|	ПО
	|		ДанныеДокумента.Соглашение = ДанныеСоглашения.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ДоговорыКонтрагентов КАК ДанныеДоговора
	|	ПО
	|		ДанныеДокумента.Договор = ДанныеДоговора.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|
	|	ВЫБОР КОГДА ДанныеСоглашения.ИспользуютсяДоговорыКонтрагентов ИЛИ ДанныеСоглашения.Ссылка ЕСТЬ NULL ТОГДА
	|		ЕСТЬNULL(ДанныеДоговора.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДанныеСоглашения.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	КОНЕЦ КАК ПорядокРасчетов
	|
	|ИЗ
	|	Документ.ОтчетКомитентуОСписании КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СоглашенияСПоставщиками КАК ДанныеСоглашения
	|	ПО
	|		ДанныеДокумента.Соглашение = ДанныеСоглашения.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ДоговорыКонтрагентов КАК ДанныеДоговора
	|	ПО
	|		ДанныеДокумента.Договор = ДанныеДоговора.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|
	|	ВЫБОР КОГДА ДанныеСоглашения.ИспользуютсяДоговорыКонтрагентов ИЛИ ДанныеСоглашения.Ссылка ЕСТЬ NULL ТОГДА
	|		ЕСТЬNULL(ДанныеДоговора.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДанныеСоглашения.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	КОНЕЦ КАК ПорядокРасчетов
	|
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СоглашенияСПоставщиками КАК ДанныеСоглашения
	|	ПО
	|		ДанныеДокумента.Соглашение = ДанныеСоглашения.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ДоговорыКонтрагентов КАК ДанныеДоговора
	|	ПО
	|		ДанныеДокумента.Договор = ДанныеДоговора.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|
	|	ВЫБОР КОГДА ДанныеСоглашения.ИспользуютсяДоговорыКонтрагентов ИЛИ ДанныеСоглашения.Ссылка ЕСТЬ NULL ТОГДА
	|		ЕСТЬNULL(ДанныеДоговора.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДанныеСоглашения.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	КОНЕЦ КАК ПорядокРасчетов
	|
	|ИЗ
	|	Документ.ПоступлениеУслугПрочихАктивов КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СоглашенияСПоставщиками КАК ДанныеСоглашения
	|	ПО
	|		ДанныеДокумента.Соглашение = ДанныеСоглашения.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ДоговорыКонтрагентов КАК ДанныеДоговора
	|	ПО
	|		ДанныеДокумента.Договор = ДанныеДоговора.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|
	|	ВЫБОР КОГДА ДанныеСоглашения.ИспользуютсяДоговорыКонтрагентов ИЛИ ДанныеСоглашения.Ссылка ЕСТЬ NULL ТОГДА
	|		ЕСТЬNULL(ДанныеДоговора.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДанныеСоглашения.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	КОНЕЦ КАК ПорядокРасчетов
	|
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СоглашенияСКлиентами КАК ДанныеСоглашения
	|	ПО
	|		ДанныеДокумента.Соглашение = ДанныеСоглашения.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ДоговорыКонтрагентов КАК ДанныеДоговора
	|	ПО
	|		ДанныеДокумента.Договор = ДанныеДоговора.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|
	|	ВЫБОР КОГДА ДанныеСоглашения.ИспользуютсяДоговорыКонтрагентов ИЛИ ДанныеСоглашения.Ссылка ЕСТЬ NULL ТОГДА
	|		ЕСТЬNULL(ДанныеДоговора.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДанныеСоглашения.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	КОНЕЦ КАК ПорядокРасчетов
	|
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СоглашенияСКлиентами КАК ДанныеСоглашения
	|	ПО
	|		ДанныеДокумента.Соглашение = ДанныеСоглашения.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ДоговорыКонтрагентов КАК ДанныеДоговора
	|	ПО
	|		ДанныеДокумента.Договор = ДанныеДоговора.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|
	|	ВЫБОР КОГДА ДанныеСоглашения.ИспользуютсяДоговорыКонтрагентов ИЛИ ДанныеСоглашения.Ссылка ЕСТЬ NULL ТОГДА
	|		ЕСТЬNULL(ДанныеДоговора.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДанныеСоглашения.ПорядокРасчетов, ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|	КОНЕЦ КАК ПорядокРасчетов
	|
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СоглашенияСПоставщиками КАК ДанныеСоглашения
	|	ПО
	|		ДанныеДокумента.Соглашение = ДанныеСоглашения.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ДоговорыКонтрагентов КАК ДанныеДоговора
	|	ПО
	|		ДанныеДокумента.Договор = ДанныеДоговора.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным) КАК ПорядокРасчетов
	|
	|ИЗ
	|	Документ.ОтчетПоКомиссииМеждуОрганизациями КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным) КАК ПорядокРасчетов
	|
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПустаяСсылка)
	|");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОбъект.ПорядокРасчетов = Выборка.ПорядокРасчетов;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		ДокументОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации)
	
	ВходящиеДанные = Новый Соответствие;
	
	// из этого модуля + модулей счетов-фактур, см. СформироватьДвиженияПоНДС()	
	ВходящиеДанные.Вставить(Метаданные.Документы.АктВыполненныхРабот);
	ВходящиеДанные.Вставить(Метаданные.Документы.АннулированиеПодарочныхСертификатов);
	ВходящиеДанные.Вставить(Метаданные.Документы.ВозвратПодарочныхСертификатов);
	ВходящиеДанные.Вставить(Метаданные.Документы.КорректировкаПоступления);
	ВходящиеДанные.Вставить(Метаданные.Документы.КорректировкаРеализации);
	ВходящиеДанные.Вставить(Метаданные.Документы.ОтчетКомиссионера);
	ВходящиеДанные.Вставить(Метаданные.Документы.ОтчетКомиссионераОСписании);
	ВходящиеДанные.Вставить(Метаданные.Документы.ОтчетКомитенту);
	ВходящиеДанные.Вставить(Метаданные.Документы.ОтчетКомитентуОСписании);
	ВходящиеДанные.Вставить(Метаданные.Документы.ОтчетОРозничныхПродажах);
	ВходящиеДанные.Вставить(Метаданные.Документы.ОтчетПоКомиссииМеждуОрганизациями);
	ВходящиеДанные.Вставить(Метаданные.Документы.ПередачаТоваровМеждуОрганизациями);
	ВходящиеДанные.Вставить(Метаданные.Документы.ПоступлениеБезналичныхДенежныхСредств);
	ВходящиеДанные.Вставить(Метаданные.Документы.ПоступлениеТоваровУслуг);
	ВходящиеДанные.Вставить(Метаданные.Документы.ПоступлениеУслугПрочихАктивов);
	ВходящиеДанные.Вставить(Метаданные.Документы.РеализацияТоваровУслуг);
	ВходящиеДанные.Вставить(Метаданные.Документы.РеализацияУслугПрочихАктивов);
	ВходящиеДанные.Вставить(Метаданные.Документы.СписаниеБезналичныхДенежныхСредств);
	ВходящиеДанные.Вставить(Метаданные.Документы.ТаможеннаяДекларацияИмпорт);
	
	
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПодарочныеСертификаты);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.РасчетыСКлиентами);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоДокументам);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.РасчетыСПоставщиками);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоДокументам);
	
	ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.АналитикаУчетаПоПартнерам);
	ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ДанныеПервичныхДокументов);
	ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.СуммыДокументовВВалютеРегл);
	ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ЗаданияКРаспределениюРасчетовСКлиентами);
	ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ЗаданияКРаспределениюРасчетовСПоставщиками);
	
	ЗакрытиеМесяцаУТВызовСервера.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти