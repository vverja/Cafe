////////////////////////////////////////////////////////////////////////////////
// МОДУЛЬ СОДЕРЖИТ ПРОЦЕДУРЫ РЕГИСТРАЦИИ ОБЪЕКТОВ ОБМЕНА С МОБИЛЬНЫМ ПРИЛОЖЕНИЕМ "МОНИТОР ERP"
// - регистрация объектов в узлах обмена
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Обработчик подписки на событие "МониторERPПриЗаписиСправочника".
//
// Параметры:
//  Источник - объект метаданных - источник события;
//  Отказ - булево.
//
Процедура ЗарегистрироватьИзмененияПриЗаписиСправочникаДляОбменаСМониторERP(Источник, Отказ) Экспорт
	
	ЗарегистрироватьИзменения(Источник);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Регистрирует изменения по узлам планов обмена с мобильным приложением "Монитор ERP".
//
// Параметры:
//  Объект - Объект метаданных - источник события.
//
Процедура ЗарегистрироватьИзменения(Объект)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	МобильноеПриложениеМониторERP.Ссылка
	|ИЗ
	|	ПланОбмена.МобильноеПриложениеМониторERP КАК МобильноеПриложениеМониторERP
	|ГДЕ
	|	НЕ МобильноеПриложениеМониторERP.ПометкаУдаления
	|	И НЕ МобильноеПриложениеМониторERP.Ссылка = &ЭтотУзел");
	
	Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.МобильноеПриложениеМониторERP.ЭтотУзел());
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	МассивУзлов = Результат.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Если ТипЗнч(Объект) = Тип("СправочникОбъект.КонтактныеЛицаПартнеров") Тогда
		ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, Объект.Владелец);
	Иначе
		ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, Объект.Ссылка);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
