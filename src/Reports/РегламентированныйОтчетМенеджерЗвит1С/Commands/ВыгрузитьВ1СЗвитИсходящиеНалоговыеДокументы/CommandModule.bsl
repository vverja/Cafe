
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если Не РегламентированнаяОтчетностьКлиент.ПодключитьМенеджерЗвит1С(Истина) Тогда
		Возврат;
	КонецЕсли;

	ДополнитьВыгружаемыеДокументыНННаобычныеЦены(ПараметрКоманды);
	
	ПроверитьПризнакНеобходимостиВключенияВЕРНН(ПараметрКоманды);
	
	Если ПараметрКоманды.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	глМенеджерЗвит1С.ВыгрузитьДокумент(ПараметрКоманды, ПараметрыВыполненияКоманды.Источник);
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьВыгружаемыеДокументыНННаобычныеЦены(МассивДокуменов);
	
	Если РегламентированнаяОтчетностьПереопределяемый.ИДКонфигурации() = "ЕРП" 
		ИЛИ РегламентированнаяОтчетностьПереопределяемый.ИДКонфигурации() = "BASУТ" Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	Док.НалоговаяДляРезерваНомераПриПродажаНижеОбычнойЦены КАК Ссылка
	               |ИЗ
	               |	Документ.НалоговаяНакладная КАК Док
	               |ГДЕ
	               |	Док.Ссылка В(&МассивДокументов)
	               |	И Док.ПродажаНижеОбычнойЦены
	               |	И Док.Дата > ДАТАВРЕМЯ(2015, 1, 1)
	               |	И НЕ Док.НалоговаяДляРезерваНомераПриПродажаНижеОбычнойЦены = ЗНАЧЕНИЕ(Документ.НалоговаяНакладная.ПустаяСсылка)
	               |	И НЕ Док.НалоговаяДляРезерваНомераПриПродажаНижеОбычнойЦены В (&МассивДокументов)
				   |";
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокуменов);
	
	ВыборкаНННаПревышениеОбычныхЦен = Запрос.Выполнить().Выбрать();
	Пока ВыборкаНННаПревышениеОбычныхЦен.Следующий() Цикл
		МассивДокуменов.Добавить(ВыборкаНННаПревышениеОбычныхЦен.Ссылка);	
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПроверитьПризнакНеобходимостиВключенияВЕРНН(ПараметрКоманды)
	Если РегламентированнаяОтчетностьПереопределяемый.ИДКонфигурации() = "БП" Тогда
		Возврат;
	КонецЕсли;
	
	МассивУдаляемыхДокументов = Новый Массив;
	
	ж = 0;
	Пока ж <= ПараметрКоманды.ВГраница() Цикл
		Если НЕ ПараметрКоманды[ж].ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных Тогда
			МассивУдаляемыхДокументов.Добавить(ПараметрКоманды[ж]);
			ПараметрКоманды.Удалить(ж);
		Иначе
			ж = ж + 1;
		КонецЕсли;
	КонецЦикла;
	
	КоличествоУдаляемыхДокументов = МассивУдаляемыхДокументов.Количество();
	
	Если КоличествоУдаляемыхДокументов > 0 Тогда
		
		Если КоличествоУдаляемыхДокументов = 1 Тогда
			ТекстСообщения = НСтр("ru='Документ %1 не будет выгружен, поскольку в нем не установлен флаг ""Подлежит включению в Единый реестр налоговых накладных""';uk='Документ %1 не буде вивантажений, оскільки в ньому не встановлений прапор ""Підлягає включенню до Єдиного реєстру податкових накладних""'");
		Иначе
			ТекстСообщения = НСтр("ru='Документы %1 не будут выгружены, поскольку в них не установлен флаг ""Подлежит включению в Единый реестр налоговых накладных""';uk='Документи %1 не будуть вивантажені, оскільки в них не встановлений прапор ""Підлягає включенню до Єдиного реєстру податкових накладних""'");
		КонецЕсли;
		
		СписокДокументов = "";
		Для каждого УдаляемыйДокумент Из МассивУдаляемыхДокументов Цикл
			
			Если НЕ ПустаяСтрока(СписокДокументов) Тогда
				СписокДокументов = СписокДокументов + ", ";
			КонецЕсли;
			
			СписокДокументов = СписокДокументов + УдаляемыйДокумент;
			
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СписокДокументов));
			
	КонецЕсли;
	
КонецПроцедуры

