
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Запись, ЭтотОбъект);

	Если ЗначениеЗаполнено(Параметры.ФизЛицо) Тогда
		Запись.Физлицо = Параметры.ФизЛицо;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если Запись.ЯвляетсяДокументомУдостоверяющимЛичность Тогда
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("ВидДокумента", Запись.ВидДокумента);
		ПараметрыЗаписи.Вставить("ПаспортСерия", Запись.Серия);
		ПараметрыЗаписи.Вставить("ПаспортНомер", Запись.Номер);
		ПараметрыЗаписи.Вставить("ПаспортВыдан", Запись.КемВыдан);
		ПараметрыЗаписи.Вставить("ПаспортДатаВыдачи", Запись.ДатаВыдачи);
		ПараметрыЗаписи.Вставить("Представление", Запись.Представление);
		Оповестить("Запись_ДокументФизЛица", ПараметрыЗаписи, ЭтаФорма);
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидДокументаПриИзменении(Элемент)
	
	Если ЯвляетсяУдостоверениемЛичности(Запись.Физлицо, Запись.ВидДокумента, Запись.Период) Тогда
		Запись.ЯвляетсяДокументомУдостоверяющимЛичность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ЯвляетсяУдостоверениемЛичности(Физлицо, ВидДокумента, Дата)
	
	Возврат РегистрыСведений.ДокументыФизическихЛиц.ЯвляетсяУдостоверениемЛичности(Физлицо, ВидДокумента, Дата);
	
КонецФункции

#КонецОбласти

