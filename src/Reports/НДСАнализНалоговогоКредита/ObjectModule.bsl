#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ФиксНастройкаПериода = ФиксированнаяНастройкаПараметра("Период");
	
	Если ФиксНастройкаПериода.Использование = Истина Тогда
		
		ПользНастройкаПериода = ПользовательскаяНастройкаПараметра("Период");
		ПользНастройкаПериода.Использование = Истина;
		ПользНастройкаПериода.Значение.ДатаНачала = ФиксНастройкаПериода.Значение.ДатаНачала;
		ПользНастройкаПериода.Значение.ДатаОкончания = ФиксНастройкаПериода.Значение.ДатаОкончания;
		
		ФиксНастройкаПериода.Использование = Ложь;
		
	КонецЕсли;

	// Для расшифровки в РегистрацияВходящегоНалоговогоДокумента.ВходящиеНалоговыеДокументы
	ПрочиеОтборы = Новый Массив;
	ПрочиеОтборы.Добавить("Организация");
	ПрочиеОтборы.Добавить("Партнер");
	ПрочиеОтборы.Добавить("Контрагент");
	Для каждого ИмяПоляОтбора Из ПрочиеОтборы Цикл
		
		ФиксНастройка = ФиксированнаяНастройкаОтбора(ИмяПоляОтбора);
		Если ФиксНастройка = Неопределено ИЛИ НЕ ФиксНастройка.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		ПользНастройка = ПользовательскаяНастройкаОтбора(ИмяПоляОтбора);
		ПользНастройка.Использование = Истина;
		ПользНастройка.ПравоеЗначение = ФиксНастройка.ПравоеЗначение;		
		ФиксНастройка.Использование = Ложь;
		
	КонецЦикла;
	
	СегментыСервер.ВключитьОтборПоСегментуПартнеровВСКД(КомпоновщикНастроек);
	
КонецПроцедуры

#КонецОбласти

#Область СобытияОбщейФормыФормаОтчетаНДС 

Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	//Установим параметры для выполнения команды Актуализации расчетов с поставщиками
	Форма.ТекстВопросаАктуализацииРасчетов = НСтр("ru='Актуализировать расчеты с поставщиками?';uk='Актуалізувати розрахунки з постачальниками?'");
	Форма.ТекстПоясненияПоОкончаниюАктуализацииРасчетов = НСтр("ru='Расчеты с поставщиками актуализированы';uk='Розрахунки з постачальниками актуалізовані'");
	Форма.АктуализироватьРасчетыСКлиентами = Ложь;
	
	Форма.Элементы.АктуализироватьРасчеты.Видимость = Истина;
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

Функция ФиксированнаяНастройкаПараметра(ИмяПараметра)

	ПараметрДанных = КомпоновщикНастроек.ФиксированныеНастройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	
	Возврат ПараметрДанных;

КонецФункции

Функция ПользовательскаяНастройкаПараметра(ИмяПараметра)

	ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	Если ПараметрДанных <> Неопределено Тогда
		ПараметрПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
		Если ПараметрПользовательскойНастройки <> Неопределено Тогда
			Возврат ПараметрПользовательскойНастройки;
		Иначе
			Возврат ПараметрДанных;
		КонецЕсли;
	КонецЕсли;
	Возврат Неопределено;

КонецФункции

Функция ФиксированнаяНастройкаОтбора(ИмяОтбора)
	
	ПолеКомпоновкиДанныхДляПоиска = Новый ПолеКомпоновкиДанных(ИмяОтбора);
	Для каждого ЭлементОтбораКомпоновкиДанных Из КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы Цикл
		Если ЭлементОтбораКомпоновкиДанных.ЛевоеЗначение = ПолеКомпоновкиДанныхДляПоиска Тогда
			Возврат ЭлементОтбораКомпоновкиДанных; 
		КонецЕсли;		
	КонецЦикла;
	
	Возврат Неопределено;

КонецФункции

Функция ПользовательскаяНастройкаОтбора(ИмяОтбора)

	ПараметрДанных = Неопределено;
	ПолеКомпоновкиДанныхДляПоиска = Новый ПолеКомпоновкиДанных(ИмяОтбора);
	Для каждого ЭлементОтбораКомпоновкиДанных Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		Если ЭлементОтбораКомпоновкиДанных.ЛевоеЗначение = ПолеКомпоновкиДанныхДляПоиска Тогда
			ПараметрДанных = ЭлементОтбораКомпоновкиДанных; 
			Прервать;
		КонецЕсли;		
	КонецЦикла;
	
	//ПараметрДанных = КомпоновщикНастроек.Настройки.Отбор.Элементы.Найти(ИмяОтбора);
	Если ПараметрДанных <> Неопределено Тогда
		ПараметрПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
		Если ПараметрПользовательскойНастройки <> Неопределено Тогда
			Возврат ПараметрПользовательскойНастройки;
		Иначе
			Возврат ПараметрДанных;
		КонецЕсли;
	КонецЕсли;
	Возврат Неопределено;

КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
