#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Функция предназначена для получения данных о минимальном размере оплаты труда.
// Данные включают в себя сам размер и дату, с которой он действует.
//
// Параметры:
//	ДатаАктуальности - дата, на которую нужно получить размер МРОТ (необязательный), 
//						если не заполнен, возвращается последнее действующее значение.
//
// Возвращаемое значение - структура, полями которой являются Размер и Период.
//
Функция ДанныеМинимальногоРазмераОплатыТруда(ДатаАктуальности = Неопределено) Экспорт
	
	ДанныеМРОТ = Новый Структура("Размер, Часовая, Период");
	
	СрезПоследних = РегистрыСведений.МинимальнаяОплатаТруда.СрезПоследних(ДатаАктуальности);
	Если СрезПоследних.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru='Сведения о минимальном размере оплаты труда не заполнены';uk='Відомості про мінімальний розмір оплати праці не заповнено'");
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДанныеМРОТ, СрезПоследних[0]);
	
	Возврат ДанныеМРОТ;
	
КонецФункции

// Проверяет заполненность данных о минимальном размере оплаты труда.
//
// Возвращаемое значение - булево, Истина - если есть хотя бы одно значение.
//
Функция МинимальныйРазмерОплатыТрудаЗаполнен() Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	РегистрСведений.МинимальнаяОплатаТруда КАК МинимальнаяОплатаТруда");
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли
