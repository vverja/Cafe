#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Процедура ПерезаполнитьНДСНачальныеПериодыНОДляДокументовПоставкиОтметитьКОтработке(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСНачальныеПериодыНОДляДокументовПоставки.ДокументПоставки
	|ИЗ
	|	РегистрСведений.НДСНачальныеПериодыНОДляДокументовПоставки КАК НДСНачальныеПериодыНОДляДокументовПоставки
	|ГДЕ
	|	НДСНачальныеПериодыНОДляДокументовПоставки.АналитикаУчетаПоПартнерам = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)";
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаДокументовПоставки = РезультатЗапроса.Выгрузить();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрСведений.НДСНачальныеПериодыНОДляДокументовПоставки";
    
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ТаблицаДокументовПоставки, ДополнительныеПараметры);
	
КонецПроцедуры

// Обработчик обновления BAS УТ 3.2.4
// Заполняет реквизит АналитикаУчетаПоПартнерам для регистра "Начальные периоды налоговых обязательств в разрезе документов поставки"
Процедура ПерезаполнитьНДСНачальныеПериодыНОДляДокументовПоставки(Параметры) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НДСНачальныеПериодыНОДляДокументовПоставки.ДокументПоставки,
	|	НДСНачальныеПериодыНОДляДокументовПоставки.Дата
	|ПОМЕСТИТЬ ВТНачальныеПериодыНО
	|ИЗ
	|	РегистрСведений.НДСНачальныеПериодыНОДляДокументовПоставки КАК НДСНачальныеПериодыНОДляДокументовПоставки
	|ГДЕ
	|	НДСНачальныеПериодыНОДляДокументовПоставки.ДокументПоставки В
	|			(ВЫБРАТЬ
	|				ВТДокументыПоставки.ДокументПоставки
	|			ИЗ
	|				&ВТДляОбработкиСсылка КАК ВТДокументыПоставки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСКлиентами.Регистратор,
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам
	|ПОМЕСТИТЬ ВТАналитикиУчетаПоПартнерам
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|ГДЕ
	|	РасчетыСКлиентами.Регистратор В
	|			(ВЫБРАТЬ
	|				ВТДокументыПоставки.ДокументПоставки
	|			ИЗ
	|				&ВТДляОбработкиСсылка КАК ВТДокументыПоставки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНачальныеПериодыНО.ДокументПоставки,
	|	ВТНачальныеПериодыНО.Дата,
	|	ЕСТЬNULL(ВТАналитикиУчетаПоПартнерам.АналитикаУчетаПоПартнерам, ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)) КАК АналитикаУчетаПоПартнерам
	|ИЗ
	|	ВТНачальныеПериодыНО КАК ВТНачальныеПериодыНО
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТАналитикиУчетаПоПартнерам КАК ВТАналитикиУчетаПоПартнерам
	|		ПО ВТНачальныеПериодыНО.ДокументПоставки = ВТАналитикиУчетаПоПартнерам.Регистратор";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь,
		"РегистрСведений.НДСНачальныеПериодыНОДляДокументовПоставки",
		МенеджерВременныхТаблиц);
	
	Если НЕ Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
    КонецЕсли;
    
	Если НЕ Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли; 
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВТДляОбработкиСсылка", Результат.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НДСНачальныеПериодыНОДляДокументовПоставки");
			ЭлементБлокировки.УстановитьЗначение("ДокументПоставки", Выборка.ДокументПоставки);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			Блокировка.Заблокировать();
		Исключение
			ОтменитьТранзакцию();
			Продолжить;
		КонецПопытки;
		
		Набор = РегистрыСведений.НДСНачальныеПериодыНОДляДокументовПоставки.СоздатьНаборЗаписей();
		Набор.Отбор.ДокументПоставки.Установить(Выборка.ДокументПоставки);
		
		Если ЗначениеЗаполнено(Выборка.АналитикаУчетаПоПартнерам) Тогда
		
			СтрокаНабора = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаНабора, Выборка);	
		
		КонецЕсли;
		
		Попытка
			Если Параметры.Очередь <> Неопределено Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
			Иначе
				Набор.Записать(Истина);
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			ТекстСообщения = НСтр("ru='Не удалось установить аналитику учета по партнерам для документа поставки: %Ссылка% по причине: %Причина%';uk='Не вдалося встановити аналітику обліку за партнерам для документа поставки: %Ссылка% по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.ДокументПоставки);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				, Выборка.ДокументПоставки, ТекстСообщения);
			ОтменитьТранзакцию();
		КонецПопытки;
		
	КонецЦикла;
    
    Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
		Параметры.Очередь,
		"РегистрСведений.НДСНачальныеПериодыНОДляДокументовПоставки");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

 