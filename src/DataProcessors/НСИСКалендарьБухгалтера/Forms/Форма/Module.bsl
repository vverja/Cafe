
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказыватьПриНачалеРаботы = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("RDI",
			"RDI_КалендарьБухгалтераЗапуск",
			Истина);
	
	ПоказыватьПриНачалеРаботыНастройки = НСИССервер.ПолучитьНастройкиЗагрузок().ОткрыватьИнформациюПриСтарте;		
	
	Если Параметры.Свойство("ЗапускПриСтарте") И (НЕ ПоказыватьПриНачалеРаботы ИЛИ НЕ ПоказыватьПриНачалеРаботыНастройки ИЛИ НЕ ПравоДоступа("Просмотр", Метаданные.Обработки.НСИСНовости)) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Месяц = НачалоМесяца(ТекущаяДата());
	
	Элементы.ФормаОбновитьКалендарь.Видимость = ПравоДоступа("Просмотр", Метаданные.Обработки.НСИСЗапускСервисов);
	Элементы.ПоказыватьПриНачалеРаботы.Видимость = ПравоДоступа("СохранениеДанныхПользователя", Метаданные) И (Параметры.Свойство("ЗапускПриСтарте") ИЛИ Параметры.Свойство("ЗапускИзИнтерфейса"));
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьКалендарьНаСервере()
	
	ИмяФайла = "Календарь" + Формат(Год(Месяц)-2000,"ЧЦ=2; ЧВН=") + Формат(Месяц(Месяц),"ЧЦ=2; ЧВН=");

	КалендарьФайл = РегистрыСведений.НСИСФайлы.Получить(Новый Структура("Идентификатор", ИмяФайла));
	Если КалендарьФайл.Версия = 0 Тогда
		ТекстКалендаря = "<html>" + НСтр("ru='Нет данных календаря';uk='Немає даних календаря'") + " " + Формат(Месяц,"ДФ='MMMM yyyy'") + "</html>";
		
		Возврат;
	КонецЕсли;	
	
	Попытка
		
		ТекстКалендаря = КалендарьФайл.Файл.Получить().ПолучитьТекст();
		
	Исключение
		
		ТекстКалендаря = "<html>" + НСтр("ru='Нет данных календаря';uk='Немає даних календаря'") + " " + Формат(Месяц,"ДФ='MMMM yyyy'") + "</html>";
				
	КонецПопытки;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКалендарь()
	
	ПоказатьКалендарьНаСервере();
#Если ВебКлиент Тогда
	Календарь = ТекстКалендаря;
	
	Возврат;
	
#Иначе	
	ИмяФайла = ПолучитьИмяВременногоФайла("htm");
	Файл = Новый ЗаписьТекста(ИмяФайла);
	Файл.ЗаписатьСтроку(ТекстКалендаря);
	Файл.Закрыть();
		
	Календарь = ИмяФайла;
#КонецЕсли	
		
КонецПроцедуры	

&НаСервере
Функция ВыполнитьЗагрузку()
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаименованиеФоновогоЗадания = НСтр("ru='Обновление календаря';uk='Оновлення календаря'");
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("ВыводитьПротокол",  Ложь);
	ПараметрыЗагрузки.Вставить("ВФоне",  Истина);
	ПараметрыЗагрузки.Вставить("ЗагружатьНормативныеВеличины",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьКлассификаторы",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьКурсыВалют",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьНовости",  Ложь);
	ПараметрыЗагрузки.Вставить("ЗагружатьКалендарь",  Истина);
	ПараметрыЗагрузки.Вставить("МесяцКалендаря",  Месяц);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("Обработки.НСИСЗапускСервисов.ЗапуститьСервисы", ПараметрыЗагрузки, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ГруппаВыполнение.Видимость = Ложь;
	ПоказатьКалендарь();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииЗагрузки(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.ГруппаВыполнение.Видимость = Ложь;
	Элементы.ФормаКоманднаяПанель.Доступность = Истина;
	
	ПоказатьКалендарь();	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКалендарь(Команда)
	
	Элементы.ГруппаВыполнение.Видимость = Истина;
	Элементы.ФормаКоманднаяПанель.Доступность = Ложь;

	ОперацияЗагрузки = ВыполнитьЗагрузку();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииЗагрузки", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ОперацияЗагрузки, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры


&НаКлиенте
Процедура Назад(Команда)
	
	Месяц = ДобавитьМесяц(Месяц, -1);
	ПоказатьКалендарь();
	
КонецПроцедуры


&НаКлиенте
Процедура Вперед(Команда)
	
	Месяц = ДобавитьМесяц(Месяц, 1);
	ПоказатьКалендарь();
	
КонецПроцедуры

&НаСервере
Процедура ПоказыватьПриНачалеРаботыПриИзмененииНаСервере()
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("RDI",
				"RDI_КалендарьБухгалтераЗапуск",
				ПоказыватьПриНачалеРаботы);
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьПриНачалеРаботыПриИзменении(Элемент)
	ПоказыватьПриНачалеРаботыПриИзмененииНаСервере();
КонецПроцедуры
