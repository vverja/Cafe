
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	//установить заголовок с учетом вылюты управленческого учета
	Элементы.Потенциал.Заголовок = НСтр("ru='Потенциал (%Валюта%)';uk='Потенціал (%Валюта%)'");
	Элементы.Потенциал.Заголовок = СтрЗаменить(Элементы.Потенциал.Заголовок, "%Валюта%",Константы.ВалютаУправленческогоУчета.Получить());

	//загрузить список документов
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	&ПервичныйСпрос КАК Документ,
		|	1 КАК ДПОРЯДОК,
		|	ВЫБОР
		|		КОГДА СделкиСКлиентами.ВалютаПервичногоСпроса = КурсыВалютыУпр.Валюта
		|			ТОГДА Документ.СуммаДокумента
		|		ИНАЧЕ Документ.СуммаДокумента * ЕСТЬNULL(КурсыВалютыДокумента.Курс, 1) * ЕСТЬNULL(КурсыВалютыУпр.Кратность, 1) / (ЕСТЬNULL(КурсыВалютыУпр.Курс, 1) * ЕСТЬNULL(КурсыВалютыДокумента.Кратность, 1))
		|	КОНЕЦ КАК Потенциал,
		|	"""" КАК Статус
		|ИЗ
		|	Справочник.СделкиСКлиентами КАК СделкиСКлиентами
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			СУММА(СделкиСКлиентамиПервичныйСпрос.Сумма) КАК СуммаДокумента,
		|			СделкиСКлиентамиПервичныйСпрос.Ссылка КАК Ссылка
		|		ИЗ
		|			Справочник.СделкиСКлиентами.ПервичныйСпрос КАК СделкиСКлиентамиПервичныйСпрос
		|		
		|		СГРУППИРОВАТЬ ПО
		|			СделкиСКлиентамиПервичныйСпрос.Ссылка) КАК Документ
		|		ПО СделкиСКлиентами.Ссылка = Документ.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних КАК КурсыВалютыДокумента
		|		ПО СделкиСКлиентами.ВалютаПервичногоСпроса = КурсыВалютыДокумента.Валюта,
		|	РегистрСведений.КурсыВалют.СрезПоследних(
		|			,
		|			Валюта В
		|				(ВЫБРАТЬ
		|					Константы.ВалютаУправленческогоУчета
		|				ИЗ
		|					Константы КАК Константы)) КАК КурсыВалютыУпр
		|ГДЕ
		|	СделкиСКлиентами.Ссылка = &ОтборДокумента
		|	И Документ.СуммаДокумента > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Документ.Ссылка,
		|	3,
		|	ВЫБОР
		|		КОГДА Документ.Валюта = КурсыВалютыУпр.Валюта
		|			ТОГДА Документ.СуммаДокумента
		|		ИНАЧЕ Документ.СуммаДокумента * ЕСТЬNULL(КурсыВалютыДокумента.Курс, 1) * ЕСТЬNULL(КурсыВалютыУпр.Кратность, 1) / (ЕСТЬNULL(КурсыВалютыУпр.Курс, 1) * ЕСТЬNULL(КурсыВалютыДокумента.Кратность, 1))
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(Документ.Статус)
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(
		|			,
		|			Валюта В
		|				(ВЫБРАТЬ
		|					Константы.ВалютаУправленческогоУчета
		|				ИЗ
		|					Константы КАК Константы)) КАК КурсыВалютыУпр,
		|	Справочник.СделкиСКлиентами КАК СделкиСКлиентами
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияСКлиентами КАК Документ
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних КАК КурсыВалютыДокумента
		|			ПО Документ.Валюта = КурсыВалютыДокумента.Валюта
		|		ПО СделкиСКлиентами.СоглашениеСКлиентом = Документ.Ссылка
		|ГДЕ
		|	Документ.ПометкаУдаления = ЛОЖЬ
		|	И СделкиСКлиентами.Ссылка = &ОтборДокумента
		|	И Документ.СуммаДокумента > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Документ.Ссылка,
		|	2,
		|	ВЫБОР
		|		КОГДА Документ.Валюта = КурсыВалютыУпр.Валюта
		|			ТОГДА Документ.СуммаДокумента
		|		ИНАЧЕ Документ.СуммаДокумента * ЕСТЬNULL(КурсыВалютыДокумента.Курс, 1) * ЕСТЬNULL(КурсыВалютыУпр.Кратность, 1) / (ЕСТЬNULL(КурсыВалютыУпр.Курс, 1) * ЕСТЬNULL(КурсыВалютыДокумента.Кратность, 1))
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(Документ.Статус)
		|ИЗ
		|	Документ.КоммерческоеПредложениеКлиенту КАК Документ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних КАК КурсыВалютыДокумента
		|		ПО Документ.Валюта = КурсыВалютыДокумента.Валюта,
		|	РегистрСведений.КурсыВалют.СрезПоследних(
		|			,
		|			Валюта В
		|				(ВЫБРАТЬ
		|					Константы.ВалютаУправленческогоУчета
		|				ИЗ
		|					Константы КАК Константы)) КАК КурсыВалютыУпр
		|ГДЕ
		|	Документ.ПометкаУдаления = ЛОЖЬ
		|	И Документ.Сделка = &ОтборДокумента
		|	И Документ.СуммаДокумента > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Документ.Ссылка,
		|	4,
		|	ВЫБОР
		|		КОГДА Документ.Валюта = КурсыВалютыУпр.Валюта
		|			ТОГДА Документ.СуммаДокумента
		|		ИНАЧЕ Документ.СуммаДокумента * ЕСТЬNULL(КурсыВалютыДокумента.Курс, 1) * ЕСТЬNULL(КурсыВалютыУпр.Кратность, 1) / (ЕСТЬNULL(КурсыВалютыУпр.Курс, 1) * ЕСТЬNULL(КурсыВалютыДокумента.Кратность, 1))
		|	КОНЕЦ,
		|	ПРЕДСТАВЛЕНИЕ(Документ.Статус)
		|ИЗ
		|	Документ.ЗаказКлиента КАК Документ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних КАК КурсыВалютыДокумента
		|		ПО Документ.Валюта = КурсыВалютыДокумента.Валюта,
		|	РегистрСведений.КурсыВалют.СрезПоследних(
		|			,
		|			Валюта В
		|				(ВЫБРАТЬ
		|					Константы.ВалютаУправленческогоУчета
		|				ИЗ
		|					Константы КАК Константы)) КАК КурсыВалютыУпр
		|ГДЕ
		|	Документ.ПометкаУдаления = ЛОЖЬ
		|	И Документ.Сделка = &ОтборДокумента
		|	И Документ.СуммаДокумента > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДПОРЯДОК");
	Запрос.УстановитьПараметр("ОтборДокумента", Параметры.Ключ);
	Запрос.УстановитьПараметр("ПервичныйСпрос", НСтр("ru='Первичный спрос';uk='Первинний попит'"));
	СписокДокументов = Запрос.Выполнить().Выгрузить();
	СписокДокументов.Колонки.Добавить("Выбран");
	ЗначениеВРеквизитФормы(СписокДокументов, "Список");

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура ВыбранПриИзменении(Элемент)

	СуммаПотенциала = 0;

	Для Каждого Элемент Из Список Цикл
		Если Элемент.Выбран Тогда
			СуммаПотенциала = СуммаПотенциала + Элемент.Потенциал;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьВсе(Команда)

	СуммаПотенциала = 0;

	Для Каждого Элемент Из Список Цикл
		Элемент.Выбран = Истина;
		СуммаПотенциала = СуммаПотенциала + Элемент.Потенциал;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВыбор(Команда)

	СуммаПотенциала = 0;

	Для Каждого Элемент Из Список Цикл
		Элемент.Выбран = Ложь;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьПотенциал(Команда)

	Закрыть(СуммаПотенциала);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти
