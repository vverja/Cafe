
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
		
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	Если ТекущийВариантИнтерфейсаКлиентскогоПриложения() = ВариантИнтерфейсаКлиентскогоПриложения.Версия8_2 Тогда
		Элементы.ГруппаИтоги.ЦветФона = Новый Цвет();
	КонецЕсли;

	// Обработчик механизма "ВерсионированиеОбъектов"
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	
	// Обработчик подсистемы "Внешние обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	
	// Обработчик механизма "Свойства"
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтаФорма,
		Новый Структура("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты"));
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать
	
	ДоставкаТоваровКлиентСервер.ЗаполнитьСписокВыбораПоляВремени(Элементы.ВремяС);
	ДоставкаТоваровКлиентСервер.ЗаполнитьСписокВыбораПоляВремени(Элементы.ВремяПо);
	ДоставкаТоваров.УстановитьДоступностьАдресовДоставки(ЭтаФорма.Элементы);
	
	ВводНаОсновании.ПриСозданииНаСервере(ЭтаФорма, Элементы.ПодменюСоздатьНаОсновании);
	
	МенюОтчеты.ПриСозданииНаСервере(ЭтаФорма, Элементы.ПодменюОтчеты);
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборот.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ИнтеграцияС1СДокументооборотом

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);

	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	НаборЗаписей = РегистрыСведений.ТоварыКДоставке.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ЗаданиеНаПеревозку.Установить(Объект.Ссылка);
	НаборЗаписей.Прочитать();
	ТоварыКДоставке.Загрузить(НаборЗаписей.Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтаФорма, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ТоварыКДоставке", ТоварыКДоставке.Выгрузить());
	
	// Обработчик механизма "Свойства"
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);

	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ТранспортнаяНакладная" Тогда
		ЗаполнитьПризнакНаличияТранспортнойНакладнойПоКаждомуАдресуДоставки();	
	КонецЕсли; 	
	
	// Обработчик механизма "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтаФорма, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ИсточникВыбора.ИмяФормы = "Обработка.РабочееМестоМенеджераПоДоставке.Форма.ФормаВыбораРаспоряжений" Тогда
		РазбитьПунктДоставки(ВыбранноеЗначение);
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьВремяБезДаты();

	ЗаполнитьПризнакНаличияТранспортнойНакладнойПоКаждомуАдресуДоставки();	
	ЗаполнитьСлужебныеРеквизитыРаспоряжений();
	
	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	Оповестить("Запись_ЗаданиеНаПеревозку", Неопределено, Объект.Ссылка);
	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
		
	Если Объект.Распоряжения.Количество() > 0 Тогда
		Если Склад <> Объект.Склад Тогда		
			Ответ = Неопределено;
		
			ПоказатьВопрос(Новый ОписаниеОповещения("СкладПриИзмененииЗавершение", ЭтотОбъект),
				НСтр("ru='Списки будут очищены. Продолжить?';uk='Списки будуть очищені. Продовжити?'"), РежимДиалогаВопрос.ДаНет);
            Возврат;
		Иначе
			Возврат;	
		КонецЕсли;
	Иначе 	
		
		Склад = Объект.Склад;
		
	КонецЕсли;
	
	ПриИзмененииСкладаСервер();
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Ответ = РезультатВопроса;
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        
        Объект.Склад = Склад;
        Возврат;
        
    Иначе
        
        Склад = Объект.Склад;
        
    КонецЕсли;
    
    ПриИзмененииСкладаСервер();

КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	
	ТранспортноеСредствоПриИзмененииНаСервере();
	ОбновитьЗаполненностьНаКлиенте();

КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Ссылка)
		И Объект.Операция = ПредопределенноеЗначение("Перечисление.ВидыДоставки.СоСклада")
		И ЕстьОрдерныеСклады
		И СтатусБыл <> Объект.Статус
		И СтатусБыл <> ПредопределенноеЗначение("Перечисление.СтатусыЗаданийНаПеревозку.Закрыто")
		И СтатусБыл <> ПредопределенноеЗначение("Перечисление.СтатусыЗаданийНаПеревозку.Отправлено")
		И (Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаданийНаПеревозку.Закрыто")
			ИЛИ Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаданийНаПеревозку.Отправлено")) Тогда
		
		Режим = Новый СписокЗначений;
		Режим.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Перезаполнить';uk='Перезаповнити'"));
		Режим.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Не перезаполнять';uk='Не перезаповнювати'"));
		Текст = НСтр("ru='Перезаполнить задание по связанным расходным ордерам на товары?';uk='Перезаповнити завдання за пов''язаними видатковими ордерами на товари?'");
		ПоказатьВопрос(Новый ОписаниеОповещения("ПослеВопросаОПерезаполненииПоОрдерам", ЭтотОбъект), Текст, Режим);
		
	Иначе
		
		ТекстОповещения = ПриИзмененииСтатусаИПерезаполнении();
		ОповеститьОбИзменениях(ТекстОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаВремяРейсаПланСПриИзменении(Элемент)
	
	Для Каждого СтрокаМаршрута Из Объект.Маршрут Цикл
		
		Если СтрокаМаршрута.ВремяС = '00010101'
			ИЛИ СтрокаМаршрута.ВремяС < Объект.ДатаВремяРейсаПланС Тогда
			СтрокаМаршрута.ВремяС = Объект.ДатаВремяРейсаПланС;
			СтрокаМаршрута.ВремяСБезДаты = Дата(1,1,1,Час(СтрокаМаршрута.ВремяС),Минута(СтрокаМаршрута.ВремяС),0);
			Если СтрокаМаршрута.ВремяС > СтрокаМаршрута.ВремяПо Тогда
				СтрокаМаршрута.ВремяПо = СтрокаМаршрута.ВремяС;
				СтрокаМаршрута.ВремяСБезДаты = Дата(1,1,1,Час(СтрокаМаршрута.ВремяПо),Минута(СтрокаМаршрута.ВремяПо),0);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаданиеВыполняетПриИзменении(Элемент)
	
	НастроитьПоТипуИсполнителя()
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикПриИзменении(Элемент)
	ПеревозчикПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВодительПриИзменении(Элемент)
	ВодительПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМаршрут

&НаКлиенте
Процедура МаршрутПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("МаршрутПриАктивизацииСтрокиОбработчикОжидания", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПередУдалением(Элемент, Отказ)
		
	ВыделенныеСтроки = Элементы.Маршрут.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество()>0 Тогда
		Модифицированность = Истина;
		МаршрутПередУдалениемНаСервере(Элементы.Маршрут.ВыделенныеСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
 
КонецПроцедуры

&НаКлиенте
Процедура МаршрутДоставленоПриИзменении(Элемент)
	
	Если Элементы.Маршрут.ТекущиеДанные.Доставлено Тогда
				
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("КлючСвязи",Элементы.Маршрут.ТекущиеДанные.КлючСвязи);
		СтруктураПоиска.Вставить("Доставлено", Ложь);
		
		Для Каждого СтрокаРаспоряжений Из Объект.Распоряжения.НайтиСтроки(СтруктураПоиска) Цикл
			СтрокаРаспоряжений.Доставлено = Истина; 	
		КонецЦикла;	
		
	Иначе 
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("КлючСвязи",Элементы.Маршрут.ТекущиеДанные.КлючСвязи);
		СтруктураПоиска.Вставить("Доставлено", Истина);
		
		Для Каждого СтрокаРаспоряжений Из Объект.Распоряжения.НайтиСтроки(СтруктураПоиска) Цикл
			СтрокаРаспоряжений.Доставлено = Ложь; 	
		КонецЦикла;	
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура АдресПриИзменении(Элемент)
	
	ИмяРеквизитаАдресаДоставки = "Адрес";
	ТекущиеДанные = Элементы.Маршрут.ТекущиеДанные;
	
	ДоставкаТоваровКлиент.ПриИзмененииПредставленияАдреса(
	    Элемент,
		ТекущиеДанные[ИмяРеквизитаАдресаДоставки],
		ТекущиеДанные[ИмяРеквизитаАдресаДоставки + "ЗначенияПолей"]);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОчистка(Элемент, СтандартнаяОбработка)
	
	АдресПриИзменении(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИмяРеквизитаАдресаДоставки = "Адрес";
	ТекущиеДанные = Элементы.Маршрут.ТекущиеДанные;
	
	ДоставкаТоваровКлиент.ОткрытьФормуВыбораАдресаИОбработатьРезультат(
	    Элемент,
		ТекущиеДанные,
		ИмяРеквизитаАдресаДоставки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутВремяСПриИзменении(Элемент)
	
	ПриИзмененииВремяС();
	ОтсортироватьСписокМаршрута();
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутДатаСПриИзменении(Элемент)
	
	ПриИзмененииВремяС();
	ОтсортироватьСписокМаршрута();
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутВремяПоПриИзменении(Элемент)

	ПриИзмененииВремяПо();
	ОтсортироватьСписокМаршрута();
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутДатаПоПриИзменении(Элемент)
	
	ПриИзмененииВремяПо();
	ОтсортироватьСписокМаршрута();
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутВесОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ВесСтроки = Элементы.Маршрут.ТекущиеДанные.Вес;
	Объект.Вес = Объект.Вес + (ВыбранноеЗначение - ВесСтроки)*КоэффициентПересчетаВТонны;
	ОбновитьЗаполненностьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура МаршрутОбъемОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбъемСтроки = Элементы.Маршрут.ТекущиеДанные.Объем;
	Объект.Объем = Объект.Объем + (ВыбранноеЗначение - ОбъемСтроки)*КоэффициентПересчетаВКубическиеМетры;
	ОбновитьЗаполненностьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура МаршрутВесПриИзменении(Элемент)
	ОбновитьИтоговыйВесОбъемЗаполненность();
КонецПроцедуры

&НаКлиенте
Процедура МаршрутОбъемПриИзменении(Элемент)
	ОбновитьИтоговыйВесОбъемЗаполненность();
КонецПроцедуры

&НаКлиенте
Процедура МаршрутВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "МаршрутТранспортнаяНакладнаяОформлена" Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗаданиеНаПеревозку", Объект.Ссылка);
		ПараметрыФормы.Вставить("АдресДоставки", Элемент.ТекущиеДанные.Адрес);
		ОткрытьФорму("Документ.ТранспортнаяНакладная.Форма.СозданныеТранспортныеНакладные", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРаспоряжения

&НаКлиенте
Процедура РаспоряженияПередУдалением(Элемент, Отказ)
	
	РаспоряженияПередУдалениемСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура РаспоряженияПослеУдаления(Элемент)
	РаспоряженияПослеУдаленияСервер();
КонецПроцедуры

&НаКлиенте
Процедура РаспоряженияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	Если Не ЗначениеЗаполнено(Склад) Тогда
		ПоказатьПредупреждение(,НСтр("ru='Перед подбором распоряжений необходимо указать склад.';uk='Перед підбором розпоряджень необхідно вказати склад.'"));
		Возврат;
	КонецЕсли;
	ОткрытьФормуПодбораРаспоряжений();
	
КонецПроцедуры

&НаКлиенте
Процедура РаспоряженияДоставленоПриИзменении(Элемент)
	
	РаспоряженияДоставленоПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура РаспоряженияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущаяСтрока = Объект.Распоряжения.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	Если Поле = Элементы.РаспоряженияДоставляетсяПолностью Тогда
		Если Не ТекущаяСтрока.ЭтоПоручениеЭкспедитору Тогда
			ОткрытьПодборТоваровКДоставке();
		КонецЕсли;
	ИначеЕсли Поле = Элементы.РаспоряженияРаспоряжение Тогда
		ПоказатьЗначение(,Элементы.Распоряжения.ТекущиеДанные.Распоряжение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьСклады(Команда)
	
	ПараметрыФормы = Новый Структура("МассивДокументов, ЗаголовокФормы",
		СкладыПогрузки.ВыгрузитьЗначения(), НСтр("ru='Склады погрузки';uk='Склади завантаження'"));
	ОткрытьФорму("ОбщаяФорма.СписокПроизвольныхОбъектовУП",
		ПараметрыФормы,
		ЭтаФорма,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РазнестиПоВремени(Команда)
	
	ОчиститьСообщения();
	
	Если Элементы.Маршрут.ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(,НСтр("ru='Для выполнения команды требуется выбрать строку табличной части.';uk='Для виконання команди потрібно вибрати рядок табличної частини.'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ПроверитьРазбитьПунктДоставкиСервер() Тогда
			
		АдресРаспоряжений = ПоместитьРаспоряженияВХранилище(Элементы.Маршрут.ТекущиеДанные.КлючСвязи);
		ПараметрыФормы = Новый Структура("АдресРаспоряжений", АдресРаспоряжений);
		ОткрытьФорму("Обработка.РабочееМестоМенеджераПоДоставке.Форма.ФормаВыбораРаспоряжений", ПараметрыФормы, ЭтаФорма);	
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъединитьПоВремени(Команда)
	
	ОчиститьСообщения();	

	ВыделенныеСтроки = Элементы.Маршрут.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() < 2 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Для объединения необходимо выделить хотя бы 2 строки.';uk='Для об''єднання необхідно виділити хоча б 2 рядки.'"));
		Возврат;
	КонецЕсли;
	
	СтруктураВозврата = ПроверитьПередОбъединением(ВыделенныеСтроки);
	
	Если СтруктураВозврата.РазныеАдреса Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не удалось объединить выделенные строки: выберите строки с одинаковыми адресами.';uk='Не вдалося об''єднати виділені рядки: виберіть рядки з однаковими адресами.'"));
		Возврат;	
	ИначеЕсли СтруктураВозврата.РазныеДанные Тогда
		СтрВопроса = НСтр("ru='Выделенные строки содержат несколько значений данных (время, доп. информация). Объединение строк приведет к потере всех значений, кроме первой строки. Продолжить?';uk='Виділені рядки містять кілька значень даних (час, дод. інформація). Об''єднання рядків призведе до втрати всіх значень, крім першого рядка. Продовжити?'");
		Ответ = Неопределено;

		ПоказатьВопрос(Новый ОписаниеОповещения("ОбъединитьПоВремениЗавершение",
			ЭтотОбъект, Новый Структура("ВыделенныеСтроки, СтруктураВозврата", ВыделенныеСтроки, СтруктураВозврата)),
			СтрВопроса, РежимДиалогаВопрос.ДаНет);
        Возврат;
	КонецЕсли;
		
	 ОбъединитьПоВремениФрагмент(ВыделенныеСтроки, СтруктураВозврата);
КонецПроцедуры

&НаКлиенте
Процедура ОбъединитьПоВремениЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    ВыделенныеСтроки = ДополнительныеПараметры.ВыделенныеСтроки;
    СтруктураВозврата = ДополнительныеПараметры.СтруктураВозврата;
    
    
    Ответ = РезультатВопроса;
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    ОбъединитьПоВремениФрагмент(ВыделенныеСтроки, СтруктураВозврата);

КонецПроцедуры

&НаКлиенте
Процедура ОбъединитьПоВремениФрагмент(Знач ВыделенныеСтроки, Знач СтруктураВозврата)
    
    ОбъединитьСтрокиМаршрута(ВыделенныеСтроки,СтруктураВозврата.МассивКодовСтрок)

КонецПроцедуры

&НаКлиенте
Процедура МаршрутДоставленоУстановитьВсе(Команда)
		
	Для Каждого СтрокаРаспоряжений Из Объект.Распоряжения Цикл
		СтрокаРаспоряжений.Доставлено = Истина; 	
	КонецЦикла;	
	
	Для Каждого СтрокаМаршрута Из Объект.Маршрут Цикл
		СтрокаМаршрута.Доставлено = Истина; 	
	КонецЦикла;
			
КонецПроцедуры

&НаКлиенте
Процедура МаршрутДоставленоСнятьВсе(Команда)
		
	Для Каждого СтрокаРаспоряжений Из Объект.Распоряжения Цикл
		СтрокаРаспоряжений.Доставлено = Ложь; 	
	КонецЦикла;	
	
	Для Каждого СтрокаМаршрута Из Объект.Маршрут Цикл
		СтрокаМаршрута.Доставлено = Ложь; 	
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОформитьТранспортныеНакладные(Команда)
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени(
		"Документ.ЗаданиеНаПеревозку.ФормаДокумента.Команда.ОформитьТранспортныеНакладные");
	
	ОчиститьСообщения();
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка)
		ИЛИ НЕ Объект.Проведен
		ИЛИ Модифицированность Тогда 
		
		ТекстВопроса = НСтр("ru='Ввод транспортных накладных возможен только после проведения документа.';uk='Введення транспортних накладних можливе тільки після проведення документа.'");
		СписокКнопок = Новый СписокЗначений;
		СписокКнопок.Добавить(КодВозвратаДиалога.ОК,     НСтр("ru='Провести';uk='Провести'"));
		СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru='Отмена';uk='Відмінити'"));
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ОформитьТранспортныеНакладныеОбработкаОтвета", ЭтотОбъект),
			ТекстВопроса, СписокКнопок);
		Возврат;
		
	КонецЕсли;
	
	ОформитьТранспортныеНакладныеЗавершение();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоРасходнымОрдерам(Команда)
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени(
		"Документ.ЗаданиеНаПеревозку.ФормаДокумента.Команда.ЗаполнитьПоРасходнымОрдерам");
	
	Режим = Новый СписокЗначений;
	Режим.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Перезаполнить';uk='Перезаповнити'"));
	Режим.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Не перезаполнять';uk='Не перезаповнювати'"));
	Текст = НСтр("ru='Перезаполнить задание по связанным расходным ордерам на товары?';uk='Перезаповнити завдання за пов''язаними видатковими ордерами на товари?'");
	ПоказатьВопрос(Новый ОписаниеОповещения("ПослеВопросаОПерезаполненииПоОрдерам", ЭтотОбъект), Текст, Режим);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТоварыКДоставке(Команда)
	Если Элементы.Распоряжения.ТекущаяСтрока <> Неопределено Тогда
		ОткрытьПодборТоваровКДоставке();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыТТН(Команда)
	
	ПараметрыТТН = Новый Структура;
	ПараметрыТТН.Вставить("ЗаданиеНаПеревозку");
	ПараметрыТТН.Вставить("ТранспортноеСредство");
	ПараметрыТТН.Вставить("ВодительФИО");
	ПараметрыТТН.Вставить("УдостоверениеСерия");
	ПараметрыТТН.Вставить("УдостоверениеНомер");
	ПараметрыТТН.Вставить("АвтомобильГосударственныйНомер");
	ПараметрыТТН.Вставить("АвтомобильМарка");	
	ПараметрыТТН.Вставить("ВидПеревозки");
	ПараметрыТТН.Вставить("АвтомобильТип");
	ПараметрыТТН.Вставить("АвтомобильВместимостьВКубическихМетрах");
	ПараметрыТТН.Вставить("АвтомобильГрузоподъемностьВТоннах");
	ПараметрыТТН.Вставить("ЛицензионнаяКарточкаСерия");
	ПараметрыТТН.Вставить("ЛицензионнаяКарточкаНомер");
	ПараметрыТТН.Вставить("ЛицензионнаяКарточкаВид");
	ПараметрыТТН.Вставить("ЛицензионнаяКарточкаРегистрационныйНомер");
	ПараметрыТТН.Вставить("Прицеп");
	ПараметрыТТН.Вставить("ГосударственныйНомерПрицепа");
	Если Объект.ЗаданиеВыполняет = ПредопределенноеЗначение("Перечисление.ТипыИсполнителейЗаданийНаПеревозку.Перевозчик") 
		И ЗначениеЗаполнено(Объект.Перевозчик) Тогда 
		ПараметрыТТН.Вставить("Перевозчик");
	КонецЕсли;	
	ЗаполнитьЗначенияСвойств(ПараметрыТТН, Объект);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("РеквизитыТТНЗавершение", ЭтаФорма);
	
	ОткрытьФорму("Документ.ЗаданиеНаПеревозку.Форма.РеквизитыТТН", 
				ПараметрыТТН, 
				ЭтаФорма,,,,
				ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств(Команда)
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтаФорма);
	
КонецПроцедуры

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуОтчет(Команда)
	
	МенюОтчетыКлиент.ВыполнитьПодключаемуюКомандуОтчет(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуСоздатьНаОсновании(Команда)
	
	ВводНаОснованииКлиент.ВыполнитьПодключаемуюКомандуСоздатьНаОсновании(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры
//Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
  Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
    РезультатВыполнения = Неопределено;
    ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
    ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
  КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЗаполненностьПоВесу.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЗаполненностьПоВесу");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
	ОтборЭлемента.ПравоеЗначение = 1;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.SpecialTextColor);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЗаполненностьПоОбъему.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЗаполненностьПоОбъему");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
	ОтборЭлемента.ПравоеЗначение = 1;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.SpecialTextColor);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТранспортноеСредство.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ЗаданиеВыполняет");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыИсполнителейЗаданийНаПеревозку.Перевозчик;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Статус");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыЗаданийНаПеревозку.Формируется;

	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНеЗаполненного", Ложь);
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.РаспоряженияДоставляетсяПолностью.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Распоряжения.ЭтоПоручениеЭкспедитору");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.РаспоряженияСклад.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Распоряжения.ЭтоПоручениеЭкспедитору");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаСервере
 Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма);
	
 КонецПроцедуры

 // СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
  ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

#Область ТабличнаяЧастьРаспоряжения

&НаСервере
Процедура РаспоряженияПередУдалениемСервер(Знач ИДСтроки = Неопределено)
	
	Если ИДСтроки = Неопределено Тогда
		ВыделенныеСтроки = Элементы.Распоряжения.ВыделенныеСтроки;
	Иначе
		ВыделенныеСтроки = Новый Массив;
		ВыделенныеСтроки.Добавить(ИДСтроки);
	КонецЕсли;
	МассивУдаляемыхРаспоряжений = Новый Массив;
	СтруктураПоиска = Новый Структура("Распоряжение, Склад");
	Для Каждого Идентификатор Из ВыделенныеСтроки Цикл
		СтрокаРаспоряжений = Объект.Распоряжения.НайтиПоИдентификатору(Идентификатор);	
		МассивУдаляемыхРаспоряжений.Добавить(СтрокаРаспоряжений.Распоряжение);
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаРаспоряжений);
		НайденныеСтроки = ТоварыКДоставке.НайтиСтроки(СтруктураПоиска);
		Для Каждого СтрокаТовары Из НайденныеСтроки Цикл
			ТоварыКДоставке.Удалить(СтрокаТовары);
		КонецЦикла;
	КонецЦикла;
	
	КлючСвязи = СтрокаРаспоряжений.КлючСвязи;
	СтрокаМаршрута = СтрокаМаршрутаПоКлючуСвязи(КлючСвязи);
	
	Документы.ТранспортнаяНакладная.АктуализироватьТранспортныеНакладныеИзЗаданияНаПеревозку(
		МассивУдаляемыхРаспоряжений, Объект.Ссылка, СтрокаМаршрута.НомерСтроки);
	
	//Удаление строки маршрута, если не остается распоряжений по данному адресу доставки
	Распоряжения = Объект.Распоряжения.НайтиСтроки(Новый Структура("КлючСвязи",КлючСвязи));
	Если (Распоряжения.Количество() = ВыделенныеСтроки.Количество()) Тогда
		Объект.Маршрут.Удалить(СтрокаМаршрута);
	Иначе
		ОбновитьВесИОбъемСтрокиМаршрута(СтрокаМаршрута);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПодбораРаспоряжений()
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Склад",Склад);
	МаршрутТекущаяСтрока = Элементы.Маршрут.ТекущаяСтрока;
	Если МаршрутТекущаяСтрока <> Неопределено Тогда
		ПараметрыФормы.Вставить("Зона",Элементы.Маршрут.ТекущиеДанные.Зона);
	КонецЕсли;
	ПараметрыФормы.Вставить("ВидДоставки", Объект.Операция);
	ПараметрыФормы.Вставить("ЗаданиеНаПеревозку", Объект.Ссылка);
	ПараметрыФормы.Вставить("ОтборПоТипуИсполнителей", Объект.ЗаданиеВыполняет);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодбораРаспоряжений", ЭтотОбъект);
	ОткрытьФорму("Документ.ЗаданиеНаПеревозку.Форма.ФормаПодбораРаспоряжений", ПараметрыФормы,
		ЭтаФорма,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодбораРаспоряжений(СтруктураВозврата, ДополнительныеПараметры) Экспорт
	
	Если СтруктураВозврата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПослеПодбораРаспоряженийСервер(СтруктураВозврата);
	
КонецПроцедуры

&НаСервере
Процедура ПослеПодбораРаспоряженийСервер(СтруктураВозврата)
	
	Распоряжения = ПолучитьИзВременногоХранилища(СтруктураВозврата.АдресРаспоряжений);
	Товары = ПолучитьИзВременногоХранилища(СтруктураВозврата.АдресТоваров);
	
	Запрос = Новый Запрос(ТекстЗапросаИзмененныеДобавленныеСтрокиРаспоряжений());
	Запрос.УстановитьПараметр("ИсходнаяТаблица",Объект.Распоряжения.Выгрузить());
	Запрос.УстановитьПараметр("ПодобранныеРаспоряжения", Распоряжения);
	Запрос.УстановитьПараметр("ИсходныеТовары", ТоварыКДоставке.Выгрузить());
	Запрос.УстановитьПараметр("ПодобранныеТовары", Товары);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Модифицированность = Не РезультатЗапроса[5].Пустой()
		Или Не РезультатЗапроса[6].Пустой()
		Или Не РезультатЗапроса[7].Пустой();
	
	ИзмененныеДобавленныеСтроки = Новый Массив;
	СтрокаМаршрута = Неопределено;
	
	СтруктураПоиска = Новый Структура("Распоряжение,Склад");
	
	ВыборкаИзмененныеСтроки = РезультатЗапроса[5].Выбрать();
	
	Пока ВыборкаИзмененныеСтроки.Следующий() Цикл
		
		СтрокаРаспоряжений = Объект.Распоряжения[ВыборкаИзмененныеСтроки.НомерСтроки - 1];
		ИзменениеВес = ВыборкаИзмененныеСтроки.Вес - СтрокаРаспоряжений.Вес;
		ИзменениеОбъем = ВыборкаИзмененныеСтроки.Объем - СтрокаРаспоряжений.Объем;
		ЗаполнитьЗначенияСвойств(СтрокаРаспоряжений, ВыборкаИзмененныеСтроки);
		ИзменитьВесОбъемМаршрута(СтрокаРаспоряжений.КлючСвязи, ИзменениеВес, ИзменениеОбъем);
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаРаспоряжений);
		// Товары будут заново добавлены, удалим строки до изменения
		Для Каждого СтрокаТовары Из ТоварыКДоставке.НайтиСтроки(СтруктураПоиска) Цикл
			ТоварыКДоставке.Удалить(СтрокаТовары);
		КонецЦикла;
		ИзмененныеДобавленныеСтроки.Добавить(СтрокаРаспоряжений);
	КонецЦикла;
	
	ВыборкаДобавляемыеСтроки = РезультатЗапроса[6].Выбрать();
	Пока ВыборкаДобавляемыеСтроки.Следующий() Цикл
		
		СтрокаРаспоряжений = Объект.Распоряжения.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРаспоряжений, ВыборкаДобавляемыеСтроки);
		
		СтрокаМаршрута = ДоставкаТоваров.ДобавитьИзменитьПунктПоРеквизитамДоставки(
			Объект.Маршрут, ВыборкаДобавляемыеСтроки, Объект.ДатаВремяРейсаПланС);
		
		СтрокаРаспоряжений.КлючСвязи = СтрокаМаршрута.КлючСвязи;
		
		ИзмененныеДобавленныеСтроки.Добавить(СтрокаРаспоряжений);
		
	КонецЦикла;
	
	ТоварыИзмененныхИДобавленныхРаспоряжений = РезультатЗапроса[7].Выгрузить();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТоварыИзмененныхИДобавленныхРаспоряжений, ТоварыКДоставке);
	
	ОбновитьИтоговыйВесОбъемЗаполненность();
	
	ОбновитьСкладыПогрузки();
	
	ЗаполнитьПризнакНаличияТранспортнойНакладнойПоКаждомуАдресуДоставки();
	
	ОтборСтрокКЗаполнению = Новый Структура("ДоставляетсяПолностью", Ложь);
	ДоставкаТоваров.ЗаполнитьПризнакДоставляетсяПолностью(ТоварыИзмененныхИДобавленныхРаспоряжений,
		Объект.Распоряжения, ИзмененныеДобавленныеСтроки);
	
	Если СтрокаМаршрута <> Неопределено Тогда
		Элементы.Маршрут.ТекущаяСтрока = СтрокаМаршрута.ПолучитьИдентификатор();
		Элементы.Распоряжения.ТекущаяСтрока = СтрокаРаспоряжений.ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ТекстЗапросаИзмененныеДобавленныеСтрокиРаспоряжений()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ИсходнаяТаблица.НомерСтроки,
	|	ИсходнаяТаблица.Распоряжение,
	|	ИсходнаяТаблица.Склад,
	|	ИсходнаяТаблица.Вес,
	|	ИсходнаяТаблица.Объем
	|ПОМЕСТИТЬ ИсходнаяТаблица
	|ИЗ
	|	&ИсходнаяТаблица КАК ИсходнаяТаблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодобранныеРаспоряжения.Распоряжение,
	|	ПодобранныеРаспоряжения.Вес,
	|	ПодобранныеРаспоряжения.Объем,
	|	ПодобранныеРаспоряжения.Перевозчик,
	|	ПодобранныеРаспоряжения.ПолучательОтправитель,
	|	ПодобранныеРаспоряжения.ВремяС,
	|	ПодобранныеРаспоряжения.ВремяПо,
	|	ПодобранныеРаспоряжения.ДополнительнаяИнформация,
	|	ПодобранныеРаспоряжения.Склад,
	|	ПодобранныеРаспоряжения.Адрес,
	|	ПодобранныеРаспоряжения.АдресЗначенияПолей,
	|	ПодобранныеРаспоряжения.Зона
	|ПОМЕСТИТЬ ПодобранныеРаспоряжения
	|ИЗ
	|	&ПодобранныеРаспоряжения КАК ПодобранныеРаспоряжения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеТовары.Распоряжение,
	|	ИсходныеТовары.Склад,
	|	ИсходныеТовары.ПолучательОтправитель,
	|	ИсходныеТовары.Номенклатура,
	|	ИсходныеТовары.Характеристика,
	|	ИсходныеТовары.Назначение,
	|	ИсходныеТовары.Серия,
	|	ИсходныеТовары.Количество,
	|	ИсходныеТовары.ВсеТовары
	|ПОМЕСТИТЬ ИсходныеТовары
	|ИЗ
	|	&ИсходныеТовары КАК ИсходныеТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодобранныеТовары.Распоряжение,
	|	ПодобранныеТовары.Склад,
	|	ПодобранныеТовары.ПолучательОтправитель,
	|	ПодобранныеТовары.Номенклатура,
	|	ПодобранныеТовары.Характеристика,
	|	ПодобранныеТовары.Назначение,
	|	ПодобранныеТовары.Серия,
	|	ПодобранныеТовары.Количество,
	|	ПодобранныеТовары.ВсеТовары
	|ПОМЕСТИТЬ ПодобранныеТовары
	|ИЗ
	|	&ПодобранныеТовары КАК ПодобранныеТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаспоряженияСвернутые.Распоряжение,
	|	РаспоряженияСвернутые.Склад,
	|	СУММА(РаспоряженияСвернутые.Вес) КАК Вес,
	|	СУММА(РаспоряженияСвернутые.Объем) КАК Объем,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ИсходнаяТаблица.Распоряжение ЕСТЬ NULL 
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ) КАК ИзмененнаяСтрока
	|ПОМЕСТИТЬ РаспоряженияСвернутые
	|ИЗ
	|	ПодобранныеРаспоряжения КАК РаспоряженияСвернутые
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИсходнаяТаблица КАК ИсходнаяТаблица
	|		ПО (ИсходнаяТаблица.Распоряжение = РаспоряженияСвернутые.Распоряжение)
	|			И (ИсходнаяТаблица.Склад = РаспоряженияСвернутые.Склад)
	|
	|СГРУППИРОВАТЬ ПО
	|	РаспоряженияСвернутые.Склад,
	|	РаспоряженияСвернутые.Распоряжение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИзмененныеСтроки.Вес,
	|	ИзмененныеСтроки.Объем,
	|	ИсходнаяТаблица.НомерСтроки
	|ИЗ
	|	РаспоряженияСвернутые КАК ИзмененныеСтроки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИсходнаяТаблица КАК ИсходнаяТаблица
	|		ПО (ИсходнаяТаблица.Распоряжение = ИзмененныеСтроки.Распоряжение)
	|			И (ИсходнаяТаблица.Склад = ИзмененныеСтроки.Склад)
	|ГДЕ
	|	ИзмененныеСтроки.ИзмененнаяСтрока
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДобавляемыеСтроки.Распоряжение,
	|	РаспоряженияСвернутые.Вес,
	|	ДобавляемыеСтроки.Объем,
	|	ДобавляемыеСтроки.Перевозчик,
	|	ДобавляемыеСтроки.ПолучательОтправитель,
	|	ДобавляемыеСтроки.ВремяС,
	|	ДобавляемыеСтроки.ВремяПо,
	|	ДобавляемыеСтроки.ДополнительнаяИнформация,
	|	ДобавляемыеСтроки.Склад,
	|	ДобавляемыеСтроки.Адрес,
	|	ДобавляемыеСтроки.АдресЗначенияПолей,
	|	ДобавляемыеСтроки.Зона,
	|	ДобавляемыеСтроки.Распоряжение Ссылка Документ.ПоручениеЭкспедитору КАК ЭтоПоручениеЭкспедитору
	|ИЗ
	|	ПодобранныеРаспоряжения КАК ДобавляемыеСтроки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РаспоряженияСвернутые КАК РаспоряженияСвернутые
	|		ПО (РаспоряженияСвернутые.Распоряжение = ДобавляемыеСтроки.Распоряжение)
	|			И (РаспоряженияСвернутые.Склад = ДобавляемыеСтроки.Склад)
	|			И (НЕ РаспоряженияСвернутые.ИзмененнаяСтрока)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодобранныеТовары.Распоряжение,
	|	ПодобранныеТовары.Склад,
	|	ПодобранныеТовары.ПолучательОтправитель,
	|	ПодобранныеТовары.Номенклатура,
	|	ПодобранныеТовары.Характеристика,
	|	ПодобранныеТовары.Назначение,
	|	ПодобранныеТовары.Серия,
	|	ПодобранныеТовары.Количество,
	|	ПодобранныеТовары.ВсеТовары
	|ИЗ
	|	ПодобранныеТовары КАК ПодобранныеТовары";
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Процедура РаспоряженияПослеУдаленияСервер()
	
	ЗаполнитьПризнакНаличияТранспортнойНакладнойПоКаждомуАдресуДоставки();
	ОбновитьИтоговыйВесОбъемЗаполненность();
	ОбновитьСкладыПогрузки();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыРаспоряжений()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Т.Распоряжение,
	|	Т.НомерСтроки
	|ПОМЕСТИТЬ НомераСтрокСПоручениямиЭкспедитору
	|ИЗ
	|	&Распоряжения КАК Т
	|ГДЕ
	|	Т.Распоряжение ССЫЛКА Документ.ПоручениеЭкспедитору
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.НомерСтроки
	|ИЗ
	|	НомераСтрокСПоручениямиЭкспедитору КАК Т";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Распоряжения", Объект.Распоряжения.Выгрузить(,"Распоряжение,НомерСтроки"));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Объект.Распоряжения[Выборка.НомерСтроки - 1].ЭтоПоручениеЭкспедитору = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РаспоряженияДоставленоПриИзмененииСервер()
	
	ТекущиеДанные = Объект.Распоряжения.НайтиПоИдентификатору(Элементы.Распоряжения.ТекущаяСтрока);
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("КлючСвязи", ТекущиеДанные.КлючСвязи);
	НайденныеСтрокиМаршрута = Объект.Маршрут.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтрокиМаршрута.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Доставлено Тогда
		СтруктураПоиска.Вставить("Доставлено", Ложь);
		КоличествоНедоставленныхРаспоряжений = Объект.Распоряжения.НайтиСтроки(СтруктураПоиска).Количество();
		Если КоличествоНедоставленныхРаспоряжений = 0 Тогда
			НайденныеСтрокиМаршрута[0].Доставлено = Истина;
		КонецЕсли	
	Иначе 
		НайденныеСтрокиМаршрута[0].Доставлено = Ложь;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ТабличнаяЧастьМаршрут

&НаКлиенте
Процедура МаршрутПриАктивизацииСтрокиОбработчикОжидания()
	
	СтрокаМаршрута = Элементы.Маршрут.ТекущиеДанные;
	Если СтрокаМаршрута = Неопределено тогда
		Элементы.Распоряжения.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи",NULL);
	Иначе
		Элементы.Распоряжения.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", СтрокаМаршрута.КлючСвязи);	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере 
Процедура МаршрутПередУдалениемНаСервере(Знач ВыделенныеСтроки)
	
	Для Каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		
		ТекущаяСтрока = Объект.Маршрут.НайтиПоИдентификатору(ИдентификаторСтроки);
		
		КлючСвязи = ТекущаяСтрока.КлючСвязи;
		
		Объект.Маршрут.Удалить(ТекущаяСтрока);
		
		СтруктураПоиска = Новый Структура("Распоряжение, Склад");
		НайденныеСтроки = Объект.Распоряжения.НайтиСтроки(Новый Структура("КлючСвязи",КлючСвязи));
		Для Каждого СтрокаРаспоряжений Из НайденныеСтроки Цикл
			Объект.Распоряжения.Удалить(СтрокаРаспоряжений);
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаРаспоряжений);
			НайденныеСтрокиТоваров = ТоварыКДоставке.НайтиСтроки(СтруктураПоиска);
			Для Каждого СтрокаТовары Из НайденныеСтрокиТоваров Цикл
				ТоварыКДоставке.Удалить(СтрокаТовары);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ОбновитьИтоговыйВесОбъемЗаполненность();
	
	ОбновитьСкладыПогрузки();
	
КонецПроцедуры

&НаСервере
Функция ПроверитьРазбитьПунктДоставкиСервер()
	
	КлючСвязи = Объект.Маршрут.НайтиПоИдентификатору(Элементы.Маршрут.ТекущаяСтрока).КлючСвязи;
	КоличествоРаспоряжений = Объект.Распоряжения.НайтиСтроки(Новый Структура("КлючСвязи",КлючСвязи)).Количество();
	Если КоличествоРаспоряжений < 2 Тогда
		Текст = НСтр("ru='Разбиение строки возможно для адресов, к которым относится более одного распоряжения';uk='Розбиття рядка можливе для адрес, до яких відноситься більше одного розпорядження'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
		Возврат Истина;
	КонецЕсли;
	Если КоличествоРаспоряжений > 2 Тогда
		Возврат Ложь; //Вернуться в клиент и открыть форму выбора распоряжений
	КонецЕсли;
	МассивРаспоряжений = Новый Массив;
	Отбор = Новый Структура("КлючСвязи", КлючСвязи);
	СтрокаРаспоряжения = Объект.Распоряжения.НайтиСтроки(Отбор)[1];
	МассивРаспоряжений.Добавить(Новый Структура("Распоряжение,Склад", СтрокаРаспоряжения.Распоряжение, СтрокаРаспоряжения.Склад));
	РазбитьПунктДоставки(МассивРаспоряжений); // Случай с двумя распоряжениями - просто разбиваем без выбора
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура РазбитьПунктДоставки(Распоряжения) Экспорт
	
	Если ТипЗнч(Распоряжения) <> Тип("Массив") Тогда		
		МассивРаспоряжений = Новый Массив;
		Для Каждого Строка Из ПолучитьИзВременногоХранилища(Распоряжения.АдресРаспоряжений) Цикл
			МассивРаспоряжений.Добавить(Новый Структура("Распоряжение,Склад", Строка.Распоряжение, Строка.Склад));	
		КонецЦикла;	
	Иначе
		МассивРаспоряжений = Распоряжения;
	КонецЕсли;
	
	ИндентификаторСтрокиМаршрута = Элементы.Маршрут.ТекущаяСтрока;
	
	СтрокаМаршрута = Объект.Маршрут.НайтиПоИдентификатору(ИндентификаторСтрокиМаршрута); 
	НоваяСтрока = Объект.Маршрут.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаМаршрута);
	КлючСвязи = Строка(Новый УникальныйИдентификатор);
	НоваяСтрока.КлючСвязи = КлючСвязи;
	
	Для Каждого СтруктураПоиск Из МассивРаспоряжений Цикл
		СтрокаРаспоряжений = Объект.Распоряжения.НайтиСтроки(СтруктураПоиск)[0];
		СтрокаРаспоряжений.КлючСвязи = КлючСвязи;
	КонецЦикла;
		
	Элементы.Маршрут.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	
	ОбновитьВесИОбъемСтрокиМаршрута(СтрокаМаршрута);
	ОбновитьВесИОбъемСтрокиМаршрута(НоваяСтрока);
	ОбновитьИтоговыйВесОбъемЗаполненность();
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьПередОбъединением(ВыделенныеСтроки)
	
	СоответствиеАдресов = Новый Соответствие;
	СоответствиеЗонДоставки = Новый Соответствие;
	СоответствиеВремяС = Новый Соответствие;
	СоответствиеВремяПо = Новый Соответствие;
	СоответствиеДополнительнаяИнформация = Новый Соответствие;
	СоответствиеДоставлено = Новый Соответствие;	
	
	МассивКодовСтрок = Новый Массив;
	СтруктураВозврата = Новый Структура;
	
	Для Каждого Идентификатор Из ВыделенныеСтроки Цикл
		
		СтрокаРаспоряжений = Объект.Маршрут.НайтиПоИдентификатору(Идентификатор);
		
		СоответствиеАдресов.Вставить(СтрокаРаспоряжений.Адрес,"");
		СоответствиеЗонДоставки.Вставить(СтрокаРаспоряжений.Зона,"");
		СоответствиеВремяС.Вставить(СтрокаРаспоряжений.ВремяС,"");
		СоответствиеВремяПо.Вставить(СтрокаРаспоряжений.ВремяПо,"");
		СоответствиеДополнительнаяИнформация.Вставить(СтрокаРаспоряжений.ДополнительнаяИнформация,"");
		СоответствиеДоставлено.Вставить(СтрокаРаспоряжений.Доставлено,"");
		
		МассивКодовСтрок.Добавить(СтрокаРаспоряжений.КлючСвязи);
	КонецЦикла;
		
	Если СоответствиеАдресов.Количество()>1 Тогда
		СтруктураВозврата.Вставить("РазныеАдреса",Истина);
		Возврат СтруктураВозврата;
	ИначеЕсли СоответствиеЗонДоставки.Количество()>1
			ИЛИ СоответствиеВремяС.Количество() > 1
			ИЛИ СоответствиеВремяПо.Количество() > 1
			ИЛИ СоответствиеДополнительнаяИнформация.Количество()>1
			ИЛИ СоответствиеДоставлено.Количество() > 1
			ИЛИ СоответствиеЗонДоставки.Количество() > 1 Тогда
		СтруктураВозврата.Вставить("РазныеАдреса",Ложь);
		СтруктураВозврата.Вставить("РазныеДанные",Истина);
	Иначе
		СтруктураВозврата.Вставить("РазныеАдреса",Ложь);
		СтруктураВозврата.Вставить("РазныеДанные",Ложь);
	КонецЕсли;
	
	СтруктураВозврата.Вставить("МассивКодовСтрок",МассивКодовСтрок);
	
	Возврат СтруктураВозврата;	
		
КонецФункции

&НаСервере
Функция ОбъединитьСтрокиМаршрута(ВыделенныеСтроки,МассивКодовСтрок)
		
	СтрокаМаршрута = Объект.Маршрут.НайтиПоИдентификатору(ВыделенныеСтроки[0]);
	НовыйКлючСвязи = СтрокаМаршрута.КлючСвязи;
	СтрокаМаршрута.Вес = 0;
	СтрокаМаршрута.Объем = 0;
	
	Для Каждого СтрокаРаспоряжений Из Объект.Распоряжения Цикл
		
		Если МассивКодовСтрок.Найти(СтрокаРаспоряжений.КлючСвязи)<>Неопределено Тогда
			СтрокаРаспоряжений.КлючСвязи = НовыйКлючСвязи;
			СтрокаМаршрута.Вес = СтрокаМаршрута.Вес + СтрокаРаспоряжений.Вес;
			СтрокаМаршрута.Объем = СтрокаМаршрута.Объем + СтрокаРаспоряжений.Объем;
		КонецЕсли;	
		
	КонецЦикла;	
	
	СтрокаПропущена = Ложь;
	Для Каждого Идентификатор Из ВыделенныеСтроки Цикл
		Если Не СтрокаПропущена Тогда
			СтрокаПропущена = Истина;
			Продолжить;
		КонецЕсли;
		
		Объект.Маршрут.Удалить(Объект.Маршрут.НайтиПоИдентификатору(Идентификатор));	
		
	КонецЦикла;	
	
КонецФункции

&НаКлиенте
Процедура ОтсортироватьСписокМаршрута()
	
	Объект.Маршрут.Сортировать("ВремяС, ВремяПо, Адрес");
		
КонецПроцедуры	

 &НаКлиенте
Процедура ПриИзмененииВремяС()
	
	ТекущаяСтрокаМаршрута = Элементы.Маршрут.ТекущиеДанные;
	ТекущаяСтрокаМаршрута.ВремяС = НачалоДня(ТекущаяСтрокаМаршрута.ВремяС)
		+ Час(ТекущаяСтрокаМаршрута.ВремяСБезДаты)*60*60 + Минута(ТекущаяСтрокаМаршрута.ВремяСБезДаты)*60;
		
	Если ТекущаяСтрокаМаршрута.ВремяПо < ТекущаяСтрокаМаршрута.ВремяС Тогда
		ТекущаяСтрокаМаршрута.ВремяПо = ТекущаяСтрокаМаршрута.ВремяС;
		ТекущаяСтрокаМаршрута.ВремяПоБезДаты = ТекущаяСтрокаМаршрута.ВремяСБезДаты;
	КонецЕсли;
	
	Если ТекущаяСтрокаМаршрута.ВремяС < Объект.ДатаВремяРейсаПланС Тогда
		Объект.ДатаВремяРейсаПланС = ТекущаяСтрокаМаршрута.ВремяС;
		ПоказатьОповещениеПользователя(НСтр("ru='Плановая дата начала рейса изменена';uk='Планова дата початку рейсу змінена'"),,
			НСтр("ru='Выбранная дата раньше плановой даты начала рейса.';uk='Вибрана дата раніше планової дати початку рейсу.'"));
	КонецЕсли;
	
КонецПроцедуры	

 &НаКлиенте
Процедура ПриИзмененииВремяПо()
	
	ТекущаяСтрокаМаршрута = Элементы.Маршрут.ТекущиеДанные;
	ТекущаяСтрокаМаршрута.ВремяПо = НачалоДня(ТекущаяСтрокаМаршрута.ВремяПо)
		+ Час(ТекущаяСтрокаМаршрута.ВремяПоБезДаты)*60*60 + Минута(ТекущаяСтрокаМаршрута.ВремяПоБезДаты)*60;
	
	Если ТекущаяСтрокаМаршрута.ВремяПо < ТекущаяСтрокаМаршрута.ВремяС Тогда
		ТекущаяСтрокаМаршрута.ВремяС = ТекущаяСтрокаМаршрута.ВремяПо;
		ТекущаяСтрокаМаршрута.ВремяСБезДаты = ТекущаяСтрокаМаршрута.ВремяПоБезДаты;
	КонецЕсли;
	
	Если Объект.ДатаВремяРейсаПланС <> '00010101' И Объект.ДатаВремяРейсаПланПо < ТекущаяСтрокаМаршрута.ВремяПо Тогда
		Объект.ДатаВремяРейсаПланПо = ТекущаяСтрокаМаршрута.ВремяПо;
		ПоказатьОповещениеПользователя(НСтр("ru='Плановая дата окончания рейса изменена';uk='Планова дата закінчення рейсу змінена'"),,
			НСтр("ru='Выбранная дата позже плановой даты окончания рейса.';uk='Вибрана дата пізніше планової дати закінчення рейсу.'"));
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти

#Область ТранспортнаяНакладная

&НаСервере
Функция СоздатьТранспортныеНакладныеНаСервере(ВыделенныеСтрокиИдентификаторы)
	
	ВыделенныеСтроки = Новый Массив;
	
	Для Каждого ИдентификаторСтроки Из ВыделенныеСтрокиИдентификаторы Цикл
		ИсходныйНомерСтроки = Объект.Маршрут.НайтиПоИдентификатору(ИдентификаторСтроки).ИсходныйНомерСтроки;
		ВыделенныеСтроки.Добавить(ИсходныйНомерСтроки);
	КонецЦикла;
	
	МассивОбъектов = Новый Массив;
	МассивОбъектов.Добавить(Объект.Ссылка);
	ЗаданияНаПеревозкуДляОформленияТН = Документы.ТранспортнаяНакладная.ПроверитьЗаданияНаПеревозкуДляОформленияТранспортныхНакладных(
		МассивОбъектов, ВыделенныеСтроки);
	
	СозданныеТранспортныеНакладные = Документы.ТранспортнаяНакладная.СоздатьТранспортныеНакладныеДляЗаданийНаПеревозку(
			ЗаданияНаПеревозкуДляОформленияТН, 
			ВыделенныеСтроки);
			
	ЗаполнитьПризнакНаличияТранспортнойНакладнойПоКаждомуАдресуДоставки();
			
	Возврат СозданныеТранспортныеНакладные;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПризнакНаличияТранспортнойНакладнойПоКаждомуАдресуДоставки()
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьТТН") 
		ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.Документы.ТранспортнаяНакладная) Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Распоряжения.Распоряжение,
	|	Распоряжения.Склад,
	|	Распоряжения.КлючСвязи
	|ПОМЕСТИТЬ Распоряжения
	|ИЗ
	|	&Распоряжения КАК Распоряжения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Маршрут.Адрес,
	|	Маршрут.НомерСтроки,
	|	Маршрут.КлючСвязи
	|ПОМЕСТИТЬ Маршрут
	|ИЗ
	|	&Маршрут КАК Маршрут
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Маршрут.Адрес,
	|	Распоряжения.Распоряжение,
	|	Распоряжения.Склад,
	|	Маршрут.НомерСтроки
	|ПОМЕСТИТЬ РаспоряженияПоМаршрутам
	|ИЗ
	|	Маршрут КАК Маршрут
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Распоряжения КАК Распоряжения
	|		ПО Маршрут.КлючСвязи = Распоряжения.КлючСвязи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Распоряжения.Адрес,
	|	ДокументТовары.Ссылка КАК Накладная,
	|	Распоряжения.НомерСтроки
	|ПОМЕСТИТЬ НакладныеПоЗаданиямНаПеревозку
	|ИЗ
	|	РаспоряженияПоМаршрутам КАК Распоряжения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК ДокументТовары
	|		ПО Распоряжения.Распоряжение = ДокументТовары.ЗаказКлиента
	|			И Распоряжения.Склад = ДокументТовары.Склад
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Распоряжения.Адрес,
	|	ДокументТовары.Ссылка,
	|	Распоряжения.НомерСтроки
	|ИЗ
	|	РаспоряженияПоМаршрутам КАК Распоряжения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров.Товары КАК ДокументТовары
	|		ПО Распоряжения.Распоряжение = ДокументТовары.ЗаказНаПеремещение
	|			И Распоряжения.Склад = ДокументТовары.Ссылка.СкладОтправитель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|
	|ВЫБРАТЬ
	|	Распоряжения.Адрес,
	|	Распоряжения.Распоряжение,
	|	Распоряжения.НомерСтроки
	|ИЗ
	|	РаспоряженияПоМаршрутам КАК Распоряжения
	|ГДЕ
	|	(Распоряжения.Распоряжение ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ИЛИ Распоряжения.Распоряжение ССЫЛКА Документ.ПеремещениеТоваров)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НакладныеПоЗаданиямНаПеревозку.НомерСтроки,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ТранспортнаяНакладнаяДокументыОснованияОснования.Ссылка ЕСТЬ NULL 
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ) КАК ТранспортнаяНакладнаяОформлена
	|ИЗ
	|	НакладныеПоЗаданиямНаПеревозку КАК НакладныеПоЗаданиямНаПеревозку
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТранспортнаяНакладная.ДокументыОснования КАК ТранспортнаяНакладнаяДокументыОснованияОснования
	|		ПО (ТранспортнаяНакладнаяДокументыОснованияОснования.Ссылка.АдресДоставки = НакладныеПоЗаданиямНаПеревозку.Адрес)
	|			И (ТранспортнаяНакладнаяДокументыОснованияОснования.ДокументОснование = НакладныеПоЗаданиямНаПеревозку.Накладная)
	|			И (ТранспортнаяНакладнаяДокументыОснованияОснования.Ссылка.ЗаданиеНаПеревозку = &ЗаданиеНаПеревозку)
	|			И (ТранспортнаяНакладнаяДокументыОснованияОснования.Ссылка.Проведен)
	|
	|СГРУППИРОВАТЬ ПО
	|	НакладныеПоЗаданиямНаПеревозку.НомерСтроки";
	
	Запрос.УстановитьПараметр("Распоряжения", Объект.Распоряжения.Выгрузить(,"Распоряжение,КлючСвязи,Склад"));
	Запрос.УстановитьПараметр("Маршрут", Объект.Маршрут.Выгрузить(,"Адрес,НомерСтроки,КлючСвязи"));
	Запрос.УстановитьПараметр("ЗаданиеНаПеревозку",	Объект.Ссылка);
		
	Результат = Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() Цикл 
		Объект.Маршрут[Результат.НомерСтроки - 1].ТранспортнаяНакладнаяОформлена = Результат.ТранспортнаяНакладнаяОформлена;
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ОформитьТранспортныеНакладныеЗавершение(РезультатПроверки = Истина)
		
	Если Не РезультатПроверки Тогда 
		Возврат;
	КонецЕсли;
	
	ВыделенныеСтрокиИдентификаторы = Элементы.Маршрут.ВыделенныеСтроки;
	
	Если ВыделенныеСтрокиИдентификаторы.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru='Невозможно создать транспортные накладные. Не выделено ни одного адреса доставки.';uk='Неможливо створити транспортні накладні. Не виділено жодної адреси доставки.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.Маршрут");
		Возврат;
	КонецЕсли;
	
	СозданныеТранспортныеНакладные = СоздатьТранспортныеНакладныеНаСервере(ВыделенныеСтрокиИдентификаторы);
	
	Если СозданныеТранспортныеНакладные.Количество() > 0 Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТранспортныеНакладные", СозданныеТранспортныеНакладные);
		ОткрытьФорму(
			"Документ.ТранспортнаяНакладная.Форма.СозданныеТранспортныеНакладные", 
			ПараметрыФормы);
					
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьТранспортныеНакладныеОбработкаОтвета(КодОтвета, ДополнительныеПараметры) Экспорт 
		
	РезультатПроверки = Истина;
	
	Если КодОтвета = КодВозвратаДиалога.ОК И ПроверитьЗаполнение() Тогда
		Попытка
			РезультатПроверки = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
		Исключение
			ПоказатьИнформациюОбОшибке(ИнформацияОбОшибке());
			РезультатПроверки = Ложь;
		КонецПопытки;
	Иначе
		РезультатПроверки = Ложь;
	КонецЕсли;
	
	ОформитьТранспортныеНакладныеЗавершение(РезультатПроверки)
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ЕдиницаИзмеренияВеса = Константы.ЕдиницаИзмеренияВеса.Получить();
	ЕдиницаИзмеренияОбъема = Константы.ЕдиницаИзмеренияОбъема.Получить();
	
	КоэффициентПересчетаВТонны     			 = ДоставкаТоваров.КоэффициентПересчетаВТонны();
	КоэффициентПересчетаВКубическиеМетры     = ДоставкаТоваров.КоэффициентПересчетаВКубическиеМетры();
		
	УстановитьГрузоподъемностьЕмкостьТранспортаНаСервере();
	
	ТекстЗаголовка = НСтр("ru='Объем, %ЕдиницаИзмеренияОбъема%';uk='Об''єм, %ЕдиницаИзмеренияОбъема%'");
	ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%ЕдиницаИзмеренияОбъема%", Строка(ЕдиницаИзмеренияОбъема));
	Элементы.РаспоряженияОбъем.Заголовок = ТекстЗаголовка;
	Элементы.МаршрутОбъем.Заголовок = ТекстЗаголовка;
	
	ТекстЗаголовка = НСтр("ru='Вес, %ЕдиницаИзмеренияВеса%';uk='Вага, %ЕдиницаИзмеренияВеса%'");
	ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%ЕдиницаИзмеренияВеса%", Строка(ЕдиницаИзмеренияВеса));
	Элементы.РаспоряженияВес.Заголовок = ТекстЗаголовка;
	Элементы.МаршрутВес.Заголовок = ТекстЗаголовка;

	Склад = Объект.Склад;
	
	Если Не ЗначениеЗаполнено(Объект.Ответственный) Тогда
		Объект.Ответственный = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
	Если Объект.ЗаданиеВыполняет <> Перечисления.ТипыИсполнителейЗаданийНаПеревозку.Перевозчик
		И Объект.Маршрут.Количество()> 0 Тогда
		Элементы.Распоряжения.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", Объект.Маршрут[0].КлючСвязи);	
	КонецЕсли;
	
	УстановитьОтметкуНезаполненного();
	
	Если Не ПравоДоступа("Чтение", Метаданные.Документы.ТранспортнаяНакладная) Тогда
		Элементы.МаршрутТранспортнаяНакладнаяОформлена.Видимость = Ложь;
	КонецЕсли;
	
	Если Не ПравоДоступа("Добавление", Метаданные.Документы.ТранспортнаяНакладная) Тогда 
		Элементы.МаршрутОформитьТранспортныеНакладные.Видимость = Ложь;	
	КонецЕсли;
	
	ЗаполнитьПризнакНаличияТранспортнойНакладнойПоКаждомуАдресуДоставки();
	ЗаполнитьСлужебныеРеквизитыРаспоряжений();
	
	ЗаполнитьВремяБезДаты();
	
	СтатусБыл = Объект.Статус;
	
	ОбновитьСкладыПогрузки();
	
	Если Объект.Операция = Перечисления.ВидыДоставки.НаСклад Тогда
		Элементы.РаспоряженияПолучательОтправитель.Заголовок = НСтр("ru='Отправитель';uk='Відправник'");
		Элементы.МаршрутТранспортнаяНакладнаяОформлена.Видимость = Ложь;
		Элементы.МаршрутТранспортнаяНакладнаяОформлена.Видимость = Ложь;
		Элементы.МаршрутОформитьТранспортныеНакладные.Видимость = Ложь;
		Элементы.Статус.СписокВыбора.Очистить();
		Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыЗаданийНаПеревозку.Формируется);
		Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыЗаданийНаПеревозку.Отправлено);
		Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыЗаданийНаПеревозку.Закрыто);
	Иначе
		Элементы.РаспоряженияПолучательОтправитель.Заголовок = НСтр("ru='Получатель';uk='Одержувач'");
		Элементы.МаршрутТранспортнаяНакладнаяОформлена.Видимость = Истина;
		Элементы.МаршрутТранспортнаяНакладнаяОформлена.Видимость = Истина;
		Элементы.МаршрутОформитьТранспортныеНакладные.Видимость = Истина;
		Элементы.Статус.СписокВыбора.Очистить();
		Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыЗаданийНаПеревозку.Формируется);
		Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыЗаданийНаПеревозку.КПогрузке);
		Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыЗаданийНаПеревозку.Отправлено);
		Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыЗаданийНаПеревозку.Закрыто);
	КонецЕсли;
	
	НастроитьПоТипуИсполнителя();
	
КонецПроцедуры

&НаСервере
Процедура ТранспортноеСредствоПриИзмененииНаСервере()
	
	ПараметрыТТН = Новый Структура;
	ПараметрыТТН.Вставить("АвтомобильГосударственныйНомер");
	ПараметрыТТН.Вставить("АвтомобильМарка");	
	ПараметрыТТН.Вставить("ВидПеревозки");
	ПараметрыТТН.Вставить("АвтомобильТип");
	ПараметрыТТН.Вставить("АвтомобильВместимостьВКубическихМетрах");
	ПараметрыТТН.Вставить("АвтомобильГрузоподъемностьВТоннах");
	ПараметрыТТН.Вставить("ЛицензионнаяКарточкаСерия");
	ПараметрыТТН.Вставить("ЛицензионнаяКарточкаНомер");
	ПараметрыТТН.Вставить("ЛицензионнаяКарточкаВид");
	ПараметрыТТН.Вставить("ЛицензионнаяКарточкаРегистрационныйНомер");
	ПараметрыТТН.Вставить("Прицеп");
	ПараметрыТТН.Вставить("ГосударственныйНомерПрицепа");
	
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТранспортныеСредства.Код КАК АвтомобильГосударственныйНомер,
		|	ТранспортныеСредства.Марка КАК АвтомобильМарка,
		|	ТранспортныеСредства.ВидПеревозки КАК ВидПеревозки,
		|	ТранспортныеСредства.Тип КАК АвтомобильТип,
		|	ТранспортныеСредства.ВместимостьВКубическихМетрах КАК АвтомобильВместимостьВКубическихМетрах,
		|	ТранспортныеСредства.ГрузоподъемностьВТоннах КАК АвтомобильГрузоподъемностьВТоннах,
		|	ТранспортныеСредства.ЛицензионнаяКарточкаВид,
		|	ТранспортныеСредства.ЛицензионнаяКарточкаНомер,
		|	ТранспортныеСредства.ЛицензионнаяКарточкаРегистрационныйНомер,
		|	ТранспортныеСредства.ЛицензионнаяКарточкаСерия,
		|	ТранспортныеСредства.Прицеп,
		|	ТранспортныеСредства.ГосударственныйНомерПрицепа КАК ГосударственныйНомерПрицепа
		|ИЗ
		|	Справочник.ТранспортныеСредства КАК ТранспортныеСредства
		|ГДЕ
		|	ТранспортныеСредства.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ТипыТранспортныхСредств.Наименование,
		|	ТипыТранспортныхСредств.ВместимостьВКубическихМетрах,
		|	ТипыТранспортныхСредств.ГрузоподъемностьВТоннах,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО
		|ИЗ
		|	Справочник.ТипыТранспортныхСредств КАК ТипыТранспортныхСредств
		|ГДЕ
		|	ТипыТранспортныхСредств.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Объект.ТранспортноеСредство);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ПараметрыТТН, Выборка);
			Грузоподъемность = Выборка.АвтомобильВместимостьВКубическихМетрах;	
			ЕмкостьТранспорта = Выборка.АвтомобильГрузоподъемностьВТоннах;
		КонецЕсли;
	Иначе
		Грузоподъемность = 0;	
		ЕмкостьТранспорта = 0;			
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект, ПараметрыТТН);
		
КонецПроцедуры

&НаСервере
Процедура УстановитьГрузоподъемностьЕмкостьТранспортаНаСервере()
	
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		
		РеквизитыТранспортногоСредства = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ТранспортноеСредство, 
		Новый Структура("ГрузоподъемностьВТоннах, ВместимостьВКубическихМетрах")); 
		
		Грузоподъемность = РеквизитыТранспортногоСредства.ГрузоподъемностьВТоннах;	
		ЕмкостьТранспорта = РеквизитыТранспортногоСредства.ВместимостьВКубическихМетрах;
		
	Иначе
		
		Грузоподъемность = 0;	
		ЕмкостьТранспорта = 0;
		
	КонецЕсли;
	
	ОбновитьИтоговыйВесОбъемЗаполненность();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИтоговыйВесОбъемЗаполненность()
	 
	Объект.Вес   = КоэффициентПересчетаВТонны * Объект.Маршрут.Итог("Вес");
	Объект.Объем = КоэффициентПересчетаВКубическиеМетры * Объект.Маршрут.Итог("Объем");	 
	ЗаполненностьПоВесу   = ?(Грузоподъемность=0,0,Объект.Вес/Грузоподъемность);
	ЗаполненностьПоОбъему = ?(ЕмкостьТранспорта=0,0,Объект.Объем/ЕмкостьТранспорта);
	
КонецПроцедуры	

&НаСервере
Процедура ОбновитьВесИОбъемСтрокиМаршрута(СтрокаМаршрута)
	
	СтрокиРаспоряжения = Объект.Распоряжения.НайтиСтроки(Новый Структура("КлючСвязи",СтрокаМаршрута.КлючСвязи));
	
	СтрокаМаршрута.Вес = 0;
	СтрокаМаршрута.Объем = 0;
		
	Для Каждого СтрокаРаспоряжений Из СтрокиРаспоряжения Цикл
		
		СтрокаМаршрута.Вес = СтрокаРаспоряжений.Вес + СтрокаМаршрута.Вес;
		СтрокаМаршрута.Объем = СтрокаРаспоряжений.Объем + СтрокаМаршрута.Объем;
		
	КонецЦикла;	 
		 	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаполненностьНаКлиенте()
	
	Если КоэффициентПересчетаВТонны <> 0 Тогда 
		ЗаполненностьПоВесу   = ?(Грузоподъемность=0,0,Объект.Вес/Грузоподъемность);	
	КонецЕсли;
	
	Если КоэффициентПересчетаВКубическиеМетры <> 0 Тогда
		ЗаполненностьПоОбъему = ?(ЕмкостьТранспорта=0,0,Объект.Объем/ЕмкостьТранспорта);
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Функция ПоместитьРаспоряженияВХранилище(КлючСвязи)
	
	СтруктураПоиск = Новый Структура("КлючСвязи",КлючСвязи);
	Распоряжения = Объект.Распоряжения.Выгрузить().Скопировать(СтруктураПоиск);
	
	Возврат ПоместитьВоВременноеХранилище(Распоряжения, УникальныйИдентификатор);

КонецФункции

&НаСервере
Процедура ПриИзмененииСкладаСервер()
	
	Объект.Распоряжения.Очистить();
	Объект.Маршрут.Очистить();
	Объект.Вес = 0;
	Объект.Объем = 0;
	ЗаполненностьПоВесу = 0;
	ЗаполненностьПоОбъему = 0;
	
	ОбновитьСкладыПогрузки();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСкладыПогрузки()
	
	Склады = Объект.Распоряжения.Выгрузить(,"Склад");
	Склады.Свернуть("Склад");
	СкладыПогрузки.ЗагрузитьЗначения(Склады.ВыгрузитьКолонку("Склад"));
	
	СкладГруппа = Склад.ЭтоГруппа;
	ЕстьОрдерныеСклады = СкладыСервер.ЕстьОрдерныйНаОтгрузкуСклад(Склад, Объект.ДатаВремяРейсаПланС);
		
	Элементы.КартинкаНесколькоСкладов.Видимость = СкладГруппа;
	Элементы.ОткрытьСклады.Видимость            = СкладГруппа;
	Элементы.РаспоряженияСклад.Видимость        = СкладГруппа;
	Элементы.РаспоряженияЗаполнитьПоРасходнымОрдерам.Видимость    = ЕстьОрдерныеСклады
		И Объект.Операция = Перечисления.ВидыДоставки.СоСклада;
	Элементы.РаспоряженияЗаполнитьПоРасходнымОрдерам1.Видимость   = ЕстьОрдерныеСклады
		И Объект.Операция = Перечисления.ВидыДоставки.СоСклада;
	Элементы.РаспоряженияЗаполнитьПоРасходнымОрдерам.Доступность  = ЗначениеЗаполнено(Объект.Ссылка);
	Элементы.РаспоряженияЗаполнитьПоРасходнымОрдерам1.Доступность = ЗначениеЗаполнено(Объект.Ссылка);
	Если Объект.Операция = Перечисления.ВидыДоставки.НаСклад Тогда
		ТекстЗаголовка = НСтр("ru='Склады назначения (%СкладыКоличество%)';uk='Склади призначення (%СкладыКоличество%)'");
	Иначе
		ТекстЗаголовка = НСтр("ru='Склады погрузки (%СкладыКоличество%)';uk='Склади навантаження (%СкладыКоличество%)'");
	КонецЕсли;
	ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%СкладыКоличество%", Склады.Количество());
	Элементы.ОткрытьСклады.Заголовок = ТекстЗаголовка;
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаОПерезаполненииПоОрдерам(Результат, ДополнительныеПараметры) Экспорт
	
	ТекстОповещения = ПриИзмененииСтатусаИПерезаполнении(Результат = КодВозвратаДиалога.Да);
	
	ОповеститьОбИзменениях(ТекстОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОбИзменениях(ТекстОповещения)
	
	Если ТекстОповещения <> "" Тогда
		ПоказатьОповещениеПользователя(,,
			ТекстОповещения,
			БиблиотекаКартинок.Информация32);
	КонецЕсли;

КонецПроцедуры

 &НаСервере
Функция ПриИзмененииСтатусаИПерезаполнении(ПерезаполнитьПоРасходнымОрдерам = Ложь)
	
	ТекстСообщенияОПерезаполненииПоОрдерам = "";
	Если ПерезаполнитьПоРасходнымОрдерам Тогда
		Если ЗаполнитьПоРасходнымОрдерамНаСервере() Тогда
			Если Объект.Распоряжения.Количество() > 0 Тогда
				ТекстСообщенияОПерезаполненииПоОрдерам = НСтр("ru='Задание на перевозку перезаполнено по расходным ордерам на товары.';uk='Завдання на перевезення перезаповнене за видатковими ордерами на товари.'");
			Иначе
				ТекстСообщенияОПерезаполненииПоОрдерам = НСтр("ru='После перезаполнения по расходным ордерам на товары в задании на перевозку не осталось распоряжений на доставку.';uk='Після перезаповнення за видатковими ордерами на товари в завданні на перевезення не залишилося розпоряджень на доставку.'");
			КонецЕсли;
		Иначе
			ТекстСообщенияОПерезаполненииПоОрдерам = НСтр("ru='Задание на перевозку не перезаполнено, т.к. соответствует расходным ордерам на товары.';uk='Завдання на перевезення не перезаповнене, так як відповідає видатковим ордерам на товари.'");
		КонецЕсли;
	КонецЕсли;
	
	Если СтатусБыл = Объект.Статус Тогда
		Возврат ТекстСообщенияОПерезаполненииПоОрдерам;
	КонецЕсли;
	
	ТекстСообщенияОВремени = "";
	
	СтатусБыл = Объект.Статус;
	
	МаксимальноеВремяПо = ДоставкаТоваров.МаксимальноеВремяПо(Объект);
	
	ТекстыСообщений = Новый Массив;
	
	Если Объект.Статус <> Перечисления.СтатусыЗаданийНаПеревозку.Формируется
		И Не ЗначениеЗаполнено(Объект.ДатаВремяРейсаПланС) Тогда
		
		Объект.ДатаВремяРейсаПланС = ТекущаяДата();
		ТекстыСообщений.Добавить(НСтр("ru='плановое время начала рейса';uk='плановий час початку рейсу'"));
		
	КонецЕсли;
	
	Если Объект.Статус <> Перечисления.СтатусыЗаданийНаПеревозку.Формируется
		И Объект.Статус <> Перечисления.СтатусыЗаданийНаПеревозку.КПогрузке
		И Не ЗначениеЗаполнено(Объект.ДатаВремяРейсаПланПо) Тогда
		
		Объект.ДатаВремяРейсаПланПо = Макс(МаксимальноеВремяПо,Объект.ДатаВремяРейсаПланС);
		ТекстыСообщений.Добавить(НСтр("ru='плановое время окончания рейса';uk='плановий час закінчення рейсу'"));
		
	КонецЕсли;
	
	Если (Объект.Статус = Перечисления.СтатусыЗаданийНаПеревозку.Отправлено
			Или Объект.Статус = Перечисления.СтатусыЗаданийНаПеревозку.Закрыто)
		И Не ЗначениеЗаполнено(Объект.ДатаВремяРейсаФактС) Тогда
		
		Объект.ДатаВремяРейсаФактС = ТекущаяДата();
		ТекстыСообщений.Добавить(НСтр("ru='фактическое время начала рейса';uk='фактичний час початку рейсу'"));
		
	КонецЕсли;
	
	Если Объект.Статус = Перечисления.СтатусыЗаданийНаПеревозку.Закрыто
		И Не ЗначениеЗаполнено(Объект.ДатаВремяРейсаФактПо) Тогда
		
		Объект.ДатаВремяРейсаФактПо = ТекущаяДата();
		ТекстыСообщений.Добавить(НСтр("ru='фактическое время окончания рейса';uk='фактичний час закінчення рейсу'"));
		
	КонецЕсли;
	
	Если ТекстыСообщений.Количество() > 0 Тогда
		ТекстСообщенияОВремени = НСтр("ru='Заполнено %Время0, %Время1, %Время2, %Время3.';uk='Заповнено %Время0, %Время1, %Время2, %Время3.'");
		Для Сч = 0 По ТекстыСообщений.ВГраница() Цикл
			ТекстСообщенияОВремени = СтрЗаменить(ТекстСообщенияОВремени, "%Время"+Сч, ТекстыСообщений[Сч]);
		КонецЦикла;
		Для Сч = 1 По 3 Цикл
			ТекстСообщенияОВремени = СтрЗаменить(ТекстСообщенияОВремени, ", %Время"+Сч, "");
		КонецЦикла;
	КонецЕсли;
	
	УстановитьОтметкуНезаполненного();
	
	Возврат ТекстСообщенияОВремени;
	
КонецФункции

&НаСервере
Процедура УстановитьОтметкуНезаполненного()
	
	Если Объект.Статус = Перечисления.СтатусыЗаданийНаПеревозку.Формируется
			ИЛИ Объект.Статус = Перечисления.СтатусыЗаданийНаПеревозку.ПустаяСсылка() Тогда
		Элементы.Распоряжения.АвтоОтметкаНезаполненного = Ложь;
		Элементы.Распоряжения.ОтметкаНезаполненного = Ложь;
	Иначе
		Элементы.Распоряжения.АвтоОтметкаНезаполненного = Истина;
	КонецЕсли;
	
	Элементы.ДатаВремяРейсаПланС.АвтоОтметкаНезаполненного	= Ложь;
	Элементы.ДатаВремяРейсаПланС1.АвтоОтметкаНезаполненного	= Ложь;
	Элементы.ДатаВремяРейсаФактС.АвтоОтметкаНезаполненного	= Ложь;
	Элементы.ДатаВремяРейсаФактПо.АвтоОтметкаНезаполненного	= Ложь;
	Элементы.ДатаВремяРейсаПланС.ОтметкаНезаполненного		= Ложь;
	Элементы.ДатаВремяРейсаПланС1.ОтметкаНезаполненного		= Ложь;
	Элементы.ДатаВремяРейсаФактС.ОтметкаНезаполненного		= Ложь;
	Элементы.ДатаВремяРейсаФактПо.ОтметкаНезаполненного		= Ложь;
	Если Объект.Статус = Перечисления.СтатусыЗаданийНаПеревозку.КПогрузке Тогда
		Элементы.ДатаВремяРейсаПланС.АвтоОтметкаНезаполненного	= Истина;
		Элементы.ДатаВремяРейсаПланС1.АвтоОтметкаНезаполненного	= Истина;
	ИначеЕсли Объект.Статус = Перечисления.СтатусыЗаданийНаПеревозку.Отправлено Тогда
		Элементы.ДатаВремяРейсаПланС.АвтоОтметкаНезаполненного	= Истина;
		Элементы.ДатаВремяРейсаПланС1.АвтоОтметкаНезаполненного	= Истина;
		Элементы.ДатаВремяРейсаФактС.АвтоОтметкаНезаполненного	= Истина;
	ИначеЕсли Объект.Статус = Перечисления.СтатусыЗаданийНаПеревозку.Закрыто Тогда
		Элементы.ДатаВремяРейсаПланС.АвтоОтметкаНезаполненного  = Истина;
		Элементы.ДатаВремяРейсаПланС1.АвтоОтметкаНезаполненного = Истина;
		Элементы.ДатаВремяРейсаФактС.АвтоОтметкаНезаполненного  = Истина;
		Элементы.ДатаВремяРейсаФактПо.АвтоОтметкаНезаполненного = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВремяБезДаты()
	
	Для Каждого Стр Из Объект.Маршрут Цикл
		Стр.ВремяСБезДаты   = ДоставкаТоваровКлиентСервер.ВремяБезДаты(Стр.ВремяС);
		Стр.ВремяПоБезДаты  = ДоставкаТоваровКлиентСервер.ВремяБезДаты(Стр.ВремяПо);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодборТоваровКДоставке()
	
	СтрокаРаспоряжений = Элементы.Распоряжения.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗаданиеНаПеревозку", Объект.Ссылка);
	ПараметрыФормы.Вставить("Распоряжение", СтрокаРаспоряжений.Распоряжение);
	ПараметрыФормы.Вставить("Склад", СтрокаРаспоряжений.Склад);
	ПараметрыФормы.Вставить("ТранспортноеСредство", Объект.ТранспортноеСредство);
	ПараметрыФормы.Вставить("ПолучательОтправитель", СтрокаРаспоряжений.ПолучательОтправитель);
	ПараметрыФормы.Вставить("ВесЗадания", Объект.Вес);
	ПараметрыФормы.Вставить("ОбъемЗадания", Объект.Объем);
	ПараметрыФормы.Вставить("ДатаВремяРейсаПланС", Объект.ДатаВремяРейсаПланС);
	ПараметрыФормы.Вставить("АдресСпискаТоваровКДоставке",
		ПоместитьВХранилищеТоварыКДоставке(СтрокаРаспоряжений.Распоряжение, СтрокаРаспоряжений.Склад));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодбораТоваров",ЭтотОбъект);
	
	ОткрытьФорму("Обработка.РабочееМестоМенеджераПоДоставке.Форма.ФормаПодбораТоваровКДоставке",
		ПараметрыФормы, ЭтаФорма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаСервере
Функция ПоместитьВХранилищеТоварыКДоставке(Распоряжение, Склад)
	
	Возврат ПоместитьВоВременноеХранилище(ТоварыКДоставке.Выгрузить(Новый Структура("Распоряжение, Склад",Распоряжение,Склад)));
	
КонецФункции

&НаКлиенте
Процедура ПослеПодбораТоваров(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	ПослеПодбораТоваровСервер(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ПослеПодбораТоваровСервер(Результат)
	
	Модифицированность = Истина;
	СтрокаРаспоряжений = Объект.Распоряжения.НайтиПоИдентификатору(Элементы.Распоряжения.ТекущаяСтрока);
	СтруктураПоиска = Новый Структура("Распоряжение, Склад");
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаРаспоряжений);
	Для Каждого Стр Из ТоварыКДоставке.НайтиСтроки(СтруктураПоиска) Цикл
		ТоварыКДоставке.Удалить(Стр);
	КонецЦикла;
	
	
	ТаблицаТовары = ПолучитьИзВременногоХранилища(Результат.АдресТоварыКДоставке);
	
	Если ТаблицаТовары.Количество() = 0 Тогда
		ТекущаяСтрока = Элементы.Распоряжения.ТекущаяСтрока;
		РаспоряженияПередУдалениемСервер(ТекущаяСтрока);
		Объект.Распоряжения.Удалить(Объект.Распоряжения.НайтиПоИдентификатору(ТекущаяСтрока));
		
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаТовары, ТоварыКДоставке);
	
	ИзменениеВес = Результат.ВесРаспоряжения - СтрокаРаспоряжений.Вес;
	ИзменениеОбъем = Результат.ОбъемРаспоряжения - СтрокаРаспоряжений.Объем;
	
	СтрокаРаспоряжений.Вес = Результат.ВесРаспоряжения;
	СтрокаРаспоряжений.Объем = Результат.ОбъемРаспоряжения;
	СтрокаРаспоряжений.ДоставляетсяПолностью = ТаблицаТовары[0].ВсеТовары;
	
	ИзменитьВесОбъемМаршрута(СтрокаРаспоряжений.КлючСвязи, ИзменениеВес, ИзменениеОбъем);
	
	ОбновитьИтоговыйВесОбъемЗаполненность();
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьВесОбъемМаршрута(КлючСвязи, ИзменениеВес, ИзменениеОбъем)
	
	СтрокаМаршрута = СтрокаМаршрутаПоКлючуСвязи(КлючСвязи);
	СтрокаМаршрута.Вес = СтрокаМаршрута.Вес + ИзменениеВес;
	СтрокаМаршрута.Объем = СтрокаМаршрута.Объем + ИзменениеОбъем;
	
КонецПроцедуры

&НаСервере
Функция СтрокаМаршрутаПоКлючуСвязи(КлючСвязи)
	Возврат Объект.Маршрут.НайтиСтроки(Новый Структура("КлючСвязи",КлючСвязи))[0];
КонецФункции

&НаСервере
Функция ЗаполнитьПоРасходнымОрдерамНаСервере()
	
	ЕстьИзменения = ДоставкаТоваров.ПерезаполнитьЗаданиеНаПеревозкуПоРасходнымОрдерам(Объект, ТоварыКДоставке);
	ОбновитьИтоговыйВесОбъемЗаполненность();
	ОбновитьСкладыПогрузки();
	ЗаполнитьПризнакНаличияТранспортнойНакладнойПоКаждомуАдресуДоставки();
	Возврат ЕстьИзменения;
	
КонецФункции

&НаСервере
Процедура НастроитьПоТипуИсполнителя()
	
	ВыполняетПеревозчик = Объект.ЗаданиеВыполняет = Перечисления.ТипыИсполнителейЗаданийНаПеревозку.Перевозчик;
	
	Элементы.Перевозчик.Видимость           = ВыполняетПеревозчик;
	Элементы.Маршрут.Видимость              = Не ВыполняетПеревозчик;
	Элементы.Водитель.Видимость             = Не ВыполняетПеревозчик;
	Элементы.КурьерЭкспедитор.Видимость     = Не ВыполняетПеревозчик;
	Элементы.ГруппаРеквизитыТТН.Видимость 	= ВыполняетПеревозчик;
	
	Если ВыполняетПеревозчик Тогда
		Элементы.ФормаОформитьТранспортныеНакладные.Видимость = Истина;
		Элементы.КоманднаяПанельРаспоряженияВнеТаблицы.Видимость = Ложь;
		Элементы.КоманднаяПанельРаспоряженияПриТаблице.Видимость = Истина;
		Элементы.ГруппаРаспоряжения.Заголовок = НСтр("ru='Распоряжения';uk='Розпорядження'");
		Элементы.Распоряжения.ОтборСтрок = Неопределено;
	Иначе
		Элементы.ФормаОформитьТранспортныеНакладные.Видимость = Ложь;
		Элементы.КоманднаяПанельРаспоряженияВнеТаблицы.Видимость = Истина;
		Элементы.КоманднаяПанельРаспоряженияПриТаблице.Видимость = Ложь;
		Элементы.ГруппаРаспоряжения.Заголовок = НСтр("ru='Маршрут и распоряжения';uk='Маршрут і розпорядження'");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыТТНЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Ответ) <> Тип("Структура") Тогда 
		Возврат;
	КонецЕсли;
	
	СтруктураРеквизитов = Ответ;
	
	ЗаполнитьЗначенияСвойств(Объект, СтруктураРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура ПеревозчикПриИзмененииНаСервере()
КонецПроцедуры

&НаСервере
Процедура ВодительПриИзмененииНаСервере()
	
	ПараметрыТТН = Новый Структура;
	ПараметрыТТН.Вставить("ВодительФИО");
	ПараметрыТТН.Вставить("УдостоверениеСерия");
	ПараметрыТТН.Вставить("УдостоверениеНомер");
	
	Если ЗначениеЗаполнено(Объект.ТранспортноеСредство) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФизическиеЛица.Наименование КАК ВодительФИО,
		|	ДокументыФизическихЛиц.Серия КАК УдостоверениеСерия,
		|	ДокументыФизическихЛиц.Номер КАК УдостоверениеНомер
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДокументыФизическихЛиц.СрезПоследних КАК ДокументыФизическихЛиц
		|		ПО ФизическиеЛица.Ссылка = ДокументыФизическихЛиц.Физлицо
		|			И (ДокументыФизическихЛиц.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ВодительскоеУдостоверение))
		|ГДЕ
		|	ФизическиеЛица.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Объект.Водитель);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ПараметрыТТН, Выборка);
		КонецЕсли;			
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект, ПараметрыТТН);

КонецПроцедуры

&НаКлиенте
Процедура МаршрутДополнительнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Оповещение = Новый ОписаниеОповещения("МаршрутДополнительнаяИнформацияНачалоВыбораЗавершение", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияМногострочногоТекста(
		Оповещение,
		Элементы.МаршрутДополнительнаяИнформация.ТекстРедактирования,
		Элементы.Маршрут.ТекущиеДанные.ДополнительнаяИнформация);
КонецПроцедуры

&НаКлиенте
Процедура МаршрутДополнительнаяИнформацияНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт

	Если РезультатЗакрытия <> Неопределено Тогда
		ТекущиеДанные = Элементы.Маршрут.ТекущиеДанные;
		ТекущиеДанные.ДополнительнаяИнформация = РезультатЗакрытия;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
