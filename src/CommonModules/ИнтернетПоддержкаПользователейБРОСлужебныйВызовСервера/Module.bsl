
////////////////////////////////////////////////////////////////////////////////
// Подсистема "Интернет-поддержка пользователей (БРО)".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Функция ПроверитьВозможностьВыполненияОперацииНаСервере(ПараметрыАутентификации) Экспорт
	
	Если ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		Возврат "ВыполнениеРазрешено";		
	Иначе
		Если Не ЗначениеЗаполнено(ПараметрыАутентификации) Тогда
			ПараметрыАутентификации = СтандартныеПодсистемыСервер.ПараметрыАутентификацииНаСайте();
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПараметрыАутентификации) Тогда
			Возврат "ПараметрыАутентификацииНеЗаполнены";
		Иначе
			Результат = ПараметрыАутентификацииКорректные(ПараметрыАутентификации);
			Если Результат = "НекорректноеИмяПользователяИлиПароль" Тогда
				Возврат "ПараметрыАутентификацииУказаныНеВерно";
			ИначеЕсли Результат = "АутентификацияВыполнена" Тогда				
				Возврат "ВыполнениеРазрешено";
			ИначеЕсли Результат = "НеизвестнаяОшибка" Тогда
				Возврат "НеизвестнаяОшибкаПриПроверке";
			Иначе
				Возврат "ВыполнениеЗапрещено";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ПараметрыАутентификацииКорректные(ПараметрыАутентификацииНаСайте, ОписаниеОшибки = "")
	
	Если НЕ ЗначениеЗаполнено(ПараметрыАутентификацииНаСайте)
		ИЛИ НЕ ЗначениеЗаполнено(ПараметрыАутентификацииНаСайте.Логин)
		ИЛИ НЕ ЗначениеЗаполнено(ПараметрыАутентификацииНаСайте.Пароль) Тогда
		Результат = "НекорректноеИмяПользователяИлиПароль";
		Возврат Результат;
	КонецЕсли;
	
	Результат = "НеизвестнаяОшибка";
	Билет = "";
	Попытка
		ВебСервис = ОбщегоНазначения.WSПрокси(
			"https://login.bas-soft.eu/api/public/ticket?wsdl",
			"http://api.cas.jasig.org/",
			"TicketApiImplService",
			"TicketApiImplPort",
			ПараметрыАутентификацииНаСайте.Логин,
			ПараметрыАутентификацииНаСайте.Пароль,
			5,
			Ложь);
		
		Билет = ВебСервис.getTicket(
			ПараметрыАутентификацииНаСайте.Логин,
			ПараметрыАутентификацииНаСайте.Пароль,
			"its.bas-soft.eu");
			
		Если ЗначениеЗаполнено(Билет) Тогда
			Результат = "АутентификацияВыполнена";
		КонецЕсли;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		КраткоеПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		Если СтрНайти(КраткоеПредставлениеОшибки, "IncorrectLoginOrPasswordApiException") > 0 Тогда
			Результат = "НекорректноеИмяПользователяИлиПароль";
		КонецЕсли;
		ЗаписьЖурналаРегистрации(
			НСтр("ru='БРО. Интернет-поддержка пользователей. getTicket';uk='БРО. Інтернет-підтримка користувачів. getTicket'"),
		    УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти