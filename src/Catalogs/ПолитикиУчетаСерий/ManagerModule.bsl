#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//Возвращает имена блокруемых реквизитов для механизма блокирования реквизитов БСП
//	Возвращаемое значание:
//		Массив - имена блокируемых реквизитов
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт

	Результат = Новый Массив;
	Результат.Добавить("ТипПолитики");
	
	Результат.Добавить("УказыватьПриОтгрузке");
	Результат.Добавить("УказыватьПриОтгрузкеВРозницу");
	Результат.Добавить("УказыватьПриОтгрузкеКлиенту");
	Результат.Добавить("УказыватьПриОтгрузкеКомплектовДляРазборки");
	Результат.Добавить("УказыватьПриОтгрузкеКомплектующихДляСборки");
	Результат.Добавить("УказыватьПриОтгрузкеНаВнутренниеНужды");
	Результат.Добавить("УказыватьПриОтгрузкеПоВозвратуПоставщику");
	Результат.Добавить("УказыватьПриОтгрузкеПоПеремещению");
	
	Результат.Добавить("УказыватьПриПриемке");
	Результат.Добавить("УказыватьПриПриемкеКомплектующихПослеРазборки");
	Результат.Добавить("УказыватьПриПриемкеОтПоставщика");
	Результат.Добавить("УказыватьПриПриемкеПоВозвратуОтКлиента");
	Результат.Добавить("УказыватьПриВозвратеНепринятыхПолучателемТоваров");
	Результат.Добавить("УказыватьПриПриемкеПоПеремещению");
	Результат.Добавить("УказыватьПриПриемкеПоПрочемуОприходованию");
	Результат.Добавить("УказыватьПриПриемкеСобранныхКомплектов");
	
	Результат.Добавить("УказыватьПриОтраженииИзлишков");
	Результат.Добавить("УказыватьПриОтраженииНедостач");
	Результат.Добавить("УказыватьПриПеремещенииМеждуПомещениями");

	
	Возврат Результат;

КонецФункции

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	НоменклатураВызовСервера.ПолитикиУчетаСерийОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы


// Обработчик обновления BAS УТ 3.2.1
//	Взводит новые флаги:
//	- "Указывать серии при отражении недостач" - если взведен флаг "Указывать серии при отражении излишков, недостач и порчи" (старое название);
//	- "Указывать серии при приемке непринятых получателем товаров", если был взведен флаг "Указывать серии при отгрузке"
Процедура ВзвестиФлагиИзлишкиНедостачиВозвратНепринятых() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПолитикиУчетаСерий.УказыватьПриПриемке КАК УказыватьПриПриемке,
	|	ПолитикиУчетаСерий.УказыватьПриОтгрузке КАК УказыватьПриОтгрузке,
	|	ПолитикиУчетаСерий.ТипПолитики КАК ТипПолитики,
	|	ПолитикиУчетаСерий.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|ГДЕ
	|		НЕ ПолитикиУчетаСерий.УказыватьПриОтраженииНедостач
	|		И НЕ ПолитикиУчетаСерий.ТипПолитики
	|				= ЗНАЧЕНИЕ(Перечисление.ТипыПолитикУказанияСерий.АвторасчетПоFEFOОстатковСерий)
	|		И НЕ ПолитикиУчетаСерий.ТипПолитики
	|				= ЗНАЧЕНИЕ(Перечисление.ТипыПолитикУказанияСерий.СправочноеУказаниеСерий)
	|	ИЛИ
	|		НЕ ПолитикиУчетаСерий.УказыватьПриВозвратеНепринятыхПолучателемТоваров
	|		И (НЕ ПолитикиУчетаСерий.ТипПолитики
	|				= ЗНАЧЕНИЕ(Перечисление.ТипыПолитикУказанияСерий.СправочноеУказаниеСерий)
	|			ИЛИ ПолитикиУчетаСерий.УказыватьПриОтгрузке И ПолитикиУчетаСерий.УказыватьПриПриемке)
	|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если Не Выборка.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.СправочноеУказаниеСерий
			И Не Выборка.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.АвторасчетПоFEFOОстатковСерий Тогда
			СправочникОбъект.УказыватьПриОтраженииНедостач = Истина;
		КонецЕсли;
		Если Не Выборка.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.СправочноеУказаниеСерий
			Или Выборка.УказыватьПриОтгрузке И Выборка.УказыватьПриПриемке Тогда
			СправочникОбъект.УказыватьПриВозвратеНепринятыхПолучателемТоваров = Истина;
		КонецЕсли;
        
        ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
		
	КонецЦикла;

КонецПроцедуры

// Обработчик обновления BAS УТ 3.2.1
// Взводит флаг: ДляСклада
Процедура ЗаполнитьРеквизитДляСклада() Экспорт    

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПолитикиУчетаСерий.Ссылка
	|ИЗ
	|	Справочник.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|ГДЕ
	|	НЕ ПолитикиУчетаСерий.ДляСклада
	|	И (ПолитикиУчетаСерий.УказыватьПриПриемке
	|			ИЛИ ПолитикиУчетаСерий.УказыватьПриПересчетеТоваров
	|			ИЛИ ПолитикиУчетаСерий.УказыватьПриОтгрузке
	|			ИЛИ ПолитикиУчетаСерий.УказыватьПриОтраженииИзлишков
	|			ИЛИ ПолитикиУчетаСерий.УказыватьПриОтраженииНедостач
	|			ИЛИ ПолитикиУчетаСерий.УказыватьПриПеремещенииМеждуПомещениями)
	|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
 		
        СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
 			
        СправочникОбъект.ДляСклада = СправочникОбъект.УказыватьПриПриемке
                                        ИЛИ СправочникОбъект.УказыватьПриПересчетеТоваров
                                        ИЛИ СправочникОбъект.УказыватьПриОтгрузке
                                        ИЛИ СправочникОбъект.УказыватьПриОтраженииИзлишков
                                        ИЛИ СправочникОбъект.УказыватьПриОтраженииНедостач
                                        ИЛИ СправочникОбъект.УказыватьПриПеремещенииМеждуПомещениями;
											
 		ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
 			
 	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#КонецЕсли