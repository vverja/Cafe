#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получает реквизиты объекта, которые необходимо блокировать от изменения
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Массив - блокируемые реквизиты объекта
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт

	Результат = Новый Массив;
	Результат.Добавить("Владелец");
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	КодЯзыка               = Локализация.ПолучитьЯзыкФормированияПечатныхФорм();
    КодЯзыкаАльтернативный = ?(КодЯзыка = "uk", "ru", "uk");
	ПараметрыВывода = Новый Структура;
	ПараметрыВывода.Вставить("КодЯзыкаДляМногоязычныхПечатныхФорм",	КодЯзыка);	
	ПараметрыВывода = УправлениеПечатью.ПодготовитьСтруктуруПараметровВывода(ПараметрыВывода);
	ПараметрыВыводаАльтернативные = Новый Структура;
	ПараметрыВыводаАльтернативные.Вставить("КодЯзыкаДляМногоязычныхПечатныхФорм", КодЯзыкаАльтернативный);
	ПараметрыВыводаАльтернативные = УправлениеПечатью.ПодготовитьСтруктуруПараметровВывода(ПараметрыВыводаАльтернативные);
    
	// Анкета (Microsoft Word)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "КартыЛояльностиКлиент.ПечатьАнкетыMicrosoftWord";
	КомандаПечати.МенеджерПечати = "";
	КомандаПечати.Идентификатор = "АнкетаMicrosoftWord";
    КомандаПечати.Представление = ?(КодЯзыка = "ru", НСтр("ru='Анкета (Microsoft Word, русский язык)';uk='Анкета (Microsoft Word, російська мова)'"), НСтр("ru='Анкета (Microsoft Word, украинский язык)';uk='Анкета (Microsoft Word, українська мова)'"));
    КомандаПечати.ДополнительныеПараметры.Вставить("ПараметрыВывода", ПараметрыВывода);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
    КомандаПечати.Порядок = 20;
    
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "КартыЛояльностиКлиент.ПечатьАнкетыMicrosoftWord";
	КомандаПечати.МенеджерПечати = "";
	КомандаПечати.Идентификатор = "АнкетаMicrosoftWord";
    КомандаПечати.Представление = ?(КодЯзыкаАльтернативный = "ru", НСтр("ru='Анкета (Microsoft Word, русский язык)';uk='Анкета (Microsoft Word, російська мова)'"), НСтр("ru='Анкета (Microsoft Word, украинский язык)';uk='Анкета (Microsoft Word, українська мова)'"));
    КомандаПечати.ДополнительныеПараметры.Вставить("ПараметрыВывода", ПараметрыВыводаАльтернативные);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
    КомандаПечати.Порядок = 30;

	// Анкета (OpenOffice.org Writer)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "КартыЛояльностиКлиент.ПечатьАнкетыOpenOfficeOrgWriter";
	КомандаПечати.МенеджерПечати = "";
	КомандаПечати.Идентификатор = "АнкетаOpenOfficeOrgWriter";
    КомандаПечати.Представление = ?(КодЯзыка = "ru", НСтр("ru='Анкета (OpenOffice.org Writer, русский язык)';uk='Анкета (OpenOffice.org Writer, російська мова)'"), НСтр("ru='Анкета (OpenOffice.org Writer, украинский язык)';uk='Анкета (OpenOffice.org Writer, українська мова)'"));
    КомандаПечати.ДополнительныеПараметры.Вставить("ПараметрыВывода", ПараметрыВывода);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
    КомандаПечати.Порядок = 40;
    
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "КартыЛояльностиКлиент.ПечатьАнкетыOpenOfficeOrgWriter";
	КомандаПечати.МенеджерПечати = "";
	КомандаПечати.Идентификатор = "АнкетаOpenOfficeOrgWriter";
    КомандаПечати.Представление = ?(КодЯзыкаАльтернативный = "ru", НСтр("ru='Анкета (OpenOffice.org Writer, русский язык)';uk='Анкета (OpenOffice.org Writer, російська мова)'"), НСтр("ru='Анкета (OpenOffice.org Writer, украинский язык)';uk='Анкета (OpenOffice.org Writer, українська мова)'"));
    КомандаПечати.ДополнительныеПараметры.Вставить("ПараметрыВывода", ПараметрыВыводаАльтернативные);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
    КомандаПечати.Порядок = 50;

	// Анкета (mxl)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "";
	КомандаПечати.Идентификатор = "Анкета";
	КомандаПечати.Представление = НСтр("ru='Анкета (mxl)';uk='Анкета (mxl)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
    КомандаПечати.Порядок = 10;

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Анкета") Тогда
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
            КоллекцияПечатныхФорм, 
            "Анкета", 
            "Анкета", 
            СформироватьПечатнуюФормуАнкеты(МассивОбъектов, ОбъектыПечати, ПараметрыВывода),
            ,
            ,
            ,
            Истина
        );
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьДанныеПечати(знач МассивДокументов, знач МассивИменМакетов, ПараметрыВывода) Экспорт    
	
	Возврат Новый Структура("Данные,Макеты",
				ПолучитьДанныеОбъектаПоМакетам(МассивДокументов, МассивИменМакетов),
                ПолучитьМакетыИОписанияСекций(МассивИменМакетов, ПараметрыВывода));
	
КонецФункции

Функция ПолучитьОписаниеОбластейАнкета()

	Секции = Новый Структура;
	
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "Заголовок",    "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "Штрихкод",     "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "МагнитныйКод", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(Секции, "Подвал",       "Общая");
	
	Возврат Секции;

КонецФункции

Функция ПолучитьДанныеОбъектаПоМакетам(знач МассивДокументов, знач МассивИменМакетов) Экспорт
	
	ДанныеПоВсемОбъектам = Новый Соответствие;
	
	РезультатЗапроса = ПолучитьДанныеДляПечати(МассивДокументов);
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДанныеОбъектаПоМакетам = Новый Структура;
		ДанныеОбъекта = ПолучитьДанныеОбъектаПоВыборке(Выборка);
		Для Каждого ИмяМакета Из МассивИменМакетов Цикл
			ДанныеОбъектаПоМакетам.Вставить(ИмяМакета, ДанныеОбъекта);
		КонецЦикла;
		ДанныеПоВсемОбъектам.Вставить(Выборка.Ссылка, ДанныеОбъектаПоМакетам);
	КонецЦикла;
	
	Возврат ДанныеПоВсемОбъектам;
	
КонецФункции

Функция ПолучитьМакетыИОписанияСекций(знач МассивИменМакетов, ПараметрыВывода) Экспорт
    
	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;	
	
	ОписаниеСекций = Новый Структура;
	ДвоичныеДанныеМакетов = Новый Структура;
	
	Для Каждого ИмяМакета Из МассивИменМакетов Цикл
		
		Макет = Неопределено;
		ОписаниеСекцийМакета = Неопределено;
		
		Если ИмяМакета = "ПФ_DOC_Анкета" Тогда
			ОписаниеСекцийМакета = ПолучитьОписаниеОбластейАнкета();
            Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.КартыЛояльности.ПФ_DOC_Анкета", КодЯзыкаПечать);
		ИначеЕсли ИмяМакета = "ПФ_ODT_Анкета" Тогда
			ОписаниеСекцийМакета = ПолучитьОписаниеОбластейАнкета();
            Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.КартыЛояльности.ПФ_ODT_Анкета", КодЯзыкаПечать);
		КонецЕсли;
		
		Если ОписаниеСекцийМакета <> Неопределено И Макет <> Неопределено Тогда
			
			ОписаниеСекций.Вставить(ИмяМакета, ОписаниеСекцийМакета);
			ДвоичныеДанныеМакетов.Вставить(ИмяМакета, Макет);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Новый Структура(
						"ОписаниеСекций,ДвоичныеДанныеМакетов",
						ОписаниеСекций,
						ДвоичныеДанныеМакетов);
	
КонецФункции

Функция СформироватьПечатнуюФормуАнкеты(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)    
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	РезультатЗапроса = ПолучитьДанныеДляПечати(МассивОбъектов);
	Выборка = РезультатЗапроса.Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока Выборка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
        
        ЗаполнитьТабличныйДокументПоДаннымОбъекта(ТабличныйДокумент, ПолучитьДанныеОбъектаПоВыборке(Выборка), ПараметрыВывода);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		
	КонецЦикла;
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПолучитьДанныеДляПечати(МассивОбъектов)
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КартыЛояльности.Ссылка                                  КАК Ссылка,
	|	КартыЛояльности.Штрихкод                                КАК Штрихкод,
	|	КартыЛояльности.МагнитныйКод                            КАК МагнитныйКод,
	|	КартыЛояльности.Владелец.Наименование                   КАК ВидКарты,
	|	КартыЛояльности.Владелец.ТипКарты                       КАК ТипКарты,
	|	КартыЛояльности.Владелец.Организация.НаименованиеПолное КАК Организация
	|ИЗ
	|	Справочник.КартыЛояльности КАК КартыЛояльности
	|ГДЕ
	|	КартыЛояльности.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	КартыЛояльности.Ссылка";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ПолучитьДанныеОбъектаПоВыборке(Выборка)
	
	ДанныеОбъекта = Новый Структура;
	
	ДанныеОбъекта.Вставить("Ссылка",       Выборка.Ссылка);
	ДанныеОбъекта.Вставить("ТипКарты",     Выборка.ТипКарты);
	ДанныеОбъекта.Вставить("Штрихкод",     Выборка.Штрихкод);
	ДанныеОбъекта.Вставить("МагнитныйКод", Выборка.МагнитныйКод);
	ДанныеОбъекта.Вставить("ВидКарты",     Выборка.ВидКарты);
	ДанныеОбъекта.Вставить("Организация",  Выборка.Организация);
	
	Возврат ДанныеОбъекта;
	
КонецФункции

Процедура ЗаполнитьТабличныйДокументПоДаннымОбъекта(ТабличныйДокумент, ДанныеОбъекта, ПараметрыВывода)    
    
	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.КартыЛояльности.ПФ_MXL_Анкета", КодЯзыкаПечать);
	
	Область = Макет.ПолучитьОбласть("Заголовок");
	Область.Параметры.ВидКарты = ДанныеОбъекта.ВидКарты;
	ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, Область, ДанныеОбъекта.Ссылка);
	ТабличныйДокумент.Вывести(Область);
	
	Если ДанныеОбъекта.ТипКарты = ПредопределенноеЗначение("Перечисление.ТипыКарт.Штриховая")
		ИЛИ ДанныеОбъекта.ТипКарты = ПредопределенноеЗначение("Перечисление.ТипыКарт.Смешанная") Тогда
		Область = Макет.ПолучитьОбласть("Штрихкод");
		Область.Параметры.Штрихкод = ДанныеОбъекта.Штрихкод;
		ТабличныйДокумент.Вывести(Область);
	КонецЕсли;
	
	Если ДанныеОбъекта.ТипКарты = ПредопределенноеЗначение("Перечисление.ТипыКарт.Магнитная")
		ИЛИ ДанныеОбъекта.ТипКарты = ПредопределенноеЗначение("Перечисление.ТипыКарт.Смешанная") Тогда
		Область = Макет.ПолучитьОбласть("МагнитныйКод");
		Область.Параметры.МагнитныйКод = ДанныеОбъекта.МагнитныйКод;
		ТабличныйДокумент.Вывести(Область);
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть("Подвал");
	Область.Параметры.ВидКарты = ДанныеОбъекта.ВидКарты;
	Область.Параметры.Организация = ДанныеОбъекта.Организация;
	ТабличныйДокумент.Вывести(Область);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
