
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	// Обработчик подсистемы "Дополнительные отчеты и обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	
	//установить заголовок по отбору
	Если Параметры.Свойство("РозничныеМагазины") Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = НСтр("ru='Розничные магазины';uk='Роздрібні магазини'");
	КонецЕсли;
		
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРозничныеПродажи") Тогда
		Элементы.ФормаСоздать.Видимость = Ложь;
		Элементы.ГруппаСоздатьСклад.Видимость = Истина;
	Иначе
		Элементы.ФормаСоздать.Видимость = Истина;
		Элементы.ГруппаСоздатьСклад.Видимость = Ложь;
	КонецЕсли;
	
	Если НЕ ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Справочники.Склады) Тогда
		Элементы.ГруппаСоздатьСклад.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.Список.ИзменятьСоставСтрок = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов");
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура СоздатьОптовыйСклад(Команда)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ТипСклада", ПредопределенноеЗначение("Перечисление.ТипыСкладов.ОптовыйСклад"));
	ЗначенияЗаполнения.Вставить("Группа", ТекущийЭлемент.ТекущийРодитель);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Справочник.Склады.ФормаОбъекта", СтруктураПараметров, ЭтаФорма);
	
КонецПроцедуры
		
&НаКлиенте
Процедура СоздатьРозничныйМагазин(Команда)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ТипСклада", ПредопределенноеЗначение("Перечисление.ТипыСкладов.РозничныйМагазин"));
	ЗначенияЗаполнения.Вставить("Группа", ТекущийЭлемент.ТекущийРодитель);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Справочник.Склады.ФормаОбъекта", СтруктураПараметров, ЭтаФорма);
	
КонецПроцедуры

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Элементы.Список);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#КонецОбласти
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры


#КонецОбласти

