#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если РасчетСебестоимости.ДвиженияЗаписываютсяРасчетомСебестоимости(ЭтотОбъект)
	 ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
	 ИЛИ ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	РасчетСебестоимости.СохранитьДвиженияСформированныеРасчетомСебестоимости(ЭтотОбъект, Замещение);
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Себестоимость.Период                        КАК Период,
	|	Себестоимость.Регистратор                   КАК Регистратор,
	|	Себестоимость.АналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры,
	|	Себестоимость.РазделУчета                   КАК РазделУчета,
	|	Себестоимость.ВидЗапасов                    КАК ВидЗапасов,
	|	Себестоимость.Организация                   КАК Организация,
	|
	|	Себестоимость.Количество             КАК Количество,
	|	Себестоимость.Стоимость              КАК Стоимость,
	|	Себестоимость.СтоимостьБезНДС        КАК СтоимостьБезНДС,
	|	Себестоимость.СуммаДопРасходов       КАК СуммаДопРасходов,
	|	Себестоимость.СуммаДопРасходовБезНДС КАК СуммаДопРасходовБезНДС,
	|	Себестоимость.СтоимостьРегл          КАК СтоимостьРегл,
	|	Себестоимость.СтоимостьРеглБезНДС    КАК СтоимостьРеглБезНДС,
	|	Себестоимость.НДСРегл                КАК НДСРегл,
	|	Себестоимость.ПостояннаяРазница      КАК ПостояннаяРазница,
	|	Себестоимость.ВременнаяРазница       КАК ВременнаяРазница,
	|
	|	Себестоимость.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|	Себестоимость.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	|	Себестоимость.КорРазделУчета                КАК КорРазделУчета,
	|	Себестоимость.КорВидЗапасов                 КАК КорВидЗапасов,
	|	Себестоимость.КорОрганизация                КАК КорОрганизация,
	|	Себестоимость.КорСтоимость                  КАК КорСтоимость,
	|	Себестоимость.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
	|	Себестоимость.ЗаказКлиента                  КАК ЗаказКлиента,
	|	Себестоимость.Подразделение                 КАК Подразделение,
	|	Себестоимость.АналитикаРасходов             КАК АналитикаРасходов,
	|	Себестоимость.СтатьяРасходовСписания        КАК СтатьяРасходовСписания,
	|	Себестоимость.СтатьяДоходов                 КАК СтатьяДоходов,
	|	Себестоимость.АналитикаДоходов              КАК АналитикаДоходов,
	|	Себестоимость.ПериодПродажи                 КАК ПериодПродажи,
	|	Себестоимость.СтатьяАктивовПассивов         КАК СтатьяАктивовПассивов,
	|	Себестоимость.АналитикаАктивовПассивов      КАК АналитикаАктивовПассивов,
	|	Себестоимость.ДокументДвижения              КАК ДокументДвижения,
	|	Себестоимость.НалоговоеНазначение           КАК НалоговоеНазначение,
	|	Себестоимость.КорНалоговоеНазначение        КАК КорНалоговоеНазначение,
	|	Себестоимость.ИдентификаторСтроки           КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ СебестоимостьТоваровПередЗаписью
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
	|ГДЕ
	|	Себестоимость.Регистратор = &Регистратор
	|");
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если РасчетСебестоимости.ДвиженияЗаписываютсяРасчетомСебестоимости(ЭтотОбъект)
	 ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
	 ИЛИ ОбменДанными.Загрузка
	 ИЛИ ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК МЕСЯЦ,
	|	Таблица.Организация                  КАК Организация,
	|	Таблица.Регистратор                  КАК Документ
	|ИЗ
	|	(ВЫБРАТЬ
	|		Себестоимость.Период                        КАК Период,
	|		Себестоимость.Регистратор                   КАК Регистратор,
	|		Себестоимость.АналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры,
	|		Себестоимость.РазделУчета                   КАК РазделУчета,
	|		Себестоимость.ВидЗапасов                    КАК ВидЗапасов,
	|		Себестоимость.Организация                   КАК Организация,
	|
	|		Себестоимость.Количество             КАК Количество,
	|		Себестоимость.Стоимость              КАК Стоимость,
	|		Себестоимость.СтоимостьБезНДС        КАК СтоимостьБезНДС,
	|		Себестоимость.СуммаДопРасходов       КАК СуммаДопРасходов,
	|		Себестоимость.СуммаДопРасходовБезНДС КАК СуммаДопРасходовБезНДС,
	|		Себестоимость.СтоимостьРегл          КАК СтоимостьРегл,
	|		Себестоимость.СтоимостьРеглБезНДС    КАК СтоимостьРеглБезНДС,
	|		Себестоимость.НДСРегл                КАК НДСРегл,
	|		Себестоимость.ПостояннаяРазница      КАК ПостояннаяРазница,
	|		Себестоимость.ВременнаяРазница       КАК ВременнаяРазница,
	|
	|		Себестоимость.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|		Себестоимость.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	|		Себестоимость.КорРазделУчета                КАК КорРазделУчета,
	|		Себестоимость.КорВидЗапасов                 КАК КорВидЗапасов,
	|		Себестоимость.КорОрганизация                КАК КорОрганизация,
	|		Себестоимость.КорСтоимость                  КАК КорСтоимость,
	|		Себестоимость.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
	|		Себестоимость.ЗаказКлиента                  КАК ЗаказКлиента,
	|		Себестоимость.Подразделение                 КАК Подразделение,
	|		Себестоимость.АналитикаРасходов             КАК АналитикаРасходов,
	|		Себестоимость.СтатьяРасходовСписания        КАК СтатьяРасходовСписания,
	|		Себестоимость.СтатьяДоходов                 КАК СтатьяДоходов,
	|		Себестоимость.АналитикаДоходов              КАК АналитикаДоходов,
	|		Себестоимость.ПериодПродажи                 КАК ПериодПродажи,
	|		Себестоимость.СтатьяАктивовПассивов         КАК СтатьяАктивовПассивов,
	|		Себестоимость.АналитикаАктивовПассивов      КАК АналитикаАктивовПассивов,
	|		Себестоимость.ДокументДвижения              КАК ДокументДвижения,
	|		Себестоимость.НалоговоеНазначение           КАК НалоговоеНазначение,
	|		Себестоимость.КорНалоговоеНазначение        КАК КорНалоговоеНазначение,
	|		Себестоимость.ИдентификаторСтроки           КАК ИдентификаторСтроки
	|	ИЗ
	|		СебестоимостьТоваровПередЗаписью КАК Себестоимость
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Себестоимость.Период                        КАК Период,
	|		Себестоимость.Регистратор                   КАК Регистратор,
	|		Себестоимость.АналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры,
	|		Себестоимость.РазделУчета                   КАК РазделУчета,
	|		Себестоимость.ВидЗапасов                    КАК ВидЗапасов,
	|		Себестоимость.Организация                   КАК Организация,
	|
	|		-Себестоимость.Количество             КАК Количество,
	|		-Себестоимость.Стоимость              КАК Стоимость,
	|		-Себестоимость.СтоимостьБезНДС        КАК СтоимостьБезНДС,
	|		-Себестоимость.СуммаДопРасходов       КАК СуммаДопРасходов,
	|		-Себестоимость.СуммаДопРасходовБезНДС КАК СуммаДопРасходовБезНДС,
	|		-Себестоимость.СтоимостьРегл          КАК СтоимостьРегл,
	|		-Себестоимость.СтоимостьРеглБезНДС    КАК СтоимостьРеглБезНДС,
	|		-Себестоимость.НДСРегл                КАК НДСРегл,
	|		-Себестоимость.ПостояннаяРазница      КАК ПостояннаяРазница,
	|		-Себестоимость.ВременнаяРазница       КАК ВременнаяРазница,
	|
	|		Себестоимость.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|		Себестоимость.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	|		Себестоимость.КорРазделУчета                КАК КорРазделУчета,
	|		Себестоимость.КорВидЗапасов                 КАК КорВидЗапасов,
	|		Себестоимость.КорОрганизация                КАК КорОрганизация,
	|		Себестоимость.КорСтоимость                  КАК КорСтоимость,
	|		Себестоимость.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
	|		Себестоимость.ЗаказКлиента                  КАК ЗаказКлиента,
	|		Себестоимость.Подразделение                 КАК Подразделение,
	|		Себестоимость.АналитикаРасходов             КАК АналитикаРасходов,
	|		Себестоимость.СтатьяРасходовСписания        КАК СтатьяРасходовСписания,
	|		Себестоимость.СтатьяДоходов                 КАК СтатьяДоходов,
	|		Себестоимость.АналитикаДоходов              КАК АналитикаДоходов,
	|		Себестоимость.ПериодПродажи                 КАК ПериодПродажи,
	|		Себестоимость.СтатьяАктивовПассивов         КАК СтатьяАктивовПассивов,
	|		Себестоимость.АналитикаАктивовПассивов      КАК АналитикаАктивовПассивов,
	|		Себестоимость.ДокументДвижения              КАК ДокументДвижения,
	|		Себестоимость.НалоговоеНазначение           КАК НалоговоеНазначение,
	|		Себестоимость.КорНалоговоеНазначение        КАК КорНалоговоеНазначение,
	|		Себестоимость.ИдентификаторСтроки           КАК ИдентификаторСтроки
	|	ИЗ
	|		РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
	|	ГДЕ
	|		Себестоимость.Регистратор = &Регистратор
	|	) КАК Таблица
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ),
	|	Таблица.Период,
	|	Таблица.Регистратор,
	|	Таблица.АналитикаУчетаНоменклатуры,
	|	Таблица.РазделУчета,
	|	Таблица.ВидЗапасов,
	|	Таблица.Организация,
	|	Таблица.ХозяйственнаяОперация,
	|	Таблица.КорАналитикаУчетаНоменклатуры,
	|	Таблица.КорРазделУчета,
	|	Таблица.КорВидЗапасов,
	|	Таблица.КорОрганизация,
	|	Таблица.КорСтоимость,
	|	Таблица.АналитикаУчетаПоПартнерам,
	|	Таблица.ЗаказКлиента,
	|	Таблица.Подразделение,
	|	Таблица.АналитикаРасходов,
	|	Таблица.СтатьяРасходовСписания,
	|	Таблица.СтатьяДоходов,
	|	Таблица.АналитикаДоходов,
	|	Таблица.ПериодПродажи,
	|	Таблица.СтатьяАктивовПассивов,
	|	Таблица.АналитикаАктивовПассивов,
	|	Таблица.ДокументДвижения,
	|	Таблица.НалоговоеНазначение,
	|	Таблица.КорНалоговоеНазначение,
	|	Таблица.ИдентификаторСтроки
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Количество) <> 0
	|	ИЛИ СУММА(Таблица.Стоимость) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.СуммаДопРасходов) <> 0
	|	ИЛИ СУММА(Таблица.СуммаДопРасходовБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьРегл) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьРеглБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.НДСРегл) <> 0
	|	ИЛИ СУММА(Таблица.ПостояннаяРазница) <> 0
	|	ИЛИ СУММА(Таблица.ВременнаяРазница) <> 0
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СебестоимостьТоваровПередЗаписью
	|");
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	РегистрыСведений.ЗаданияКРасчетуСебестоимости.СоздатьЗаписиРегистраПоДаннымВыборки(Запрос.Выполнить().Выбрать());
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
