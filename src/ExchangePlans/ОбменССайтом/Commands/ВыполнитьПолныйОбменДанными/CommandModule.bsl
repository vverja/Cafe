
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ТекстСообщения = НСтр("ru='Внимание! Выполнение полного обмена может занять длительное время. Продолжить?';uk='Увага! Виконання повного обміну може зайняти тривалий час. Продовжити?'");
	
	ПараметрыОбмена = новый Структура;
	ПараметрыОбмена.Вставить("УзелОбмена", ПараметрКоманды);
	
	// Процедуру запуска обмена следует поместить в ОписаниеОповещения.
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьОбменОтмена", ЭтотОбъект, ПараметрыОбмена);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстСообщения, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбменОтмена(Ответ, ДопПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	УзелОбмена = ДопПараметры.УзелОбмена;
	
	СостояниеНачалоОбмена = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1 начат обмен данными с сайтом';uk='%1 розпочатий обмін даними з сайтом'"),
			Формат(ОбщегоНазначенияКлиент.ДатаСеанса(), "ДЛФ=DT"));
			
	СостояниеУзелОбмена = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='по узлу обмена ""%1""...';uk='по вузлу обміну ""%1""...'"),
			УзелОбмена);
			
	Состояние(СостояниеНачалоОбмена,, СостояниеУзелОбмена);
		
	ОбменВыполненСервер(УзелОбмена);
	
	СостояниеОкончаниеОбмена = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1 ""%2""';uk='%1 ""%2""'"),
			Формат(ОбщегоНазначенияКлиент.ДатаСеанса(), "ДЛФ=DT"),
			УзелОбмена);
			
	ПоказатьОповещениеПользователя(СостояниеОкончаниеОбмена ,,
		НСтр("ru='Обмен с сайтом завершен';uk='Обмін з сайтом завершений'"),
		БиблиотекаКартинок.Информация32);
		
	Оповестить("ЗавершенСеансОбменаССайтом");

КонецПроцедуры

&НаСервере
Функция ОбменВыполненСервер(УзелОбмена)

	Если ОбменССайтомПовтИсп.ПолучитьЭтотУзелПланаОбмена(УзелОбмена) Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	ОбменССайтомСобытия.ВыполнитьОбмен(УзелОбмена, НСтр("ru='Интерактивный обмен';uk='Інтерактивний обмін'"), Ложь);
	
	Возврат Истина;
	
КонецФункции

