
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);

	Если Параметры.Свойство("Владелец") Тогда
		Объект.Владелец = Параметры.Владелец;
	КонецЕсли;
	
	СсылкаКопирования = Параметры.ЗначениеКопирования;
	Если НЕ СсылкаКопирования.Пустая() Тогда
		
		ПрочитатьНаборСервер("ГрафикТраншей", СсылкаКопирования);
		ПрочитатьНаборСервер("ГрафикОплат", СсылкаКопирования);
		ПрочитатьНаборСервер("ГрафикНачислений", СсылкаКопирования);
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Объект.Утвержден = ЭтоПервый();
		Объект.Используется = Объект.Утвержден;
		ДоговорПриИзмененииСервер();
		
	КонецЕсли;
	
	Элементы.Используется.ТолькоПросмотр = Объект.Используется;
	Элементы.Утвержден.ТолькоПросмотр = Объект.Утвержден;
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбновитьСвязанныеДанные();
	ДоговорПриИзмененииСервер();
	УстановитьДоступностьПоДоговору();

	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.АвторИзменения = ПараметрыСеанса.ТекущийПользователь;
	ТекущийОбъект.ДатаИзменения = ТекущаяДата();
	
	Справочники.ВариантыГрафиковКредитовИДепозитов.ПересчитатьСроки(ТекущийОбъект);

	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьНаборСервер("ГрафикТраншей");
	ЗаписатьНаборСервер("ГрафикОплат");
	ЗаписатьНаборСервер("ГрафикНачислений");
	ОбновитьСвязанныеДанные();
	ОбновитьФлагиАктуальныхГрафиков();
	ИтогиГрафика = Справочники.ВариантыГрафиковКредитовИДепозитов.ИтогиГрафика(Объект.Ссылка);
	ПараметрыЗаписи.Вставить("ИтогиГрафика", ИтогиГрафика);

	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзмененыСвязанныеДанныеПоКредитуДепозиту", ПараметрыЗаписи, ЭтаФорма);

	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	ДоговорПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьГрафик(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьВариантГрафика(Команда)
	
	ЗаписатьГрафик();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьГрафик(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Ответ = Неопределено;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗагрузитьГрафикПослеВопроса", ЭтотОбъект, Новый Структура("Команда", Команда)), 
		НСтр("ru='Перед загрузкой необходимо записать объект.
            |Записать и продолжить?'
            |;uk='Перед завантаженням необхідно записати об''єкт.
            |Записати і продовжити?'"),
		РежимДиалогаВопрос.ДаНет);
		Возврат;
		
	КонецЕсли;
	
	ЗагрузитьГрафикФрагмент(Команда);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьГрафикПослеВопроса(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Команда = ДополнительныеПараметры.Команда;
    
    
    Ответ = РезультатВопроса;
    
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    Записать();
    
    
    ЗагрузитьГрафикФрагмент(Команда);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьГрафикФрагмент(Знач Команда)

    ТипГрафика = СтрЗаменить(Команда.Имя,"ЗагрузитьГрафик_","");
    ПараметрыФормы = Новый Структура("ВариантГрафика, ТипГрафика", Объект.Ссылка, ТипГрафика);
    ОткрытьФорму("Справочник.ВариантыГрафиковКредитовИДепозитов.Форма.ФормаЗагрузки", ПараметрыФормы,,,,, Новый ОписаниеОповещения("ЗагрузитьГрафикЗавершение", ЭтотОбъект, Новый Структура("ТипГрафика", ТипГрафика)), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьГрафикЗавершение(Результат, ДополнительныеПараметры) Экспорт

	ТипГрафика = ДополнительныеПараметры.ТипГрафика;
	ОбновитьСвязанныеДанные(ТипГрафика);
	Модифицированность = Истина;

КонецПроцедуры

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтаФорма, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
		ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтаФорма, РезультатВыполнения);
	КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

// МенюОтчеты
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуОтчет(Команда)
	
	МенюОтчетыКлиент.ВыполнитьПодключаемуюКомандуОтчет(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры
// Конец МенюОтчеты

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура ТаблицаГрафикаПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	УказаннаяСумма = Элемент.ТекущиеДанные.Проценты + Элемент.ТекущиеДанные.Комиссия;
	Если Элемент.Имя = "Оплаты" Тогда
		УказаннаяСумма = УказаннаяСумма + Элемент.ТекущиеДанные.Сумма;
	КонецЕсли;
	Если УказаннаяСумма = 0 И (НЕ НоваяСтрока ИЛИ НЕ ОтменаРедактирования)  Тогда
		Текст  = НСтр("ru='В строке должна быть указана хотя бы одна из сумм!';uk='У рядку повинна бути вказана хоча б одна із сум!'");
		Если НЕ НоваяСтрока И ОтменаРедактирования Тогда
			Текст  = НСтр("ru='Строки с нулевыми суммами не могут хранится в базе данных!
            |Удалите всю строку целиком.'
            |;uk='Рядки з нульовими сумами не можуть зберігається в базі даних!
            |Видаліть весь рядок.'");
		КонецЕсли;
		ПоказатьПредупреждение(,Текст);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

// ТРАНШИ
&НаКлиенте
Процедура ТраншиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Объект.СуммаТраншей = ГрафикТраншей.Итог("Сумма");
	ОбновитьДаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ТраншиПослеУдаления(Элемент)
	
	Объект.СуммаТраншей = ГрафикТраншей.Итог("Сумма");
	ОбновитьДаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатыПослеУдаления(Элемент)
	
	ОбновитьИтогиОплат();
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьИтогиОплат();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПослеУдаления(Элемент)
	
	ОбновитьИтогиНачислений();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьИтогиНачислений();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтаФорма, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

#Область ПриИзмененииРеквизитов

&НаКлиенте
Процедура ОбновитьДаты()
	
	// Обновим дату первого транша
	Если ГрафикТраншей.Количество() > 0 Тогда
		ГрафикТраншей.Сортировать("Период");
		Объект.ДатаПервогоТранша = ГрафикТраншей[0].Период;
	Иначе
		Объект.ДатаПервогоТранша = Неопределено;
	КонецЕсли;
	
	// Обновим дату последнего платежа
	Если ГрафикОплат.Количество() > 0 Тогда
		ГрафикОплат.Сортировать("Период");
		Объект.ДатаПоследнегоПлатежа = ГрафикОплат[ГрафикОплат.Количество()-1].Период;
	Иначе
		Объект.ДатаПоследнегоПлатежа = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИтогиНачислений()
	
	НачисленияПроцентов = ГрафикНачислений.Итог("Проценты");
	НачисленияКомиссии = ГрафикНачислений.Итог("Комиссия");
	ГрафикНачислений.Сортировать("Период");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИтогиОплат()
	
	СуммаОплаты = ГрафикОплат.Итог("Сумма");
	Объект.СуммаПроцентов = ГрафикОплат.Итог("Проценты");
	Объект.СуммаКомиссии = ГрафикОплат.Итог("Комиссия");
	ОбновитьДаты();
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ДоговорПриИзмененииСервер()
	
	РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Владелец,"ВалютаВзаиморасчетов,ХарактерДоговора");
	ВалютаДоговора = РеквизитыДоговора.ВалютаВзаиморасчетов;
	ХарактерДоговора = РеквизитыДоговора.ХарактерДоговора;
	
	ПрописатьВалюту(ВалютаДоговора);
	УстановитьДоступностьПоДоговору();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьПоДоговору()
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Владелец,"Статус,ТипКомиссии");
	ДоговорЗакрыт = Реквизиты.Статус = Перечисления.СтатусыДоговоровКонтрагентов.Закрыт;
	Элементы.НадписьДоговорЗакрыт.Видимость = ДоговорЗакрыт;
	
	ДоступностьЭлементов = Реквизиты.Статус <> Перечисления.СтатусыДоговоровКонтрагентов.Закрыт;
	ЕстьКомиссия = Реквизиты.ТипКомиссии <> Перечисления.ТипыКомиссииКредитовИДепозитов.Нет;
	Элементы.ОплатыКомиссия.Видимость = ЕстьКомиссия;
	Элементы.СуммаКомиссии.Видимость = ЕстьКомиссия;
	Элементы.НачисленияКомиссия.Видимость = ЕстьКомиссия;
	Элементы.НачисленияИтогКомиссии.Видимость = ЕстьКомиссия;
	
	// Элементы управления шапки
	МассивДействует = Новый Массив();
	МассивДействует.Добавить("ФормаЗаписать");
	МассивДействует.Добавить("ФормаЗаписатьИЗакрыть");
	МассивДействует.Добавить("Флаги");
	МассивДействует.Добавить("ОписаниеДоговора");
	МассивДействует.Добавить("НаименованиеКод");
	МассивДействует.Добавить("ТраншиКоманднаяПанель");
	МассивДействует.Добавить("ОплатыКоманднаяПанель");
	МассивДействует.Добавить("НачисленияКоманднаяПанель");
	МассивДействует.Добавить("Комментарий");
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивДействует, "Доступность", ДоступностьЭлементов);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаборСервер(ТипГрафика)
	
	ИмяНабора = "";
	Набор = НаборГрафика(ТипГрафика, ИмяНабора);
	Набор.Заполнить(Новый Структура("ВариантГрафика",Объект.Ссылка));
	Набор.Записать();
	ЗначениеВРеквизитФормы(Набор,ИмяНабора);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяНабораГрафика(ТипГрафика)
	
	Если ТипГрафика = "Транши" Тогда
		ИмяНабора = "ГрафикТраншей";
	ИначеЕсли ТипГрафика = "Оплаты" Тогда
		ИмяНабора = "ГрафикОплат";
	ИначеЕсли ТипГрафика = "Начисления" Тогда
		ИмяНабора = "ГрафикНачислений";
	Иначе
		ИмяНабора = ТипГрафика;
	КонецЕсли;
	
	Возврат ИмяНабора;
	
КонецФункции

&НаСервере
Функция НаборГрафика(ТипГрафика, ИмяНабора = "", СсылкаОбъекта = Неопределено)
	
	Если СсылкаОбъекта = Неопределено Тогда
		СсылкаОбъекта = Объект.Ссылка;
	КонецЕсли;
	
	ИмяНабора = ИмяНабораГрафика(ТипГрафика);
	Набор = РеквизитФормыВЗначение(ИмяНабора, Тип("РегистрСведенийНаборЗаписей."+ИмяНабора+"КредитовИДепозитов"));
	Набор.Отбор.ВариантГрафика.Установить(СсылкаОбъекта);
	
	Возврат Набор;
	
КонецФункции

&НаСервере
Процедура ПрописатьВалюту(Валюта)
	
	Если НЕ ЗначениеЗаполнено(Валюта) Тогда
		Возврат;
	КонецЕсли;
	
	// Подпишем итоговые группы
	ШаблонТекста = НСтр("ru='Всего траншей (%1)';uk='Всього траншів (%1)'");
	Элементы.ГруппаТранши.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
	ШаблонТекста = НСтр("ru='Всего оплата (%1)';uk='Всього оплата (%1)'");
	Элементы.ГруппаОплаты.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
	ШаблонТекста = НСтр("ru='Всего начисления (%1)';uk='Всього нарахування (%1)'");
	Элементы.ГруппаНачисления.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
	// Подпишем колонки таблицы "Транши"
	ШаблонТекста = НСтр("ru='Сумма (%1)';uk='Сума (%1)'");
	Элементы.ТраншиСумма.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
	// Подпишем колонки таблицы "Оплаты"
	ШаблонТекста = НСтр("ru='Сумма основного долга (%1)';uk='Сума основного боргу (%1)'");
	Элементы.ОплатыСумма.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
	ШаблонТекста = НСтр("ru='Сумма процентов (%1)';uk='Сума відсотків (%1)'");
	Элементы.ОплатыПроценты.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
	ШаблонТекста = НСтр("ru='Сумма комиссии (%1)';uk='Сума комісії (%1)'");
	Элементы.ОплатыКомиссия.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
	// Подпишем колонки таблицы "Начисления"
	ШаблонТекста = НСтр("ru='Сумма процентов (%1)';uk='Сума відсотків (%1)'");
	Элементы.НачисленияПроценты.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
	ШаблонТекста = НСтр("ru='Сумма комиссии (%1)';uk='Сума комісії (%1)'");
	Элементы.НачисленияКомиссия.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Валюта);
	
КонецПроцедуры

&НаСервере
Функция СоответствуетЛимитуДоговора()
	
	Результат = Истина;
	
	Набор = НаборГрафика("Транши");
	СуммаТраншей = Набор.Итог("Сумма");
	СуммаЛимита = Объект.Владелец.СуммаЛимита;
	Если СуммаЛимита > 0 Тогда
		Результат = СуммаТраншей <= СуммаЛимита;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция СуммыГрафиковСходятся()
	
	СуммыДолгаРавны = Объект.СуммаТраншей = СуммаОплаты;
	ПроцентыРавны   = Объект.СуммаПроцентов = НачисленияПроцентов;
	КомиссияРавна   = Объект.СуммаКомиссии = НачисленияКомиссии;

	// Проверим суммы основного долга
	Если НЕ СуммыДолгаРавны Тогда
		Текст  = НСтр("ru='Различаются итоговые суммы основного долга на закладке ""Транши"" и ""Оплаты""!';uk='Розрізняються підсумкові суми основного боргу на закладці ""Транші"" і ""Оплати""!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
	КонецЕсли;
	
	// Проверим суммы процентов
	Если НЕ ПроцентыРавны Тогда
		Текст  = НСтр("ru='Различаются итоговые суммы процентов на закладке ""Оплаты"" и ""Начисления""!';uk='Розрізняються підсумкові суми відсотків на закладці ""Оплати"" і ""Нарахування""!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
	КонецЕсли;
	
	// Проверим суммы комиссии
	Если ЕстьКомиссия И НЕ КомиссияРавна Тогда
		Текст  = НСтр("ru='Различаются итоговые суммы комиссии на закладке ""Оплаты"" и ""Начисления""!';uk='Розрізняються підсумкові суми комісії на закладці ""Оплати"" і ""Нарахування""!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
	КонецЕсли;
	
	Возврат СуммыДолгаРавны И ПроцентыРавны И (КомиссияРавна ИЛИ НЕ ЕстьКомиссия);
	
КонецФункции

&НаСервере
Процедура ПрочитатьНаборСервер(ТипГрафика, СсылкаОбъекта = Неопределено)
	
	ИмяНабора = "";
	Набор = НаборГрафика(ТипГрафика, ИмяНабора, СсылкаОбъекта);
	Набор.Прочитать();
	ЗначениеВРеквизитФормы(Набор, ИмяНабора);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСвязанныеДанные(ТипГрафика = Неопределено)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		// Обновим данные графиков
		Если ТипГрафика = Неопределено Тогда
			ПрочитатьНаборСервер("ГрафикТраншей");
			ПрочитатьНаборСервер("ГрафикОплат");
			ПрочитатьНаборСервер("ГрафикНачислений");
		Иначе
			ПрочитатьНаборСервер(ТипГрафика);
		КонецЕсли;
		
		ИтогиГрафика = Справочники.ВариантыГрафиковКредитовИДепозитов.ИтогиГрафика(Объект.Ссылка);
		ЗаполнитьЗначенияСвойств(Объект,ИтогиГрафика,"ДатаПервогоТранша,ДатаПоследнегоПлатежа,СуммаТраншей,СуммаПроцентов,СуммаКомиссии");
		ЗаполнитьЗначенияСвойств(ЭтаФорма,ИтогиГрафика,"СуммаОплаты,НачисленияПроцентов,НачисленияКомиссии");
		Справочники.ВариантыГрафиковКредитовИДепозитов.ПересчитатьСроки(Объект);
		
	КонецЕсли;// ссылка существует
	
КонецПроцедуры

&НаСервере
Функция ЭтоПервый()
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВариантыГрафиковКредитовИДепозитов.Ссылка
	|ИЗ
	|	Справочник.ВариантыГрафиковКредитовИДепозитов КАК ВариантыГрафиковКредитовИДепозитов
	|ГДЕ
	|	НЕ ВариантыГрафиковКредитовИДепозитов.ПометкаУдаления
	|	И ВариантыГрафиковКредитовИДепозитов.Владелец = &Владелец";
	
	Запрос.УстановитьПараметр("Владелец", Объект.Владелец);
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции

&НаСервере
Процедура ОбновитьФлагиАктуальныхГрафиков()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВариантыГрафиковКредитовИДепозитов.Ссылка
	|ИЗ
	|	Справочник.ВариантыГрафиковКредитовИДепозитов КАК ВариантыГрафиковКредитовИДепозитов
	|ГДЕ
	|	ВариантыГрафиковКредитовИДепозитов.Владелец = &Владелец
	|	И ВариантыГрафиковКредитовИДепозитов.Ссылка <> &Ссылка
	|	И (ВариантыГрафиковКредитовИДепозитов.Утвержден
	|			ИЛИ ВариантыГрафиковКредитовИДепозитов.Используется)";
	
	Запрос.УстановитьПараметр("Владелец", Объект.Владелец);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Утвержден", Объект.Утвержден);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОбъектГрафика = Выборка.Ссылка.ПолучитьОбъект();
		Если Объект.Утвержден Тогда
			ОбъектГрафика.Утвержден = Ложь;
		КонецЕсли;
		
		Если Объект.Используется Тогда
			ОбъектГрафика.Используется = Ложь;
		КонецЕсли;
		
		ОбъектГрафика.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьГрафик(ЗакрыватьПослеЗаписи = Ложь)
	
	ОбновитьДаты();
	
	// Обновим итоги
	Объект.СуммаТраншей = ГрафикТраншей.Итог("Сумма");
	ОбновитьИтогиОплат();
	ОбновитьИтогиНачислений();
	
	Если НЕ СоответствуетЛимитуДоговора() Тогда
		
		Текст = НСтр("ru='Сумма траншей превосходит заданный лимит по договору!';uk='Сума траншів перевершує заданий ліміт по договору!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
		Возврат;
		
	КонецЕсли;// есть ограничение по лимиту
		
	Если НЕ СуммыГрафиковСходятся() Тогда
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ОбработкаОтветаНаВопросИгнорировать", ЭтотОбъект, ЗакрыватьПослеЗаписи),
						НСтр("ru='В текущем варианте графика различаются итоговые суммы.
                            |Игнорировать?'
                            |;uk='У поточному варіанті графіка розрізняються підсумкові суми.
                            |Ігнорувати?'"),
						РежимДиалогаВопрос.ДаНет);
		Возврат;
		
	КонецЕсли;// суммы итоговые суммы графика не сходятся
	
	ВыполнитьЗаписьОбъекта(ЗакрыватьПослеЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаНаВопросИгнорировать(Ответ, ЗакрыватьПослеЗаписи) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьЗаписьОбъекта(ЗакрыватьПослеЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗаписьОбъекта(ЗакрыватьПослеЗаписи = Ложь)
	
	Записать();
	Если ЗакрыватьПослеЗаписи Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
