
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидОперации = Параметры.ВидОперации;
	
	ИспользоватьШаблон = ЗначениеЗаполнено(ВидОперации)
		И ВидОперации <> Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Произвольный;
		
	Если ИспользоватьШаблон Тогда
		
		ТипыРасчетов = Новый Соответствие;
		ТипыРасчетов.Вставить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Клиента,                      0);
		ТипыРасчетов.Вставить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.МеждуКлиентами,               0);
		ТипыРасчетов.Вставить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями,    0);
		ТипыРасчетов.Вставить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Поставщика,                   1);
		ТипыРасчетов.Вставить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.МеждуПоставщиками,            1);
		ТипыРасчетов.Вставить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями, 1);
		ТипыРасчетов.Вставить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Бартер,                       2);
		
		ТипРасчетов = ТипыРасчетов[ВидОперации];
		
		ВариантыУчастия = Новый Соответствие;
		ВариантыУчастия.Вставить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Клиента,                      0);
		ВариантыУчастия.Вставить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.МеждуКлиентами,               1);
		ВариантыУчастия.Вставить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями,    2);
		ВариантыУчастия.Вставить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Поставщика,                   0);
		ВариантыУчастия.Вставить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.МеждуПоставщиками,            1);
		ВариантыУчастия.Вставить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями, 2);
		ВариантыУчастия.Вставить(Перечисления.ВидыОперацийВзаимозачетаЗадолженности.Бартер,                       0);
		
		ВариантУчастия = ВариантыУчастия[ВидОперации];
		
	КонецЕсли;
	
	ИспользоватьНесколькоОрганизаций = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ВыполняетсяЗакрытие и Модифицированность И НЕ СохранитьПараметры Тогда
		
		СписокКнопок = Новый СписокЗначений();
		СписокКнопок.Добавить("Закрыть", НСтр("ru='Закрыть';uk='Закрити'"));
		СписокКнопок.Добавить("НеЗакрывать", НСтр("ru='Не закрывать';uk='Не закривати'"));
		
		Отказ = Истина;
		
		Оповещение   = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru='Настройка не будет изменена. Закрыть форму без сохранения результата?';uk='Настройка не буде змінена. Закрити форму без збереження результату?'");
		
		ПоказатьВопрос(Оповещение,ТекстВопроса, СписокКнопок);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = "Закрыть" Тогда
		ВыполняетсяЗакрытие = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьШаблонПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипРасчетовПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	Если ИспользоватьШаблон Тогда
		
		Если ТипРасчетов = 0 Тогда
			Если ВариантУчастия = 0 Тогда
				ВидОперации = ПредопределенноеЗначение(
					"Перечисление.ВидыОперацийВзаимозачетаЗадолженности.Клиента");
			ИначеЕсли ВариантУчастия = 1 Тогда
				ВидОперации = ПредопределенноеЗначение(
					"Перечисление.ВидыОперацийВзаимозачетаЗадолженности.МеждуКлиентами");
			Иначе
				ВидОперации = ПредопределенноеЗначение(
					"Перечисление.ВидыОперацийВзаимозачетаЗадолженности.КлиентаМеждуОрганизациями");
			КонецЕсли;
		ИначеЕсли ТипРасчетов = 1 Тогда
			Если ВариантУчастия = 0 Тогда
				ВидОперации = ПредопределенноеЗначение(
					"Перечисление.ВидыОперацийВзаимозачетаЗадолженности.Поставщика");
			ИначеЕсли ВариантУчастия = 1 Тогда
				ВидОперации = ПредопределенноеЗначение(
					"Перечисление.ВидыОперацийВзаимозачетаЗадолженности.МеждуПоставщиками");
			Иначе
				ВидОперации = ПредопределенноеЗначение(
					"Перечисление.ВидыОперацийВзаимозачетаЗадолженности.ПоставщикаМеждуОрганизациями");
			КонецЕсли;
		Иначе
			ВидОперации = ПредопределенноеЗначение(
				"Перечисление.ВидыОперацийВзаимозачетаЗадолженности.Бартер");
		КонецЕсли;
		
	Иначе
		
		ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийВзаимозачетаЗадолженности.Произвольный");
		
	КонецЕсли;
	
	СохранитьПараметры = Истина;
	
	Закрыть(ВидОперации);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	
	Элементы.ПараметрыШаблона.Доступность = ИспользоватьШаблон;
	
	ЭтоБартер = (ТипРасчетов = 2);
	
	Элементы.ВРамкахКонтрагента.Видимость = Не ЭтоБартер;
	Элементы.МеждуКонтрагентами.Видимость = Не ЭтоБартер;
	Элементы.МеждуОрганизациями.Видимость = Не ЭтоБартер И ИспользоватьНесколькоОрганизаций;
	Элементы.ДекорацияВРамкахКонтрагента.Видимость = ЭтоБартер;
	
	Элементы.ПояснениеМеждуКонтрагентами.Видимость = Не ЭтоБартер;
	Элементы.ПояснениеМеждуОрганизациями.Видимость = Не ЭтоБартер И ИспользоватьНесколькоОрганизаций;
	
	Если ТипРасчетов = 0 Тогда //клиент
		
		Элементы.ПояснениеВРамкахКонтрагента.Заголовок =
		 НСтр("ru='Взаимозачет отгрузок выбранному клиенту (дебиторская задолженность) и его авансов (кредиторская задолженность).';uk='Взаємозалік відвантажень вибраного клієнта (дебіторська заборгованість) та його авансів (кредиторська заборгованість).'");
		Элементы.ПояснениеМеждуКонтрагентами.Заголовок =
		 НСтр("ru='Взаимозачет отгрузок одному клиенту (дебиторская задолженность) и авансов другого клиента (кредиторская задолженность).';uk='Взаємозалік відвантажень одному клієнту (дебіторська заборгованість) та авансів іншого клієнта (кредиторська заборгованість).'");
		Элементы.ПояснениеМеждуОрганизациями.Заголовок =
		 НСтр("ru='Взаимозачет отгрузок клиенту от одной организации и его авансов на другую организацию.';uk='Взаємозалік відвантажень клієнта від однієї організації і його авансів на іншу організацію.'");
		
	ИначеЕсли ТипРасчетов = 1 Тогда //поставщик
		
		Элементы.ПояснениеВРамкахКонтрагента.Заголовок =
		 НСтр("ru='Взаимозачет отгрузок выбранного поставщика (кредиторская задолженность) и авансов (дебиторская задолженность).';uk='Взаємозалік відвантажень вибраного постачальника (кредиторська заборгованість) та авансів (дебіторська заборгованість).'");
		Элементы.ПояснениеМеждуКонтрагентами.Заголовок =
		 НСтр("ru='Взаимозачет отгрузок одного поставщика (кредиторская задолженность) и авансов другому поставщику (дебиторская задолженность).';uk='Взаємозалік відвантажень одного постачальника (кредиторська заборгованість) та авансів іншому постачальнику (дебіторська заборгованість).'");
		Элементы.ПояснениеМеждуОрганизациями.Заголовок =
		 НСтр("ru='Взаимозачет отгрузок поставщика от одной организации и авансов другой организации.';uk='Взаємозалік відвантажень постачальника від однієї організації і авансів іншої організації.'");
		
	Иначе //бартер
		
		Элементы.ПояснениеВРамкахКонтрагента.Заголовок =
		 НСтр("ru='Взаимозачет задолженности организации перед контрагентом как поставщиком и задолженности контрагента как клиента перед организацией.';uk='Взаємозалік заборгованості організації перед контрагентом як постачальником і заборгованості контрагента як клієнта перед організацією.'");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
