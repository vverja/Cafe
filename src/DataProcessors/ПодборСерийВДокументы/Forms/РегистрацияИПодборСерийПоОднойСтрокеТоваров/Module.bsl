&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

&НаКлиенте
Перем ТекущееКоличество; //кеш количества в текущей строке для расчета количества серий

&НаКлиенте
Перем НужноЗадаватьВопросПередЗакрытием;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	Если Не ЭтоАдресВременногоХранилища(Параметры.АдресВоВременномХранилище) Тогда
		ТекстСообщения = НСтр("ru='Предусмотрено открытие обработки только из документов.';uk='Передбачено відкриття обробки тільки з документів.'");
		
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	ТолькоПросмотр = Параметры.ТолькоПросмотр;
	ТолькоРедактированиеКоличества = Параметры.ТолькоРедактированиеКоличества;
	
	АвторизованВнешнийПользователь = ОбщегоНазначенияУТКлиентСервер.АвторизованВнешнийПользователь();
	
	ОбщегоНазначенияУТ.НастроитьПодключаемоеОборудование(ЭтаФорма, "Серии");
	
	
	АдресВоВременномХранилище = Параметры.АдресВоВременномХранилище;
	ПараметрыУказанияСерий    = Параметры.ПараметрыУказанияСерий;
	КоличествоВДокументе      = Параметры.Количество;
	НомераСтрокПредставление  = Параметры.НомераСтрокДокумента;
	Номенклатура 			  = Параметры.Номенклатура;
	Характеристика			  = Параметры.Характеристика;
	Склад					  = Параметры.Склад;
	Помещение 				  = Параметры.Помещение;
	Регистратор				  = Параметры.Регистратор;
	ЕстьУпаковки = ПараметрыУказанияСерий.ПоляСвязи.Найти("Упаковка") <> Неопределено;
	
	Если Параметры.Свойство("Назначение") Тогда
		БезНазначения = Ложь;
		Назначение = Параметры.Назначение;
	Иначе
		БезНазначения = Истина;		
	КонецЕсли;
	
	РеквизитыНоменклатуры = Новый Структура;
	РеквизитыНоменклатуры.Вставить("ВидНоменклатуры", "ВидНоменклатуры");
	РеквизитыНоменклатуры.Вставить("ЕдиницаИзмеренияПредставление", "ЕдиницаИзмерения.Наименование");
	РеквизитыНоменклатуры.Вставить("СрокГодности", "СрокГодности");
	РеквизитыНоменклатуры.Вставить("ЕдиницаИзмеренияСрокаГодности", "ЕдиницаИзмеренияСрокаГодности");
	
	РеквизитыНоменклатуры = Новый ФиксированнаяСтруктура(
		ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, РеквизитыНоменклатуры)); 
	
	
	НастройкиИспользованияСерий = Новый ФиксированнаяСтруктура(ЗначениеНастроекПовтИсп.НастройкиИспользованияСерий(
																						РеквизитыНоменклатуры.ВидНоменклатуры,
																						Склад));
		
	ВидНоменклатуры = НастройкиИспользованияСерий.ВладелецСерий;
	
	ШаблонСерии = НастройкиИспользованияСерий.ШаблонЭтикеткиСерии;
	
	Если Не ЗначениеЗаполнено(ШаблонСерии) Тогда
		ШаблонСерии = Справочники.ШаблоныЭтикетокИЦенников.ШаблонПоУмолчанию(Перечисления.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаСерииНоменклатуры);
	КонецЕсли;
	
	Если Не НастройкиИспользованияСерий.ИспользоватьСерии Тогда
		ТекстИсключения = НСтр("ru='Для вида номенклатуры ""%ВидНоменклатуры%"" серии не используются';uk='Для виду номенклатури ""%ВидНоменклатуры%"" серії не використовуються'");
		
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ВидНоменклатуры%",НастройкиИспользованияСерий.ВидНоменклатуры);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Упаковка                  = Параметры.Упаковка;
	
	Если Не ЗначениеЗаполнено(Упаковка) Тогда
		Если ЗначениеЗаполнено(Параметры.УпаковкаДляПодстановки) Тогда 
			
			ТипИзмеряемойВеличиныНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ЕдиницаИзмерения.ТипИзмеряемойВеличины"); 
			Если ТипИзмеряемойВеличиныНоменклатуры <> Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук 
				Или ТипИзмеряемойВеличиныНоменклатуры = Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук
				И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.УпаковкаДляПодстановки, "ТипИзмеряемойВеличины") = Перечисления.ТипыИзмеряемыхВеличин.Упаковка Тогда
				Упаковка = Параметры.УпаковкаДляПодстановки;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	ИсходнаяУпаковка = Упаковка;
	
	Заголовок = НСтр("ru='Регистрация серий товаров';uk='Реєстрація серій товарів'");
	ТоварПредставление = 
		СтрЗаменить(
			НСтр("ru='%ПредставлениеТовара%';uk='%ПредставлениеТовара%'"),
			"%ПредставлениеТовара%",
			НоменклатураКлиентСервер.ПредставлениеНоменклатуры(Номенклатура, Характеристика));
	
	Параметры.Свойство("ЗаголовокКолонкиКоличество", ПараметрЗаголовокКолонкиКоличество);
	
	Элементы.Упаковка.Доступность = Не ЕстьУпаковки;
	
	Элементы.Серии.ИзменятьСоставСтрок              = Не ТолькоПросмотр;
	Элементы.СерииКоличествоУпаковок.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ГруппаКоманднаяПанельТаблицы.Видимость = Не ТолькоПросмотр;
	
	Элементы.Количество.Видимость              = Не ТолькоПросмотр;
	
	Элементы.ОстаткиСерийКоличествоУпаковок.ТолькоПросмотр = ТолькоПросмотр;
	
	Элементы.СерииНомер.Видимость                = НастройкиИспользованияСерий.ИспользоватьНомерСерии;
	Элементы.СерииЗагрузитьДанныеИзТСД.Видимость = НастройкиИспользованияСерий.ИспользоватьНомерСерии;
	
	Элементы.СерииГоденДо.Видимость              = НастройкиИспользованияСерий.ИспользоватьСрокГодностиСерии;
    
	Элементы.ЗаполнитьПоFEFO.Видимость  = НастройкиИспользованияСерий.УчетСерийПоFEFO 
										И Перечисления.СкладскиеОперации.ЕстьОтгрузка(ПараметрыУказанияСерий.СкладскиеОперации)
										И Не ЕстьУпаковки
										И Не ТолькоРедактированиеКоличества;
    
	Если НастройкиИспользованияСерий.ИспользоватьНомерСерии Тогда
		Элементы.СгенерироватьНомер.Видимость  = Истина;
		Элементы.ЗаполнитьНомера.Видимость = Истина;
		Элементы.СерииДобавить.Заголовок 	   = НСтр("ru='Ввести номер';uk='Ввести номер'");
        ПроверятьЗаполнениеНомера = Истина;
	Иначе
		Элементы.СгенерироватьНомер.Видимость  = Ложь;
		Элементы.ЗаполнитьНомера.Видимость = Ложь;
		Элементы.СерииДобавить.Заголовок       = НСтр("ru='Ввести';uk='Ввести'");
        ПроверятьЗаполнениеНомера = Ложь;
	КонецЕсли;
		
	Элементы.ГруппаПечать.Видимость = Не АвторизованВнешнийПользователь;
	Элементы.ФормаНастроитьПорядокСканированияСерий.Видимость = Не АвторизованВнешнийПользователь;
	Если АвторизованВнешнийПользователь Тогда
		Элементы.СерииЗагрузитьДанныеИзТСД.Видимость = Ложь;
	КонецЕсли;
		
	Если НастройкиИспользованияСерий.ИспользоватьСрокГодностиСерии Тогда
		Элементы.СерииГоденДо.Формат               = НастройкиИспользованияСерий.ФорматнаяСтрокаСрокаГодности;
		Элементы.СерииГоденДо.ФорматРедактирования = НастройкиИспользованияСерий.ФорматнаяСтрокаСрокаГодности;
		
		СрокГодности                  = РеквизитыНоменклатуры.СрокГодности;
		ЕдиницаИзмеренияСрокаГодности = РеквизитыНоменклатуры.ЕдиницаИзмеренияСрокаГодности;
		            
		Если НастройкиИспользованияСерий.ТочностьУказанияСрокаГодностиСерии = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоДней Тогда
			Элементы.СерииГоденДо.Заголовок = НСтр("ru='Годен до (дата)';uk='Придатний до (дата)'");
			Элементы.ОстаткиСерийГоденДо.Заголовок = НСтр("ru='Годен до (дата)';uk='Придатний до (дата)'");
		Иначе
			Элементы.СерииГоденДо.Заголовок = НСтр("ru='Годен до (дата, час)';uk='Придатний до (дата, година)'");
			Элементы.ОстаткиСерийГоденДо.Заголовок = НСтр("ru='Годен до (дата, час)';uk='Придатний до (дата, година)'");
		КонецЕсли;
	КонецЕсли;
	
	РежимОтображенияСерий = "СерииВДокументе";
	Распоряжение = Обработки.ПодборСерийВДокументы.РаспоряжениеПоПараметрамФормы(Параметры);
	
	ВариантПолучениеДанныхИзРегистров = Обработки.ПодборСерийВДокументы.ВариантПолучениеДанныхИзРегистровПоПараметрамФормы(ПараметрыУказанияСерий, Распоряжение, Параметры.Склад, ВидНоменклатуры);
	Заголовки = Обработки.ПодборСерийВДокументы.ЗаголовкиПоВариантуПолучениеДанныхИзРегистров(ВариантПолучениеДанныхИзРегистров);
	ЗаголовокСвободногоОстаткаПоУмолчанию = Заголовки.ЗаголовокСвободногоОстатка;
	
	Если ВариантПолучениеДанныхИзРегистров <> "ВсеСерииНоменклатуры" Тогда
		Элементы.РежимОтображенияСерий.СписокВыбора[1].Представление = Заголовки.ЗаголовокКнопки;
	Иначе	
		Элементы.ОстаткиСерийСвободныйОстаток.Видимость = Ложь;
		Элементы.РежимОтображенияСерий.СписокВыбора.Удалить(Элементы.РежимОтображенияСерий.СписокВыбора[1]);
	КонецЕсли;
	
	Элементы.ОстаткиСерийГоденДо.Видимость = НастройкиИспользованияСерий.ИспользоватьСрокГодностиСерии;
	Элементы.ОстаткиСерийНомер.Видимость   = НастройкиИспользованияСерий.ИспользоватьНомерСерии;
	
	Элементы.ОстаткиСерийГоденДо.Формат = НастройкиИспользованияСерий.ФорматнаяСтрокаСрокаГодности;

	// Ввод серий
	ТаблицаСерий = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаСерий.Серия,
	|	ТаблицаСерий.Количество,
	|	ТаблицаСерий.КоличествоУпаковок
	|ПОМЕСТИТЬ ТаблицаСерий
	|ИЗ
	|	&ТаблицаСерий КАК ТаблицаСерий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСерий.Серия,
	|	СУММА(ТаблицаСерий.Количество) КАК Количество,
	|	СУММА(ТаблицаСерий.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	ВЫРАЗИТЬ(ТаблицаСерий.Серия КАК Справочник.СерииНоменклатуры).Номер КАК Номер,
	|	ВЫРАЗИТЬ(ТаблицаСерий.Серия КАК Справочник.СерииНоменклатуры).ГоденДо КАК ГоденДо
	|ИЗ
	|	ТаблицаСерий КАК ТаблицаСерий
	|ГДЕ
	|	(&ИспользоватьКоличествоСерии
	|		ИЛИ ТаблицаСерий.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСерий.Серия
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоденДо,
	|	Номер";
	Запрос.УстановитьПараметр("ТаблицаСерий", ТаблицаСерий);
	Запрос.УстановитьПараметр("ИспользоватьКоличествоСерии", НастройкиИспользованияСерий.ИспользоватьКоличествоСерии);
	
	Объект.Серии.Загрузить(Запрос.Выполнить().Выгрузить());
	
	КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Упаковка, Номенклатура);
	ЭтоЕдиничнаяУпаковка = КоэффициентУпаковки = 1;
	
	Если Не НастройкиИспользованияСерий.ИспользоватьКоличествоСерии Тогда
		
		Если ТолькоРедактированиеКоличества Тогда
			Элементы.СерииКоличествоУпаковок.Вид = ВидПоляФормы.ПолеФлажка;
		Иначе
			Элементы.СерииКоличествоУпаковок.Видимость = Ложь;
		КонецЕсли;
		Элементы.ОстаткиСерийКоличествоУпаковок.Вид = ВидПоляФормы.ПолеФлажка;
        Элементы.Упаковка.Видимость = Ложь;
		
		Если Не ЭтоЕдиничнаяУпаковка Тогда
			Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
	УпаковкаПриИзмененииСервер();
		
	ОбновитьКартинкуПечати();
	
	ПравоДобавленияСерий = ПравоДоступа("Добавление", Метаданные.Справочники.СерииНоменклатуры);
	
	Если Не ПравоДобавленияСерий
			Тогда
		Элементы.СтраницаВводСерии.Видимость = Ложь;
		Элементы.СтраницыСерий.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Если КоличествоСерий = 0 Тогда
			РежимОтображенияСерий = "ТолькоОстатки";
		КонецЕсли; 
		Если ТипЗнч(Склад) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
			Заголовок = НСтр("ru='Подбор серий по остаткам в производстве';uk='Підбір серій по залишкам у виробництві'");
		Иначе
			Заголовок = НСтр("ru='Подбор серий по остаткам на складе';uk='Підбір серій за залишками на складі'");
		КонецЕсли;
	КонецЕсли;
	
	// Доп. реквизиты
	УправлениеСвойствамиУТ.ДобавитьКолонкиДополнительныхРеквизитов(
		Обработки.ПодборСерийВДокументы.ВладелецСвойствСерий(ВидНоменклатуры),
		ЭтаФорма,
		"Объект.Серии",
		"Серии",
		"СерииКоличествоУпаковок");
	УправлениеСвойствамиУТ.ЗаполнитьКолонкиДополнительныхРеквизитов(
		ЭтаФорма,
		"Объект.Серии",
		"Серия");
	
	Если ТолькоРедактированиеКоличества Тогда
		
		Элементы.СерииДобавить.Видимость = Ложь;
		Элементы.СгенерироватьНомер.Видимость = Ложь;
		Элементы.ЗаполнитьНомера.Видимость = Ложь;
		Элементы.СерииСкопировать.Видимость = Ложь;
		Элементы.СерииИзменить.Видимость = Ложь;
		Элементы.СерииЗагрузитьДанныеИзТСД.Видимость = Ложь;
			
		Для Каждого Элемент Из Элементы.ОстаткиСерий.ПодчиненныеЭлементы Цикл
			Если Не Элемент = Элементы.ОстаткиСерийКоличествоУпаковок Тогда
				Элемент.ТолькоПросмотр = Истина;
			КонецЕсли;
		КонецЦикла;
		Для Каждого Элемент Из Элементы.Серии.ПодчиненныеЭлементы Цикл
			Если Не Элемент = Элементы.СерииКоличествоУпаковок Тогда
				Элемент.ТолькоПросмотр = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	ОбновитьКартинкуПечати();
	НоменклатураСервер.ЗагрузитьНастройкуРежимСканированияСерий(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	МенеджерОборудованияКлиентПереопределяемый.НачатьПодключениеОборудованиеПриОткрытииФормы(ЭтаФорма, "СканерШтрихкода");
	НужноЗадаватьВопросПередЗакрытием = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	Если Модифицированность
		И НужноЗадаватьВопросПередЗакрытием Тогда

		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект), НСтр("ru='Список серий был изменен.
                |Сохранить изменения?'
                |;uk='Список серій був змінений.
                |Зберегти зміни?'"), РежимДиалогаВопрос.ДаНетОтмена);
				
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()

	МенеджерОборудованияКлиентПереопределяемый.НачатьОтключениеОборудованиеПриЗакрытииФормы(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
    
	ТекущиеДанные = Элементы.Серии.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ИдентификаторТекущейСтроки = ТекущиеДанные.ПолучитьИдентификатор(); 	
	Иначе
		ИдентификаторТекущейСтроки = Неопределено;
	КонецЕсли;
	
	// ПодключаемоеОборудование
    Если Источник = "ПодключаемоеОборудование" И ВводДоступен() И НЕ ТолькоПросмотр Тогда
        
		РезультатОбработки = Неопределено;
        
        Если ИмяСобытия = "ScanData" Тогда
            
			ОчиститьСообщения(); 
			РезультатОбработки = ОбработатьШтрихкодыСервер(МенеджерОборудованияКлиент.ПреобразоватьДанныеСоСканераВМассив(Параметр), КэшированныеЗначения, ИдентификаторТекущейСтроки);
			
        КонецЕсли;
        
		Если РезультатОбработки <> Неопределено Тогда
			Если РезультатОбработки.ЗакрытьФорму Тогда
				ОчиститьСообщения(); 
				ПодключитьОбработчикОжидания("ЗакрытьФормуПриСканировании",0.1, Истина);
			Иначе
				Если РезультатОбработки.ИдентификаторТекущейСтроки <> Неопределено Тогда
					Элементы.Серии.ТекущаяСтрока = РезультатОбработки.ИдентификаторТекущейСтроки;
				КонецЕсли;
			КонецЕсли;

		КонецЕсли;
        
	КонецЕсли;
	// Конец ПодключаемоеОборудование

	Если ИмяСобытия = "Запись_СерииНоменклатуры" Тогда
		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ТекущиеДанные.Серия = Источник;
		ЗаполнитьЗначенияСвойств(ТекущиеДанные,Параметр);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

&НаКлиенте
Процедура СтраницыСерийПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если ТекущаяСтраница = Элементы.СтраницаОстаткиСерий Тогда
		ВыполнитьЗапросЗаполненияТаблицыОстатков();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УпаковкаОчистка(Элемент, СтандартнаяОбработка)
	
	УпаковкаПриИзменении(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура УпаковкаПриИзменении(Элемент)
	
	УпаковкаПриИзмененииСервер();
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСерии

&НаКлиенте
Процедура СерииНомерПриИзменении(Элемент)
	ПодобратьСерию(Элемент,"Номер","ГоденДо");
	СерииНомерПриИзмененииНаСервере(Элементы.Серии.ТекущиеДанные.Номер);	
КонецПроцедуры

&НаКлиенте
Процедура СерииГоденДоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Серии.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ГоденДо) Тогда
 		ПодобратьСерию(Элемент,"ГоденДо","Номер");
	Иначе
		ТекущиеДанные.Серия   = Неопределено;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СерииГоденДоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Серии.ТекущиеДанные;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ФорматДаты", НастройкиИспользованияСерий.ФорматнаяСтрокаСрокаГодности);
	СтруктураПараметров.Вставить("ГоденДо", ТекущиеДанные.ГоденДо);
	СтруктураПараметров.Вставить("СрокГодности", СрокГодности);
	СтруктураПараметров.Вставить("ЕдиницаИзмеренияСрокаГодности", ЕдиницаИзмеренияСрокаГодности);
	
	ГоденДо = Неопределено;

	ОписаниеОповещения = Новый ОписаниеОповещения("СерииГоденДоНачалоВыбораЗавершение",
							ЭтотОбъект,
							Новый Структура("ТекущиеДанные, Элемент", ТекущиеДанные, Элемент));
	
	ОткрытьФорму("Обработка.ПодборСерийВДокументы.Форма.УказаниеДаты",
				СтруктураПараметров,
				Элемент,
				,
				,
				,
				ОписаниеОповещения,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СерииГоденДоНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
    Элемент = ДополнительныеПараметры.Элемент;
    
    
    ГоденДо = Результат;
    
    Если ЗначениеЗаполнено(ГоденДо)
        И ТипЗнч(ГоденДо) = Тип("Дата") Тогда
        
        ТекущиеДанные.ГоденДо = ГоденДо;
        ПодобратьСерию(Элемент,"ГоденДо","Номер");
        Элементы.Серии.ЗакончитьРедактированиеСтроки(Ложь);
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СерииКоличествоУпаковокПриИзменении(Элемент)
		
	ТекущиеДанные = Элементы.Серии.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура("ПересчитатьКоличествоЕдиниц", Новый Структура("Упаковка,Номенклатура", Упаковка, Номенклатура));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
		
КонецПроцедуры

&НаКлиенте
Процедура СерииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если Копирование Тогда
		ТекущееКоличество = 0;
	Иначе
		ТекущееКоличество =  ТекущиеДанные.КоличествоУпаковок;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СерииПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если Не ОтменаРедактирования Тогда
		
		Если Не НастройкиИспользованияСерий.ИспользоватьКоличествоСерии Тогда
			
			ТекущиеДанные = Элемент.ТекущиеДанные;
			
			Если ЗначениеЗаполнено(ТекущиеДанные.Номер)
				Или ЗначениеЗаполнено(ТекущиеДанные.ГоденДо) Тогда
				Если ТекущиеДанные.КоличествоУпаковок = 0
					И Не ТолькоРедактированиеКоличества Тогда
					ТекущиеДанные.КоличествоУпаковок = 1;
					
					СтруктураДействий = Новый Структура;
					СтруктураДействий = Новый Структура("ПересчитатьКоличествоЕдиниц", Новый Структура("Упаковка,Номенклатура", Упаковка, Номенклатура));
					
					ТекущиеДанные = Элементы.Серии.ТекущиеДанные;
					
					ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
				КонецЕсли;
			Иначе
				ТекущиеДанные.КоличествоУпаковок = 0;
				ТекущиеДанные.Количество         = 0;
			КонецЕсли;
		КонецЕсли;
		
		СтруктураПараметров = Новый Структура();
		СтруктураПараметров.Вставить("Номер",Элементы.Серии.ТекущиеДанные.Номер);
		СтруктураПараметров.Вставить("ГоденДо",Элементы.Серии.ТекущиеДанные.ГоденДо);
		СтруктураПараметров.Вставить("КоличествоУпаковок",Элементы.Серии.ТекущиеДанные.КоличествоУпаковок);
		СтруктураПараметров.Вставить("Количество",Элементы.Серии.ТекущиеДанные.Количество);
		
		Если НастройкиИспользованияСерий.ИспользоватьКоличествоСерии Тогда
			Дельта = ТекущееКоличество - Элементы.Серии.ТекущиеДанные.КоличествоУпаковок;
			КоличествоСерий = КоличествоСерий - Дельта;
		КонецЕсли;
		
		Если ОбъединитьСтроки(СтруктураПараметров,Элементы.Серии.ТекущиеДанные.ПолучитьИдентификатор()) Тогда			
			Элементы.Серии.ТекущиеДанные.КоличествоУпаковок = 0;
			Элементы.Серии.ТекущиеДанные.Количество         = 0;
			Объект.Серии.Удалить(Элементы.Серии.ТекущиеДанные);
		КонецЕсли;
		
		Если Не НастройкиИспользованияСерий.ИспользоватьКоличествоСерии Тогда
			КоличествоСерий = Объект.Серии.Количество();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СерииПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	ВыделенныеСтроки = Новый Массив;
	
	Для Каждого ИДСтроки Из Элементы.Серии.ВыделенныеСтроки Цикл
		
		ВыделенныеСтроки.Добавить(Объект.Серии.НайтиПоИдентификатору(ИДСтроки));
		
	КонецЦикла;	
	
	Для Каждого ТекущиеДанные из ВыделенныеСтроки Цикл 
		ТекущееКоличество =  ТекущиеДанные.КоличествоУпаковок;
		
		Если ТолькоРедактированиеКоличества Тогда
			Если ТекущиеДанные.КоличествоУпаковок > 0 Тогда
				ТекущиеДанные.КоличествоУпаковок = 0;
				СтруктураДействий = Новый Структура;
				СтруктураДействий = Новый Структура("ПересчитатьКоличествоЕдиниц", Новый Структура("Упаковка,Номенклатура", Упаковка, Номенклатура));
				ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
			КонецЕсли;
		Иначе
			 Объект.Серии.Удалить(ТекущиеДанные);	
		КонецЕсли;
		
		КоличествоСерий = КоличествоСерий - ТекущееКоличество;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СерииПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если ТолькоРедактированиеКоличества Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОстаткиСерий

&НаКлиенте
Процедура ОстаткиСерийКоличествоУпаковокПриИзменении(Элемент)
	Модифицированность = Истина;
	
	ИдентификаторТекущейСтроки = Элементы.ОстаткиСерий.ТекущиеДанные.ПолучитьИдентификатор();
	
 	ОстаткиСерийКоличествоУпаковокПриИзмененииСервер(ИдентификаторТекущейСтроки, КэшированныеЗначения);	
КонецПроцедуры

&НаКлиенте
Процедура РежимОтображенияСерийПриИзменении(Элемент)
	ВыполнитьЗапросЗаполненияТаблицыОстатков();
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиСерийПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	ТекущееКоличество =  ТекущиеДанные.КоличествоУпаковок;
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиСерийПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Дельта = ТекущееКоличество - ТекущиеДанные.КоличествоУпаковок;
	КоличествоСерий = КоличествоСерий - Дельта;
		
	ТекущиеДанные.СвободныйОстаток = ТекущиеДанные.СвободныйОстаток + ТекущееКоличество;
	ТекущиеДанные.СвободныйОстаток = ТекущиеДанные.СвободныйОстаток - ТекущиеДанные.КоличествоУпаковок;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)
	Модифицированность = Ложь;
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВводСерий(Команда)
	ОчиститьСообщения();
	Если НЕ СохранитьВводСерийСервер() Тогда
		Возврат;
	КонецЕсли;
	Если Не Модифицированность Тогда
		Закрыть(АдресВоВременномХранилище);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеИзТСД(Команда)
	
	МенеджерОборудованияКлиент.НачатьЗагрузкуДанныеИзТСД(
		Новый ОписаниеОповещения("ЗагрузитьИзТСДЗавершение", ЭтотОбъект),
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзТСДЗавершение(Результат, Параметры) Экспорт
	
	Если Результат.Результат Тогда
		
		ЗакрытьФорму = ОбработатьШтрихкодыСервер(Результат, КэшированныеЗначения, Неопределено);
		
		Если ЗакрытьФорму Тогда
			Закрыть(АдресВоВременномХранилище);
		КонецЕсли;
		
	Иначе
		МенеджерОборудованияКлиентПереопределяемый.СообщитьОбОшибке(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьПорядокСканированияСерий(Команда)
	Оповещение = Новый ОписаниеОповещения("НастроитьПорядокСканированияСерийЗавершение",ЭтаФорма);
	
	ОткрытьФорму("Обработка.ПодборСерийВДокументы.Форма.НастройкиСканированияИПечатиЭтикеток",
		Новый Структура("ПечатьНаПринтер",ПечатьНаПринтер),
		ЭтаФорма,
		,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкоду(Команда)
	
	ОчиститьСообщения();
	Оповещение = Новый ОписаниеОповещения("ПоискПоШтрихкодуЗавершение", ЭтотОбъект);
	ЗаголовокФормыВводаСерий = НСтр("ru='Введите штрихкод серии';uk='Введіть штрихкод серії'");
	ШтрихкодированиеНоменклатурыКлиент.ПоказатьВводШтрихкода(Оповещение,,ЗаголовокФормыВводаСерий);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкодуЗавершение(ДанныхШтрихкода, ДополнительныеПараметры) Экспорт
		
	ТекущиеДанные = Элементы.Серии.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ИдентификаторТекущейСтроки = ТекущиеДанные.ПолучитьИдентификатор(); 	
	Иначе
		ИдентификаторТекущейСтроки = Неопределено;
	КонецЕсли;
	
	ЗакрытьФорму = ОбработатьШтрихкодыСервер(ДанныхШтрихкода,КэшированныеЗначения, ИдентификаторТекущейСтроки);
	Если ЗакрытьФорму Тогда
		Закрыть(АдресВоВременномХранилище);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСерию(Команда)
	ИзменитьСериюКлиент();
КонецПроцедуры

&НаКлиенте
Процедура СгенерироватьНомер(Команда)
	
	Результат = ВычислитьМаксимальныйНомерИКоличество(ВидНоменклатуры, Упаковка, 1);
	ТекущаяСтрока = Результат.ТекущаяСтрока;
	Номер = Результат.Номер;	
	
	Номер = Номер+1;
	Элементы.Серии.ДобавитьСтроку();
	ТекущиеДанные = Элементы.Серии.ТекущиеДанные;

	ТекущиеДанные.Номер =  Формат(Номер, "ЧЦ=8; ЧВН=; ЧГ=");	
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные,ТекущаяСтрока); 
	МаксимальныйНомерВДокументе = ТекущиеДанные.Номер;
	
	Элементы.Серии.ЗакончитьРедактированиеСтроки(Ложь);
	
	Если НастройкиИспользованияСерий.ИспользоватьСрокГодностиСерии Тогда
		Элементы.Серии.ТекущийЭлемент = Элементы.СерииГоденДо;		
	ИначеЕсли НастройкиИспользованияСерий.ИспользоватьКоличествоСерии Тогда
		Элементы.Серии.ТекущийЭлемент = Элементы.СерииКоличествоУпаковок;
	КонецЕсли;		

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНомера(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КоличествоСерий",Макс(Количество - КоличествоСерий,0)); 
	ПараметрыФормы.Вставить("ИспользоватьКоличество",НастройкиИспользованияСерий.ИспользоватьКоличествоСерии); 
	ПараметрыФормы.Вставить("ВКаждойСерии",ВКаждойСерии); 
	ПараметрыФормы.Вставить("ЕдиницаИзмеренияСтр",ЕдиницаИзмеренияСтр); 
	ПараметрыФормы.Вставить("ВидНоменклатуры",ВидНоменклатуры); 
	
	ОткрытьФорму(
		"Обработка.ПодборСерийВДокументы.Форма.НастройкиЗаполненияНомеров",
		ПараметрыФормы,
		ЭтаФорма,
		,
		,
		,
		Новый ОписаниеОповещения("ЗаполнитьНомераЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНомераЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
    АктивнаяСтрока = ДобавитьСтрокиНаСервере(
		ВидНоменклатуры,
		Результат,
		Упаковка);
    
    Элементы.Серии.ЗакончитьРедактированиеСтроки(Ложь);
    
    Если НастройкиИспользованияСерий.ИспользоватьСрокГодностиСерии Тогда
        Элементы.Серии.ТекущаяСтрока = АктивнаяСтрока;
        Элементы.Серии.ТекущийЭлемент = Элементы.СерииГоденДо;	
    ИначеЕсли НастройкиИспользованияСерий.ИспользоватьКоличествоСерии Тогда
        Элементы.Серии.ТекущаяСтрока = АктивнаяСтрока;
        Элементы.Серии.ТекущийЭлемент = Элементы.СерииКоличествоУпаковок;
    КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьКаждойШт(Команда)
	Печать(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПечатьКаждойСерии(Команда)
	Печать(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоFEFO(Команда)
	Если Количество = 0 Тогда
		ПоказатьПредупреждение(,НСтр("ru='В документе не указано количество товара. Заполнение по FEFO не возможно.';uk='У документі не вказана кількість товару. Заповнення по FEFO не можливо.'"));
		Возврат;
	КонецЕсли;	
	
	ЗаполнитьПоFEFOНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КоличествоСерий.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КоличествоСерий");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Количество");

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОтличающейсяСтрокиДокумента);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КоличествоСерий.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КоличествоСерий");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Количество");

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Seagreen);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СерииСерия.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Серии.Серия");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(WindowsШрифты.DefaultGUIFont, , , Истина, Ложь, Ложь, Ложь, ));
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Новая';uk='Нова'"));

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СерииСерия.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Серии.Серия");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Зарегистрированная';uk='Зареєстрована'"));
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СерииКоличествоУпаковок.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТолькоРедактированиеКоличества");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Серии.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Серии.КоличествоУпаковок");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТолькоРедактированиеКоличества");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
КонецПроцедуры

#Область ШтрихкодыИТорговоеОборудование

&НаСервере
Функция ОбработатьШтрихкодыСервер(ДанныеШтрихкодов,КэшированныеЗначения, ИдентификаторТекущейСтроки)
	
	ШтрихкодыПоТипам = ШтрихкодированиеНоменклатурыСервер.СтруктураПоТипамШтрихкодов(ДанныеШтрихкодов);
		
	ЕстьОшибки = Ложь;
	ОбрабатываемаяСтрока = Неопределено;
	
	Если ШтрихкодыПоТипам.БезТипа.Количество() > 0 Тогда
		ТаблицаШтрихКодов = Новый ТаблицаЗначений;
		ТаблицаШтрихКодов.Колонки.Добавить("Номер",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(50)));
		ТаблицаШтрихКодов.Колонки.Добавить("ГоденДо",Новый ОписаниеТипов("Дата"));
		ТаблицаШтрихКодов.Колонки.Добавить("Количество",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3,ДопустимыйЗнак.Неотрицательный)));
		Для Каждого СтрМас из ШтрихкодыПоТипам.БезТипа Цикл
			НоваяСтрока            = ТаблицаШтрихКодов.Добавить();
			НоваяСтрока.Количество = СтрМас.Количество;
			
			ПоляИзШтрихкода = НоменклатураКлиентСервер.ИнформацияОСерииИзШтрихкода(СтрМас.Штрихкод, 
			НастройкиИспользованияСерий.ИспользоватьНомерСерии,
			НастройкиИспользованияСерий.ИспользоватьСрокГодностиСерии);
			
			НоваяСтрока.Номер   = ПоляИзШтрихкода.Номер;
			НоваяСтрока.ГоденДо = ПоляИзШтрихкода.ГоденДо;
			
			Если ПоляИзШтрихкода.ЕстьОшибка Тогда
				ЕстьОшибки = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
		Если Не ЕстьОшибки Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	Штрихкоды.Номер КАК Номер,
			|	Штрихкоды.ГоденДо КАК ГоденДо,
			|	Штрихкоды.Количество КАК Количество
			|ПОМЕСТИТЬ Штрихкоды
			|ИЗ
			|	&Штрихкоды КАК Штрихкоды
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЕСТЬNULL(СерииНоменклатуры.Ссылка, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)) КАК Серия,
			|	ЕСТЬNULL(СерииНоменклатуры.ГоденДо, Штрихкоды.ГоденДо)   КАК ГоденДо,
			|	Штрихкоды.Номер,
			|	Штрихкоды.Количество
			|ПОМЕСТИТЬ НайденныеНомераДляЗапроса
			|ИЗ
			|	Штрихкоды КАК Штрихкоды
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
			|		ПО Штрихкоды.Номер = СерииНоменклатуры.Номер
			|			И (Штрихкоды.ГоденДо = СерииНоменклатуры.ГоденДо
			|				ИЛИ Штрихкоды.ГоденДо = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))
			|			И (СерииНоменклатуры.ВидНоменклатуры = &ВидНоменклатуры)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СУММА(1) КАК КоличествоНайденныхСерий,
			|	НайденныеНомераДляЗапроса.Номер
			|ПОМЕСТИТЬ КоличествоСерийПоНомеру
			|ИЗ
			|	НайденныеНомераДляЗапроса КАК НайденныеНомераДляЗапроса
			|
			|СГРУППИРОВАТЬ ПО
			|	НайденныеНомераДляЗапроса.Номер
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВЫБОР
			|		КОГДА КоличествоСерийПоНомеру.КоличествоНайденныхСерий > 1
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
			|		ИНАЧЕ НайденныеНомераДляЗапроса.Серия
			|	КОНЕЦ КАК Серия,
			|	ВЫБОР
			|		КОГДА КоличествоСерийПоНомеру.КоличествоНайденныхСерий > 1
			|			ТОГДА ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
			|		ИНАЧЕ НайденныеНомераДляЗапроса.ГоденДо
			|	КОНЕЦ КАК ГоденДо,
			|	НайденныеНомераДляЗапроса.Номер,
			|	НайденныеНомераДляЗапроса.Количество
			|ИЗ
			|	НайденныеНомераДляЗапроса КАК НайденныеНомераДляЗапроса
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КоличествоСерийПоНомеру КАК КоличествоСерийПоНомеру
			|		ПО НайденныеНомераДляЗапроса.Номер = КоличествоСерийПоНомеру.Номер
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ Штрихкоды
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ НайденныеНомераДляЗапроса
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ КоличествоСерийПоНомеру";
			
			Запрос.УстановитьПараметр("ВидНоменклатуры",ВидНоменклатуры);
			Запрос.УстановитьПараметр("Штрихкоды",ТаблицаШтрихКодов);
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			СрокГодностиЗаполнен = Истина;
			
			Пока Выборка.Следующий() Цикл
				СтруктураПоиска = Новый Структура("Номер,ГоденДо,Серия",Выборка.Номер, Выборка.ГоденДо, Выборка.Серия);
				
				НайденныеСтроки = Объект.Серии.НайтиСтроки(СтруктураПоиска);
				
				Если НайденныеСтроки.Количество() = 0 Тогда
					ОбрабатываемаяСтрока = Объект.Серии.Добавить();
					ЗаполнитьЗначенияСвойств(ОбрабатываемаяСтрока, Выборка);
					
					Если ЗначениеЗаполнено(Выборка.Серия) Тогда
						ЗаполнитьЗначенияСвойств(ОбрабатываемаяСтрока, ПрочитатьДопРеквизитыСерии(Выборка.Серия));
					КонецЕсли;
				
					Если НастройкиИспользованияСерий.ИспользоватьКоличествоСерии Тогда
						ОбрабатываемаяСтрока.КоличествоУпаковок = Выборка.Количество;
					Иначе
						ОбрабатываемаяСтрока.КоличествоУпаковок = 1;
					КонецЕсли;
					
					СтруктураДействий = Новый Структура("ПересчитатьКоличествоЕдиниц", Новый Структура("Упаковка,Номенклатура", Упаковка, Номенклатура));
					
					ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ОбрабатываемаяСтрока, СтруктураДействий, КэшированныеЗначения);
					
					КоличествоСерий = КоличествоСерий + ОбрабатываемаяСтрока.КоличествоУпаковок;
					
					Модифицированность   = Истина;
					
					Если Не ЗначениеЗаполнено(ОбрабатываемаяСтрока.ГоденДо) Тогда
						СрокГодностиЗаполнен = Ложь;
					КонецЕсли;
				Иначе
					ОбрабатываемаяСтрока = НайденныеСтроки[0];
					Если НастройкиИспользованияСерий.ИспользоватьКоличествоСерии Тогда
						
						ТекущееКоличество = ОбрабатываемаяСтрока.КоличествоУпаковок;
						
						ОбрабатываемаяСтрока.КоличествоУпаковок = ОбрабатываемаяСтрока.КоличествоУпаковок + Выборка.Количество;
						
						СтруктураДействий = Новый Структура("ПересчитатьКоличествоЕдиниц", Новый Структура("Упаковка,Номенклатура", Упаковка, Номенклатура));
						ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ОбрабатываемаяСтрока, СтруктураДействий, КэшированныеЗначения);
						
						Дельта = ТекущееКоличество - ОбрабатываемаяСтрока.КоличествоУпаковок;
						КоличествоСерий = КоличествоСерий - Дельта;				
						
						Модифицированность = Истина;
						
					Иначе
						ТекстСообщения = НСтр("ru='Серия с номером %НомерСерии% уже присутствует в списке серий';uk='Серія з номером %НомерСерии% вже присутня у списку серій'");
						ТекстСообщения = СтрЗаменить(ТекстСообщения,"%НомерСерии%",Выборка.Номер);
						Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Серии",НайденныеСтроки[0].НомерСтроки,"Номер");
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,"Объект",ЕстьОшибки);
					КонецЕсли;
					
					Если Не ЗначениеЗаполнено(ОбрабатываемаяСтрока.ГоденДо) Тогда
						СрокГодностиЗаполнен = Ложь;
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
    
	ЗакрытьФорму = Ложь;
	
	Если Не ЕстьОшибки
		И РежимСканированияСерий = "ТоварСерияТовар"
		И (СрокГодностиЗаполнен
		Или Не НастройкиИспользованияСерий.ИспользоватьСрокГодностиСерии) Тогда
		СохранитьВводСерийСервер();
		Если Не Модифицированность Тогда
			ЗакрытьФорму = Истина;
		КонецЕсли;
	КонецЕсли;                              
	
	Результат = Новый Структура;
	Результат.Вставить("ЗакрытьФорму", ЗакрытьФорму);
	Результат.Вставить("ИдентификаторТекущейСтроки", ?(ОбрабатываемаяСтрока <> Неопределено,
														ОбрабатываемаяСтрока.ПолучитьИдентификатор(),
														Неопределено));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Печать

 &НаКлиенте
Процедура Печать(ПечатьКаждойШт)
	
	Если Не ЗначениеЗаполнено(ШаблонСерии) Тогда
		
		ПараметрыФормы = Новый Структура();
		Отбор = Новый Структура;
		Отбор.Вставить("Назначение",ПредопределенноеЗначение("Перечисление.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаСерииНоменклатуры"));
		ПараметрыФормы.Вставить("Отбор",Отбор);
		
		ОткрытьФорму(
			"Справочник.ШаблоныЭтикетокИЦенников.ФормаВыбора",
			ПараметрыФормы,ЭтаФорма,,,,
			Новый ОписаниеОповещения("ПечатьЗавершение", 
				ЭтотОбъект,
				Новый Структура("ПечатьКаждойШт", ПечатьКаждойШт)),
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
		Возврат;
		
	КонецЕсли;
	
	ПечатьФрагмент(ПечатьКаждойШт);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    ПечатьКаждойШт = ДополнительныеПараметры.ПечатьКаждойШт;
    
    ШаблонСерии = Результат;
    
    Если Не ЗначениеЗаполнено(ШаблонСерии) Тогда
        Возврат;
    КонецЕсли;
    
    ПечатьФрагмент(ПечатьКаждойШт);

КонецПроцедуры

&НаКлиенте
Процедура ПечатьФрагмент(Знач ПечатьКаждойШт)
    
    Перем ПараметрКоманды, СтруктураПараметров;
    
    СтруктураПараметров = Новый Структура();
    
    Если Модифицированность Тогда
        
        Если СохранитьВводСерийСервер() Тогда
            СтруктураПараметров.Вставить("АдресВХранилище",ПодготовитьДанныеДляПечати(ПечатьКаждойШт,Элементы.Серии.ВыделенныеСтроки));	
        Иначе
            Возврат;
        КонецЕсли;
        
    ИначеЕсли Элементы.Серии.ТекущиеДанные <> Неопределено Тогда
        СтруктураПараметров.Вставить("АдресВХранилище",ПодготовитьДанныеДляПечати(ПечатьКаждойШт,Элементы.Серии.ВыделенныеСтроки));	
    Иначе
        Возврат;		
    КонецЕсли;
    
    СтруктураПараметров.Вставить("ШаблонЭтикетки",ШаблонСерии); 
    СтруктураПараметров.Вставить("НазначениеШаблона",ПредопределенноеЗначение("Перечисление.НазначенияШаблоновЭтикетокИЦенников.ЭтикеткаСерииНоменклатуры"));
    СтруктураПараметров.Вставить("КоличествоЭкземпляров",1);
    
    ПараметрКоманды = Новый Массив;
    ПараметрКоманды.Добавить(ПредопределенноеЗначение("Справочник.СерииНоменклатуры.ПустаяСсылка"));
    
    Если ПечатьНаПринтер Тогда
        УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(
        "Обработка.ПечатьЭтикетокИЦенников", // Для вызова метода менеджера обработки "Печать".
        "ЭтикеткаСерииНоменклатуры",
        ПараметрКоманды,
        СтруктураПараметров);
        
    Иначе
        УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
        "Обработка.ПечатьЭтикетокИЦенников", // Для вызова метода менеджера обработки "Печать".
        "ЭтикеткаСерииНоменклатуры",
        ПараметрКоманды,
        ЭтаФорма,
        СтруктураПараметров); // Форма владелец
        
    КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПодготовитьДанныеДляПечати(ПечатьКаждойШт,Знач ВыделенныеСтроки)
			
	СерииНоменклатуры = Новый ТаблицаЗначений;
	СерииНоменклатуры.Колонки.Добавить("Серия",Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	СерииНоменклатуры.Колонки.Добавить("Штрихкод",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(16)));
	
	Для Каждого Идентификатор Из ВыделенныеСтроки Цикл
		Строка = Объект.Серии.НайтиПоИдентификатору(Идентификатор);
		
		Если ПечатьКаждойШт Тогда
			КоличествоЭкз = Строка.КоличествоУпаковок;
		Иначе
			КоличествоЭкз = 1;
		КонецЕсли;
		
		Пока КоличествоЭкз > 0 Цикл
			КоличествоЭкз = КоличествоЭкз - 1;
			НоваяСтрока = СерииНоменклатуры.Добавить();
			НоваяСтрока.Серия    = Строка.Серия;
			Если НастройкиИспользованияСерий.ИспользоватьСрокГодностиСерии 
				И НастройкиИспользованияСерий.ИспользоватьНомерСерии Тогда
				НоваяСтрока.ШтрихКод = СокрЛП(Строка.Номер) + Формат(Строка.ГоденДо, "ДФ=""ддММггЧЧ""");
			ИначеЕсли НастройкиИспользованияСерий.ИспользоватьНомерСерии Тогда
				НоваяСтрока.ШтрихКод = СокрЛП(Строка.Номер);
			Иначе
				НоваяСтрока.ШтрихКод = Формат(Строка.ГоденДо, "ДФ=""ддММггЧЧ""");
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ПоместитьВоВременноеХранилище(СерииНоменклатуры);;	
КонецФункции

#КонецОбласти

#Область ГенерацияСерий

&НаСервере
Функция ВычислитьМаксимальныйНомерИКоличество(ВидНоменклатуры, Упаковка, Количество)
	
	МаксимальныйНомерИзСправочника = Справочники.СерииНоменклатуры.ВычислитьМаксимальныйНомерСерии(ВидНоменклатуры);
	
	ОписаниеТипаЧисла = Новый ОписаниеТипов("Число");
	Номер = Макс(ОписаниеТипаЧисла.ПривестиЗначение(МаксимальныйНомерВДокументе), МаксимальныйНомерИзСправочника);
	
	ТекущаяСтрока 	  = Новый Структура("Номенклатура, Упаковка, Количество, КоличествоУпаковок", Номенклатура, Упаковка, Количество, Количество);
	СтруктураДействий = Новый Структура("ПересчитатьКоличествоЕдиниц", Новый Структура("Упаковка,Номенклатура", Упаковка, Номенклатура));
	
	ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено);

	Результат = Новый Структура("Номер, ТекущаяСтрока", Номер, ТекущаяСтрока);
	
	Возврат Результат;
	
КонецФункции
	
#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура НастроитьПорядокСканированияСерийЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		РежимСканированияСерий = Результат.РежимСканированияСерий;
		Если Результат.ПечатьНаПринтер <> ПечатьНаПринтер Тогда
			ПечатьНаПринтер = Результат.ПечатьНаПринтер;
			ОбновитьКартинкуПечати();
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗапросЗаполненияТаблицыОстатков()
	
	КоэффицентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Упаковка, Номенклатура);
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Номенклатура", Номенклатура);
	ПараметрыЗапроса.Вставить("Характеристика", Характеристика);
	ПараметрыЗапроса.Вставить("БезНазначения", БезНазначения);
	Если ЗначениеЗаполнено(Назначение) Тогда 
		Если ТипЗнч(Склад) = Тип("СправочникСсылка.Склады") Тогда
			ДвиженияПоСкладскимРегистрам = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Назначение,"ДвиженияПоСкладскимРегистрам");
			Если ДвиженияПоСкладскимРегистрам Тогда
				ПараметрыЗапроса.Вставить("Назначение", Назначение);
			Иначе
				ПараметрыЗапроса.Вставить("Назначение", Справочники.Назначения.ПустаяСсылка());
			КонецЕсли;
		Иначе
			ПараметрыЗапроса.Вставить("Назначение", Назначение);
		КонецЕсли; 
	Иначе
		ПараметрыЗапроса.Вставить("Назначение", Справочники.Назначения.ПустаяСсылка());
	КонецЕсли;
	ПараметрыЗапроса.Вставить("Склад", Склад);
	ПараметрыЗапроса.Вставить("Помещение", Помещение);
	ПараметрыЗапроса.Вставить("Регистратор", Регистратор);
	ПараметрыЗапроса.Вставить("Распоряжение", Распоряжение);
	ПараметрыЗапроса.Вставить("КоэффицентУпаковки", КоэффицентУпаковки);
	
	
	ТекстЗапроса = Обработки.ПодборСерийВДокументы.ТекстЗапросаФормированияТаблицыДанныеРегистров(ВариантПолучениеДанныхИзРегистров, Склад);
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	ТекстЗапроса = ТекстЗапроса + 
	"ВЫБРАТЬ
	|	СерииВДокументе.Серия,
	|	СерииВДокументе.Количество,
	|	СерииВДокументе.КоличествоУпаковок
	|ПОМЕСТИТЬ СерииВДокументе
	|ИЗ
	|	&СерииВДокументе КАК СерииВДокументе
	|ГДЕ
	|	СерииВДокументе.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Если РежимОтображенияСерий = "СерииВДокументе" Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	СерииВДокументе.Серия КАК Серия,
		|	ЕСТЬNULL(ДанныеРегистров.СвободныйОстаток, 0) / &КоэффицентУпаковки * &ЗнакОстатка - СерииВДокументе.КоличествоУпаковок КАК СвободныйОстаток,
		|	СерииВДокументе.Количество КАК Количество,
		|	СерииВДокументе.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ВЫРАЗИТЬ(СерииВДокументе.Серия КАК Справочник.СерииНоменклатуры).Номер КАК Номер,
		|	ВЫРАЗИТЬ(СерииВДокументе.Серия КАК Справочник.СерииНоменклатуры).ГоденДо КАК ГоденДо
		|ИЗ
		|	СерииВДокументе КАК СерииВДокументе
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеРегистров КАК ДанныеРегистров
		|		ПО (ДанныеРегистров.Серия = СерииВДокументе.Серия)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГоденДо,
		|	Номер";
	ИначеЕсли РежимОтображенияСерий = "ТолькоОстатки" Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.Серия,
		|	СУММА(ВложенныйЗапрос.СвободныйОстаток) КАК СвободныйОстаток,
		|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
		|	СУММА(ВложенныйЗапрос.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	ВЫРАЗИТЬ(ВложенныйЗапрос.Серия КАК Справочник.СерииНоменклатуры).Номер КАК Номер,
		|	ВЫРАЗИТЬ(ВложенныйЗапрос.Серия КАК Справочник.СерииНоменклатуры).ГоденДо КАК ГоденДо
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДанныеРегистров.Серия КАК Серия,
		|		ДанныеРегистров.СвободныйОстаток / &КоэффицентУпаковки * &ЗнакОстатка КАК СвободныйОстаток,
		|		0 КАК Количество,
		|		0 КАК КоличествоУпаковок
		|	ИЗ
		|		ДанныеРегистров КАК ДанныеРегистров
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СерииВДокументе.Серия,
		|		-СерииВДокументе.КоличествоУпаковок,
		|		СерииВДокументе.Количество,
		|		СерииВДокументе.КоличествоУпаковок
		|	ИЗ
		|		СерииВДокументе КАК СерииВДокументе) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Серия,
		|	ВЫРАЗИТЬ(ВложенныйЗапрос.Серия КАК Справочник.СерииНоменклатуры).Номер,
		|	ВЫРАЗИТЬ(ВложенныйЗапрос.Серия КАК Справочник.СерииНоменклатуры).ГоденДо
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВложенныйЗапрос.СвободныйОстаток) > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГоденДо,
		|	Номер";
	ИначеЕсли РежимОтображенияСерий = "ВсеСерии" Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.Серия,
		|	СУММА(ВложенныйЗапрос.СвободныйОстаток) КАК СвободныйОстаток,
		|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
		|	СУММА(ВложенныйЗапрос.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	ВЫРАЗИТЬ(ВложенныйЗапрос.Серия КАК Справочник.СерииНоменклатуры).Номер КАК Номер,
		|	ВЫРАЗИТЬ(ВложенныйЗапрос.Серия КАК Справочник.СерииНоменклатуры).ГоденДо КАК ГоденДо
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДанныеРегистров.Серия КАК Серия,
		|		ДанныеРегистров.СвободныйОстаток / &КоэффицентУпаковки * &ЗнакОстатка КАК СвободныйОстаток,
		|		0 КАК Количество,
		|		0 КАК КоличествоУпаковок
		|	ИЗ
		|		ДанныеРегистров КАК ДанныеРегистров
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СерииВДокументе.Серия,
		|		-СерииВДокументе.КоличествоУпаковок,
		|		СерииВДокументе.Количество,
		|		СерииВДокументе.КоличествоУпаковок
		|	ИЗ
		|		СерииВДокументе КАК СерииВДокументе) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Серия,
		|	ВЫРАЗИТЬ(ВложенныйЗапрос.Серия КАК Справочник.СерииНоменклатуры).Номер,
		|	ВЫРАЗИТЬ(ВложенныйЗапрос.Серия КАК Справочник.СерииНоменклатуры).ГоденДо
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГоденДо,
		|	Номер";
	КонецЕсли;	
		
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Для Каждого Параметр из ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	Запрос.УстановитьПараметр("ВсеСерии", РежимОтображенияСерий = "ВсеСерии");
	Запрос.УстановитьПараметр("СерииВДокументе", Объект.Серии.Выгрузить());
	
	Если ВариантПолучениеДанныхИзРегистров = "ТоварыКОформлениюПоступления"
		И ПараметрыУказанияСерий.ЭтоОрдер Тогда
		Запрос.УстановитьПараметр("ЗнакОстатка", -1);
	Иначе
		Запрос.УстановитьПараметр("ЗнакОстатка", 1);
	КонецЕсли;	
	
	ОстаткиСерий.Загрузить(Запрос.Выполнить().Выгрузить());
	
	// Доп. реквизиты
	УправлениеСвойствамиУТ.ДобавитьКолонкиДополнительныхРеквизитов(
		Обработки.ПодборСерийВДокументы.ВладелецСвойствСерий(ВидНоменклатуры),
		ЭтаФорма,
		"ОстаткиСерий",
		"ОстаткиСерий",
		"ОстаткиСерийСвободныйОстаток",
		Истина);
	УправлениеСвойствамиУТ.ЗаполнитьКолонкиДополнительныхРеквизитов(
		ЭтаФорма,
		"ОстаткиСерий",
		"Серия");
	
КонецПроцедуры

&НаСервере
Процедура ОстаткиСерийКоличествоУпаковокПриИзмененииСервер(ИдентификаторТекущейСтроки, КэшированныеЗначения)
	СтруктураДействий = Новый Структура("ПересчитатьКоличествоЕдиниц", Новый Структура("Упаковка,Номенклатура", Упаковка, Номенклатура));

	ТекущиеДанные = ОстаткиСерий.НайтиПоИдентификатору(ИдентификаторТекущейСтроки);
	
	ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
	НайденныеСтроки = Объект.Серии.НайтиСтроки(Новый Структура("Серия", ТекущиеДанные.Серия));
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		НайденныеСтроки = Объект.Серии.НайтиСтроки(Новый Структура("Серия",Справочники.СерииНоменклатуры.ПустаяСсылка()));
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			КоличествоСерий = КоличествоСерий - НайденныеСтроки[0].КоличествоУпаковок;
		КонецЕсли;	
	КонецЕсли;
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		СтрокаСерий = НайденныеСтроки[0];
		Если ТекущиеДанные.КоличествоУпаковок = 0 Тогда
			Объект.Серии.Удалить(СтрокаСерий);
			Возврат;
		КонецЕсли;
	ИначеЕсли ТекущиеДанные.КоличествоУпаковок = 0 Тогда
		Возврат;
	Иначе
		СтрокаСерий = Объект.Серии.Добавить();
	КонецЕсли;
		
	ЗаполнитьЗначенияСвойств(СтрокаСерий, ТекущиеДанные);
    ЗаполнитьЗначенияСвойств(СтрокаСерий, ПрочитатьДопРеквизитыСерии(СтрокаСерий.Серия));
	
КонецПроцедуры

&НаСервере
Функция СохранитьВводСерийСервер(СтрокаДляЗаписи = Неопределено)
	
	Если Объект.Серии.Количество() > 0 Тогда
		ПоследняяСтрока = Объект.Серии[Объект.Серии.Количество()-1];
		Если Не ЗначениеЗаполнено(ПоследняяСтрока.Серия)
			И Не ЗначениеЗаполнено(ПоследняяСтрока.Номер)
			И Не ЗначениеЗаполнено(ПоследняяСтрока.ГоденДо)
			И (ПоследняяСтрока.КоличествоУпаковок = 1
			Или ПоследняяСтрока.КоличествоУпаковок = 0) Тогда
			
			Объект.Серии.Удалить(ПоследняяСтрока);
		КонецЕсли;
	КонецЕсли;
    
	ПараметрыПроверки = Новый Структура("ТолькоРедактированиеКоличества,
		|ИспользоватьСрокГодностиСерии,ПроверятьЗаполнениеНомера, ИспользоватьНомерСерии, ИспользоватьКоличествоСерии, Номенклатура, Упаковка");
	ЗаполнитьЗначенияСвойств(ПараметрыПроверки, НастройкиИспользованияСерий);
	ЗаполнитьЗначенияСвойств(ПараметрыПроверки, ЭтаФорма, "Номенклатура,Упаковка,ПроверятьЗаполнениеНомера");
	ПараметрыПроверки.ТолькоРедактированиеКоличества = ТолькоРедактированиеКоличества;
	
	Объект.ПараметрыПроверки = Новый ФиксированнаяСтруктура(ПараметрыПроверки);
	
	Если НЕ (ПроверитьЗаполнение()
	 И УправлениеСвойствамиУТ.ПроверитьЗаполнениеДополнительныхРеквизитов(ЭтаФорма, "Объект.Серии")) Тогда
		Возврат Ложь;
	КонецЕсли;	
			
	НайтиЗарегистрированныеСерии();
	
	ТаблицаВозврата = Новый ТаблицаЗначений;
	ТаблицаВозврата.Колонки.Добавить("Серия",Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	ТаблицаВозврата.Колонки.Добавить("Количество",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3,ДопустимыйЗнак.Неотрицательный)));
	ТаблицаВозврата.Колонки.Добавить("КоличествоУпаковок",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3,ДопустимыйЗнак.Неотрицательный)));

	КоэффициентИсходнойУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(ИсходнаяУпаковка, Номенклатура);
	
	Для Каждого СтрТабл Из Объект.Серии Цикл
		
		Если СтрТабл.Количество > 0
			Или ТолькоРедактированиеКоличества Тогда // В режиме ТолькоРедактированиеКоличества нужно также вернуть строки с нулевым количеством
			
			СохранитьСериюПоСтроке(СтрТабл);
			
			НоваяСтрока = ТаблицаВозврата.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрТабл);
			НоваяСтрока.КоличествоУпаковок = НоваяСтрока.Количество / КоэффициентИсходнойУпаковки;
			
		КонецЕсли;
	КонецЦикла;
	
	Если ПараметрыУказанияСерий.ИмяТЧТовары = ПараметрыУказанияСерий.ИмяТЧСерии
		И ТаблицаВозврата.Количество() = 0 Тогда
		
		НоваяСтрока = ТаблицаВозврата.Добавить();
		НоваяСтрока.КоличествоУпаковок = Количество;
		
		СтруктураДействий = Новый Структура("ПересчитатьКоличествоЕдиниц", Новый Структура("Упаковка,Номенклатура", Упаковка, Номенклатура));
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, Неопределено);
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(ТаблицаВозврата,АдресВоВременномХранилище);
	Модифицированность = Ложь;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция СохранитьСериюПоСтроке(СтрТаблИдентификатор)
	
	Если ТипЗнч(СтрТаблИдентификатор) <> Тип("ДанныеФормыЭлементКоллекции") Тогда
		СтрТабл = Объект.Серии.НайтиПоИдентификатору(СтрТаблИдентификатор);
	Иначе
		СтрТабл = СтрТаблИдентификатор; 
	КонецЕсли;
	
	Результат = Ложь;
	
	Если ПравоДобавленияСерий Тогда
		СправочникОбъект = Неопределено;
		Если Не ЗначениеЗаполнено(СтрТабл.Серия) Тогда
			
			СправочникОбъект                 = Справочники.СерииНоменклатуры.СоздатьЭлемент();
			СправочникОбъект.ВидНоменклатуры = ВидНоменклатуры;
			СправочникОбъект.Номер           = СтрТабл.Номер;
			СправочникОбъект.ГоденДо         = СтрТабл.ГоденДо;
			
		ИначеЕсли УправлениеСвойствамиУТ.ЕстьДопРеквизитыВТаблице(ЭтотОбъект, "Объект.Серии") Тогда
			
			СправочникОбъект = СтрТабл.Серия.ПолучитьОбъект();
			
		КонецЕсли;
		
		Если СправочникОбъект <> Неопределено Тогда
			Если УправлениеСвойствамиУТ.ЕстьДопРеквизитыВТаблице(ЭтотОбъект, "Объект.Серии") Тогда
				УправлениеСвойствамиУТ.ПеренестиДополнительныеРеквизитыИзФормыВОбъект(СправочникОбъект, ЭтаФорма, СтрТабл, "Объект.Серии");
			КонецЕсли;
			
			СправочникОбъект.Номер     		= СтрТабл.Номер;
			
			СправочникОбъект.Записать();
			Результат = Истина;
			СтрТабл.Серия = СправочникОбъект.Ссылка; 
			
		Иначе
			Результат = Истина;
		КонецЕсли;	
	Иначе
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		НужноЗадаватьВопросПередЗакрытием = Ложь;
		ЗавершитьВводСерий(Неопределено);// Сохранение результатов модификации.
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		
		НужноЗадаватьВопросПередЗакрытием = Ложь;
		Закрыть(Неопределено);
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПрочитатьДопРеквизитыСерии(Серия)
	
	Возврат УправлениеСвойствамиУТ.ПрочитатьДопРеквизитыСерии(ЭтотОбъект, "Объект.Серии", Серия);
	
КонецФункции

&НаКлиенте
Процедура ПодобратьСерию(Элемент,ВводимыйРеквизит,ПроверяемыеРеквизиты)
	ТекущиеДанные = Элементы.Серии.ТекущиеДанные;
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить(ВводимыйРеквизит,ТекущиеДанные[ВводимыйРеквизит]);
	
	ПроверяемыеРеквизитыМассив = СтрРазделить(ПроверяемыеРеквизиты,",");
	
	Для каждого СтрМас из ПроверяемыеРеквизитыМассив Цикл 
		ПараметрыПоиска.Вставить(СтрМас,Неопределено);
	КонецЦикла;
	
	ПараметрыПоиска.Вставить("ВидНоменклатуры",ВидНоменклатуры);
	ПараметрыПоиска.Вставить("Отбор", Новый Структура);
	ПараметрыПоиска.Вставить("СтрокаПоиска", Неопределено);
	
	СписокВыбора = ПолучитьДанныеВыбора(Тип("СправочникСсылка.СерииНоменклатуры"),ПараметрыПоиска);
	
	ВыбранныйЭлемент = Неопределено;
		
	Значение = Новый Структура;
	Значение.Вставить("Серия", Неопределено);
	Значение.Вставить("Номер",ТекущиеДанные.Номер);
	Значение.Вставить("ГоденДо",ТекущиеДанные.ГоденДо);
	
	Наименование = НоменклатураКлиентСервер.ПредставлениеСерии(НастройкиИспользованияСерий, ТекущиеДанные, Истина);
	
	СписокВыбора.Вставить(0,Значение,Наименование);
	
	Если СписокВыбора.Количество() = 1 Тогда
		ВыбранныйЭлемент = СписокВыбора[0];
	ИначеЕсли СписокВыбора.Количество() = 2 Тогда
		Если СокрЛП(СписокВыбора[0].Значение.Номер) <> СокрЛП(СписокВыбора[1].Значение.Номер)
			Или (СписокВыбора[0].Значение.ГоденДо <> СписокВыбора[1].Значение.ГоденДо
			     И ЗначениеЗаполнено(СписокВыбора[0].Значение.ГоденДо))
            Тогда
				 
				 ДопПараметрыВыбора = Новый Структура("СписокВыбора, ТекущиеДанные, Элемент", СписокВыбора, ТекущиеДанные, Элемент);
				 ОписаниеОповещения = Новый ОписаниеОповещения("ПодобратьСериюЗавершение", ЭтотОбъект, ДопПараметрыВыбора);
				 
				 ПоказатьВыборИзСписка(ОписаниеОповещения, СписокВыбора, Элемент);
		Иначе
			ВыбранныйЭлемент = СписокВыбора[1];
		КонецЕсли;
	ИначеЕсли СписокВыбора.Количество() > 0 Тогда
		ДопПараметрыВыбора = Новый Структура("СписокВыбора, ТекущиеДанные, Элемент", СписокВыбора, ТекущиеДанные, Элемент);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПодобратьСериюЗавершение", ЭтотОбъект, ДопПараметрыВыбора);
		
		ПоказатьВыборИзСписка(ОписаниеОповещения, СписокВыбора, Элемент);
	КонецЕсли;
	
	ПодобратьСериюФрагмент(ВыбранныйЭлемент, ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьСериюЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	СписокВыбора = ДополнительныеПараметры.СписокВыбора;
	ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
	Элемент = ДополнительныеПараметры.Элемент;
	
	ПодобратьСериюФрагмент(ВыбранныйЭлемент, ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ПодобратьСериюФрагмент(Знач ВыбранныйЭлемент, Знач ТекущиеДанные)
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		
		ЗаполнитьЗначенияСвойств(ТекущиеДанные, ВыбранныйЭлемент.Значение); 
		ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПрочитатьДопРеквизитыСерии(ТекущиеДанные.Серия));
		
	Иначе 
		ТекущиеДанные.Серия   = Неопределено; 
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСериюКлиент()
	ТекущиеДанные = Элементы.Серии.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияЗаполненияСерии = Новый Структура;
	ЗначенияЗаполненияСерии.Вставить("ВидНоменклатуры", ВидНоменклатуры);
	ЗначенияЗаполненияСерии.Вставить("Номер", ТекущиеДанные.Номер);
	ЗначенияЗаполненияСерии.Вставить("ГоденДо", ТекущиеДанные.ГоденДо);
	
	ПараметрыФормыСерии = Новый Структура;
	ПараметрыФормыСерии.Вставить("ЗначенияЗаполнения",ЗначенияЗаполненияСерии);
	ПараметрыФормыСерии.Вставить("Ключ",ТекущиеДанные.Серия);
	
	ОткрытьФорму("Справочник.СерииНоменклатуры.Форма.ФормаЭлемента",ПараметрыФормыСерии, ЭтаФорма,,,, Неопределено, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуПриСканировании()
	Закрыть(АдресВоВременномХранилище);
КонецПроцедуры

&НаСервере
Функция ОбъединитьСтроки(СтруктураПараметров,Идентификатор)
	
	Для Каждого Строка Из Объект.Серии Цикл
		
		Если Строка.Номер = СтруктураПараметров.Номер И 
			Строка.ГоденДо = СтруктураПараметров.ГоденДо И
			Строка.ПолучитьИдентификатор() <> Идентификатор  Тогда
			
			Если НастройкиИспользованияСерий.ИспользоватьКоличествоСерии Тогда
				Строка.Количество = Строка.Количество + СтруктураПараметров.Количество;
				Строка.КоличествоУпаковок = Строка.КоличествоУпаковок + СтруктураПараметров.КоличествоУпаковок;
			КонецЕсли;
			
			Возврат Истина;	
		КонецЕсли;	
		
	КонецЦикла;	
	Возврат Ложь;
КонецФункции

&НаСервере
Процедура ОбновитьКартинкуПечати()
	Если ПечатьНаПринтер Тогда
		Элементы.ГруппаПечать.Картинка = БиблиотекаКартинок.ПечатьСразу;
	Иначе
	    Элементы.ГруппаПечать.Картинка = БиблиотекаКартинок.Печать;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ДобавитьСтрокиНаСервере(ВидНоменклатуры, Результат, Упаковка)
	
    КолСерий = Результат.КоличествоСерий;
    ВКаждойСерии = Результат.ВКаждойСерии;
    ВКаждойСерииКоличество = Результат.ВКаждойСерииКоличество;
	
	АктивнаяСтрока = Неопределено;
	
	ПараметрыСтроки = Новый Структура("Номенклатура, Упаковка, Количество, КоличествоУпаковок", Номенклатура, Упаковка, ВКаждойСерииКоличество, ВКаждойСерииКоличество);
	СтруктураДействий = Новый Структура("ПересчитатьКоличествоЕдиниц", Новый Структура("Упаковка,Номенклатура", Упаковка, Номенклатура));
	ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ПараметрыСтроки, СтруктураДействий, Неопределено);
	
	СтруктураДопРеквизитов = УправлениеСвойствамиУТ.ПрочитатьДопРеквизитыСерии(ЭтотОбъект, "Объект.Серии", Результат);
	
	Если Результат.СпособНумерацииСерий = 0 И Результат.ОграничениеГенерацииНомеров = 0 Тогда
	
	    СтруктураВозврата = ВычислитьМаксимальныйНомерИКоличество(
			ВидНоменклатуры,
			Упаковка,
			ВКаждойСерииКоличество);
		
		Номер = СтруктураВозврата.Номер;

		Пока КолСерий > 0 Цикл
			                             
			КолСерий = КолСерий-1;
			Номер = Номер+1;
			ТекущаяСтрока = Объект.Серии.Добавить();
			   
			Если АктивнаяСтрока = Неопределено Тогда
				АктивнаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();
			КонецЕсли;
			
			ТекущаяСтрока.Номер = Формат(Номер, "ЧЦ=8; ЧВН=; ЧГ=");
			
			ЗаполнитьЗначенияСвойств(ТекущаяСтрока, ПараметрыСтроки); 
			ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтруктураДопРеквизитов); 
			
			КоличествоСерий = КоличествоСерий + ТекущаяСтрока.КоличествоУпаковок;
			
		КонецЦикла;	
		
		МаксимальныйНомерВДокументе = ТекущаяСтрока.Номер;
		
	Иначе
		
		Если Результат.СпособНумерацииСерий = 0 И Результат.ОграничениеГенерацииНомеров = 1 Тогда
		
			ТаблицаНомеров = Справочники.СерииНоменклатуры.СгенерироватьНомераСерийВДиапазоне(
				ВидНоменклатуры,
				Результат.НачальныйНомер,
				Результат.КонечныйНомер,
				КолСерий);
		
			Если ТаблицаНомеров.Количество() < КолСерий Тогда
				ТаблицаНомеров.Очистить();
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='В указанном диапазоне недостаточно свободных номеров.';uk='В зазначеному діапазоні недостатньо вільних номерів.'"));
			КонецЕсли;
			
		Иначе
			
			ТаблицаНомеров = Справочники.СерииНоменклатуры.ПолучитьНомераСерийВДиапазоне(
				ВидНоменклатуры,
				Результат.НачальныйНомер,
				Результат.КонечныйНомер);
			
		КонецЕсли;
			
		Для Каждого ТекСтр Из ТаблицаНомеров Цикл
			
			СуществующиеСтроки = Объект.Серии.НайтиСтроки(Новый Структура("Номер", ТекСтр.Номер));
			
			Если СуществующиеСтроки.Количество() > 0 Тогда 
				
				ТекущаяСтрока = СуществующиеСтроки[0];
				
				ТекущаяСтрока.Количество = ТекущаяСтрока.Количество + ПараметрыСтроки.Количество;
				ТекущаяСтрока.КоличествоУпаковок = ТекущаяСтрока.КоличествоУпаковок + ПараметрыСтроки.КоличествоУпаковок;
				
			Иначе
				
				ТекущаяСтрока = Объект.Серии.Добавить();
				
				ЗаполнитьЗначенияСвойств(ТекущаяСтрока, ТекСтр);
				ЗаполнитьЗначенияСвойств(ТекущаяСтрока, ПараметрыСтроки); 
				
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтруктураДопРеквизитов); 
			
			Если АктивнаяСтрока = Неопределено Тогда
				АктивнаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();
			КонецЕсли;
			
			КоличествоСерий = КоличествоСерий + ПараметрыСтроки.КоличествоУпаковок;
			
			МаксимальныйНомерВДокументе = ТекущаяСтрока.Номер;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат АктивнаяСтрока;
	
КонецФункции

&НаСервере
Процедура СерииНомерПриИзмененииНаСервере(Номер)
	
	Если СтрДлина(Номер) <> 8 Тогда
		Возврат;
	КонецЕсли;	
	
	ТаблицаСерии = Новый ТаблицаЗначений;
	ТаблицаСерии.Колонки.Добавить("Номер", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(8)));
	НоваяСтрока = ТаблицаСерии.Добавить();
	НоваяСтрока.Номер = Номер;
	Запрос = Новый Запрос;
	Запрос.Текст =	
	"ВЫБРАТЬ
	|	ТаблицаСерии.Номер
	|ПОМЕСТИТЬ ТаблицаСерии
	|ИЗ
	|	&ТаблицаСерии КАК ТаблицаСерии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаСерии.Номер
	|ИЗ
	|	ТаблицаСерии КАК ТаблицаСерии
	|ГДЕ
	|	ТаблицаСерии.Номер ПОДОБНО ""[0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9]""";
	Запрос.УстановитьПараметр("ТаблицаСерии", ТаблицаСерии);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ОписаниеТипаЧисла = Новый ОписаниеТипов("Число");
		НомерЧисло = ОписаниеТипаЧисла.ПривестиЗначение(Выборка.Номер);
		МаксимальныйНомерВДокументеЧисло = ОписаниеТипаЧисла.ПривестиЗначение(МаксимальныйНомерВДокументе);
		Если НомерЧисло > МаксимальныйНомерВДокументеЧисло Тогда
			МаксимальныйНомерВДокументе = Номер; 		
		КонецЕсли;	
	КонецЕсли;	
		
КонецПроцедуры	

&НаСервере
Процедура НайтиЗарегистрированныеСерии()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Серии.Серия КАК Серия,
	|	Серии.Номер КАК Номер,
	|	ВЫБОР
	|		КОГДА Серии.Номер = """"
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НомерНеУказан,
	|	Серии.ГоденДо КАК ГоденДо,
	|	ВЫБОР
	|		КОГДА Серии.ГоденДоСтрока = """"
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ГоденДоНеУказан,
	|	Серии.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	&Серии КАК Серии
	|ГДЕ
	|	Серии.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СерииНоменклатуры.Ссылка КАК Серия,
	|	Серии.НомерСтроки
	|ИЗ
	|	Серии КАК Серии
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО (ВЫБОР
	|				КОГДА СерииНоменклатуры.ВидНоменклатуры.ИспользоватьНомерСерии
	|					ТОГДА Серии.Номер = СерииНоменклатуры.Номер
	|				ИНАЧЕ Серии.НомерНеУказан
	|			КОНЕЦ)
	|			И (ВЫБОР
	|				КОГДА СерииНоменклатуры.ВидНоменклатуры.ИспользоватьСрокГодностиСерии
	|					ТОГДА Серии.ГоденДо = СерииНоменклатуры.ГоденДо
	|				ИНАЧЕ Серии.ГоденДоНеУказан
	|			КОНЕЦ)
	|ГДЕ
	|	СерииНоменклатуры.ВидНоменклатуры = &ВидНоменклатуры";
	
	Запрос.УстановитьПараметр("ВидНоменклатуры",ВидНоменклатуры);
	Запрос.УстановитьПараметр("Серии",Объект.Серии.Выгрузить());

	Выборка = Запрос.Выполнить().Выбрать();
			
	Пока Выборка.Следующий() Цикл
		Объект.Серии[Выборка.НомерСтроки-1].Серия = Выборка.Серия;		
	КонецЦикла;	
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьПоFEFOНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеРегистров.Серия КАК Серия,
	|	ДанныеРегистров.Серия.Номер КАК Номер,
	|	ДанныеРегистров.Серия.ГоденДо КАК ГоденДо,
	|	СУММА(ДанныеРегистров.СвободныйОстаток) КАК Количество
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыНаСкладахОстатки.Серия КАК Серия,
	|		ТоварыНаСкладахОстатки.ВНаличииОстаток - ТоварыНаСкладахОстатки.КОтгрузкеОстаток КАК СвободныйОстаток
	|	ИЗ
	|		РегистрНакопления.ТоварыНаСкладах.Остатки(
	|				,
	|				Номенклатура = &Номенклатура
	|					И Характеристика = &Характеристика
	|					И (&БезНазначения
	|						ИЛИ Назначение = &Назначение)
	|					И Склад = &Склад
	|					И Помещение = &Помещение) КАК ТоварыНаСкладахОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыНаСкладах.Серия,
	|		ВЫБОР
	|			КОГДА ТоварыНаСкладах.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТоварыНаСкладах.ВНаличии + ТоварыНаСкладах.КОтгрузке
	|			ИНАЧЕ ТоварыНаСкладах.ВНаличии - ТоварыНаСкладах.КОтгрузке
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ТоварыНаСкладах КАК ТоварыНаСкладах
	|	ГДЕ
	|		ТоварыНаСкладах.Номенклатура = &Номенклатура
	|		И ТоварыНаСкладах.Характеристика = &Характеристика
	|		И (&БезНазначения
	|				ИЛИ ТоварыНаСкладах.Назначение = &Назначение)
	|		И ТоварыНаСкладах.Склад = &Склад
	|		И ТоварыНаСкладах.Помещение = &Помещение
	|		И ТоварыНаСкладах.Регистратор = &Регистратор) КАК ДанныеРегистров
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистров.Серия,
	|	ДанныеРегистров.Серия.Номер,
	|	ДанныеРегистров.Серия.ГоденДо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоденДо";	
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Запрос.УстановитьПараметр("Назначение", Назначение);
	Запрос.УстановитьПараметр("БезНазначения", БезНазначения);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Помещение", Помещение);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	КоличествоКРаспределению = Количество;
	Объект.Серии.Очистить();
	КоличествоСерий = 0;
	
	Если Элементы.СтраницыСерий.ТекущаяСтраница = Элементы.СтраницаВводСерии Тогда
			
		Пока Выборка.Следующий() И КоличествоКРаспределению > 0 Цикл
				
			КоличествоПоСтроке =  Мин(Выборка.Количество, КоличествоКРаспределению);
			КоличествоКРаспределению = КоличествоКРаспределению - КоличествоПоСтроке;
			
			НоваяСтрока = Объект.Серии.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.Количество = КоличествоПоСтроке;
			НоваяСтрока.КоличествоУпаковок = КоличествоПоСтроке;
			
			КоличествоСерий = КоличествоСерий + КоличествоПоСтроке;
			
		КонецЦикла;
		
	Иначе
		
		Для Каждого Строка Из ОстаткиСерий Цикл
			Строка.СвободныйОстаток = Строка.СвободныйОстаток + Строка.Количество;
			Строка.КоличествоУпаковок = 0;
			Строка.Количество = 0;
		КонецЦикла;
		
		Пока Выборка.Следующий() И КоличествоКРаспределению > 0 Цикл
			
			Отбор = Новый Структура("Серия", Выборка.Серия);			
			МассивСтрок = ОстаткиСерий.НайтиСтроки(Отбор);
			Если МассивСтрок.Количество() <> 0 Тогда
				
				КоличествоПоСтроке =  Мин(Выборка.Количество, КоличествоКРаспределению);
				КоличествоКРаспределению = КоличествоКРаспределению - КоличествоПоСтроке;
				
				СтрокаСерий = МассивСтрок[0];	
				СтрокаСерий.Количество = КоличествоПоСтроке;
				СтрокаСерий.КоличествоУпаковок = КоличествоПоСтроке;
								
				СтрокаСерий.СвободныйОстаток = СтрокаСерий.СвободныйОстаток - КоличествоПоСтроке;
				
				НоваяСтрокаСерии = Объект.Серии.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСерии, СтрокаСерий);
				
				КоличествоСерий = КоличествоСерий + КоличествоПоСтроке;
				
			Иначе		
				
				КоличествоПоСтроке =  Мин(Выборка.Количество, КоличествоКРаспределению);
				КоличествоКРаспределению = КоличествоКРаспределению - КоличествоПоСтроке;
				
				НоваяСтрока = ОстаткиСерий.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				НоваяСтрока.СвободныйОстаток = Выборка.Количество;
				НоваяСтрока.Количество = КоличествоПоСтроке;
				НоваяСтрока.КоличествоУпаковок = КоличествоПоСтроке;
								
				НоваяСтрока.СвободныйОстаток = НоваяСтрока.СвободныйОстаток - КоличествоПоСтроке;
					
				НоваяСтрокаСерии = Объект.Серии.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСерии, НоваяСтрока);
				
				КоличествоСерий = КоличествоСерий + КоличествоПоСтроке;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовкиПоУпаковке()
	
	ЗаголовокКоличествоВДокументе = НСтр("ru='Необходимо зарегистрировать, %ЕдиницаИзмерения%';uk='Необхідно зареєструвати, %ЕдиницаИзмерения%'");
	ЗаголовокКоличествоСерий      = НСтр("ru='Зарегистрировано серий, %ЕдиницаИзмерения%';uk='Зареєстровано серій, %ЕдиницаИзмерения%'");
	Если ЗначениеЗаполнено(ПараметрЗаголовокКолонкиКоличество) Тогда
		ЗаголовокКолонкиКоличество = ПараметрЗаголовокКолонкиКоличество; 
	Иначе
		ЗаголовокКолонкиКоличество = НСтр("ru='Количество';uk='Кількість'"); 
	КонецЕсли;
	ЗаголовокКолонкиКоличество    = ЗаголовокКолонкиКоличество + ", %ЕдиницаИзмерения%"; 
	ЗаголовокКомандыПечати        = НСтр("ru='Этикетки для каждой(ого) %ЕдиницаИзмерения%';uk='Етикетки для кожної(го) %ЕдиницаИзмерения%'");
	
	Если ЗначениеЗаполнено(Упаковка) Тогда
		
		ЗаголовокКоличествоВДокументе = СтрЗаменить(ЗаголовокКоличествоВДокументе,"%ЕдиницаИзмерения%",Упаковка);
		ЗаголовокКоличествоСерий      = СтрЗаменить(ЗаголовокКоличествоСерий,"%ЕдиницаИзмерения%",Упаковка);
		ЗаголовокКолонкиКоличество    = СтрЗаменить(ЗаголовокКолонкиКоличество,"%ЕдиницаИзмерения%",Упаковка);
		ЗаголовокКомандыПечати        = СтрЗаменить(ЗаголовокКомандыПечати,"%ЕдиницаИзмерения%",Упаковка);
		ЗаголовокСвободногоОстатка    = СтрЗаменить(ЗаголовокСвободногоОстаткаПоУмолчанию,"%ЕдиницаИзмерения%", Упаковка);
		ЕдиницаИзмеренияСтр           = Упаковка;
	    
	Иначе
		
		ЗаголовокКолонкиКоличество    = СтрЗаменить(ЗаголовокКолонкиКоличество,"%ЕдиницаИзмерения%",РеквизитыНоменклатуры.ЕдиницаИзмеренияПредставление);
		ЗаголовокКоличествоВДокументе = СтрЗаменить(ЗаголовокКоличествоВДокументе,"%ЕдиницаИзмерения%",РеквизитыНоменклатуры.ЕдиницаИзмеренияПредставление);
		ЗаголовокКоличествоСерий      = СтрЗаменить(ЗаголовокКоличествоСерий,"%ЕдиницаИзмерения%",РеквизитыНоменклатуры.ЕдиницаИзмеренияПредставление);
		ЗаголовокКолонкиКоличество    = СтрЗаменить(ЗаголовокКолонкиКоличество,"%ЕдиницаИзмерения%",РеквизитыНоменклатуры.ЕдиницаИзмеренияПредставление);
		ЗаголовокКомандыПечати        = СтрЗаменить(ЗаголовокКомандыПечати,"%ЕдиницаИзмерения%",РеквизитыНоменклатуры.ЕдиницаИзмеренияПредставление);
		ЗаголовокСвободногоОстатка    = СтрЗаменить(ЗаголовокСвободногоОстаткаПоУмолчанию,"%ЕдиницаИзмерения%", РеквизитыНоменклатуры.ЕдиницаИзмеренияПредставление);
		ЕдиницаИзмеренияСтр 		  = РеквизитыНоменклатуры.ЕдиницаИзмеренияПредставление;
		
	КонецЕсли;
	
	Если Не НастройкиИспользованияСерий.ИспользоватьКоличествоСерии Тогда
		ЗаголовокКолонкиКоличество = " ";
	КонецЕсли;
	
	Элементы.СерииКоличествоУпаковок.Заголовок = ЗаголовокКолонкиКоличество;
	Элементы.Количество.Заголовок              = ЗаголовокКоличествоВДокументе;
	Элементы.КоличествоСерий.Заголовок         = ЗаголовокКоличествоСерий;
	Элементы.ПечатьКаждойШт.Заголовок		     = ЗаголовокКомандыПечати;

	Элементы.ОстаткиСерийСвободныйОстаток.Заголовок   = ЗаголовокСвободногоОстатка;
	Элементы.ОстаткиСерийКоличествоУпаковок.Заголовок = ЗаголовокКолонкиКоличество;
	
КонецПроцедуры

&НаСервере
Процедура УпаковкаПриИзмененииСервер()
	
	КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Упаковка, Номенклатура);
	
	ЭтоЕдиничнаяУпаковка = КоэффициентУпаковки = 1;
	
	Для Каждого ТекСтр Из Объект.Серии Цикл
		ТекСтр.КоличествоУпаковок = ТекСтр.Количество / КоэффициентУпаковки;
	КонецЦикла;
	
	ВыполнитьЗапросЗаполненияТаблицыОстатков();
	
	КоличествоСерий = Объект.Серии.Итог("Количество") / КоэффициентУпаковки;
	Количество = КоличествоВДокументе / КоэффициентУпаковки;
	
	УстановитьЗаголовкиПоУпаковке()
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
