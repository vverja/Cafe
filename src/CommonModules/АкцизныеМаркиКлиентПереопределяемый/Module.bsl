#Область ПрограммныйИнтерфейс

#Область ПрограммныйИнтерфейсУТ

Функция РазложитьСтрокуВМассивПодстрок(Знач Стр, Разделитель = ",") Экспорт
	
	МассивСтрок = Новый Массив();
	Если Разделитель = " " Тогда
		Стр = СокрЛП(Стр);
		Пока 1=1 Цикл
			Поз = Найти(Стр,Разделитель);
			Если Поз=0 Тогда
				МассивСтрок.Добавить(Стр);
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр,Поз-1));
			Стр = СокрЛ(Сред(Стр,Поз));
		КонецЦикла;
	Иначе
		ДлинаРазделителя = СтрДлина(Разделитель);
		Пока 1=1 Цикл
			Поз = Найти(Стр,Разделитель);
			Если Поз=0 Тогда
				МассивСтрок.Добавить(Стр);
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр,Поз-1));
			Стр = Сред(Стр,Поз+ДлинаРазделителя);
		КонецЦикла;
	КонецЕсли;
	
КонецФункции

// Проверяет, что считанный ШК является акцизной маркой
//
// Параметры:
//  Форма						 - УправляемаяФорма - Форма
//  ТекущиеДанные				 - СтрокаТЧ - Текущие данные
//  ДанныеШтрихкодов			 - Структура - Данные считанного штрихкода
//  ИмяКолонкиКоличество		 - Строка - Имя колонки "Количество"
//  СопоставленнаяНоменклатура	 - Структура - найденная сопоставленная номенклатура
// 
// Возвращаемое значение:
//   - 
//
Функция СчитанаАкцизнаяМарка(Форма, ТекущиеДанные, ДанныеШтрихкодов, ИмяКолонкиКоличество = "Количество", СопоставленнаяНоменклатура = Неопределено) Экспорт
	
	Если ДанныеШтрихкодов.Количество() = 1
		И СтрДлина(ДанныеШтрихкодов[0].Штрихкод) = 10 Тогда
		
		СопоставленнаяНоменклатура = Неопределено;
		
		Штрихкод = ДанныеШтрихкодов[0].Штрихкод;
		
		ЭтоШтрихкодАкцизнойМарки = РозничныеПродажиВызовСервера.ЭтоШтрихкодАкцизнойМарки(Штрихкод);
			
		Если ЭтоШтрихкодАкцизнойМарки Тогда
			
			Если ТекущиеДанные <> Неопределено Тогда
				ИдентификаторСтроки = ТекущиеДанные.ПолучитьИдентификатор();
			Иначе
				ИдентификаторСтроки = Неопределено;
			КонецЕсли;
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("ИдентификаторСтроки", ИдентификаторСтроки);
			ДополнительныеПараметры.Вставить("Редактирование", Ложь);
			ДополнительныеПараметры.Вставить("Форма", Форма);
			ДополнительныеПараметры.Вставить("ИмяКолонкиКоличество", ИмяКолонкиКоличество);
			ДополнительныеПараметры.Вставить("ЗапрашиватьНоменклатуру", Истина);
			
			Результат = Новый Структура;
			Результат.Вставить("ШтрихкодАкцизнойМарки",      Штрихкод);
			Результат.Вставить("СопоставленнаяНоменклатура", СопоставленнаяНоменклатура);
			ВыполнитьОбработкуОповещения(
				Новый ОписаниеОповещения("ВводАкцизнойМаркиЗавершение", Форма, ДополнительныеПараметры),
				Результат);
				
			Возврат Истина;
			
		Иначе
			
			Возврат Ложь;
			
		КонецЕсли;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

// Процедура - Открыть форму считывания акцизной марки
//
// Параметры:
//  Результат				 - Булево - Не используется
//  ДополнительныеПараметры	 - Структура - Параметры открытия формы считывания марки: (Форма, ИдентификаторСтроки, Редактирование, АдресВоВременномХранилище)
//
Процедура ОткрытьФормуСчитыванияАкцизнойМарки(Результат, ДополнительныеПараметры) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДополнительныеПараметры.Форма, "Объект") Тогда
		Источник = ДополнительныеПараметры.Форма.Объект;
	Иначе
		Источник = ДополнительныеПараметры.Форма;
	КонецЕсли;
	
	ТекущиеДанные = Источник.Товары.НайтиПоИдентификатору(ДополнительныеПараметры.ИдентификаторСтроки);
	Если Не ТекущиеДанные.УказыватьШтрихкодАкцизнойМаркиПриПечатиЧека Тогда
		ПоказатьПредупреждение(
			Неопределено,
			НСтр("ru='Для данной строки не указываются акцизные марки';uk='Для даного рядка не зазначаються акцизні марки'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("Номенклатура",   ТекущиеДанные.Номенклатура);
	ПараметрыОткрытияФормы.Вставить("Характеристика", ТекущиеДанные.Характеристика);
	ПараметрыОткрытияФормы.Вставить("Редактирование",            ДополнительныеПараметры.Редактирование);
	ПараметрыОткрытияФормы.Вставить("АдресВоВременномХранилище", ДополнительныеПараметры.АдресВоВременномХранилище);
	
	ДополнительныеПараметры.Вставить("ЗапрашиватьНоменклатуру", Ложь);
	
	ОткрытьФорму(
		"Документ.ЧекККМ.Форма.ФормаВводаАкцизнойМарки",
		ПараметрыОткрытияФормы,
		ДополнительныеПараметры.Форма,,,,
		Новый ОписаниеОповещения("ВводАкцизнойМаркиЗавершение", ДополнительныеПараметры.Форма, ДополнительныеПараметры))
	
КонецПроцедуры
	
// Процедура - Открыть форму считывания акцизной марки
//
// Параметры:
//  Результат				 - Булево - Не используется
//  ДополнительныеПараметры	 - Структура - Параметры открытия формы считывания марки: (Форма, ИдентификаторСтроки, Редактирование, АдресВоВременномХранилище)
//
Процедура ОткрытьФормуПросмотраАкцизнойМарки(Результат, ДополнительныеПараметры) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДополнительныеПараметры.Форма, "Объект") Тогда
		Источник = ДополнительныеПараметры.Форма.Объект;
	Иначе
		Источник = ДополнительныеПараметры.Форма;
	КонецЕсли;
	
	ТекущиеДанные = Источник.Товары.НайтиПоИдентификатору(ДополнительныеПараметры.ИдентификаторСтроки);
	
	Если Не ТекущиеДанные.УказыватьШтрихкодАкцизнойМаркиПриПечатиЧека Тогда
		ПоказатьПредупреждение(
			Неопределено,
			НСтр("ru='Для данной строки не указываются акцизные марки';uk='Для даного рядка не зазначаються акцизні марки'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("Номенклатура",   ТекущиеДанные.Номенклатура);
	ПараметрыОткрытияФормы.Вставить("Характеристика", ТекущиеДанные.Характеристика);
	ПараметрыОткрытияФормы.Вставить("АдресВоВременномХранилище", ДополнительныеПараметры.АдресВоВременномХранилище);
	
	ДополнительныеПараметры.Вставить("ЗапрашиватьНоменклатуру", Ложь);
	
	ОткрытьФорму(
		"Документ.ЧекККМ.Форма.ФормаПросмотраАкцизнойМарки",
		ПараметрыОткрытияФормы,
		ДополнительныеПараметры.Форма,,,,);
	
КонецПроцедуры
	

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныеПроцедурыИФункцииУТ

Процедура ВводАкцизнойМаркиЗавершение(ДанныеПоАкцизнымМаркам, ДополнительныеПараметры) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДополнительныеПараметры.Форма, "Объект") Тогда
		Источник = ДополнительныеПараметры.Форма.Объект;
	Иначе
		Источник = ДополнительныеПараметры.Форма;
	КонецЕсли;
	
	ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
	
	Если Не ДополнительныеПараметры.Редактирование Тогда
		
		Если ДанныеПоАкцизнымМаркам.АкцизнаяМаркаНайдена Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(
					НСтр("ru='Акцизная марка %1 уже введена для номенклатуры в строке %2';uk='Акцизна марка %1 вже введена для номенклатури у рядку %2'"),
					ДанныеПоАкцизнымМаркам.ШтрихкодАкцизнойМарки,
					ДополнительныеПараметры.Форма.Объект.Товары.Индекс(ТекущиеДанные) + 1));
			Возврат;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторСтроки) Тогда
			ТекущиеДанные.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;
		
		НоваяСтрока = Источник.АкцизныеМарки.Добавить();
		НоваяСтрока.ИдентификаторСтроки = ТекущиеДанные.ИдентификаторСтроки;
		НоваяСтрока.ШтрихкодАкцизнойМарки = ДанныеПоАкцизнымМаркам.ШтрихкодАкцизнойМарки;
		
		ДанныеПоАкцизнымМаркам.Количество = ДанныеПоАкцизнымМаркам.Количество + 1;
		
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("ИмяКолонкиКоличество") Тогда
		ИмяКолонкиКоличество = ДополнительныеПараметры.ИмяКолонкиКоличество;
	Иначе
		ИмяКолонкиКоличество = "Количество";
	КонецЕсли;
	
	ТекущиеДанные.КоличествоАкцизныхМарок = ДанныеПоАкцизнымМаркам.Количество;
	
	КорректноеКоличествоАМ = АкцизныеМаркиКлиентСерверПереопределяемый.КоличествоАкцизныхМарокСоответствуетКоличествуТовара(
		 ТекущиеДанные.КоличествоАкцизныхМарок, ТекущиеДанные[ИмяКолонкиКоличество]); 
	
	Если КорректноеКоличествоАМ Тогда                      
		ТекущиеДанные.ИндексАкцизнойМарки = 1;
	Иначе
		ТекущиеДанные.ИндексАкцизнойМарки = 2;
	КонецЕсли;
	
	Если ТекущиеДанные[ИмяКолонкиКоличество] < ДанныеПоАкцизнымМаркам.Количество 
		И НЕ КорректноеКоличествоАМ Тогда                      
		
		ТекущиеДанные[ИмяКолонкиКоличество] = ДанныеПоАкцизнымМаркам.Количество;
		
		ВыполнитьОбработкуОповещения(
			Новый ОписаниеОповещения(
				"ИзменениеАкцизныхМарокЗавершение",
				ДополнительныеПараметры.Форма,
				ДополнительныеПараметры),
			ТекущиеДанные);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти