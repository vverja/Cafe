#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Функция формирует массив реквизитов для блокировки редактирования
//
// Возвращаемое значение:
// Массив имен блокируемых реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт

	Результат = Новый Массив;
	
	// После записи владельца поменять нельзя, т.к. ссылка 
	// на данный справочник может оказаться в документе
	Результат.Добавить("Владелец");
	
	// Реквизиты могут быть использованы при печати налоговой накладной зарегистрированной в ЕРНН
	Результат.Добавить("КодУКТВЭД");
	Результат.Добавить("НомерГТД");

	Возврат Результат;

КонецФункции

Функция ПолучитьНаименование(НоменклатураГТД) Экспорт
	
	НаименованиеУКТВЭД = СокрЛП(НоменклатураГТД.КодУКТВЭД.Наименование);
	Наименование = НаименованиеУКТВЭД;
	
	МаксДлинаНаименования = НоменклатураГТД.Метаданные().ДлинаНаименования;
	МинДлинаНаименованияУКТВЭД = 25;
	
	Если ЗначениеЗаполнено(НоменклатураГТД.НомерГТД) Тогда
		
		НаименованиеГТД = НСтр("ru=', ГТД №: ';uk=', ВМД №: '") + СокрЛП(НоменклатураГТД.НомерГТД.Код) + НСтр("ru=' от ';uk=' від '") + Формат(НоменклатураГТД.НомерГТД.Дата, "ДФ=dd.MM.yy");
		ОбрезатьДо = МаксДлинаНаименования - СтрДлина(НаименованиеГТД);
		Если ОбрезатьДо < МинДлинаНаименованияУКТВЭД Тогда
			ОбрезатьДо = МинДлинаНаименованияУКТВЭД;
		КонецЕсли;
		
		Если ОбрезатьДо >= СтрДлина(НаименованиеУКТВЭД) Тогда
			Наименование = НаименованиеУКТВЭД + НаименованиеГТД;
		Иначе
			Наименование = Сред(НаименованиеУКТВЭД, 1, ОбрезатьДо) + НаименованиеГТД;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Наименование;
	
КонецФункции

Функция ПоискИИсправлениеНекорректныхНаименований(МассивСсылок = Неопределено) Экспорт
	    
	УстановитьПривилегированныйРежим(Истина);
	
	ОбновитьВесьСправочник = (МассивСсылок = Неопределено);
	
	Если Не ОбновитьВесьСправочник И (МассивСсылок.Количество() = 0) Тогда
		Возврат 0;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураГТД.Ссылка
	|ИЗ
	|	Справочник.НоменклатураГТД КАК НоменклатураГТД
	|ГДЕ
	|	(&ОбновитьВесьСправочник
	|			ИЛИ НоменклатураГТД.Ссылка В (&МассивСсылок))";
	Запрос.УстановитьПараметр("ОбновитьВесьСправочник", ОбновитьВесьСправочник);
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	КоличествоОбновленныхЭлементов = 0;
	Выборка = Запрос.Выполнить().Выбрать();				   
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Ссылка.Наименование = ПолучитьНаименование(Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		// Не используем ОбменДанными.Загрузка для того чтобы данные
		// корректно мигрировали в подчиненные узлы
		СправочникОбъект.ДополнительныеСвойства.Вставить("ТолькоОбновлениеНаименования");
		СправочникОбъект.Записать();
		
		КоличествоОбновленныхЭлементов = КоличествоОбновленныхЭлементов + 1;
		
	КонецЦикла;
	
	Возврат КоличествоОбновленныхЭлементов;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 50
	|	ДанныеСправочника.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НоменклатураГТД КАК ДанныеСправочника
	|ГДЕ
	|	НЕ ДанныеСправочника.ПометкаУдаления
	|	И (&ПоВсемВладельцам
	|			ИЛИ ДанныеСправочника.Владелец = &Владелец)
	|	И (&СЛюбымНаименованием
	|			ИЛИ ДанныеСправочника.Наименование ПОДОБНО &СтрокаПоиска СПЕЦСИМВОЛ ""\"")
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеСправочника.НомерГТД.Дата УБЫВ";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	СЛюбымНаименованием = Истина;
	СтрокаПоиска = "";
	Если Параметры.СтрокаПоиска <> Неопределено Тогда
		СЛюбымНаименованием = Ложь;
		СтрокаПоиска = Параметры.СтрокаПоиска;		
	КонецЕсли;
	Запрос.УстановитьПараметр("СЛюбымНаименованием", СЛюбымНаименованием);	
	Запрос.УстановитьПараметр("СтрокаПоиска", "%"+СтрЗаменить(СтрокаПоиска,"\","\\")+"%");	// Ищем по любой части наименования
	
	ПоВсемВладельцам = Истина;
	Владелец = Неопределено;
	Если Параметры.Свойство("Номенклатура", Владелец) Тогда
		ПоВсемВладельцам = Ложь;
	КонецЕсли;
	Запрос.УстановитьПараметр("ПоВсемВладельцам", ПоВсемВладельцам);	
	Запрос.УстановитьПараметр("Владелец", Владелец);	
	
	ДанныеВыбора = Новый СписокЗначений;
	ДанныеВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));

КонецПроцедуры

#КонецОбласти

#КонецЕсли
