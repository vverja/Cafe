#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураБыстрогоОтбора = Неопределено;
	Параметры.Свойство("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	
	Если СтруктураБыстрогоОтбора <> Неопределено Тогда
		СтруктураБыстрогоОтбора.Свойство("ВидНалоговойДекларации", ВидНалоговойДекларации);
	КонецЕсли;
	
	ОтборыСписковКлиентСервер.ОтборПоЗначениюСпискаПриСозданииНаСервере(
		Список, 
		"ВидНалоговойДекларации", 
		ВидНалоговойДекларации, 
		СтруктураБыстрогоОтбора
	);
	
	Элементы.Описание.Видимость = (ВидНалоговойДекларации = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхДеклараций.ДекларацияПоНалогуНаПрибыльНКУ"));
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ОтборыСписковКлиентСервер.ОтборПоЗначениюСпискаПриЗагрузкеИзНастроек(
		Список, 
		"ВидНалоговойДекларации", 
		ВидНалоговойДекларации, 
		СтруктураБыстрогоОтбора, 
		Настройки
	);
	ФильтрВидНалоговойДекларацииПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФильтрВидНалоговойДекларацииПриИзменении(Элемент)
	
	ФильтрВидНалоговойДекларацииПриИзмененииНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ФильтрНаборСтатейНалоговыхДекларацийПриИзменении(Элемент)
	
	ФильтрНаборСтатейНалоговыхДекларацийПриИзмененииНаСервере();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ФильтрВидНалоговойДекларацииПриИзмененииНаСервере()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор,
		"ВидНалоговойДекларации",
		ВидНалоговойДекларации,
		ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(ВидНалоговойДекларации)
	);
	
	Если (Не ЗначениеЗаполнено(ВидНалоговойДекларации)
	 ИЛИ ВидНалоговойДекларации<>НаборСтатейНалоговыхДеклараций.ВидНалоговойДекларации) Тогда
	 
		НаборСтатейНалоговыхДеклараций = Неопределено;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Список.Отбор,
			"Ссылка",
			Новый СписокЗначений,
			ВидСравненияКомпоновкиДанных.ВСписке,,Ложь
		);
		
	КонецЕсли;
	
	Элементы.ФильтрНаборСтатейНалоговыхДеклараций.Доступность = ЗначениеЗаполнено(ВидНалоговойДекларации);	
	Элементы.Описание.Видимость = (ВидНалоговойДекларации = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхДеклараций.ДекларацияПоНалогуНаПрибыльНКУ"));
	
КонецПроцедуры

&НаСервере
Процедура ФильтрНаборСтатейНалоговыхДекларацийПриИзмененииНаСервере()
	
	Если Не ЗначениеЗаполнено(НаборСтатейНалоговыхДеклараций) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Список.Отбор,
			"Ссылка",
			Новый СписокЗначений,
			ВидСравненияКомпоновкиДанных.ВСписке,,Ложь
		);
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор,
		"Ссылка",
		Справочники.СтатьиНалоговыхДеклараций.ПолучитьСписокСтатейПоНаборуСтатей(НаборСтатейНалоговыхДеклараций, Истина),
		ВидСравненияКомпоновкиДанных.ВСписке,,Истина
	);
	
КонецПроцедуры
 
#КонецОбласти
