#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Функция ПолноеИмяОбъекта()
	Возврат "РегистрНакопления.ПрочиеАктивыПассивы";
КонецФункции

Процедура ЗаполнитьСтатьиАктивовПассивовНалоги_КОбработке(Параметры) Экспорт
	
	ДополнительныеПараметрыОтметки = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметрыОтметки.ЭтоДвижения = Истина;
	ДополнительныеПараметрыОтметки.ПолноеИмяРегистра = ПолноеИмяОбъекта();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПрочиеАктивыПассивы.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрНакопления.ПрочиеАктивыПассивы КАК ПрочиеАктивыПассивы
	|ГДЕ
	|	(ПрочиеАктивыПассивы.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств
	|)
	|	И (ПрочиеАктивыПассивы.Статья = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ИмущественныеНалоги)
	|		ИЛИ ПрочиеАктивыПассивы.Статья = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.НалогиИВзносыСОплатыТруда))
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрочиеАктивыПассивы.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор
	|ИЗ
	|	РегистрНакопления.ПрочиеАктивыПассивы КАК ДанныеРегистра
	|
	|ГДЕ
	|	ДанныеРегистра.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств
	|	И ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.СписаниеБезналичныхДенежныхСредств).ХозяйственнаяОперация = &ПеречислениеНалога
	|	И ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.СписаниеБезналичныхДенежныхСредств).СтатьяАктивовПассивов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|	И ДанныеРегистра.Статья = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|");
	
	Запрос.УстановитьПараметр("ПеречислениеНалога", Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет);
	
	ДанныеКОбработке = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДанныеКОбработке, ДополнительныеПараметрыОтметки);
	
КонецПроцедуры

// Обработчик обновления BAS УТ 3.2.3
// Заполнение регистра после обновления.
// В регистре заменяются статьи активов и пассивов при начислении и оплате налогов на предопределенную статью "Налоги"
// Обработчик выполняется отложенно.
Процедура ЗаполнитьСтатьиАктивовПассивовНалоги(Параметры) Экспорт
	
	Регистраторы = Новый Массив;
	Регистраторы.Добавить("Документ.СписаниеБезналичныхДенежныхСредств");
	
	ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
		Регистраторы,
		"РегистрНакопления.ПрочиеАктивыПассивы",
		Параметры.Очередь);
	
	Параметры.ОбработкаЗавершена = ОбработкаЗавершена;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли