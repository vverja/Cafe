
////////////////////////////////////////////////////////////////////////////////
// Подсистема "Интернет-поддержка пользователей".
// ОбщийМодуль.КлиентЛицензированияКлиентГлобальный.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Открывает форму БИП для ввода настроек клиента лицензирования.
// Обработчик подключается во всех режимах кроме режима работы в модели сервиса.
// Подключается в методе
// ИнтернетПоддержкаПользователейКлиент.ПриНачалеРаботыСистемы_ПодключитьОбработчикБИПЗапросаНастройкиКлиентаЛицензирования()
//
// При первом вызове - до отображения системного сообщения о необходимости
// подтверждения правомерности использования конфигурации - проверяются.
//
Процедура ПриЗапросеНастроекКлиентаЛицензирования() Экспорт
	
	Если Не ИнтернетПоддержкаПользователейКлиент.ЗначениеПараметраПриложения(
		"ОбработанПервыйЗапросНастроекКлиентаЛицензирования", Ложь) Тогда
		
		// Обработка первого вызова запроса настроек клиента лицензирования
		ИнтернетПоддержкаПользователейКлиент.УстановитьЗначениеПараметраПриложения(
			"ОбработанПервыйЗапросНастроекКлиентаЛицензирования",
			Истина);
		
		РезультатПроверки = КлиентЛицензированияВызовСервера.ПроверитьНастройкиКлиентаЛицензирования();
		
		ИнтернетПоддержкаПользователейКлиент.УстановитьЗначениеПараметраПриложения(
			"ПравоПодключенияИПП",
			РезультатПроверки.ПравоПодключенияИПП);
		
		Если РезультатПроверки.НастройкиСинхронизированы Или Не РезультатПроверки.ПравоПодключенияИПП Тогда
			// Записаны настройки клиента лицензирования из БИП.
			// Если записанные настройки некорректны, тогда обработчик будет
			// вновь вызван платформой для отображения диалога
			// запроса настроек клиента лицензирования.
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ИнтернетПоддержкаПользователейКлиент.ЗначениеПараметраПриложения("ПравоПодключенияИПП", Ложь) Тогда
		// Выполняется только при втором и последующих вызовах обработчика
		ПоказатьПредупреждение(, НСтр("ru='Недостаточно прав для подключения Интернет-поддержки пользователей. Обратитесь к администратору.';uk='Недостатньо прав для підключення Інтернет-підтримки користувачів. Зверніться до адміністратора.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрОповещения = Новый Структура("Активизирована", Ложь);
	Оповестить("ИПП_АктивизироватьФормуПодключенияИПП", ПараметрОповещения);
	
	Если ПараметрОповещения.Свойство("Активизирована") И ПараметрОповещения.Активизирована = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму(
		"ОбщаяФорма.ПодключениеИнтернетПоддержки",
		Новый Структура(
			"ЗапомнитьПароль, ИзменятьФлагЗаполнитьПароль, РазрешитьОчищатьЛогинИПарольВИБ,
				|РежимВводаНастроекКлиентаЛицензирования",
			Истина, Ложь, Истина, Истина),
		,
		,
		,
		,
		,
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

#КонецОбласти