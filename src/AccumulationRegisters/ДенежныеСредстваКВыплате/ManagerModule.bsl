#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы


Процедура ИсправитьДвижения_ДанныеДляОбновления(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.ДенежныеСредстваКВыплате";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваКВыплате КАК ДанныеРегистра
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО ДанныеРегистра.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|			И (Аналитика.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК Договоры
	|		ПО (Договоры.Ссылка = 
	|				ВЫБОР КОГДА ДанныеРегистра.Заказ ССЫЛКА Справочник.ДоговорыКонтрагентов
	|					ТОГДА ДанныеРегистра.Заказ
	|				ИНАЧЕ ДанныеРегистра.Заказ.Договор
	|			КОНЕЦ)
	|");
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваКВыплате КАК ДанныеРегистра
	|
	|ГДЕ
	|	ДанныеРегистра.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходы)
	|	И ДанныеРегистра.СтатьяРасходов = &СтатьяРасходовПустаяСсылка
	|");
	
	Запрос.УстановитьПараметр("СтатьяРасходовПустаяСсылка", ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка());
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
    
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваКВыплате КАК ДанныеРегистра
	|
	|ГДЕ
	|	ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеВБюджет)
	|	И ДанныеРегистра.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	И ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) В (ТИП(Документ.СписаниеБезналичныхДенежныхСредств), ТИП(Документ.ЗаявкаНаРасходованиеДенежныхСредств))
	|	И (ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.ЗаявкаНаРасходованиеДенежныхСредств).ХозяйственнаяОперация = &ПеречислениеНалога
	|		ИЛИ ВЫРАЗИТЬ(ДанныеРегистра.Регистратор КАК Документ.СписаниеБезналичныхДенежныхСредств).ХозяйственнаяОперация = &ПеречислениеНалога)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваКВыплате КАК ДанныеРегистра
	|
	|ГДЕ
	|	ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаПоКредитам)
	|	И ДанныеРегистра.ТипСуммыКредитаДепозита = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ПустаяСсылка)
	|	И ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) В 
	|		(ТИП(Документ.ЗаявкаНаРасходованиеДенежныхСредств),
	|		ТИП(Документ.РасходныйКассовыйОрдер),
	|		ТИП(Документ.СписаниеБезналичныхДенежныхСредств)
	|		)
	|");
	
	Запрос.УстановитьПараметр("ПеречислениеНалога", Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет);
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
    
	
КонецПроцедуры

// Обработчик обновления BAS УТ 3.2.2
// Производит замену измерения "АналитикаУчетаПоПартнерам" с учетом договора в реквизите "Заказ".
Процедура ИсправитьДвижения(Параметры) Экспорт
	
	Регистраторы = Новый Массив;
	Регистраторы.Добавить("Документ.ЗаявкаНаРасходованиеДенежныхСредств");
	Регистраторы.Добавить("Документ.ОперацияПоПлатежнойКарте");
	Регистраторы.Добавить("Документ.ПоступлениеБезналичныхДенежныхСредств");
	Регистраторы.Добавить("Документ.ПриходныйКассовыйОрдер");
	Регистраторы.Добавить("Документ.РаспоряжениеНаПеремещениеДенежныхСредств");
	Регистраторы.Добавить("Документ.РасходныйКассовыйОрдер");
	Регистраторы.Добавить("Документ.СписаниеБезналичныхДенежныхСредств");
    
	ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
		Регистраторы,
		"РегистрНакопления.ДенежныеСредстваКВыплате",
		Параметры.Очередь
    );
	
	Параметры.ОбработкаЗавершена = ОбработкаЗавершена;
	
КонецПроцедуры



#КонецОбласти

#КонецОбласти

#КонецЕсли