
&НаСервере
Функция НастроеныPushУведомления()
	
	Возврат ПланыОбмена.МобильноеПриложениеЗаказыКлиентов.ПланОбменаИспользуетОтправкуPushУведомлений();
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Получатели = Новый Массив;
	УказаныПолучатели = Ложь;
	Если ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("УправляемаяФорма") Тогда
		Если ПараметрыВыполненияКоманды.Источник.ИмяФормы = "ПланОбмена.МобильноеПриложениеЗаказыКлиентов.Форма.ФормаСписка" Тогда
			Для Каждого Элемент ИЗ ПараметрыВыполненияКоманды.Источник.ПодчиненныеЭлементы.Список.ВыделенныеСтроки Цикл
				Получатели.Добавить(Элемент);
			КонецЦикла;
			УказаныПолучатели = Истина;
		ИначеЕсли ПараметрыВыполненияКоманды.Источник.ИмяФормы = "ПланОбмена.МобильноеПриложениеЗаказыКлиентов.Форма.ФормаУзла" Тогда
			Получатели.Добавить(ПараметрыВыполненияКоманды.Источник.Объект.Ссылка);
			УказаныПолучатели = Истина;
		КонецЕсли;
	КонецЕсли;
	Если УказаныПолучатели И Получатели.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru='Укажите получателей уведомления';uk='Вкажіть одержувачів повідомлення'"),, НСтр("ru='Push - уведомление';uk='Push - повідомлення'"));
		Возврат;
	КонецЕсли;
	ОтправитьPushУведомлениеПользователям(ПараметрыВыполненияКоманды.Источник, Получатели, УказаныПолучатели);
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьPushУведомлениеПользователям(ФормаВладелец, Получатели, УказаныПолучатели)
	
	Если НастроеныPushУведомления() Тогда
		ПараметрыФормы = Новый Структура("Получатели, УказаныПолучатели", Получатели, УказаныПолучатели);
		ОткрытьФорму("ПланОбмена.МобильноеПриложениеЗаказыКлиентов.Форма.ФормаОтправкиPushУведомления", ПараметрыФормы, ФормаВладелец);
	Иначе
		ДополнительныеПараметры = Новый Структура("ФормаВладелец, Получатели, УказаныПолучатели", ФормаВладелец, Получатели, УказаныПолучатели);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеНастройкиИспользованияPushУведомлений", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("ПланОбмена.МобильноеПриложениеЗаказыКлиентов.Форма.ФормаГлавногоУзла",, ФормаВладелец,,,, ОписаниеОповещения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеНастройкиИспользованияPushУведомлений(Знач Результата, Знач ДополнительныеПараметры) Экспорт
	
	Если НастроеныPushУведомления() Тогда
		ОтправитьPushУведомлениеПользователям(ДополнительныеПараметры.ФормаВладелец,
			ДополнительныеПараметры.Получатели, ДополнительныеПараметры.УказаныПолучатели);
	КонецЕсли;
КонецПроцедуры
