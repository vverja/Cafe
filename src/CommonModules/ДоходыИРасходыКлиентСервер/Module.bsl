
#Область ПрограммныйИнтерфейс

// Процедура инициализирует поле статьи составного типа в ТЧ пустой ссылкой по умолчанию
//
// Параметры:
// 	Строка - ДанныеФормыЭлементКоллекции - Строка табличной части документа
// 	ИмяРекизита - Строка - Имя реквизита статьи расходов, доходов или активов/пассивов
//
Процедура ИнициализироватьПустоеЗначениеСтатьиВСтрокеТЧ(Строка, ИмяРеквизита) Экспорт
	
	Если ИмяРеквизита = "СтатьяРасходов" Тогда
		ПустаяСтатья = ПредопределенноеЗначение("ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка");
	ИначеЕсли ИмяРеквизита = "СтатьяДоходов" Тогда
		ПустаяСтатья = ПредопределенноеЗначение("ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка");
	Иначе
		ПустаяСтатья = ПредопределенноеЗначение("ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка");
	КонецЕсли;
	
	Если Строка[ИмяРеквизита] = Неопределено Тогда
		Строка[ИмяРеквизита] = ПустаяСтатья;
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет значение реквизита ТЧ "ТипСтатьи" значением:
// 	- 1, если в качестве статьи выбрана СтатьяРасходов
// 	- 2, если в качестве статьи выбрана СтатьяДоходов
// 	- 3, если в качестве статьи выбрана СтатьяАктивовПассивов
// Данный реквизит используется в условном оформлении формы.
// 
// Параметры:
// 	Строка - ДанныеФормыЭлементКоллекции - Строка табличной части документа
// 	ИмяРеквизита - Строка - Имя поля статьи
//
Процедура ЗаполнитьТипСтатьиВСтрокеТЧ(Строка, ИмяРеквизита) Экспорт
	
	Статья = Строка[ИмяРеквизита];
	Если (ТипЗнч(Статья) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов")) Тогда 
		Строка.ТипСтатьи = 1;
	ИначеЕсли (ТипЗнч(Статья) = Тип("ПланВидовХарактеристикСсылка.СтатьиДоходов")) Тогда
		Строка.ТипСтатьи = 2;
	ИначеЕсли (ТипЗнч(Статья) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов")) Тогда
		Строка.ТипСтатьи = 3;
	Иначе
		Строка.ТипСтатьи = 0;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти