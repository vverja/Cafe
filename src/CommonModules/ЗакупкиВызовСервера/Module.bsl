////////////////////////////////////////////////////////////////////////////////
// Модуль "ЗакупкиВызовСервера", содержит процедуры и функции для 
// серверной обработки действий пользователя в процессе работы с документами закупки
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

//Проверяет, есть ли в табличной части строки с незаполненной датой поступления в неотмененных строках
//	
//Параметры:
//		Объект - ДанныеФормыСтруктура - проверяемый обеъкт
//		ИмяТЧ - Строка - имя проверяемой ТЧ
//
//Возвращаемое значение:
//		Булево - признак наличия строк с незаполненной датой поступления
Функция ДатаПоступленияПустая(Знач Объект, ИмяТЧ) Экспорт
	
	Для Каждого ТекСтрока Из Объект[ИмяТЧ] Цикл
		
		Если Не ТекСтрока.Отменено 
			И Не ЗначениеЗаполнено(ТекСтрока.ДатаПоступления) Тогда
			
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Проверяет наличие в информационной базе ранее сопоставленной номенклатуры поставщика
//
Функция ПроверитьСопоставленнуюНоменклатуруПоставщика(Знач Объект) Экспорт
	
	ВыводитьПредупреждение = Ложь;
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНоменклатуруПоставщиков") Тогда
		Возврат ВыводитьПредупреждение;
	КонецЕсли;
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	Товары.НомерСтроки            КАК НомерСтроки,
		|	Товары.НоменклатураПоставщика КАК НоменклатураПоставщика,
		|	Товары.Номенклатура           КАК Номенклатура,
		|	Товары.Характеристика         КАК Характеристика,
		|	Товары.Упаковка               КАК Упаковка
		|ПОМЕСТИТЬ
		|	Товары
		|ИЗ
		|	&Товары КАК Товары
		|ГДЕ
		|	Товары.НоменклатураПоставщика <> ЗНАЧЕНИЕ(Справочник.НоменклатураПоставщиков.ПустаяСсылка)
		|	И Товары.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|;
		|ВЫБРАТЬ
		|	ТоварыДляПроверки.НомерСтроки            КАК НомерСтроки,
		|	ТоварыДляПроверки.НоменклатураПоставщика КАК НоменклатураПоставщика,
		|	ТоварыДляПроверки.Номенклатура           КАК Номенклатура,
		|	ТоварыДляПроверки.Характеристика         КАК Характеристика,
		|	ТоварыДляПроверки.Упаковка               КАК Упаковка
		|ПОМЕСТИТЬ
		|	ТоварыДляПроверки
		|ИЗ
		|	Товары КАК ТоварыДляПроверки
		|ГДЕ
		|	ТоварыДляПроверки.НоменклатураПоставщика.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|;
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(НоменклатураПоставщиков.Ссылка) КАК КоличествоНоменклатурыПоставщика,
		|	НоменклатураПоставщиков.Номенклатура       КАК Номенклатура,
		|	НоменклатураПоставщиков.Характеристика     КАК Характеристика,
		|	НоменклатураПоставщиков.Упаковка           КАК Упаковка
		|ПОМЕСТИТЬ
		|	НоменклатураПоставщиков
		|ИЗ
		|	Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
		|ГДЕ
		|	НоменклатураПоставщиков.Владелец = &Партнер
		|	И Не НоменклатураПоставщиков.ПометкаУдаления
		|	И (Номенклатура,Характеристика,Упаковка) В (
		|		ВЫБРАТЬ
		|			Товары.Номенклатура,
		|			Товары.Характеристика,
		|			Товары.Упаковка
		|		ИЗ
		|			Товары КАК Товары
		|		)
		|СГРУППИРОВАТЬ ПО
		|	НоменклатураПоставщиков.Номенклатура,
		|	НоменклатураПоставщиков.Характеристика,
		|	НоменклатураПоставщиков.Упаковка
		|;
		|ВЫБРАТЬ
		|	ТоварыДляПроверки.НомерСтроки                             КАК НомерСтроки,
		|	ТоварыДляПроверки.Номенклатура                            КАК Номенклатура,
		|	ТоварыДляПроверки.Характеристика                          КАК Характеристика,
		|	ТоварыДляПроверки.Упаковка                                КАК Упаковка,
		|	НоменклатураПоставщиков.КоличествоНоменклатурыПоставщика  КАК КоличествоНоменклатурыПоставщика
		|ИЗ
		|	ТоварыДляПроверки КАК ТоварыДляПроверки
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	НоменклатураПоставщиков КАК НоменклатураПоставщиков
		|ПО
		|	ТоварыДляПроверки.Номенклатура = НоменклатураПоставщиков.Номенклатура
		|	И ТоварыДляПроверки.Характеристика = НоменклатураПоставщиков.Характеристика
		|	И ТоварыДляПроверки.Упаковка = НоменклатураПоставщиков.Упаковка
		|");
	
	ТаблицаТоваров = Новый ТаблицаЗначений();
	ТаблицаТоваров.Колонки.Добавить("НомерСтроки",            Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("НоменклатураПоставщика", Новый ОписаниеТипов("СправочникСсылка.НоменклатураПоставщиков"));
	ТаблицаТоваров.Колонки.Добавить("Номенклатура",           Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаТоваров.Колонки.Добавить("Характеристика",         Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаТоваров.Колонки.Добавить("Упаковка",               Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"));
	
	Для Каждого ТекСтрока Из Объект.Товары Цикл
		
		СтрокаТаблицы = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ТекСтрока);
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Товары",  ТаблицаТоваров);
	Запрос.УстановитьПараметр("Партнер", Объект.Партнер);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если Не РезультатЗапроса[3].Пустой() Тогда
		
		Выборка = РезультатЗапроса[3].Выбрать();
		
		Пока Выборка.Следующий() Цикл
		
			Если ЗначениеЗаполнено(Выборка.Характеристика) Тогда
				
				ТекстОшибки = НСтр("ru='Номенклатуре ""%Номенклатура%"" с характеристикой ""%Характеристика%"" уже сопоставлена другая номенклатура поставщика (%КоличествоНоменклатурыПоставщика%)';uk='Номенклатурі ""%Номенклатура%"" з характеристикою ""%Характеристика%"" вже зіставлена інша номенклатура постачальника (%КоличествоНоменклатурыПоставщика%)'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Номенклатура%",                     Выборка.Номенклатура);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Характеристика%",                   Выборка.Характеристика);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%КоличествоНоменклатурыПоставщика%", Выборка.КоличествоНоменклатурыПоставщика);
				
			Иначе
				
				ТекстОшибки = НСтр("ru='Номенклатуре ""%Номенклатура%"" уже сопоставлена другая номенклатура поставщика (%КоличествоНоменклатурыПоставщика%)';uk='Номенклатурі ""%Номенклатура%"" вже зіставлена інша номенклатура постачальника (%КоличествоНоменклатурыПоставщика%)'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Номенклатура%",                     Выборка.Номенклатура);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%КоличествоНоменклатурыПоставщика%", Выборка.КоличествоНоменклатурыПоставщика);
				
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				Объект.Ссылка,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.Товары", Выборка.НомерСтроки, "Номенклатура"),
				,
				ВыводитьПредупреждение);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ВыводитьПредупреждение;
	
КонецФункции

// Формирует список значений номенклатуры поставщика по партнеру, номенклатуре, характеристике, упаковке
//
Функция СформироватьСписокВыбораНоменклатурыПоставщика(Знач Партнер, Знач Номенклатура, Знач Характеристика) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НоменклатураПоставщиков.Ссылка КАК НоменклатураПоставщика
	|ИЗ
	|	Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
	|ГДЕ
	|	НоменклатураПоставщиков.Владелец = &Партнер
	|	И (НоменклатураПоставщиков.Номенклатура = &Номенклатура
	|			ИЛИ НоменклатураПоставщиков.Номенклатура В
	|				(ВЫБРАТЬ
	|					ТоварыДругогоКачества.Номенклатура
	|				ИЗ
	|					РегистрСведений.ТоварыДругогоКачества КАК ТоварыДругогоКачества
	|				ГДЕ
	|					ТоварыДругогоКачества.НоменклатураБрак = &Номенклатура))
	|	И НоменклатураПоставщиков.Характеристика = &Характеристика
	|	И (НЕ НоменклатураПоставщиков.ПометкаУдаления)");
		
	Запрос.УстановитьПараметр("Партнер",        Партнер);
	Запрос.УстановитьПараметр("Номенклатура",   Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	
	РезультатЗапроса = Запрос.Выполнить();
	СписокВыбора = Новый СписокЗначений();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			СписокВыбора.Добавить(Выборка.НоменклатураПоставщика);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СписокВыбора;
	
КонецФункции

// Формирует список значений номеров ГТД номенклатуре, характеристике
//
Функция ЗаполнитьСписокВыбораНомеровГТД(Номенклатура, Характеристика) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 100
	               |	ПоступлениеТоваровУслугТовары.Номенклатура,
	               |	ПоступлениеТоваровУслугТовары.Характеристика,
	               |	ПоступлениеТоваровУслугТовары.НомерГТД,
	               |	ПоступлениеТоваровУслугТовары.Ссылка,
	               |	ПоступлениеТоваровУслугТовары.Ссылка.Дата КАК Дата
	               |ПОМЕСТИТЬ СочетанияНоменклатураГТД
	               |ИЗ
	               |	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	               |ГДЕ
	               |	ПоступлениеТоваровУслугТовары.Номенклатура = &Номенклатура
	               |	И ПоступлениеТоваровУслугТовары.Характеристика = &Характеристика
	               |	И ПоступлениеТоваровУслугТовары.Ссылка.Проведен
	               |	И ПоступлениеТоваровУслугТовары.НомерГТД <> &НомерГТД
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ ПЕРВЫЕ 100
	               |	ОприходованиеИзлишковТоваровТовары.Номенклатура,
	               |	ОприходованиеИзлишковТоваровТовары.Характеристика,
	               |	ОприходованиеИзлишковТоваровТовары.НомерГТД,
	               |	ОприходованиеИзлишковТоваровТовары.Ссылка,
	               |	ОприходованиеИзлишковТоваровТовары.Ссылка.Дата
	               |ИЗ
	               |	Документ.ОприходованиеИзлишковТоваров.Товары КАК ОприходованиеИзлишковТоваровТовары
	               |ГДЕ
	               |	ОприходованиеИзлишковТоваровТовары.Номенклатура = &Номенклатура
	               |	И ОприходованиеИзлишковТоваровТовары.Характеристика = &Характеристика
	               |	И ОприходованиеИзлишковТоваровТовары.Ссылка.Проведен
	               |	И ОприходованиеИзлишковТоваровТовары.НомерГТД <> &НомерГТД
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ ПЕРВЫЕ 100
	               |	КорректировкаПоступленияТовары.Номенклатура,
	               |	КорректировкаПоступленияТовары.Характеристика,
	               |	КорректировкаПоступленияТовары.НомерГТД,
	               |	КорректировкаПоступленияТовары.Ссылка,
	               |	КорректировкаПоступленияТовары.Ссылка.Дата
	               |ИЗ
	               |	Документ.КорректировкаПоступления.Товары КАК КорректировкаПоступленияТовары
	               |ГДЕ
	               |	КорректировкаПоступленияТовары.Номенклатура = &Номенклатура
	               |	И КорректировкаПоступленияТовары.Характеристика = &Характеристика
	               |	И КорректировкаПоступленияТовары.Ссылка.Проведен
	               |	И КорректировкаПоступленияТовары.НомерГТД <> &НомерГТД
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ ПЕРВЫЕ 100
	               |	ПересортицаТоваровТовары.Номенклатура,
	               |	ПересортицаТоваровТовары.Характеристика,
	               |	ПересортицаТоваровТовары.НомерГТД,
	               |	ПересортицаТоваровТовары.Ссылка,
	               |	ПересортицаТоваровТовары.Ссылка.Дата
	               |ИЗ
	               |	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
	               |ГДЕ
	               |	ПересортицаТоваровТовары.Номенклатура = &Номенклатура
	               |	И ПересортицаТоваровТовары.Характеристика = &Характеристика
	               |	И ПересортицаТоваровТовары.Ссылка.Проведен
	               |	И ПересортицаТоваровТовары.НомерГТД <> &НомерГТД
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ ПЕРВЫЕ 100
	               |	ТаможеннаяДекларацияИмпортТовары.Номенклатура,
	               |	ТаможеннаяДекларацияИмпортТовары.Характеристика,
	               |	ТаможеннаяДекларацияИмпортТовары.НомерГТД,
	               |	ТаможеннаяДекларацияИмпортТовары.Ссылка,
	               |	ТаможеннаяДекларацияИмпортТовары.Ссылка.Дата
	               |ИЗ
	               |	Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ТаможеннаяДекларацияИмпортТовары
	               |ГДЕ
	               |	ТаможеннаяДекларацияИмпортТовары.Номенклатура = &Номенклатура
	               |	И ТаможеннаяДекларацияИмпортТовары.Характеристика = &Характеристика
	               |	И ТаможеннаяДекларацияИмпортТовары.Ссылка.Проведен
	               |	И ТаможеннаяДекларацияИмпортТовары.НомерГТД <> &НомерГТД
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата УБЫВ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 10
	               |	ДанныеСправочника.Ссылка КАК Ссылка,
	               |	ДанныеСправочника.Представление КАК Представление,
	               |	ДанныеСправочника.ПометкаУдаления КАК ПометкаУдаления,
	               |	МАКСИМУМ(СочетанияНоменклатураГТД.Дата) КАК Порядок
	               |ИЗ
	               |	СочетанияНоменклатураГТД КАК СочетанияНоменклатураГТД
				   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураГТД КАК ДанныеСправочника
	               |		ПО СочетанияНоменклатураГТД.НомерГТД = ДанныеСправочника.Ссылка
	               |ГДЕ
				   |    ИСТИНА
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ДанныеСправочника.Ссылка,
	               |	ДанныеСправочника.Представление,
				   |	ДанныеСправочника.ПометкаУдаления
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Порядок УБЫВ";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Запрос.УстановитьПараметр("НомерГТД", Справочники.НоменклатураГТД.ПустаяСсылка());
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаГТД = Запрос.Выполнить().Выбрать();
	СписокВыбора = Новый СписокЗначений();
	Пока ВыборкаГТД.Следующий() Цикл
		ЭлементВыбора = Новый Структура("Значение, ПометкаУдаления", ВыборкаГТД.Ссылка, ВыборкаГТД.ПометкаУдаления);
		Представление = СокрЛП(ВыборкаГТД.Представление);
		СписокВыбора.Добавить(ЭлементВыбора, Представление);
	КонецЦикла;
	
	Возврат СписокВыбора;

КонецФункции

// Формирует структуру для создания поступления по одному или нескольким заказам поставщикам
// Если в переданных заказах отличаются реквизиты шапки, выдается сообщение об ошибке
//
// Параметры:
// МассивСсылок   - Массив - заказы поставщикам, по которым необходимо ввести поступление
// РеквизитыШапки - Структура - структура, в которую будут помещены реквизиты шапки из массива заказов
//
// Возвращаемое значение:
// Булево
// Ложь, если в переданных заказах отличаются реквизиты шапки
//
Функция СформироватьДанныеЗаполненияПоступления(МассивСсылок, РеквизитыШапки) Экспорт
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	МИНИМУМ(ЗаказПоставщику.Подразделение)                  КАК Подразделение,
		|	МИНИМУМ(ЗаказПоставщику.Партнер)                        КАК Партнер,
		|	МИНИМУМ(ЗаказПоставщику.Контрагент)                     КАК Контрагент,
		|	МИНИМУМ(ЗаказПоставщику.Договор)                        КАК Договор,
		|	МИНИМУМ(ЗаказПоставщику.Организация)                    КАК Организация,
		|	МИНИМУМ(ЗаказПоставщику.Соглашение)                     КАК Соглашение,
		|	МИНИМУМ(ЗаказПоставщику.Склад)                          КАК Склад,
		|	МИНИМУМ(ЗаказПоставщику.ХозяйственнаяОперация)          КАК ХозяйственнаяОперация,
		|	МИНИМУМ(ЗаказПоставщику.ВалютаВзаиморасчетов)           КАК ВалютаВзаиморасчетов,
		|	МИНИМУМ(ЗаказПоставщику.ЦенаВключаетНДС)                КАК ЦенаВключаетНДС,
		|	МИНИМУМ(ЗаказПоставщику.ПорядокРасчетов)                КАК ПорядокРасчетов,
		|	МИНИМУМ(ЗаказПоставщику.ВернутьМногооборотнуюТару)      КАК ВернутьМногооборотнуюТару,
		|	МИНИМУМ(ЗаказПоставщику.СрокВозвратаМногооборотнойТары) КАК СрокВозвратаМногооборотнойТары,
		|	МИНИМУМ(ЗаказПоставщику.РассчитыватьДатуВозвратаТарыПоКалендарю) КАК РассчитыватьДатуВозвратаТарыПоКалендарю,
		|	МИНИМУМ(ЗаказПоставщику.КалендарьВозвратаТары)          КАК КалендарьВозвратаТары,
		|	МИНИМУМ(ЗаказПоставщику.ОписаниеПоступления)            КАК ОписаниеПоступления,
		|	МИНИМУМ(ЗаказПоставщику.НаправлениеДеятельности)        КАК НаправлениеДеятельности,
		|	ВЫБОР
		|		КОГДА
		|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказПоставщику.Подразделение) > 1
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОтличияПодразделение,
		|	ВЫБОР
		|		КОГДА
		|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказПоставщику.Партнер) > 1
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОтличияПартнер,
		|	ВЫБОР
		|		КОГДА
		|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказПоставщику.Контрагент) > 1
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОтличияКонтрагент,
		|	ВЫБОР
		|		КОГДА
		|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказПоставщику.Договор) > 1
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОтличияДоговор,
		|	ВЫБОР
		|		КОГДА
		|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказПоставщику.Организация) > 1
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОтличияОрганизация,
		|	ВЫБОР
		|		КОГДА
		|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказПоставщику.Соглашение) > 1
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОтличияСоглашение,
		|	ВЫБОР
		|		КОГДА
		|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказПоставщику.Склад) > 1
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОтличияСклад,
		|	ВЫБОР
		|		КОГДА
		|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказПоставщику.ХозяйственнаяОперация) > 1
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОтличияХозяйственнаяОперация,
		|	ВЫБОР
		|		КОГДА
		|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказПоставщику.ВалютаВзаиморасчетов) > 1
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОтличияВалютаВзаиморасчетов,
		|	ВЫБОР
		|		КОГДА
		|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказПоставщику.ЦенаВключаетНДС) > 1
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОтличияЦенаВключаетНДС,
		|	ВЫБОР
		|		КОГДА
		|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказПоставщику.ВернутьМногооборотнуюТару) > 1
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОтличияВернутьМногооборотнуюТару,
		|	ВЫБОР
		|		КОГДА
		|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказПоставщику.СрокВозвратаМногооборотнойТары) > 1
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОтличияСрокВозвратаМногооборотнойТары,
		|	ВЫБОР
		|		КОГДА
		|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказПоставщику.ПорядокРасчетов) > 1
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОтличияПорядокРасчетов,
		|	ВЫБОР
		|		КОГДА
		|			МИНИМУМ(ЗаказПоставщику.Склад.ЭтоГруппа) = ИСТИНА И МИНИМУМ(ЗаказПоставщику.Склад.ВыборГруппы) = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах)
		|		ТОГДА
		|			ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|		ИНАЧЕ
		|			МИНИМУМ(ЗаказПоставщику.Склад)
		|	КОНЕЦ КАК СкладПоступления,
		|	ВЫБОР
		|		КОГДА
		|			МИНИМУМ(ЗаказПоставщику.Склад.ЭтоГруппа) = ИСТИНА И МИНИМУМ(ЗаказПоставщику.Склад.ВыборГруппы) = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах)
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЗапрещеноВыбиратьГруппуСкладов,
		|	ВЫБОР
		|		КОГДА
		|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказПоставщику.НаправлениеДеятельности) > 1
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОтличияНаправленияДеятельности
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗаказПоставщику.Подразделение                   КАК Подразделение,
		|		ЗаказПоставщику.Партнер                         КАК Партнер,
		|		ЗаказПоставщику.Контрагент                      КАК Контрагент,
		|		ЗаказПоставщику.Договор                         КАК Договор,
		|		ЗаказПоставщику.Организация                     КАК Организация,
		|		ЗаказПоставщику.Соглашение                      КАК Соглашение,
		|		ЗаказПоставщику.Склад                           КАК Склад,
		|		ЗаказПоставщику.ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
		|		ЗаказПоставщику.Валюта                          КАК ВалютаВзаиморасчетов,
		|		ЗаказПоставщику.ЦенаВключаетНДС                 КАК ЦенаВключаетНДС,
		|		ЗаказПоставщику.ПорядокРасчетов                 КАК ПорядокРасчетов,
		|		ЗаказПоставщику.ВернутьМногооборотнуюТару       КАК ВернутьМногооборотнуюТару,
		|		ЗаказПоставщику.СрокВозвратаМногооборотнойТары  КАК СрокВозвратаМногооборотнойТары,
		|		ЕСТЬNULL(ЗаказПоставщику.Соглашение.РассчитыватьДатуВозвратаТарыПоКалендарю, ЛОЖЬ) КАК РассчитыватьДатуВозвратаТарыПоКалендарю,
		|		ЕСТЬNULL(ЗаказПоставщику.Соглашение.КалендарьВозвратаТары, Неопределено) КАК КалендарьВозвратаТары,
		|		&ОписаниеПоступленияТУ                          КАК ОписаниеПоступления,
		|		ЗаказПоставщику.НаправлениеДеятельности			КАК НаправлениеДеятельности
		|	ИЗ
		|		Документ.ЗаказПоставщику КАК ЗаказПоставщику
		|	ГДЕ
		|		ЗаказПоставщику.Ссылка В (&МассивСсылок)
		|	) КАК ЗаказПоставщику
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказПоставщику.Ссылка КАК ЗаказПоставщику,
		|	ЗаказПоставщику.Статус КАК Статус,
		|	ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.КПоступлению) КАК ДопустимыйСтатус1,
		|	ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Закрыт)       КАК ДопустимыйСтатус2,
		|	НЕОПРЕДЕЛЕНО                                                  КАК ДопустимыйСтатус3,
		|	НЕ ЗаказПоставщику.Проведен КАК ЕстьОшибкиПроведен,
		|	ВЫБОР КОГДА ЗаказПоставщику.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.КПоступлению)
		|			ИЛИ ЗаказПоставщику.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Закрыт) ТОГДА
		|		ЛОЖЬ
		|	ИНАЧЕ
		|		ИСТИНА
		|	КОНЕЦ КАК ЕстьОшибкиСтатус
		|ИЗ
		|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
		|ГДЕ
		|	ЗаказПоставщику.Ссылка В (&МассивСсылок)
		|	И (НЕ ЗаказПоставщику.Проведен ИЛИ
		|		  ЗаказПоставщику.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.КПоступлению)
		|		И ЗаказПоставщику.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Закрыт))
		|");
		
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	Запрос.УстановитьПараметр("ОписаниеПоступленияТУ", НСтр("ru='поступления товаров и услуг';uk='надходження товарів і послуг'"));
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаРеквизитыШапки = РезультатЗапроса[0].Выбрать();
	ВыборкаРеквизитыШапки.Следующий();
	
	Отказ = Ложь;
	
	ТекстСообщения = НСтр("ru='У выделенных распоряжений отличается поле ""%ПредставлениеПоля%""';uk='У виділених розпоряджень відрізняється поле ""%ПредставлениеПоля%""'");
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияПодразделение Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Подразделение';uk='Підрозділ'")),
			,
			,
			,
			Отказ);
		
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияПартнер Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Партнер';uk='Партнер'")),
			,
			,
			,
			Отказ);
		
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияКонтрагент Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Контрагент';uk='Контрагент'")),
			,
			,
			,
			Отказ);
		
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияОрганизация Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Организация';uk='Організація'")),
			,
			,
			,
			Отказ);
		
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияСоглашение Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Соглашение';uk='Оферта'")),
			,
			,
			,
			Отказ);
		
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияДоговор Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Договор';uk='Договір'")),
			,
			,
			,
			Отказ);
		
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияСклад Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Склад';uk='Склад'")),
			,
			,
			,
			Отказ);
		
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияХозяйственнаяОперация Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Операция';uk='Операція'")),
			,
			,
			,
			Отказ);
		
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияВалютаВзаиморасчетов Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Валюта взаиморасчетов';uk='Валюта взаєморозрахунків'")),
			,
			,
			,
			Отказ);
		
	КонецЕсли;
	
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияЦенаВключаетНДС Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Цена включает НДС';uk='Ціна включає ПДВ'")),
			,
			,
			,
			Отказ);
		
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияВернутьМногооборотнуюТару Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Вернуть многооборотную тару';uk='Повернути багатооборотну тару'")),
			,
			,
			,
			Отказ);
		
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияСрокВозвратаМногооборотнойТары Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Срок возврата многооборотной тары';uk='Строк повернення багатооборотної тари'")),
			,
			,
			,
			Отказ);
		
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияПорядокРасчетов Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Порядок расчетов';uk='Порядок розрахунків'")),
			,
			,
			,
			Отказ);
		
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияНаправленияДеятельности Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Направление деятельности';uk='Напрям діяльності'")),
			,
			,
			,
			Отказ);
		
	КонецЕсли;
		
	Если Отказ Тогда
		
		ТекстОшибки = НСтр("ru='Ввод одного %ОписаниеПоступления% на основании выделенных распоряжений невозможен';uk='Введення одного %ОписаниеПоступления% на підставі виділених розпоряджень неможливе'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеПоступления%", ВыборкаРеквизитыШапки.ОписаниеПоступления);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			,
			,
			,
			Отказ);
		
	Иначе
		
		Если Не РезультатЗапроса[1].Пустой() > 0 Тогда
			
			ВыборкаЗаказы = РезультатЗапроса[1].Выбрать();
			
			Пока ВыборкаЗаказы.Следующий() Цикл
			
				Если ВыборкаЗаказы.ЕстьОшибкиПроведен Тогда
					
					ТекстОшибки = НСтр("ru='Документ %Документ% не проведен. Ввод на основании непроведенного документа запрещен.';uk='Документ %Документ% не проведено. Введення на підставі непроведенного документа заборонене.'");
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ВыборкаЗаказы.ЗаказПоставщику);
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ТекстОшибки,
						,
						,
						,
						Отказ);
					
				ИначеЕсли ВыборкаЗаказы.ЕстьОшибкиСтатус Тогда
					
					ТекстОшибки = НСтр("ru='Документ %Документ% находится в статусе ""%Статус%"". Ввод на основании разрешен только в статусах';uk='Документ %Документ% знаходиться в статусі ""%Статус%"". Введення на підставі дозволене тільки в статусах'") + " ";
					
					Для Х = 1 По 3 Цикл
						
						ДопустимыйСтатус = ВыборкаЗаказы["ДопустимыйСтатус" + Х];
						Если ЗначениеЗаполнено(ДопустимыйСтатус) Тогда
							ТекстОшибки = ТекстОшибки + """" + ДопустимыйСтатус + """, "
						КонецЕсли;
						
					КонецЦикла;
					
					ТекстОшибки = Лев(ТекстОшибки, СтрДлина(ТекстОшибки) - 2); // Обрезаем последнюю запятую
					ТекстОшибки = ТекстОшибки + ".";
					
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ВыборкаЗаказы.ЗаказПоставщику);
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Статус%",   ВыборкаЗаказы.Статус);
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ТекстОшибки,
						,
						,
						,
						Отказ);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		РеквизитыШапки.Вставить("Подразделение",                  ВыборкаРеквизитыШапки.Подразделение);
		РеквизитыШапки.Вставить("Партнер",                        ВыборкаРеквизитыШапки.Партнер);
		РеквизитыШапки.Вставить("Контрагент",                     ВыборкаРеквизитыШапки.Контрагент);
		РеквизитыШапки.Вставить("Договор",                        ВыборкаРеквизитыШапки.Договор);
		РеквизитыШапки.Вставить("Организация",                    ВыборкаРеквизитыШапки.Организация);
		РеквизитыШапки.Вставить("Склад",                          ВыборкаРеквизитыШапки.Склад);
		РеквизитыШапки.Вставить("Соглашение",                     ВыборкаРеквизитыШапки.Соглашение);
		РеквизитыШапки.Вставить("ХозяйственнаяОперация",          ВыборкаРеквизитыШапки.ХозяйственнаяОперация);
		РеквизитыШапки.Вставить("ВалютаВзаиморасчетов",           ВыборкаРеквизитыШапки.ВалютаВзаиморасчетов);
		РеквизитыШапки.Вставить("ЦенаВключаетНДС",                ВыборкаРеквизитыШапки.ЦенаВключаетНДС);
		РеквизитыШапки.Вставить("ПорядокРасчетов",                ВыборкаРеквизитыШапки.ПорядокРасчетов);
		РеквизитыШапки.Вставить("ВернутьМногооборотнуюТару",      ВыборкаРеквизитыШапки.ВернутьМногооборотнуюТару);
		РеквизитыШапки.Вставить("РассчитыватьДатуВозвратаТарыПоКалендарю", ВыборкаРеквизитыШапки.РассчитыватьДатуВозвратаТарыПоКалендарю);
		РеквизитыШапки.Вставить("КалендарьВозвратаТары",          ВыборкаРеквизитыШапки.КалендарьВозвратаТары);
		РеквизитыШапки.Вставить("СрокВозвратаМногооборотнойТары", ВыборкаРеквизитыШапки.СрокВозвратаМногооборотнойТары);
		РеквизитыШапки.Вставить("СкладПоступления",               ВыборкаРеквизитыШапки.СкладПоступления);
		РеквизитыШапки.Вставить("ЗапрещеноВыбиратьГруппуСкладов", ВыборкаРеквизитыШапки.ЗапрещеноВыбиратьГруппуСкладов);
		РеквизитыШапки.Вставить("НаправлениеДеятельности", 		  ВыборкаРеквизитыШапки.НаправлениеДеятельности);
		
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

// Процедура заполняет банковский счета документа по договору.
//
// Параметры:
//		Договор - СправочникСсылка.ДоговорыКонтрагентов - Договор;
//		БанковскийСчетОрганизации - СправочникСсылка.БанковскиеСчетаОрганизаций - Банковский счет организации;
//		БанковскийСчетКонтрагента - СправочникСсылка.БанковскиеСчетаКонтрагентов - Банковский счет контрагента.
//
Процедура ЗаполнитьБанковскиеСчетаПоДоговору(Договор, БанковскийСчетОрганизации = Неопределено, БанковскийСчетКонтрагента = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(Договор) Тогда
		Справочники.ДоговорыКонтрагентов.ЗаполнитьБанковскиеСчетаПоДоговору(Договор, БанковскийСчетОрганизации, БанковскийСчетКонтрагента);
	КонецЕсли;
	
КонецПроцедуры



// Функция проверки наличия графика оплаты в соглашении
//
// Параметры:
// 		Соглашение - СправочникСсылка.Соглашение
//
// Возвращаемое значение:
// 		Булево - "Истина", если в графике есть хотя бы один этап
//
Функция ГрафикСоглашенияЗаполнен(Соглашение) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Соглашение) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СоглашенияСПоставщикамиЭтапыГрафикаОплаты.Ссылка
	|ИЗ
	|	Справочник.СоглашенияСПоставщиками.ЭтапыГрафикаОплаты КАК СоглашенияСПоставщикамиЭтапыГрафикаОплаты
	|ГДЕ
	|	СоглашенияСПоставщикамиЭтапыГрафикаОплаты.Ссылка = &Соглашение");
	Запрос.УстановитьПараметр("Соглашение", Соглашение);
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

// Возвращает количество соглашений с указанным поставщиком
//
// Параметры:
//	Партнер           - Ссылка на партнера, для которого необходимо получить количество соглашений
//	ПараметрыОтбора   - Структура с параметрами отбора соглашений 
//
// Возвращаемое значение:
//  Структура, включающая условия продаж
//
Функция ПолучитьКоличествоСоглашенийСПоставщиком(Знач Партнер,
	                                      Знач ПараметрыОтбора = Неопределено) Экспорт
	
	ВсеПараметрыОтбора = Новый Структура();
	ВсеПараметрыОтбора.Вставить("ТолькоДляЗакупки",                        Истина);
	ВсеПараметрыОтбора.Вставить("ТолькоДействующее",                       Истина);
	ВсеПараметрыОтбора.Вставить("ИсключитьГруппыСкладовДоступныеВЗаказах", Ложь);
	ВсеПараметрыОтбора.Вставить("ХозяйственныеОперации",                   Неопределено);
	
	Если ПараметрыОтбора <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ВсеПараметрыОтбора, ПараметрыОтбора);
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ 
	|	СоглашениеСПоставщиком.Ссылка                       КАК Соглашение
	|ИЗ
	|	Справочник.СоглашенияСПоставщиками КАК СоглашениеСПоставщиком
	|ГДЕ
	|	НЕ СоглашениеСПоставщиком.ПометкаУдаления И
	|	(НЕ &ОтборХозяйственныеОперации ИЛИ СоглашениеСПоставщиком.ХозяйственнаяОперация В (&ХозяйственныеОперации)) И
	|" + ?(ВсеПараметрыОтбора.ТолькоДействующее," СоглашениеСПоставщиком.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийСПоставщиками.Действует) И","") + "
	|	СоглашениеСПоставщиком.Партнер = &Партнер
	|";
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.УстановитьПараметр("Партнер",                    Партнер);
	Запрос.УстановитьПараметр("ОтборХозяйственныеОперации", ВсеПараметрыОтбора.ХозяйственныеОперации <> Неопределено);
	Запрос.УстановитьПараметр("ХозяйственныеОперации",      ВсеПараметрыОтбора.ХозяйственныеОперации);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка.Количество();
	
КонецФункции // ПолучитьКоличествоСоглашенийСПоставщиком()

#КонецОбласти
