#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если РасчетСебестоимости.ДвиженияЗаписываютсяРасчетомСебестоимости(ЭтотОбъект)
	 ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
	 ИЛИ ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	РасчетСебестоимости.СохранитьДвиженияСформированныеРасчетомСебестоимости(ЭтотОбъект, Замещение);
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Таблица.Период                            КАК Период,
	|	Таблица.Регистратор                       КАК Регистратор,
	|	Таблица.АналитикаУчетаНоменклатуры        КАК АналитикаУчетаНоменклатуры,
	|	Таблица.АналитикаУчетаПоПартнерам         КАК АналитикаУчетаПоПартнерам,
	|	Таблица.Подразделение                     КАК Подразделение,
	|	Таблица.ТипЗапасов                        КАК ТипЗапасов,
	|	Таблица.ВидЗапасов                        КАК ВидЗапасов,
	|	Таблица.Склад                             КАК Склад,
	|	Таблица.Договор                           КАК Договор,
	|
	|	Таблица.Количество                        КАК Количество,
	|	Таблица.СуммаВыручки                      КАК СуммаВыручки,
	|	Таблица.СуммаВыручкиБезНДС                КАК СуммаВыручкиБезНДС,
	|	Таблица.Себестоимость                     КАК Себестоимость,
	|	Таблица.СебестоимостьБезНДС               КАК СебестоимостьБезНДС,
	|	Таблица.СуммаДополнительныхРасходов       КАК СуммаДополнительныхРасходов,
	|	Таблица.СуммаДополнительныхРасходовБезНДС КАК СуммаДополнительныхРасходовБезНДС,
	|	Таблица.СебестоимостьРегл                 КАК СебестоимостьРегл,
	|	Таблица.СуммаВыручкиРегл                  КАК СуммаВыручкиРегл,
	|	Таблица.ПостояннаяРазница                 КАК ПостояннаяРазница,
	|	Таблица.ВременнаяРазница                  КАК ВременнаяРазница,
	|	Таблица.СуммаВыручкиСНДСРегл              КАК СуммаВыручкиСНДСРегл,
	|
	|	Таблица.НалоговоеНазначение               КАК НалоговоеНазначение,
	|	Таблица.ВалютаВзаиморасчетов              КАК ВалютаВзаиморасчетов,
	|	Таблица.СуммаВВалютеВзаиморасчетов        КАК СуммаВВалютеВзаиморасчетов,
	|	Таблица.СуммаБезНДСВВалютеВзаиморасчетов  КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|	Таблица.ВалютаДокумента                   КАК ВалютаДокумента,
	|	Таблица.СуммаВВалютеДокумента             КАК СуммаВВалютеДокумента,
	|	Таблица.СуммаБезНДСВВалютеДокумента       КАК СуммаБезНДСВВалютеДокумента
	|ПОМЕСТИТЬ втВыручкаИСебестоимостьПродажПередЗаписью
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|");

	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
		
	Перем ДатаДокументаИзменена;
	
	Если РасчетСебестоимости.ДвиженияЗаписываютсяРасчетомСебестоимости(ЭтотОбъект)
	 ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
	 ИЛИ ОбменДанными.Загрузка
	 ИЛИ ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК Месяц,
	|	Таблица.Организация                 КАК Организация,
	|	Таблица.Регистратор                 КАК Документ
	|ПОМЕСТИТЬ втИзменениеСуммыВыручкиРегл
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПередЗаписью.Период                            КАК Период,
	|		ПередЗаписью.Регистратор                       КАК Регистратор,
	|		КлючиАналитикаУчетаПоПартнерам.Организация     КАК Организация,
	|		
	|		ПередЗаписью.СуммаВыручкиРегл                  КАК СуммаВыручкиРегл
	|	ИЗ
	|		втВыручкаИСебестоимостьПродажПередЗаписью КАК ПередЗаписью
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаПоПартнерам КАК КлючиАналитикаУчетаПоПартнерам
	|		ПО
	|			ПередЗаписью.АналитикаУчетаПоПартнерам = КлючиАналитикаУчетаПоПартнерам.КлючАналитики
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПослеЗаписи.Период                            КАК Период,
	|		ПослеЗаписи.Регистратор                       КАК Регистратор,
	|		КлючиАналитикаУчетаПоПартнерам.Организация    КАК Организация,
	|
	|		-ПослеЗаписи.СуммаВыручкиРегл                 КАК СуммаВыручкиРегл
	|	ИЗ
	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ПослеЗаписи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаПоПартнерам КАК КлючиАналитикаУчетаПоПартнерам
	|		ПО
	|			ПослеЗаписи.АналитикаУчетаПоПартнерам = КлючиАналитикаУчетаПоПартнерам.КлючАналитики
	|	ГДЕ
	|		ПослеЗаписи.Регистратор = &Регистратор) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ),
	|	Таблица.Регистратор,
	|	Таблица.Организация
	|	
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.СуммаВыручкиРегл) <> 0
	|;
	|
	|//////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК МЕСЯЦ,
	|	Таблица.Организация                  КАК Организация,
	|	Таблица.Регистратор                  КАК Документ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПередЗаписью.Период                            КАК Период,
	|		ПередЗаписью.Регистратор                       КАК Регистратор,
	|		КлючиАналитикаУчетаПоПартнерам.Организация     КАК Организация,
	|		ПередЗаписью.АналитикаУчетаНоменклатуры        КАК АналитикаУчетаНоменклатуры,
	|		ПередЗаписью.АналитикаУчетаПоПартнерам         КАК АналитикаУчетаПоПартнерам,
	|		ПередЗаписью.Подразделение                     КАК Подразделение,
	|		ПередЗаписью.ТипЗапасов                        КАК ТипЗапасов,
	|		ПередЗаписью.ВидЗапасов                        КАК ВидЗапасов,
	|		ПередЗаписью.Склад                             КАК Склад,
	|		ПередЗаписью.Договор                           КАК Договор,
	|
	|		ПередЗаписью.Количество                        КАК Количество,
	|		ПередЗаписью.СуммаВыручки                      КАК СуммаВыручки,
	|		ПередЗаписью.СуммаВыручкиБезНДС                КАК СуммаВыручкиБезНДС,
	|		ПередЗаписью.Себестоимость                     КАК Себестоимость,
	|		ПередЗаписью.СебестоимостьБезНДС               КАК СебестоимостьБезНДС,
	|		ПередЗаписью.СуммаДополнительныхРасходов       КАК СуммаДополнительныхРасходов,
	|		ПередЗаписью.СуммаДополнительныхРасходовБезНДС КАК СуммаДополнительныхРасходовБезНДС,
	|		ПередЗаписью.СебестоимостьРегл                 КАК СебестоимостьРегл,
	|		ПередЗаписью.СуммаВыручкиРегл                  КАК СуммаВыручкиРегл,
	|		ПередЗаписью.ПостояннаяРазница                 КАК ПостояннаяРазница,
	|		ПередЗаписью.ВременнаяРазница                  КАК ВременнаяРазница,
	|		ПередЗаписью.СуммаВыручкиСНДСРегл              КАК СуммаВыручкиСНДСРегл
	|	ИЗ
	|		втВыручкаИСебестоимостьПродажПередЗаписью КАК ПередЗаписью
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаПоПартнерам КАК КлючиАналитикаУчетаПоПартнерам
	|		ПО
	|			ПередЗаписью.АналитикаУчетаПоПартнерам = КлючиАналитикаУчетаПоПартнерам.КлючАналитики
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПослеЗаписи.Период                             КАК Период,
	|		ПослеЗаписи.Регистратор                        КАК Регистратор,
	|		КлючиАналитикаУчетаПоПартнерам.Организация     КАК Организация,
	|		ПослеЗаписи.АналитикаУчетаНоменклатуры         КАК АналитикаУчетаНоменклатуры,
	|		ПослеЗаписи.АналитикаУчетаПоПартнерам          КАК АналитикаУчетаПоПартнерам,
	|		ПослеЗаписи.Подразделение                      КАК Подразделение,
	|		ПослеЗаписи.ТипЗапасов                         КАК ТипЗапасов,
	|		ПослеЗаписи.ВидЗапасов                         КАК ВидЗапасов,
	|		ПослеЗаписи.Склад                              КАК Склад,
	|		ПослеЗаписи.Договор                            КАК Договор,
	|
	|		-ПослеЗаписи.Количество                        КАК Количество,
	|		-ПослеЗаписи.СуммаВыручки                      КАК СуммаВыручки,
	|		-ПослеЗаписи.СуммаВыручкиБезНДС                КАК СуммаВыручкиБезНДС,
	|		-ПослеЗаписи.Себестоимость                     КАК Себестоимость,
	|		-ПослеЗаписи.СебестоимостьБезНДС               КАК СебестоимостьБезНДС,
	|		-ПослеЗаписи.СуммаДополнительныхРасходов       КАК СуммаДополнительныхРасходов,
	|		-ПослеЗаписи.СуммаДополнительныхРасходовБезНДС КАК СуммаДополнительныхРасходовБезНДС,
	|		-ПослеЗаписи.СебестоимостьРегл                 КАК СебестоимостьРегл,
	|		-ПослеЗаписи.СуммаВыручкиРегл                  КАК СуммаВыручкиРегл,
	|		-ПослеЗаписи.ПостояннаяРазница                 КАК ПостояннаяРазница,
	|		-ПослеЗаписи.ВременнаяРазница                  КАК ВременнаяРазница,
	|		-ПослеЗаписи.СуммаВыручкиСНДСРегл              КАК СуммаВыручкиСНДСРегл
	|	ИЗ
	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ПослеЗаписи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаПоПартнерам КАК КлючиАналитикаУчетаПоПартнерам
	|		ПО
	|			ПослеЗаписи.АналитикаУчетаПоПартнерам = КлючиАналитикаУчетаПоПартнерам.КлючАналитики
	|	ГДЕ
	|		ПослеЗаписи.Регистратор = &Регистратор
	|	) КАК Таблица
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.Регистратор,
	|	Таблица.Организация,
	|	Таблица.АналитикаУчетаНоменклатуры,
	|	Таблица.АналитикаУчетаПоПартнерам,
	|	Таблица.Подразделение,
	|	Таблица.ТипЗапасов,
	|	Таблица.ВидЗапасов,
	|	Таблица.Склад,
	|	Таблица.Договор
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Количество) <> 0
	|	ИЛИ СУММА(Таблица.СуммаВыручки) <> 0
	|	ИЛИ СУММА(Таблица.СуммаВыручкиБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.Себестоимость) <> 0
	|	ИЛИ СУММА(Таблица.СебестоимостьБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.СуммаДополнительныхРасходов) <> 0
	|	ИЛИ СУММА(Таблица.СуммаДополнительныхРасходовБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.СебестоимостьРегл) <> 0
	|	ИЛИ СУММА(Таблица.СуммаВыручкиРегл) <> 0
	|	ИЛИ СУММА(Таблица.ПостояннаяРазница) <> 0
	|	ИЛИ СУММА(Таблица.ВременнаяРазница) <> 0
	|	ИЛИ СУММА(Таблица.СуммаВыручкиСНДСРегл) <> 0
	|;
	|
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втВыручкаИСебестоимостьПродажПередЗаписью
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втИзменениеСуммыВыручкиРегл
	|");

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	МассивЗапросов = Запрос.ВыполнитьПакет();
	
	РегистрыСведений.ЗаданияКРасчетуСебестоимости.СоздатьЗаписиРегистраПоДаннымВыборки(МассивЗапросов[1].Выбрать());
	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
