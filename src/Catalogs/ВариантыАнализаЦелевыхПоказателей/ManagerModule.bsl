#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает настройки оформления по умолчанию для вариантов анализа 
//
// Возвращаемое значение:
//	Структура - структура, содержащая настройки оформления
//
Функция НастройкиОформленияПоУмолчанию(ПокомпонентноеСравнение = Ложь) Экспорт
	
	// Сформируем настройки цветов
	ЦветовыеНастройки = Новый Структура;
	
	НаборЦветов = Новый Соответствие;
	НаборЦветов.Вставить("Значение", WebЦвета.СинийСоСтальнымОттенком);
	НаборЦветов.Вставить("Прогноз", WebЦвета.НейтральноФиолетовоКрасный);
	НаборЦветов.Вставить("ЦелевоеЗначение", WebЦвета.ЗеленыйЛес);
	НаборЦветов.Вставить("ПозитивноеОтклонение", WebЦвета.КоролевскиГолубой);
	НаборЦветов.Вставить("НегативноеОтклонение", WebЦвета.Малиновый);
	НаборЦветов.Вставить("ЗонаДопустимыхОтклонений", Новый Цвет(255, 195, 0));
	
	ЦветовыеНастройки.Вставить("Цвета", НаборЦветов);
	
	ХранилищеНастроекОформления = Новый ХранилищеЗначения(ЦветовыеНастройки);
	
	// Сформируем настройки параметров вывода
	ТолькоЦветОсновнойСерии = Истина;
	ГрадиентДляПокомпонетногоСравнения = Истина;
	ВыделятьМаксимальноеЗначениеДляПокомпонетногоСравнения = Ложь;
	ВыводитьМаркерТочекПрогноза = Ложь;
	ОтображатьЛегенду = Истина;
	ВыводитьПодписиКДиаграммам = Истина;
	ОкантовкаДиаграмм = Ложь;
	РежимСглаживанияДиаграмм = Истина;
	РежимШкалыЗначений = Перечисления.РежимШкалыЗначенийДиаграмм.Авто;
	ВключатьНоль = Истина;
	
	СтруктураНастроекОформления  = Новый Структура("ХранилищеНастроекОформления, 
	|ТолькоЦветОсновнойСерии,
	|ГрадиентДляПокомпонетногоСравнения,
	|ВыделятьМаксимальноеЗначениеДляПокомпонетногоСравнения,
	|ВыводитьМаркерТочекПрогноза,
	|ОтображатьЛегенду,
	|ВыводитьПодписиКДиаграммам,
	|ОкантовкаДиаграмм,
	|РежимСглаживанияДиаграмм,
	|РежимШкалыЗначений,
	|ВключатьНоль", 
		ХранилищеНастроекОформления, 
		ТолькоЦветОсновнойСерии,
		ГрадиентДляПокомпонетногоСравнения,
		ВыделятьМаксимальноеЗначениеДляПокомпонетногоСравнения,
		ВыводитьМаркерТочекПрогноза,
		ОтображатьЛегенду,
		ВыводитьПодписиКДиаграммам,
		ОкантовкаДиаграмм,
		РежимСглаживанияДиаграмм,
		РежимШкалыЗначений,
		ВключатьНоль);
	
	Возврат СтруктураНастроекОформления;
	
КонецФункции

// Помещает во временное хранилище схему компоновки данных,
// настройки компоновки данных и пользовательские настройки и возвращает их адреса
//
// Параметры:
//	ВариантАнализа - Ссылка, СправочникСсылка.ВариантыАнализаЦелевыхПоказателей - вариант анализа, для которого возвращаются адреса
//
// Возвращаемое значение:
//	Структура - структура, содержащая адреса
//
Функция АдресаСхемыКомпоновкиДанныхИПользовательскихНастроек(ВариантАнализа) Экспорт
	
	Адреса = Новый Структура("СхемаКомпоновкиДанных, НастройкиКомпоновкиДанных, ПользовательскиеНастройки");
	
	// Получим схему компоновки данных
	Если ЗначениеЗаполнено(ВариантАнализа.Владелец.СхемаКомпоновкиДанных) ИЛИ ВариантАнализа.Владелец.ХранилищеСхемыКомпоновкиДанных.Получить() = Неопределено Тогда
		СхемаИНастройки = Справочники.СтруктураЦелей.ОписаниеИСхемаКомпоновкиДанныхЦелиПоИмениМакета(ВариантАнализа.Владелец, ВариантАнализа.Владелец.СхемаКомпоновкиДанных);
		СхемаКомпоновкиДанных = СхемаИНастройки.СхемаКомпоновкиДанных;
	Иначе
		СхемаКомпоновкиДанных = ВариантАнализа.Владелец.ХранилищеСхемыКомпоновкиДанных.Получить();
	КонецЕсли;
	
	Адреса.СхемаКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, Новый УникальныйИдентификатор());
	
	Настройки = ВариантАнализа.Владелец.ХранилищеНастроекКомпоновкиДанных.Получить();
	
	Если ЗначениеЗаполнено(Настройки) Тогда
		Адреса.НастройкиКомпоновкиДанных = ПоместитьВоВременноеХранилище(Настройки, Новый УникальныйИдентификатор());
	КонецЕсли;
	
	ПользовательскиеНастройки = ВариантАнализа.ХранилищеПользовательскихНастроекКомпоновкиДанных.Получить();
	
	Если ЗначениеЗаполнено(ПользовательскиеНастройки) Тогда
		Адреса.ПользовательскиеНастройки = ПоместитьВоВременноеХранилище(ПользовательскиеНастройки, Новый УникальныйИдентификатор());
	КонецЕсли;
	
	Возврат Адреса;
	
КонецФункции

// Возвращает демонстрационные данные, указанные при варианте анализа
//
// Параметры:
//	ВариантАнализа - Ссылка, СправочникСсылка.ВариантыАнализаЦелевыхПоказателей - вариант анализа, для которого возвращаются адреса
//
// Возвращаемое значение:
//	ТаблицаЗначений - таблица демонстрационных данных варианта анализа
//
Функция ДемонстрационныеДанныеВариантаАнализа(ВариантАнализа) Экспорт 
	ДемонстрационныеДанные = Новый ТаблицаЗначений;
	
	ЗначениеАнализаИмя = Строка(ВариантАнализа.ЗначениеАнализа.Получить());
	ЗначениеАнализаДополнительноеИмя = Строка(ВариантАнализа.ЗначениеАнализаДополнительное.Получить());
	ОбъектАнализаИмя = Строка(ВариантАнализа.ОбъектАнализа.Получить());
	
	ХранилищеДемонстрационныхДанныхЗначение = ВариантАнализа.ХранилищеДемонстрационныхДанных.Получить();
	Если ХранилищеДемонстрационныхДанныхЗначение <> Неопределено Тогда
		ДемонстрационныеДанные = ХранилищеДемонстрационныхДанныхЗначение.Данные.Получить();
		
		ДемонстрационныеДанные.Колонки.ЗначениеПоказателя.Имя = ЗначениеАнализаИмя;
		
		Если ВариантАнализа.ТипАнализа = Перечисления.ТипыАнализаПоказателей.ПокомпонентноеСравнение
			ИЛИ ВариантАнализа.ТипАнализа = Перечисления.ТипыАнализаПоказателей.ПокомпонентноеСравнениеДинамика Тогда
			Если ВариантАнализа.РежимПокомпонентногоСравнения = 0 Тогда
				ДемонстрационныеДанные.Колонки.ОбъектАнализа.Имя = ОбъектАнализаИмя;
			Иначе
				ДемонстрационныеДанные.Колонки.ЗначениеПоказателяДополнительное.Имя = ЗначениеАнализаДополнительноеИмя;
			КонецЕсли;
		КонецЕсли;
		Если ВариантАнализа.ТипАнализа = Перечисления.ТипыАнализаПоказателей.ПокомпонентноеСравнение Тогда
			Если ВариантАнализа.РежимПокомпонентногоСравнения = 0 Тогда
				ПоследняяДата = ДемонстрационныеДанные[0].Период;
				ДемонстрационныеДанные.ЗаполнитьЗначения(Неопределено, "Период");
				
				ИтогПоТаблице = ДемонстрационныеДанные.Итог(ЗначениеАнализаИмя);
				НоваяСтрока = ДемонстрационныеДанные.Добавить();
				НоваяСтрока.Период = ПоследняяДата;
				НоваяСтрока[ЗначениеАнализаИмя] = ИтогПоТаблице;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат ДемонстрационныеДанные;
КонецФункции
#КонецОбласти

#Область ОбновлениеИнформационнойБазы


// Обработчик обновления BAS УТ 3.2.2
// Заполняет новые реквизиты варианта анализа - ПериодичностьРасчета, РежимШкалыЗначений, ВариантОтображенияПоУмолчанию
Процедура ЗаполнитьПериодичностьРасчетаРежимШкалыЗначенийВариантОтображенияМонопольно() Экспорт
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ВАЦП.Ссылка,
	|	ВЫБОР 
	|		КОГДА ВАЦП.ПериодичностьРасчетаПоказателя.Ссылка ЕСТЬ NULL
	|			ТОГДА ВАЦП.ПериодичностьКонтроля
	|		ИНАЧЕ ВАЦП.ПериодичностьРасчетаПоказателя
	|	КОНЕЦ ПериодичностьРасчетаПоказателя,
	|
	|	ИСТИНА КАК ВключатьНоль,
	|	ЗНАЧЕНИЕ(Перечисление.РежимШкалыЗначенийДиаграмм.Авто) КАК РежимШкалыЗначений,
	|
	|	ВЫБОР
	|		КОГДА ВАЦП.ВариантОтображенияПоУмолчанию.Ссылка ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыОтображенияВариантовАнализа.Диаграмма)
	|		ИНАЧЕ ВАЦП.ВариантОтображенияПоУмолчанию
	|	КОНЕЦ КАК ВариантОтображенияПоУмолчанию
	|ИЗ
	|	Справочник.ВариантыАнализаЦелевыхПоказателей КАК ВАЦП");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если СправочникОбъект = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СправочникОбъект, Выборка);
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
	КонецЦикла;
КонецПроцедуры

// Обработчик обновления BAS УТ 3.2.2
// Очищает кэш рассчетных данных в связи с изменением структуры источника данных.
// Источники данных будут перезаполнены при ближайшем запуске соответствующего регламентного задания
// или запуске монитора целевых показателей
Процедура ОчиститьИсточникиДанныхВариантовАнализаЦелевыхПоказателей() Экспорт
	НаборЗаписей = РегистрыСведений.ИсточникиДанныхВариантовАнализаЦелевыхПоказателей.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Отчеты

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

