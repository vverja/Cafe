
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ДатаКурса = НачалоДня(ТекущаяДатаСеанса());
	Список.Параметры.УстановитьЗначениеПараметра("КонецПериода", ДатаКурса);
	Список.Параметры.УстановитьЗначениеПараметра("ПояснениеКратности", НСтр("ru='грн. за';uk='грн. за'"));
	
	Элементы.Валюты.РежимВыбора = Параметры.РежимВыбора;
	
	ДоступноИспользованиеЗагрузкаКурсовВалютФинансУА = Истина;
	РаботаСКурсамиВалютПереопределяемый.ПриОпределенииВозможностиИспользоватьЗагрузкаКурсовВалютФинансУА(ДоступноИспользованиеЗагрузкаКурсовВалютФинансУА);
	Элементы.ФормаЗагрузитьКурсыВалютФинансУА.Видимость = Метаданные.Обработки.Найти("ЗагрузкаКурсовВалютФинансУА") <> Неопределено И ДоступноИспользованиеЗагрузкаКурсовВалютФинансУА;
	Элементы.ФормаЗагрузитьКурсыВалютНСИС.Видимость = Метаданные.Обработки.Найти("НСИСЗагрузкаКурсовВалют") <> Неопределено;

	Если Не Пользователи.РолиДоступны("ДобавлениеИзменениеКурсовВалют") Тогда
		Элементы.ФормаПодборИзКВ.Видимость = Ложь;
		Элементы.ФормаЗагрузитьКурсыВалютФинансУа.Видимость = Ложь;
		Элементы.ФормаЗагрузитьКурсыВалютНСИС.Видимость = Ложь;
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют") Тогда
		Элементы.Валюты.ИзменятьСоставСтрок = Ложь;
		Элементы.ФормаПодборИзКВ.Видимость = Ложь;
		Элементы.ФормаЗагрузитьКурсыВалютФинансУа.Видимость = Ложь;
		Элементы.ФормаЗагрузитьКурсыВалютНСИС.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(РезультатВыбора, ИсточникВыбора)
	
	Элементы.Валюты.Обновить();
	Элементы.Валюты.ТекущаяСтрока = РезультатВыбора;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_КурсыВалют"
		Или ИмяСобытия = "Запись_ЗагрузкаКурсовВалют" Тогда
		Элементы.Валюты.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВалюты

&НаКлиенте
Процедура ВалютыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Текст = НСтр("ru='Есть возможность подобрать валюту из классификатора.
    |Подобрать?'
    |;uk='Є можливість підібрати валюту із класифікатора.
    |Підібрати?'");
	Оповещение = Новый ОписаниеОповещения("ВалютыПередНачаломДобавленияЗавершение", ЭтотОбъект);
	КнопкиВыбора = Новый СписокЗначений();
	КнопкиВыбора.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Подобрать';uk='Підібрати'"));
	КнопкиВыбора.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Создать';uk='Створити'"));
	КнопкиВыбора.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru='Отмена';uk='Відмінити'"));
	ПоказатьВопрос(Оповещение, Текст,КнопкиВыбора, , КодВозвратаДиалога.Да);
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодборИзКВ(Команда)		
	
	ОткрытьФорму("Справочник.Валюты.Форма.ПодборВалютИзКлассификатора",, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьКурсыВалютФинансУА(Команда)
	ПараметрыФормы = Новый Структура("ОткрытиеИзСписка");
	ИмяФормыЗагрузки = "Обработка.ЗагрузкаКурсовВалютФинансУА.Форма";
	ОткрытьФорму(ИмяФормыЗагрузки, ПараметрыФормы);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьКурсыВалютНСИС(Команда)
	
	ИмяФормыЗагрузки = "Обработка.НСИСЗагрузкаКурсовВалют.Форма";
	ПараметрыФормы = Новый Структура("ОткрытиеИзСписка");
	ОткрытьФорму(ИмяФормыЗагрузки, ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВалютыПередНачаломДобавленияЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	 
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ОткрытьФорму("Справочник.Валюты.Форма.ПодборВалютИзКлассификатора", , ЭтотОбъект);
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		ОткрытьФорму("Справочник.Валюты.ФормаОбъекта");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
