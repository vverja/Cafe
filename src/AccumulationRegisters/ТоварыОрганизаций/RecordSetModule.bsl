#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.АналитикаУчетаНоменклатуры,
	|	Таблица.Организация,
	|	Таблица.ВидЗапасов,
	|	Таблица.НомерГТД,
	|	(ВЫБОР Таблица.ВидДвижения КОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -Таблица.Количество
	|		ИНАЧЕ Таблица.Количество КОНЕЦ) КАК КоличествоПередЗаписью
	|ПОМЕСТИТЬ
	|	ДвиженияТоварыОрганизацийПередЗаписью
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	|	И &РассчитыватьИзменения
	|	И &КонтролироватьОстатки
	|	И НЕ &ЭтоОбменДанными
		// Интересует только положительный приход или сторнирующий расход
	|	И (Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И Таблица.Количество > 0
	|		ИЛИ Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И Таблица.Количество < 0
	|		ИЛИ Таблица.Регистратор ССЫЛКА Документ.КорректировкаПоступления)
	|;
	|///////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Период                        КАК Период,
	|	Товары.Регистратор                   КАК Регистратор,
	|	Товары.АналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры,
	|	Товары.Организация                   КАК Организация,
	|	Товары.ВидЗапасов                    КАК ВидЗапасов,
	|	Товары.НомерГТД                      КАК НомерГТД,
	|	Товары.Количество                    КАК Количество,
	|	Товары.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|	Товары.НалоговоеНазначение           КАК НалоговоеНазначение,
	|	Товары.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	|	Товары.КорВидЗапасов                 КАК КорВидЗапасов,
	|	Товары.ОрганизацияОтгрузки           КАК ОрганизацияОтгрузки,
	|	Товары.ДокументРеализации            КАК ДокументРеализации,
	|	Товары.СтатьяРасходов                КАК СтатьяРасходов,
	|	Товары.АналитикаРасходов             КАК АналитикаРасходов,
	|	Товары.Номенклатура                  КАК Номенклатура,
	|	Товары.Характеристика                КАК Характеристика,
	|	Товары.Первичное                     КАК Первичное
	|ПОМЕСТИТЬ ТоварыОрганизацийПередЗаписью
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций КАК Товары
	|ГДЕ
	|	Товары.Регистратор = &Регистратор
	|	И &ИспользоватьПартионныйУчет
	|");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый", ДополнительныеСвойства.ЭтоНовый);
	Запрос.УстановитьПараметр("РассчитыватьИзменения", ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства));
	Запрос.УстановитьПараметр("КонтролироватьОстатки", Константы.КонтролироватьТоварыОрганизацийПриОтменеПриходов.Получить());
	Запрос.УстановитьПараметр("ЭтоОбменДанными", ОбменДанными.Загрузка);
	Запрос.УстановитьПараметр("ИспользоватьПартионныйУчет", ДополнительныеСвойства.ДляПроведения.ПартионныйУчет);
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	МИНИМУМ(ТаблицаИзменений.ПериодПередЗаписью) КАК ПериодПередЗаписью,
		|	ТаблицаИзменений.АналитикаУчетаНоменклатуры,
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.ВидЗапасов,
		|	ТаблицаИзменений.НомерГТД,
		|	СУММА(ТаблицаИзменений.КоличествоИзменение) КАК КоличествоИзменение
		|ПОМЕСТИТЬ
		|	ДвиженияТоварыОрганизацийИзменение
		|ИЗ (
		|	ВЫБРАТЬ
		|		Таблица.Период КАК ПериодПередЗаписью,
		|		NULL КАК ПериодПриЗаписи,
		|		Таблица.АналитикаУчетаНоменклатуры,
		|		Таблица.Организация,
		|		Таблица.ВидЗапасов,
		|		Таблица.НомерГТД,
		|		Таблица.КоличествоПередЗаписью КАК КоличествоИзменение
		|	ИЗ
		|		ДвиженияТоварыОрганизацийПередЗаписью КАК Таблица
		|	ГДЕ
		|		Таблица.Период <> НЕОПРЕДЕЛЕНО
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ
		|		NULL КАК ПериодПередЗаписью,
		|		Таблица.Период КАК ПериодПриЗаписи,
		|		Таблица.АналитикаУчетаНоменклатуры,
		|		Таблица.Организация,
		|		Таблица.ВидЗапасов,
		|		Таблица.НомерГТД,
		|		(ВЫБОР Таблица.ВидДвижения КОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА Таблица.Количество
		|			ИНАЧЕ -Таблица.Количество КОНЕЦ) КАК КоличествоИзменение
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизаций КАК Таблица
		|	ГДЕ
		|		Таблица.Регистратор = &Регистратор
				// Интересует только положительный приход или сторнирующий расход
		|		И ((Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И Таблица.Количество > 0)
		|			ИЛИ (Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И Таблица.Количество < 0)
		|			ИЛИ Таблица.Регистратор ССЫЛКА Документ.КорректировкаПоступления)
		|		И &РассчитыватьИзменения
		|		И &КонтролироватьОстатки
		|		И НЕ &ЭтоОбменДанными
		|) КАК ТаблицаИзменений
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменений.АналитикаУчетаНоменклатуры,
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.ВидЗапасов,
		|	ТаблицаИзменений.НомерГТД
		|
		|ИМЕЮЩИЕ
		|	СУММА(ТаблицаИзменений.КоличествоИзменение) > 0
		|	ИЛИ МИНИМУМ(ТаблицаИзменений.ПериодПередЗаписью) < МАКСИМУМ(ТаблицаИзменений.ПериодПриЗаписи)
		|;
		|///////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК МЕСЯЦ,
		|	Таблица.Организация                  КАК Организация,
		|	Таблица.Регистратор                  КАК Документ
		|ИЗ
		|	(ВЫБРАТЬ
		|		Товары.Период                        КАК Период,
		|		Товары.Регистратор                   КАК Регистратор,
		|		Товары.АналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры,
		|		Товары.Организация                   КАК Организация,
		|		Товары.ВидЗапасов                    КАК ВидЗапасов,
		|		Товары.НомерГТД                      КАК НомерГТД,
		|		Товары.Количество                    КАК Количество,
		|		Товары.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
		|		Товары.НалоговоеНазначение           КАК НалоговоеНазначение,
		|		Товары.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|		Товары.КорВидЗапасов                 КАК КорВидЗапасов,
		|		Товары.ОрганизацияОтгрузки           КАК ОрганизацияОтгрузки,
		|		Товары.ДокументРеализации            КАК ДокументРеализации,
		|		Товары.СтатьяРасходов                КАК СтатьяРасходов,
		|		Товары.АналитикаРасходов             КАК АналитикаРасходов,
		|		Товары.Номенклатура                  КАК Номенклатура,
		|		Товары.Характеристика                КАК Характеристика,
		|		Товары.Первичное                     КАК Первичное
		|	ИЗ
		|		ТоварыОрганизацийПередЗаписью КАК Товары
		|	ГДЕ
		|		&ИспользоватьПартионныйУчет
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Товары.Период                        КАК Период,
		|		Товары.Регистратор                   КАК Регистратор,
		|		Товары.АналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры,
		|		Товары.Организация                   КАК Организация,
		|		Товары.ВидЗапасов                    КАК ВидЗапасов,
		|		Товары.НомерГТД                      КАК НомерГТД,
		|		-Товары.Количество                   КАК Количество,
		|		Товары.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
		|		Товары.НалоговоеНазначение           КАК НалоговоеНазначение,
		|		Товары.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|		Товары.КорВидЗапасов                 КАК КорВидЗапасов,
		|		Товары.ОрганизацияОтгрузки           КАК ОрганизацияОтгрузки,
		|		Товары.ДокументРеализации            КАК ДокументРеализации,
		|		Товары.СтатьяРасходов                КАК СтатьяРасходов,
		|		Товары.АналитикаРасходов             КАК АналитикаРасходов,
		|		Товары.Номенклатура                  КАК Номенклатура,
		|		Товары.Характеристика                КАК Характеристика,
		|		Товары.Первичное                     КАК Первичное
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизаций КАК Товары
		|	ГДЕ
		|		Товары.Регистратор = &Регистратор
		|		И &ИспользоватьПартионныйУчет
		|	) КАК Таблица
		|СГРУППИРОВАТЬ ПО
		|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ),
		|	Таблица.Период,
		|	Таблица.Регистратор,
		|	Таблица.АналитикаУчетаНоменклатуры,
		|	Таблица.Организация,
		|	Таблица.ВидЗапасов,
		|	Таблица.НомерГТД,
		|	Таблица.ХозяйственнаяОперация,
		|	Таблица.НалоговоеНазначение,
		|	Таблица.КорАналитикаУчетаНоменклатуры,
		|	Таблица.КорВидЗапасов,
		|	Таблица.ОрганизацияОтгрузки,
		|	Таблица.ДокументРеализации,
		|	Таблица.СтатьяРасходов,
		|	Таблица.АналитикаРасходов,
		|	Таблица.Номенклатура,
		|	Таблица.Характеристика,
		|	Таблица.Первичное
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.Количество) <> 0
		|;
		|///////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДвиженияТоварыОрганизацийПередЗаписью
		|");

	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("РассчитыватьИзменения", ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства));
	Запрос.УстановитьПараметр("КонтролироватьОстатки", Константы.КонтролироватьТоварыОрганизацийПриОтменеПриходов.Получить());
	Запрос.УстановитьПараметр("ЭтоОбменДанными", ОбменДанными.Загрузка);
	Запрос.УстановитьПараметр("ИспользоватьПартионныйУчет", ДополнительныеСвойства.ДляПроведения.ПартионныйУчет);
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Результат = Запрос.ВыполнитьПакет();
	Выборка = Результат[0].Выбрать();
	Выборка.Следующий();
	
	// Новые изменения были помещены во временную таблицу.
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияТоварыОрганизацийИзменение", Выборка.Количество > 0);
	
	РегистрыСведений.ЗаданияКРасчетуСебестоимости.СоздатьЗаписиРегистраПоДаннымВыборки(Результат[1].Выбрать());
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли