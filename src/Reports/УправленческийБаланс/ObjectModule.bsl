#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПередЗагрузкойВариантаНаСервере = Истина;
КонецПроцедуры

// Вызывается в одноименном обработчике формы отчета после выполнения кода формы.
//
// Подробнее - см. ОтчетыПереопределяемый.ПередЗагрузкойВариантаНаСервере
//
Процедура ПередЗагрузкойВариантаНаСервере(ЭтаФорма, НовыеНастройкиКД) Экспорт
	
	Отчет = ЭтаФорма.Отчет;
	КомпоновщикНастроекФормы = Отчет.КомпоновщикНастроек;
	
	// Установка значений по умолчанию
	НастроитьПараметрыОтборыПоУмолчанию(КомпоновщикНастроекФормы);
	
	НовыеНастройкиКД = КомпоновщикНастроекФормы.Настройки;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПользовательскиеНастройкиМодифицированы = Ложь;

	#Область АктуализацияВзаиморасчетов
	УстановитьПривилегированныйРежим(Истина);
	ПоляОтбора = РаспределениеВзаиморасчетов.ПоляОтбораПоУмолчанию();
	ДопСвойства = КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;
	РаспределениеВзаиморасчетов.ВосстановитьРасчетыПоОтборам(КомпоновщикНастроек, ПоляОтбора, ДопСвойства, "РасчетыСКлиентами");
	УстановитьПривилегированныйРежим(Ложь);
	#КонецОбласти
	
	СхемаКомпоновкиДанных.НаборыДанных.ОборотныеСредства.Запрос = ТекстЗапросаДинамикаОборотныхСредств();
	
	ПараметрПериодичность = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Периодичность");
	Если Не ЗначениеЗаполнено(ПараметрПериодичность.Значение) Тогда
		ВызватьИсключение НСтр("ru='Не заполнен параметр ""Периодичность""';uk='Не заповнений параметр ""Періодичність""'");
	КонецЕсли;
	
	УстановитьОбязательныеНастройки(ПользовательскиеНастройкиМодифицированы);
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	КомпоновкаДанныхСервер.УстановитьЗаголовкиМакетаКомпоновки(СтруктураДинамическихЗаголовков(), МакетКомпоновки);
	
	// Проверим, что хотя бы одна группировка отчета включена
	Если МакетКомпоновки.НаборыДанных.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru='Отчет не сформирован. Включите хотя бы одну группировку или таблицу.';uk='Звіт не сформований. Увімкніть хоча б одну групу або таблицю.'");
	КонецЕсли;
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	#Область ПроверкаВзаиморасчетов
	РегистрыСведений.ЗаданияКРаспределениюРасчетовСКлиентами.ВывестиАктуальностьРасчета(ДокументРезультат, ДопСвойства);
	#КонецОбласти

	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	КомпоновкаДанныхСервер.СкрытьВспомогательныеПараметрыОтчета(СхемаКомпоновкиДанных, КомпоновщикНастроек, ДокументРезультат, ВспомогательныеПараметрыОтчета());
	
	// Сообщим форме отчета, что настройки модифицированы
	Если ПользовательскиеНастройкиМодифицированы Тогда
		КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПользовательскиеНастройкиМодифицированы", Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстВыполненоЗакрытиеМесяца()
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Строки.Организация,
	|	НАЧАЛОПЕРИОДА(Операция.Дата, МЕСЯЦ) КАК ПериодМесяц
	|ПОМЕСТИТЬ
	|	ЗакрытияМесяца
	|ИЗ
	|	Документ.РасчетСебестоимостиТоваров КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	(Операция.Дата МЕЖДУ &НачалоПериода И &КонецПериода ИЛИ &КонецПериода = ДАТАВРЕМЯ(1,1,1))
	|	И Операция.Проведен
	|	И НЕ Операция.ПредварительныйРасчет
	|;
	|";

	Возврат ТекстЗапроса;	
КонецФункции

Функция ТекстЗапросаСебестоимостьТоваров()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив) КАК ТипПоказателя,
	|	ВЫБОР КОГДА УчетСебестоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) 
	|					И Аналитика.Склад.ТипСклада = ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.РозничныйМагазин) ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыВРознице)
	|	КОГДА УчетСебестоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) 
	|			И Аналитика.Склад.ТипСклада = ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.ОптовыйСклад) ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыНаОптовыхСкладах)
	|	КОГДА УчетСебестоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасходыТекущегоПериода)
	|	КОГДА УчетСебестоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику) ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыПереданныеВПереработку)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ТоварыПереданныеНаКомиссию)
	|	КОНЕЦ КАК Показатель,
	|	
	|	УчетСебестоимости.Организация КАК Организация,
	|	&ВалютаУправленческогоУчета КАК Валюта,
	|	NULL КАК Партнер,
	|	Аналитика.Номенклатура КАК Аналитика,
	|
	|	ВЫБОР КОГДА Не Склады.Подразделение ЕСТЬ NULL ТОГДА
	|		Склады.Подразделение
	|	КОГДА Не Подразделения.Ссылка ЕСТЬ NULL ТОГДА
	|		Подразделения.Ссылка
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|	КОНЕЦ КАК Подразделение,
	|	
	|	УчетСебестоимости.ПериодСекунда КАК ПериодСекунда,
	|	УчетСебестоимости.ПериодДень КАК ПериодДень,
	|	УчетСебестоимости.ПериодНеделя КАК ПериодНеделя,
	|	УчетСебестоимости.ПериодДекада КАК ПериодДекада,
	|	УчетСебестоимости.ПериодМесяц КАК ПериодМесяц,
	|	УчетСебестоимости.ПериодКвартал КАК ПериодКвартал,
	|	УчетСебестоимости.ПериодПолугодие КАК ПериодПолугодие,
	|	УчетСебестоимости.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА УчетСебестоимости.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		УчетСебестоимости.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	УчетСебестоимости.СтоимостьНачальныйОстаток
	|	+ УчетСебестоимости.СуммаДопРасходовНачальныйОстаток КАК СуммаНачальныйОстаток,
	|
	|	УчетСебестоимости.СтоимостьКонечныйОстаток
	|	+ УчетСебестоимости.СуммаДопРасходовКонечныйОстаток КАК СуммаКонечныйОстаток
	|	
	|ПОМЕСТИТЬ ОборотныеСредства	
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров.ОстаткиИОбороты(, , Авто,,
	|		РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		И РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|	) КАК УчетСебестоимости
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		УчетСебестоимости.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.Склады КАК Склады
	|	ПО
	|		Аналитика.Склад = Склады.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СтруктураПредприятия КАК Подразделения
	|	ПО
	|		Аналитика.Склад = Подразделения.Ссылка
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДенежныеСредства()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив) КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДенежныеСредстваНаличные) КАК Показатель,
	|	
	|	ДенежныеСредства.Организация,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		&ВалютаРегламентированногоУчета
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		&ВалютаУправленческогоУчета
	|	ИНАЧЕ
	|		ДенежныеСредства.Касса.ВалютаДенежныхСредств
	|	КОНЕЦ КАК Валюта,
	|	NULL КАК Партнер,
	|	ДенежныеСредства.Касса КАК Аналитика,
	|	ДенежныеСредства.Касса.Подразделение КАК Подразделение,
	|	
	|	ДенежныеСредства.ПериодСекунда КАК ПериодСекунда,
	|	ДенежныеСредства.ПериодДень КАК ПериодДень,
	|	ДенежныеСредства.ПериодНеделя КАК ПериодНеделя,
	|	ДенежныеСредства.ПериодДекада КАК ПериодДекада,
	|	ДенежныеСредства.ПериодМесяц КАК ПериодМесяц,
	|	ДенежныеСредства.ПериодКвартал КАК ПериодКвартал,
	|	ДенежныеСредства.ПериодПолугодие КАК ПериодПолугодие,
	|	ДенежныеСредства.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ДенежныеСредства.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ДенежныеСредства.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		ДенежныеСредства.СуммаРеглНачальныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		ДенежныеСредства.СуммаУпрНачальныйОстаток
	|	ИНАЧЕ
	|		ДенежныеСредства.СуммаНачальныйОстаток
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		ДенежныеСредства.СуммаРеглКонечныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		ДенежныеСредства.СуммаУпрКонечныйОстаток
	|	ИНАЧЕ
	|		ДенежныеСредства.СуммаКонечныйОстаток
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваНаличные.ОстаткиИОбороты(,, Авто) КАК ДенежныеСредства
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив) КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДенежныеСредстваНаличныеКПоступлению) КАК Показатель,
	|	
	|	ДенежныеСредства.Организация,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		&ВалютаРегламентированногоУчета
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		&ВалютаУправленческогоУчета
	|	ИНАЧЕ
	|		ДенежныеСредства.Валюта
	|	КОНЕЦ КАК Валюта,
	|	ДенежныеСредства.Контрагент КАК Партнер,
	|	ДенежныеСредства.Получатель КАК Аналитика,
	|	ДенежныеСредства.Получатель.Подразделение КАК Подразделение,
	|	
	|	ДенежныеСредства.ПериодСекунда КАК ПериодСекунда,
	|	ДенежныеСредства.ПериодДень КАК ПериодДень,
	|	ДенежныеСредства.ПериодНеделя КАК ПериодНеделя,
	|	ДенежныеСредства.ПериодДекада КАК ПериодДекада,
	|	ДенежныеСредства.ПериодМесяц КАК ПериодМесяц,
	|	ДенежныеСредства.ПериодКвартал КАК ПериодКвартал,
	|	ДенежныеСредства.ПериодПолугодие КАК ПериодПолугодие,
	|	ДенежныеСредства.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ДенежныеСредства.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ДенежныеСредства.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		ДенежныеСредства.СуммаРеглНачальныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		ДенежныеСредства.СуммаУпрНачальныйОстаток
	|	ИНАЧЕ
	|		ДенежныеСредства.СуммаНачальныйОстаток
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		ДенежныеСредства.СуммаРеглКонечныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		ДенежныеСредства.СуммаУпрКонечныйОстаток
	|	ИНАЧЕ
	|		ДенежныеСредства.СуммаКонечныйОстаток
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваВПути.ОстаткиИОбороты(,, Авто) КАК ДенежныеСредства
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДенежныеСредства.Получатель) В (ТИП(Справочник.Кассы), ТИП(Справочник.КассыККМ))
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив) КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДенежныеСредстваНаличные) КАК Показатель,
	|	
	|	ДенежныеСредства.Организация,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		&ВалютаРегламентированногоУчета
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		&ВалютаУправленческогоУчета
	|	ИНАЧЕ
	|		ДенежныеСредства.КассаККМ.ВалютаДенежныхСредств
	|	КОНЕЦ КАК Валюта,
	|	NULL КАК Партнер,
	|	ДенежныеСредства.КассаККМ КАК Аналитика,
	|	ДенежныеСредства.КассаККМ.Подразделение КАК Подразделение,
	|	
	|	ДенежныеСредства.ПериодСекунда КАК ПериодСекунда,
	|	ДенежныеСредства.ПериодДень КАК ПериодДень,
	|	ДенежныеСредства.ПериодНеделя КАК ПериодНеделя,
	|	ДенежныеСредства.ПериодДекада КАК ПериодДекада,
	|	ДенежныеСредства.ПериодМесяц КАК ПериодМесяц,
	|	ДенежныеСредства.ПериодКвартал КАК ПериодКвартал,
	|	ДенежныеСредства.ПериодПолугодие КАК ПериодПолугодие,
	|	ДенежныеСредства.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ДенежныеСредства.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ДенежныеСредства.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		ДенежныеСредства.СуммаРеглНачальныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		ДенежныеСредства.СуммаУпрНачальныйОстаток
	|	ИНАЧЕ
	|		ДенежныеСредства.СуммаНачальныйОстаток
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		ДенежныеСредства.СуммаРеглКонечныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		ДенежныеСредства.СуммаУпрКонечныйОстаток
	|	ИНАЧЕ
	|		ДенежныеСредства.СуммаКонечныйОстаток
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваВКассахККМ.ОстаткиИОбороты(,, Авто) КАК ДенежныеСредства
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив) КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДенежныеСредстваБезналичные) КАК Показатель,
	|	
	|	ДенежныеСредства.Организация,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		&ВалютаРегламентированногоУчета
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		&ВалютаУправленческогоУчета
	|	ИНАЧЕ
	|		ДенежныеСредства.БанковскийСчет.ВалютаДенежныхСредств
	|	КОНЕЦ КАК Валюта,
	|	NULL КАК Партнер,
	|	ДенежныеСредства.БанковскийСчет КАК Аналитика,
	|	ДенежныеСредства.БанковскийСчет.Подразделение КАК Подразделение,
	|	
	|	ДенежныеСредства.ПериодСекунда КАК ПериодСекунда,
	|	ДенежныеСредства.ПериодДень КАК ПериодДень,
	|	ДенежныеСредства.ПериодНеделя КАК ПериодНеделя,
	|	ДенежныеСредства.ПериодДекада КАК ПериодДекада,
	|	ДенежныеСредства.ПериодМесяц КАК ПериодМесяц,
	|	ДенежныеСредства.ПериодКвартал КАК ПериодКвартал,
	|	ДенежныеСредства.ПериодПолугодие КАК ПериодПолугодие,
	|	ДенежныеСредства.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ДенежныеСредства.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ДенежныеСредства.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		ДенежныеСредства.СуммаРеглНачальныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		ДенежныеСредства.СуммаУпрНачальныйОстаток
	|	ИНАЧЕ
	|		ДенежныеСредства.СуммаНачальныйОстаток
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		ДенежныеСредства.СуммаРеглКонечныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		ДенежныеСредства.СуммаУпрКонечныйОстаток
	|	ИНАЧЕ
	|		ДенежныеСредства.СуммаКонечныйОстаток
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваБезналичные.ОстаткиИОбороты(,, Авто) КАК ДенежныеСредства
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив) КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДенежныеСредстваБезналичныеКПоступлению) КАК Показатель,
	|	
	|	ДенежныеСредства.Организация,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		&ВалютаРегламентированногоУчета
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		&ВалютаУправленческогоУчета
	|	ИНАЧЕ
	|		ДенежныеСредства.Валюта
	|	КОНЕЦ КАК Валюта,
	|	ДенежныеСредства.Контрагент КАК Партнер,
	|	ДенежныеСредства.Получатель КАК Аналитика,
	|	ДенежныеСредства.Получатель.Подразделение КАК Подразделение,
	|	
	|	ДенежныеСредства.ПериодСекунда КАК ПериодСекунда,
	|	ДенежныеСредства.ПериодДень КАК ПериодДень,
	|	ДенежныеСредства.ПериодНеделя КАК ПериодНеделя,
	|	ДенежныеСредства.ПериодДекада КАК ПериодДекада,
	|	ДенежныеСредства.ПериодМесяц КАК ПериодМесяц,
	|	ДенежныеСредства.ПериодКвартал КАК ПериодКвартал,
	|	ДенежныеСредства.ПериодПолугодие КАК ПериодПолугодие,
	|	ДенежныеСредства.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ДенежныеСредства.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ДенежныеСредства.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		ДенежныеСредства.СуммаРеглНачальныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		ДенежныеСредства.СуммаУпрНачальныйОстаток
	|	ИНАЧЕ
	|		ДенежныеСредства.СуммаНачальныйОстаток
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		ДенежныеСредства.СуммаРеглКонечныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		ДенежныеСредства.СуммаУпрКонечныйОстаток
	|	ИНАЧЕ
	|		ДенежныеСредства.СуммаКонечныйОстаток
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваВПути.ОстаткиИОбороты(,, Авто, Движения) КАК ДенежныеСредства
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДенежныеСредства.Получатель) = ТИП(Справочник.БанковскиеСчетаОрганизаций)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив) КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДенежныеСредстваБезналичныеКПоступлению) КАК Показатель,
	|	
	|	ДенежныеСредства.Организация,
	|	ДенежныеСредства.БанковскийСчет.ВалютаДенежныхСредств КАК Валюта,
	|	NULL КАК Партнер,
	|	ДенежныеСредства.БанковскийСчет КАК Аналитика,
	|	ДенежныеСредства.БанковскийСчет.Подразделение КАК Подразделение,
	|	
	|	ДенежныеСредства.ПериодСекунда КАК ПериодСекунда,
	|	ДенежныеСредства.ПериодДень КАК ПериодДень,
	|	ДенежныеСредства.ПериодНеделя КАК ПериодНеделя,
	|	ДенежныеСредства.ПериодДекада КАК ПериодДекада,
	|	ДенежныеСредства.ПериодМесяц КАК ПериодМесяц,
	|	ДенежныеСредства.ПериодКвартал КАК ПериодКвартал,
	|	ДенежныеСредства.ПериодПолугодие КАК ПериодПолугодие,
	|	ДенежныеСредства.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ДенежныеСредства.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ДенежныеСредства.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ДенежныеСредства.КЗачислениюНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	ДенежныеСредства.КЗачислениюКонечныйОстаток КАК СуммаКонечныйОстаток
	|
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваБезналичные.ОстаткиИОбороты(,, Авто) КАК ДенежныеСредства
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДенежныеСредстваУПодотчетныхЛиц()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА ДенежныеСредства.СуммаНачальныйОстаток > 0 ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Пассив)
	|	КОНЕЦ КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДенежныеСредстваУПодотчетныхЛиц) КАК Показатель,
	|	
	|	ДенежныеСредства.Организация,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		&ВалютаРегламентированногоУчета
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		&ВалютаУправленческогоУчета
	|	ИНАЧЕ
	|		ДенежныеСредства.Валюта
	|	КОНЕЦ КАК Валюта,
	|	NULL КАК Партнер,
	|	ДенежныеСредства.ПодотчетноеЛицо КАК Аналитика,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	
	|	ДенежныеСредства.ПериодСекунда КАК ПериодСекунда,
	|	ДенежныеСредства.ПериодДень КАК ПериодДень,
	|	ДенежныеСредства.ПериодНеделя КАК ПериодНеделя,
	|	ДенежныеСредства.ПериодДекада КАК ПериодДекада,
	|	ДенежныеСредства.ПериодМесяц КАК ПериодМесяц,
	|	ДенежныеСредства.ПериодКвартал КАК ПериодКвартал,
	|	ДенежныеСредства.ПериодПолугодие КАК ПериодПолугодие,
	|	ДенежныеСредства.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ДенежныеСредства.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ДенежныеСредства.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		ДенежныеСредства.СуммаРеглНачальныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		ДенежныеСредства.СуммаУпрНачальныйОстаток
	|	ИНАЧЕ
	|		ДенежныеСредства.СуммаНачальныйОстаток
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|	0 КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.ОстаткиИОбороты(,, Авто) КАК ДенежныеСредства
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА ДенежныеСредства.СуммаКонечныйОстаток > 0 ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Пассив)
	|	КОНЕЦ КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДенежныеСредстваУПодотчетныхЛиц) КАК Показатель,
	|	
	|	ДенежныеСредства.Организация,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		&ВалютаРегламентированногоУчета
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		&ВалютаУправленческогоУчета
	|	ИНАЧЕ
	|		ДенежныеСредства.Валюта
	|	КОНЕЦ КАК Валюта,
	|	NULL КАК Партнер,
	|	ДенежныеСредства.ПодотчетноеЛицо КАК Аналитика,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	
	|	ДенежныеСредства.ПериодСекунда КАК ПериодСекунда,
	|	ДенежныеСредства.ПериодДень КАК ПериодДень,
	|	ДенежныеСредства.ПериодНеделя КАК ПериодНеделя,
	|	ДенежныеСредства.ПериодДекада КАК ПериодДекада,
	|	ДенежныеСредства.ПериодМесяц КАК ПериодМесяц,
	|	ДенежныеСредства.ПериодКвартал КАК ПериодКвартал,
	|	ДенежныеСредства.ПериодПолугодие КАК ПериодПолугодие,
	|	ДенежныеСредства.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ДенежныеСредства.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ДенежныеСредства.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	0 КАК СуммаНачальныйОстаток,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		ДенежныеСредства.СуммаРеглКонечныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		ДенежныеСредства.СуммаУпрКонечныйОстаток
	|	ИНАЧЕ
	|		ДенежныеСредства.СуммаКонечныйОстаток
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.ОстаткиИОбороты(,, Авто) КАК ДенежныеСредства
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПрочиеРасходы()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив) КАК ТипПоказателя,
	|	ВЫБОР КОГДА ПрочиеРасходы.СтатьяРасходов.ВариантРаспределенияРасходов
	|		= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов)
	|	ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасходыБудущихПериодов)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасходыТекущегоПериода)
	|	КОНЕЦ КАК Показатель,
	|	
	|	ПрочиеРасходы.Организация КАК Организация,
	|	&ВалютаУправленческогоУчета КАК Валюта,
	|	NULL КАК Партнер,
	|	ПрочиеРасходы.СтатьяРасходов КАК Аналитика,
	|	ПрочиеРасходы.Подразделение КАК Подразделение,
	|	
	|	ПрочиеРасходы.ПериодСекунда КАК ПериодСекунда,
	|	ПрочиеРасходы.ПериодДень КАК ПериодДень,
	|	ПрочиеРасходы.ПериодНеделя КАК ПериодНеделя,
	|	ПрочиеРасходы.ПериодДекада КАК ПериодДекада,
	|	ПрочиеРасходы.ПериодМесяц КАК ПериодМесяц,
	|	ПрочиеРасходы.ПериодКвартал КАК ПериодКвартал,
	|	ПрочиеРасходы.ПериодПолугодие КАК ПериодПолугодие,
	|	ПрочиеРасходы.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ПрочиеРасходы.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ПрочиеРасходы.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ПрочиеРасходы.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	ПрочиеРасходы.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.ОстаткиИОбороты(,, Авто) КАК ПрочиеРасходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив) КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасходыТекущегоПериода) КАК Показатель,
	|	
	|	ПрочиеРасходы.Организация КАК Организация,
	|	&ВалютаУправленческогоУчета КАК Валюта,
	|	NULL КАК Партнер,
	|	ПрочиеРасходы.СтатьяРасходов КАК Аналитика,
	|	ПрочиеРасходы.Подразделение КАК Подразделение,
	|	
	|	ПрочиеРасходы.ПериодСекунда КАК ПериодСекунда,
	|	ПрочиеРасходы.ПериодДень КАК ПериодДень,
	|	ПрочиеРасходы.ПериодНеделя КАК ПериодНеделя,
	|	ПрочиеРасходы.ПериодДекада КАК ПериодДекада,
	|	ПрочиеРасходы.ПериодМесяц КАК ПериодМесяц,
	|	ПрочиеРасходы.ПериодКвартал КАК ПериодКвартал,
	|	ПрочиеРасходы.ПериодПолугодие КАК ПериодПолугодие,
	|	ПрочиеРасходы.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ПрочиеРасходы.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ПрочиеРасходы.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ПрочиеРасходы.СтоимостьНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	ПрочиеРасходы.СтоимостьКонечныйОстаток КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов.ОстаткиИОбороты(,, Авто) КАК ПрочиеРасходы
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыСКлиентами()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив) КАК ТипПоказателя,
	|	ВЫБОР КОГДА РегистрАналитикаУчетаПоПартнерам.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьСобственныхОрганизаций)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьКлиентов)
	|	КОНЕЦ КАК Показатель,
	|	
	|	РегистрАналитикаУчетаПоПартнерам.Организация,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		&ВалютаРегламентированногоУчета
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		&ВалютаУправленческогоУчета
	|	ИНАЧЕ
	|		РасчетыСКлиентами.Валюта
	|	КОНЕЦ КАК Валюта,
	|	РегистрАналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	РасчетыСКлиентами.ЗаказКлиента КАК Аналитика,
	|	ЕСТЬNULL(РасчетыСКлиентами.ЗаказКлиента.Подразделение,
	|		ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
	|	
	|	РасчетыСКлиентами.ПериодСекунда КАК ПериодСекунда,
	|	РасчетыСКлиентами.ПериодДень КАК ПериодДень,
	|	РасчетыСКлиентами.ПериодНеделя КАК ПериодНеделя,
	|	РасчетыСКлиентами.ПериодДекада КАК ПериодДекада,
	|	РасчетыСКлиентами.ПериодМесяц КАК ПериодМесяц,
	|	РасчетыСКлиентами.ПериодКвартал КАК ПериодКвартал,
	|	РасчетыСКлиентами.ПериодПолугодие КАК ПериодПолугодие,
	|	РасчетыСКлиентами.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА РасчетыСКлиентами.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		РасчетыСКлиентами.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		РасчетыСКлиентами.ДолгУпрНачальныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		РасчетыСКлиентами.ДолгРеглНачальныйОстаток
	|	ИНАЧЕ
	|		РасчетыСКлиентами.ДолгНачальныйОстаток
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|	
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		РасчетыСКлиентами.ДолгУпрКонечныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		РасчетыСКлиентами.ДолгРеглКонечныйОстаток
	|	ИНАЧЕ
	|		РасчетыСКлиентами.ДолгКонечныйОстаток
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам.ОстаткиИОбороты(,, Авто) КАК РасчетыСКлиентами
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		РасчетыСКлиентами.АналитикаУчетаПоПартнерам = РегистрАналитикаУчетаПоПартнерам.КлючАналитики
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Пассив) КАК ТипПоказателя,
	|	ВЫБОР КОГДА РегистрАналитикаУчетаПоПартнерам.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОбязательстваПередСобственнымиОрганизациями)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПолученныеАвансы)
	|	КОНЕЦ КАК Показатель,
	|	
	|	РегистрАналитикаУчетаПоПартнерам.Организация,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		&ВалютаРегламентированногоУчета
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		&ВалютаУправленческогоУчета
	|	ИНАЧЕ
	|		РасчетыСКлиентами.Валюта
	|	КОНЕЦ КАК Валюта,
	|	РегистрАналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	РасчетыСКлиентами.ЗаказКлиента КАК Аналитика,
	|	ЕСТЬNULL(РасчетыСКлиентами.ЗаказКлиента.Подразделение,
	|		ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
	|	
	|	РасчетыСКлиентами.ПериодСекунда КАК ПериодСекунда,
	|	РасчетыСКлиентами.ПериодДень КАК ПериодДень,
	|	РасчетыСКлиентами.ПериодНеделя КАК ПериодНеделя,
	|	РасчетыСКлиентами.ПериодДекада КАК ПериодДекада,
	|	РасчетыСКлиентами.ПериодМесяц КАК ПериодМесяц,
	|	РасчетыСКлиентами.ПериодКвартал КАК ПериодКвартал,
	|	РасчетыСКлиентами.ПериодПолугодие КАК ПериодПолугодие,
	|	РасчетыСКлиентами.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА РасчетыСКлиентами.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		РасчетыСКлиентами.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		РасчетыСКлиентами.ПредоплатаУпрНачальныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		РасчетыСКлиентами.ПредоплатаРеглНачальныйОстаток
	|	ИНАЧЕ
	|		РасчетыСКлиентами.ПредоплатаНачальныйОстаток
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|	
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		РасчетыСКлиентами.ПредоплатаУпрКонечныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		РасчетыСКлиентами.ПредоплатаРеглКонечныйОстаток
	|	ИНАЧЕ
	|		РасчетыСКлиентами.ПредоплатаКонечныйОстаток
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам.ОстаткиИОбороты(,, Авто) КАК РасчетыСКлиентами
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		РасчетыСКлиентами.АналитикаУчетаПоПартнерам = РегистрАналитикаУчетаПоПартнерам.КлючАналитики
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Пассив) КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПолученныеАвансы) КАК Показатель,
	|	
	|	РеализацияСертификата.Регистратор.Организация,
	|	ОстаткиИОбороты.ПодарочныйСертификат.Владелец.Валюта КАК Валюта,
	|	ЗНАЧЕНИЕ(Справочник.Партнеры.РозничныйПокупатель) КАК Партнер,
	|	ОстаткиИОбороты.ПодарочныйСертификат.Владелец КАК Аналитика,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	
	|	ОстаткиИОбороты.ПериодСекунда КАК ПериодСекунда,
	|	ОстаткиИОбороты.ПериодДень КАК ПериодДень,
	|	ОстаткиИОбороты.ПериодНеделя КАК ПериодНеделя,
	|	ОстаткиИОбороты.ПериодДекада КАК ПериодДекада,
	|	ОстаткиИОбороты.ПериодМесяц КАК ПериодМесяц,
	|	ОстаткиИОбороты.ПериодКвартал КАК ПериодКвартал,
	|	ОстаткиИОбороты.ПериодПолугодие КАК ПериодПолугодие,
	|	ОстаткиИОбороты.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ОстаткиИОбороты.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ОстаткиИОбороты.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	-ОстаткиИОбороты.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	-ОстаткиИОбороты.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток
	|
	|ИЗ
	|	РегистрНакопления.ПодарочныеСертификаты.ОстаткиИОбороты(,, Авто) КАК ОстаткиИОбороты
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ПодарочныеСертификаты КАК РеализацияСертификата
	|	ПО
	|		РеализацияСертификата.ПодарочныйСертификат = ОстаткиИОбороты.ПодарочныйСертификат
	|		И РеализацияСертификата.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыСПоставщиками()
	
	ТекстЗапроса = "	
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Пассив) КАК ТипПоказателя,
	|	ВЫБОР КОГДА РегистрАналитикаУчетаПоПартнерам.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОбязательстваПередСобственнымиОрганизациями)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьПередПоставщиками)
	|	КОНЕЦ КАК Показатель,
	|	
	|	РегистрАналитикаУчетаПоПартнерам.Организация,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		&ВалютаУправленческогоУчета
	|	КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		&ВалютаРегламентированногоУчета
	|	ИНАЧЕ
	|		РасчетыСПоставщиками.Валюта
	|	КОНЕЦ КАК Валюта,
	|	РегистрАналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	РасчетыСПоставщиками.ЗаказПоставщику КАК Аналитика,
	|	ЕСТЬNULL(РасчетыСПоставщиками.ЗаказПоставщику.Подразделение,
	|		ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
	|	
	|	РасчетыСПоставщиками.ПериодСекунда КАК ПериодСекунда,
	|	РасчетыСПоставщиками.ПериодДень КАК ПериодДень,
	|	РасчетыСПоставщиками.ПериодНеделя КАК ПериодНеделя,
	|	РасчетыСПоставщиками.ПериодДекада КАК ПериодДекада,
	|	РасчетыСПоставщиками.ПериодМесяц КАК ПериодМесяц,
	|	РасчетыСПоставщиками.ПериодКвартал КАК ПериодКвартал,
	|	РасчетыСПоставщиками.ПериодПолугодие КАК ПериодПолугодие,
	|	РасчетыСПоставщиками.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА РасчетыСПоставщиками.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		РасчетыСПоставщиками.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		РасчетыСПоставщиками.ДолгУпрНачальныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		РасчетыСПоставщиками.ДолгРеглНачальныйОстаток
	|	ИНАЧЕ
	|		РасчетыСПоставщиками.ДолгНачальныйОстаток
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		РасчетыСПоставщиками.ДолгУпрКонечныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		РасчетыСПоставщиками.ДолгРеглКонечныйОстаток
	|	ИНАЧЕ
	|		РасчетыСПоставщиками.ДолгКонечныйОстаток
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам.ОстаткиИОбороты(,, Авто) КАК РасчетыСПоставщиками
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = РегистрАналитикаУчетаПоПартнерам.КлючАналитики
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив) КАК ТипПоказателя,
	|	ВЫБОР КОГДА РегистрАналитикаУчетаПоПартнерам.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие) ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьСобственныхОрганизаций)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ВыданныеАвансы)
	|	КОНЕЦ КАК Показатель,
	|	
	|	РегистрАналитикаУчетаПоПартнерам.Организация,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		&ВалютаУправленческогоУчета
	|	КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		&ВалютаРегламентированногоУчета
	|	ИНАЧЕ
	|		РасчетыСПоставщиками.Валюта
	|	КОНЕЦ КАК Валюта,
	|	РегистрАналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	РасчетыСПоставщиками.ЗаказПоставщику КАК Аналитика,
	|	ЕСТЬNULL(РасчетыСПоставщиками.ЗаказПоставщику.Подразделение,
	|		ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
	|	
	|	РасчетыСПоставщиками.ПериодСекунда КАК ПериодСекунда,
	|	РасчетыСПоставщиками.ПериодДень КАК ПериодДень,
	|	РасчетыСПоставщиками.ПериодНеделя КАК ПериодНеделя,
	|	РасчетыСПоставщиками.ПериодДекада КАК ПериодДекада,
	|	РасчетыСПоставщиками.ПериодМесяц КАК ПериодМесяц,
	|	РасчетыСПоставщиками.ПериодКвартал КАК ПериодКвартал,
	|	РасчетыСПоставщиками.ПериодПолугодие КАК ПериодПолугодие,
	|	РасчетыСПоставщиками.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА РасчетыСПоставщиками.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		РасчетыСПоставщиками.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		РасчетыСПоставщиками.ПредоплатаУпрНачальныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		РасчетыСПоставщиками.ПредоплатаРеглНачальныйОстаток
	|	ИНАЧЕ
	|		РасчетыСПоставщиками.ПредоплатаНачальныйОстаток
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		РасчетыСПоставщиками.ПредоплатаУпрКонечныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		РасчетыСПоставщиками.ПредоплатаРеглКонечныйОстаток
	|	ИНАЧЕ
	|		РасчетыСПоставщиками.ПредоплатаКонечныйОстаток
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам.ОстаткиИОбороты(,, Авто) КАК РасчетыСПоставщиками
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|	ПО
	|		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = РегистрАналитикаУчетаПоПартнерам.КлючАналитики
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Пассив) КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьПередПоставщиками) КАК Показатель,
	|	
	|	Расчеты.ВидЗапасов.Организация,
	|	Расчеты.ВидЗапасов.Валюта КАК Валюта,
	|	Расчеты.ВидЗапасов.Комитент КАК Партнер,
	|	Аналитика.Номенклатура КАК Аналитика,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	
	|	Расчеты.ПериодСекунда КАК ПериодСекунда,
	|	Расчеты.ПериодДень КАК ПериодДень,
	|	Расчеты.ПериодНеделя КАК ПериодНеделя,
	|	Расчеты.ПериодДекада КАК ПериодДекада,
	|	Расчеты.ПериодМесяц КАК ПериодМесяц,
	|	Расчеты.ПериодКвартал КАК ПериодКвартал,
	|	Расчеты.ПериодПолугодие КАК ПериодПолугодие,
	|	Расчеты.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА Расчеты.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		Расчеты.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА Расчеты.СуммаВыручкиНачальныйОстаток > 0 ТОГДА
	|		-Расчеты.СуммаВыручкиНачальныйОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|
	|	ВЫБОР КОГДА Расчеты.СуммаВыручкиКонечныйОстаток > 0 ТОГДА
	|		-Расчеты.СуммаВыручкиКонечныйОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту.ОстаткиИОбороты(,, Авто) КАК Расчеты
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		Расчеты.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив) КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ВыданныеАвансы) КАК Показатель,
	|	
	|	Расчеты.ВидЗапасов.Организация,
	|	Расчеты.ВидЗапасов.Валюта КАК Валюта,
	|	Расчеты.ВидЗапасов.Комитент КАК Партнер,
	|	Аналитика.Номенклатура КАК Аналитика,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	
	|	Расчеты.ПериодСекунда КАК ПериодСекунда,
	|	Расчеты.ПериодДень КАК ПериодДень,
	|	Расчеты.ПериодНеделя КАК ПериодНеделя,
	|	Расчеты.ПериодДекада КАК ПериодДекада,
	|	Расчеты.ПериодМесяц КАК ПериодМесяц,
	|	Расчеты.ПериодКвартал КАК ПериодКвартал,
	|	Расчеты.ПериодПолугодие КАК ПериодПолугодие,
	|	Расчеты.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА Расчеты.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		Расчеты.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА Расчеты.СуммаВыручкиНачальныйОстаток < 0 ТОГДА
	|		-Расчеты.СуммаВыручкиНачальныйОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|
	|	ВЫБОР КОГДА Расчеты.СуммаВыручкиКонечныйОстаток < 0 ТОГДА
	|		-Расчеты.СуммаВыручкиКонечныйОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту.ОстаткиИОбороты(,, Авто) КАК Расчеты
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		Расчеты.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Пассив) КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьПередПоставщиками) КАК Показатель,
	|	
	|	Расчеты.ДокументПоступления.Организация,
	|	Расчеты.ДокументПоступления.ВалютаВзаиморасчетов КАК Валюта,
	|	Расчеты.Партнер,
	|	Расчеты.Номенклатура КАК Аналитика,
	|	Расчеты.ДокументПоступления.Подразделение КАК Подразделение,
	|	
	|	Расчеты.ПериодСекунда КАК ПериодСекунда,
	|	Расчеты.ПериодДень КАК ПериодДень,
	|	Расчеты.ПериодНеделя КАК ПериодНеделя,
	|	Расчеты.ПериодДекада КАК ПериодДекада,
	|	Расчеты.ПериодМесяц КАК ПериодМесяц,
	|	Расчеты.ПериодКвартал КАК ПериодКвартал,
	|	Расчеты.ПериодПолугодие КАК ПериодПолугодие,
	|	Расчеты.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА Расчеты.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		Расчеты.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА Расчеты.СуммаНачальныйОстаток > 0 ТОГДА
	|		-Расчеты.СуммаНачальныйОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|
	|	ВЫБОР КОГДА Расчеты.СуммаКонечныйОстаток > 0 ТОГДА
	|		-Расчеты.СуммаКонечныйОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ПринятаяВозвратнаяТара.ОстаткиИОбороты(,, Авто,, НЕ ПредусмотренЗалог) КАК Расчеты
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыПоДоговорамКредитовИДепозитов()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Пассив) КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьПоКредитам) КАК Показатель,
	|	Аналитика.Организация,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		&ВалютаРегламентированногоУчета
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		&ВалютаУправленческогоУчета
	|	ИНАЧЕ
	|		Расчеты.Договор.ВалютаВзаиморасчетов
	|	КОНЕЦ КАК Валюта,
	|	Аналитика.Партнер КАК Партнер,
	|	Расчеты.Договор КАК Аналитика,
	|	Расчеты.Договор.Подразделение КАК Подразделение,
	|
	|	Расчеты.ПериодСекунда КАК ПериодСекунда,
	|	Расчеты.ПериодДень КАК ПериодДень,
	|	Расчеты.ПериодНеделя КАК ПериодНеделя,
	|	Расчеты.ПериодДекада КАК ПериодДекада,
	|	Расчеты.ПериодМесяц КАК ПериодМесяц,
	|	Расчеты.ПериодКвартал КАК ПериодКвартал,
	|	Расчеты.ПериодПолугодие КАК ПериодПолугодие,
	|	Расчеты.ПериодГод КАК ПериодГод,
	|	ВЫБОР КОГДА Расчеты.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		Расчеты.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		Расчеты.СуммаУпрНачальныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		Расчеты.СуммаРеглНачальныйОстаток
	|	ИНАЧЕ
	|		Расчеты.СуммаВВалютеНачальныйОстаток
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|	
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		Расчеты.СуммаУпрКонечныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		Расчеты.СуммаРеглКонечныйОстаток
	|	ИНАЧЕ
	|		Расчеты.СуммаВВалютеКонечныйОстаток
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыПоДоговорамКредитовИДепозитов.ОстаткиИОбороты(, , Авто) КАК Расчеты
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО
	|		Расчеты.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|ГДЕ
	|	Расчеты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерДоговораКредитовИДепозитов.КредитИлиЗайм)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив) КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ДепозитыВБанках) КАК Показатель,
	|	Аналитика.Организация,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		&ВалютаРегламентированногоУчета
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		&ВалютаУправленческогоУчета
	|	ИНАЧЕ
	|		Расчеты.Договор.ВалютаВзаиморасчетов
	|	КОНЕЦ КАК Валюта,
	|	Аналитика.Партнер,
	|	Расчеты.Договор КАК Аналитика,
	|	Расчеты.Договор.Подразделение КАК Подразделение,
	|
	|	Расчеты.ПериодСекунда,
	|	Расчеты.ПериодДень,
	|	Расчеты.ПериодНеделя,
	|	Расчеты.ПериодДекада,
	|	Расчеты.ПериодМесяц,
	|	Расчеты.ПериодКвартал,
	|	Расчеты.ПериодПолугодие,
	|	Расчеты.ПериодГод,
	|	ВЫБОР КОГДА Расчеты.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		Расчеты.Регистратор
	|	КОНЕЦ,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		Расчеты.СуммаУпрНачальныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		Расчеты.СуммаРеглНачальныйОстаток
	|	ИНАЧЕ
	|		Расчеты.СуммаВВалютеНачальныйОстаток
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|	
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		Расчеты.СуммаУпрКонечныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		Расчеты.СуммаРеглКонечныйОстаток
	|	ИНАЧЕ
	|		Расчеты.СуммаВВалютеКонечныйОстаток
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыПоДоговорамКредитовИДепозитов.ОстаткиИОбороты(, , Авто) КАК Расчеты
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО
	|		Расчеты.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|ГДЕ
	|	Расчеты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерДоговораКредитовИДепозитов.Депозит)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив) КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЗаймыВыданные) КАК Показатель,
	|	Аналитика.Организация,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		&ВалютаРегламентированногоУчета
	|	КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		&ВалютаУправленческогоУчета
	|	ИНАЧЕ
	|		Расчеты.Договор.ВалютаВзаиморасчетов
	|	КОНЕЦ КАК Валюта,
	|	Аналитика.Партнер,
	|	Расчеты.Договор КАК Аналитика,
	|	Расчеты.Договор.Подразделение КАК Подразделение,
	|
	|	Расчеты.ПериодСекунда,
	|	Расчеты.ПериодДень,
	|	Расчеты.ПериодНеделя,
	|	Расчеты.ПериодДекада,
	|	Расчеты.ПериодМесяц,
	|	Расчеты.ПериодКвартал,
	|	Расчеты.ПериодПолугодие,
	|	Расчеты.ПериодГод,
	|	ВЫБОР КОГДА Расчеты.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		Расчеты.Регистратор
	|	КОНЕЦ,
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		Расчеты.СуммаУпрНачальныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		Расчеты.СуммаРеглНачальныйОстаток
	|	ИНАЧЕ
	|		Расчеты.СуммаВВалютеНачальныйОстаток
	|	КОНЕЦ КАК СуммаНачальныйОстаток,
	|	
	|	ВЫБОР КОГДА &ВалютаОтчета = &ВалютаУправленческогоУчета ТОГДА
	|		Расчеты.СуммаУпрКонечныйОстаток
	|	КОГДА &ВалютаОтчета = &ВалютаРегламентированногоУчета ТОГДА
	|		Расчеты.СуммаРеглКонечныйОстаток
	|	ИНАЧЕ
	|		Расчеты.СуммаВВалютеКонечныйОстаток
	|	КОНЕЦ КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыПоДоговорамКредитовИДепозитов.ОстаткиИОбороты(, , Авто) КАК Расчеты
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО
	|		Расчеты.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|ГДЕ
	|	Расчеты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерДоговораКредитовИДепозитов.ЗаймВыданный)";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПрочиеАктивыПассивы()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеРегистра.Статья.АктивПассив КАК ТипПоказателя,
	|	ДанныеРегистра.Статья КАК Показатель,
	|	
	|	ДанныеРегистра.Организация,
	|	&ВалютаУправленческогоУчета КАК Валюта,
	|	NULL КАК Партнер,
	|	ДанныеРегистра.Аналитика КАК Аналитика,
	|	ЕСТЬNULL(Подразделения.Ссылка, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
	|	
	|	ДанныеРегистра.ПериодСекунда КАК ПериодСекунда,
	|	ДанныеРегистра.ПериодДень КАК ПериодДень,
	|	ДанныеРегистра.ПериодНеделя КАК ПериодНеделя,
	|	ДанныеРегистра.ПериодДекада КАК ПериодДекада,
	|	ДанныеРегистра.ПериодМесяц КАК ПериодМесяц,
	|	ДанныеРегистра.ПериодКвартал КАК ПериодКвартал,
	|	ДанныеРегистра.ПериодПолугодие КАК ПериодПолугодие,
	|	ДанныеРегистра.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ДанныеРегистра.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ДанныеРегистра.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	ДанныеРегистра.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	ДанныеРегистра.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ПрочиеАктивыПассивы.ОстаткиИОбороты(,, Авто) КАК ДанныеРегистра
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СтруктураПредприятия КАК Подразделения
	|	ПО
	|		ДанныеРегистра.Аналитика = Подразделения.Ссылка
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПрибыльУбыток()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Пассив) КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки) КАК Показатель,
	|	
	|	Аналитика.Организация,
	|	&ВалютаУправленческогоУчета КАК Валюта,
	|	Аналитика.Партнер КАК Партнер,
	|	АналитикаНоменклатуры.Номенклатура КАК Аналитика,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|	
	|	ДанныеРегистра.ПериодСекунда КАК ПериодСекунда,
	|	ДанныеРегистра.ПериодДень КАК ПериодДень,
	|	ДанныеРегистра.ПериодНеделя КАК ПериодНеделя,
	|	ДанныеРегистра.ПериодДекада КАК ПериодДекада,
	|	ДанныеРегистра.ПериодМесяц КАК ПериодМесяц,
	|	ДанныеРегистра.ПериодКвартал КАК ПериодКвартал,
	|	ДанныеРегистра.ПериодПолугодие КАК ПериодПолугодие,
	|	ДанныеРегистра.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ДанныеРегистра.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ДанныеРегистра.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	0 КАК СуммаНачальныйОстаток,
	|	(ВЫБОР КОГДА ДанныеРегистра.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) ТОГДА
	|		- ДанныеРегистра.СуммаВыручкиОборот + ДанныеРегистра.СебестоимостьОборот
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ
	|	+ ДанныеРегистра.СуммаДополнительныхРасходовОборот
	|	) КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(,, Авто) КАК ДанныеРегистра
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО
	|		ДанныеРегистра.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|	ПО
	|		ДанныеРегистра.АналитикаУчетаНоменклатуры = АналитикаНоменклатуры.КлючАналитики
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ЗакрытияМесяца КАК ЗакрытияМесяца
	|		ПО ЗакрытияМесяца.Организация = Аналитика.Организация
	|		И ЗакрытияМесяца.ПериодМесяц = ДанныеРегистра.ПериодМесяц
	|ГДЕ
	|	ЗакрытияМесяца.Организация ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Пассив) КАК ТипПоказателя,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки) КАК Показатель,
	|	
	|	ДанныеРегистра.Организация,
	|	&ВалютаУправленческогоУчета КАК Валюта,
	|	NULL КАК Партнер,
	|	ДанныеРегистра.СтатьяДоходов КАК Аналитика,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|	
	|	ДанныеРегистра.ПериодСекунда КАК ПериодСекунда,
	|	ДанныеРегистра.ПериодДень КАК ПериодДень,
	|	ДанныеРегистра.ПериодНеделя КАК ПериодНеделя,
	|	ДанныеРегистра.ПериодДекада КАК ПериодДекада,
	|	ДанныеРегистра.ПериодМесяц КАК ПериодМесяц,
	|	ДанныеРегистра.ПериодКвартал КАК ПериодКвартал,
	|	ДанныеРегистра.ПериодПолугодие КАК ПериодПолугодие,
	|	ДанныеРегистра.ПериодГод КАК ПериодГод,
	|	
	|	ВЫБОР КОГДА ДанныеРегистра.Регистратор = НЕОПРЕДЕЛЕНО ТОГДА
	|		NULL
	|	ИНАЧЕ
	|		ДанныеРегистра.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	
	|	- ДанныеРегистра.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	- ДанныеРегистра.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ПрочиеДоходы.ОстаткиИОбороты(,, Авто) КАК ДанныеРегистра
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДинамикаОборотныхСредств()
	
	ТекстЗапроса = ТекстВыполненоЗакрытиеМесяца()
		+ ТекстЗапросаСебестоимостьТоваров()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаДенежныеСредства()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаДенежныеСредстваУПодотчетныхЛиц()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаПрочиеРасходы()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаРасчетыСКлиентами()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаРасчетыСПоставщиками()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаРасчетыПоДоговорамКредитовИДепозитов()
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаПрибыльУбыток();
		
	Если НЕ ПолучитьФункциональнуюОпцию("БазоваяВерсия") Тогда
		ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ" + ТекстЗапросаПрочиеАктивыПассивы();
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + ";" + "
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КурсВалюты.Валюта КАК Валюта,
	|
	|	(КурсВалюты.Курс * КурсВалютыОтчета.Кратность)
	|   / (КурсВалюты.Кратность * КурсВалютыОтчета.Курс) КАК КоэффициентПересчета
	|
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних({&КонецПериода}, ) КАК КурсВалюты
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.КурсыВалют.СрезПоследних({&КонецПериода}, 
	|			Валюта = &ВалютаОтчета
	|		) КАК КурсВалютыОтчета
	|	ПО
	|		Истина
	|
	|ГДЕ
	|	КурсВалюты.Кратность <> 0
	|	И КурсВалютыОтчета.Курс <> 0
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&СтрокаБалансНеНарушен КАК АктивыМинусПассивы,
	|	ОборотныеСредства.ТипПоказателя КАК ТипПоказателя,
	|	ОборотныеСредства.Показатель КАК Показатель,
	|	ОборотныеСредства.Организация,
	|	ОборотныеСредства.Валюта КАК Валюта,
	|	ОборотныеСредства.Партнер КАК Партнер,
	|	ОборотныеСредства.Аналитика КАК Аналитика,
	|	ОборотныеСредства.Подразделение КАК Подразделение,
	|	
	|	ВЫБОР КОГДА &ВалютаОтчета = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) ТОГДА
	|		ОборотныеСредства.Валюта
	|	ИНАЧЕ
	|		&ВалютаОтчета
	|	КОНЕЦ КАК ВалютаОтчета,
	|	
	|	ОборотныеСредства.ПериодСекунда КАК ПериодСекунда,
	|	ОборотныеСредства.ПериодДень КАК ПериодДень,
	|	ОборотныеСредства.ПериодНеделя КАК ПериодНеделя,
	|	ОборотныеСредства.ПериодДекада КАК ПериодДекада,
	|	ОборотныеСредства.ПериодМесяц КАК ПериодМесяц,
	|	ОборотныеСредства.ПериодКвартал КАК ПериодКвартал,
	|	ОборотныеСредства.ПериодПолугодие КАК ПериодПолугодие,
	|	ОборотныеСредства.ПериодГод КАК ПериодГод,
	|	ОборотныеСредства.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ОборотныеСредства.Регистратор) КАК ТипРегистратора,
	|	
	|	СУММА(ОборотныеСредства.СуммаНачальныйОстаток) КАК СуммаНачальныйОстаток,
	|	СУММА(ОборотныеСредства.СуммаКонечныйОстаток) КАК СуммаКонечныйОстаток,
	|	СУММА(ОборотныеСредства.СуммаКонечныйОстаток - ОборотныеСредства.СуммаНачальныйОстаток) КАК Изменение,
	|
	|	ВЫБОР КОГДА СУММА(ОборотныеСредства.СуммаКонечныйОстаток - ОборотныеСредства.СуммаНачальныйОстаток) > 0 ТОГДА
	|		СУММА(ОборотныеСредства.СуммаКонечныйОстаток - ОборотныеСредства.СуммаНачальныйОстаток)
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК Увеличение,
	|	ВЫБОР КОГДА СУММА(ОборотныеСредства.СуммаКонечныйОстаток - ОборотныеСредства.СуммаНачальныйОстаток) < 0 ТОГДА
	|		-СУММА(ОборотныеСредства.СуммаКонечныйОстаток - ОборотныеСредства.СуммаНачальныйОстаток)
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК Уменьшение,
	|	
	|	СУММА(ВЫРАЗИТЬ(ОборотныеСредства.СуммаНачальныйОстаток
	|		* ЕСТЬNULL(КурсыВалют.КоэффициентПересчета, 1) КАК ЧИСЛО(15,2))
	|	) КАК СуммаНачальныйОстатокВВалюте,
	|
	|	СУММА(ВЫРАЗИТЬ(ОборотныеСредства.СуммаКонечныйОстаток
	|		* ЕСТЬNULL(КурсыВалют.КоэффициентПересчета, 1) КАК ЧИСЛО(15,2))
	|	) КАК СуммаКонечныйОстатокВВалюте,
	|
	|	СУММА(ВЫРАЗИТЬ((ОборотныеСредства.СуммаКонечныйОстаток - ОборотныеСредства.СуммаНачальныйОстаток)
	|		* ЕСТЬNULL(КурсыВалют.КоэффициентПересчета, 1) КАК ЧИСЛО(15,2))
	|	) КАК ИзменениеВВалюте
	|ИЗ
	|	ОборотныеСредства КАК ОборотныеСредства
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсыВалют
	|	ПО
	|		ОборотныеСредства.Валюта = КурсыВалют.Валюта
	|ГДЕ
	|	Не ОборотныеСредства.Валюта ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотныеСредства.ТипПоказателя,
	|	ОборотныеСредства.Показатель,
	|	ОборотныеСредства.Организация,
	|	ОборотныеСредства.Валюта,
	|	ОборотныеСредства.Партнер,
	|	ОборотныеСредства.Аналитика,
	|	ОборотныеСредства.Подразделение,
	|	ВЫБОР КОГДА &ВалютаОтчета = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) ТОГДА
	|		ОборотныеСредства.Валюта
	|	ИНАЧЕ
	|		&ВалютаОтчета
	|	КОНЕЦ,
	|	ОборотныеСредства.ПериодСекунда,
	|	ОборотныеСредства.ПериодДень,
	|	ОборотныеСредства.ПериодНеделя,
	|	ОборотныеСредства.ПериодДекада,
	|	ОборотныеСредства.ПериодМесяц,
	|	ОборотныеСредства.ПериодКвартал,
	|	ОборотныеСредства.ПериодПолугодие,
	|	ОборотныеСредства.ПериодГод,
	|	ОборотныеСредства.Регистратор,
	|	ТИПЗНАЧЕНИЯ(ОборотныеСредства.Регистратор)
	|	
	|ИМЕЮЩИЕ
	|	СУММА(ОборотныеСредства.СуммаНачальныйОстаток) <> 0
	|	ИЛИ СУММА(ОборотныеСредства.СуммаКонечныйОстаток) <> 0
	|	ИЛИ СУММА(ОборотныеСредства.СуммаКонечныйОстаток - ОборотныеСредства.СуммаНачальныйОстаток) <> 0
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьОбязательныеНастройки(ПользовательскиеНастройкиМодифицированы)
	КомпоновкаДанныхСервер.НастроитьДинамическийПериод(СхемаКомпоновкиДанных, КомпоновщикНастроек);
	
	ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	ПараметрВалютаУправленческогоУчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ВалютаУправленческогоУчета");
	Если ПараметрВалютаУправленческогоУчета <> Неопределено 
		И Не ЗначениеЗаполнено(ПараметрВалютаУправленческогоУчета.Значение) Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВалютаУправленческогоУчета", ВалютаУправленческогоУчета);
				
		ПользовательскиеНастройкиМодифицированы = Истина;
	КонецЕсли;
	
	ПараметрВалютаРегламентированногоУчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ВалютаРегламентированногоУчета");
	Если ПараметрВалютаРегламентированногоУчета <> Неопределено 
		И Не ЗначениеЗаполнено(ПараметрВалютаРегламентированногоУчета.Значение) Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
				
		ПользовательскиеНастройкиМодифицированы = Истина;
	КонецЕсли;
	
	ПараметрВалютаОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ВалютаОтчета");
	Если ПараметрВалютаОтчета <> Неопределено 
		И Не ЗначениеЗаполнено(ПараметрВалютаОтчета.Значение) Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВалютаОтчета", ВалютаУправленческогоУчета);
				
		ПользовательскиеНастройкиМодифицированы = Истина;
	КонецЕсли;
КонецПроцедуры

Функция СтруктураДинамическихЗаголовков()
	ДинамическиеЗаголовки = Новый Структура;
	
	ПараметрТипАнализа = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ТипАнализа");
	ДоступнаяНастройка = ОтчетыКлиентСервер.НайтиДоступнуюНастройку(КомпоновщикНастроек.Настройки, ПараметрТипАнализа);
	Если ДоступнаяНастройка <> Неопределено Тогда
		ПредставлениеЗначенияПараметра = ДоступнаяНастройка.ДоступныеЗначения[ПараметрТипАнализа.Значение];
		ДинамическиеЗаголовки.Вставить("ТипАнализа", ПредставлениеЗначенияПараметра);
	КонецЕсли;
	
	ПараметрПериодичность = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Периодичность");
	ДоступнаяНастройка = ОтчетыКлиентСервер.НайтиДоступнуюНастройку(КомпоновщикНастроек.Настройки, ПараметрПериодичность);
	Если ДоступнаяНастройка <> Неопределено Тогда
		ПредставлениеЗначенияПараметра = ДоступнаяНастройка.ДоступныеЗначения.НайтиПоЗначению(ПараметрПериодичность.Значение);
		ДинамическиеЗаголовки.Вставить("Периодичность", ПредставлениеЗначенияПараметра);
	КонецЕсли;
	
	Возврат ДинамическиеЗаголовки;
КонецФункции

Функция ВспомогательныеПараметрыОтчета()
	ВспомогательныеПараметры = Новый Массив;
	
	ВспомогательныеПараметры.Добавить("ТипАнализа");
	ВспомогательныеПараметры.Добавить("Периодичность");
	
	КомпоновкаДанныхСервер.ДобавитьВспомогательныеПараметрыОтчетаПоФункциональнымОпциям(ВспомогательныеПараметры);
	
	Возврат ВспомогательныеПараметры;
КонецФункции

Процедура НастроитьПараметрыОтборыПоУмолчанию(КомпоновщикНастроекФормы, ПользовательскиеНастройки = Ложь)
	
	ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
	
	Параметр = НастройкаПараметра(КомпоновщикНастроекФормы, "ВалютаОтчета", ПользовательскиеНастройки);
	Если Параметр <> Неопределено Тогда
		Параметр.Использование = Истина;
		Если Не ЗначениеЗаполнено(Параметр.Значение) Тогда
			Параметр.Значение = ВалютаУправленческогоУчета;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция НастройкаПараметра(КомпоновщикНастроекФормы, ИмяПараметра, Пользовательский = Ложь)
	
	ПараметрДанных = КомпоновщикНастроекФормы.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	Если ПараметрДанных <> Неопределено Тогда
		ПараметрПользовательскойНастройки = КомпоновщикНастроекФормы.ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
		Если Пользовательский И ПараметрПользовательскойНастройки <> Неопределено Тогда
			Возврат ПараметрПользовательскойНастройки;
		Иначе
			Возврат ПараметрДанных;
		КонецЕсли;
	КонецЕсли;
	Возврат Неопределено;
	
КонецФункции

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   Отказ - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Передается из параметров обработчика "как есть".
//
// См. также:
//   "УправляемаяФорма.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек;
	Параметры = ЭтаФорма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		
		ЭтаФорма.ФормаПараметры.КлючНазначенияИспользования = Параметры.ПараметрКоманды;
		ЭтаФорма.ФормаПараметры.Отбор.Вставить("Показатель", Параметры.ПараметрКоманды);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли