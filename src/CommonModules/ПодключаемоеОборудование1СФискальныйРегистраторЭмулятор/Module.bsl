
///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Функция возвращает возможность работы модуля в асинхронном режиме.
// Стандартные команды модуля:
// - ПодключитьУстройство
// - ОтключитьУстройство
// - ВыполнитьКоманду
// Команды модуля для работы асинхронном режиме (должны быть определены):
// - НачатьПодключениеУстройства
// - НачатьОтключениеУстройства
// - НачатьВыполнениеКоманды
//
Функция ПоддержкаАсинхронногоРежима() Экспорт
	Возврат Ложь;
КонецФункции


///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Функция осуществляет подключение устройства.
//
// Параметры:
//  ОбъектДрайвера   - <*>
//           - ОбъектДрайвера драйвера торгового оборудования.
//
// Возвращаемое значение:
//  <Булево> - Результат работы функции.
//
Функция ПодключитьУстройство(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры) Экспорт

	Результат = Истина;
	ПараметрыПодключения.Вставить("СерийныйНомерККМ", "0");
	ПараметрыПодключения.Вставить("ИДУстройства", "");
	ПараметрыПодключения.Вставить("ФРПодключен", Ложь);
  	ПараметрыПодключения.Вставить("СтрокаЛога", "");

	//Обязательные выходные
	ВыходныеПараметры = Новый Массив();

	// Проверка настроенных параметров
	КодСимволаЧастичногоОтреза = Неопределено;
	НомерСекции                = Неопределено;
	Модель                     = Неопределено;
	ТаблицаСоответствийНалоговыхГрупп = Неопределено;

	Параметры.Свойство("КодСимволаЧастичногоОтреза", КодСимволаЧастичногоОтреза);
	Параметры.Свойство("Модель"                    , Модель);
	Параметры.Свойство("ТаблицаСоответствийНалоговыхГрупп", ТаблицаСоответствийНалоговыхГрупп);

	Если КодСимволаЧастичногоОтреза = Неопределено
	 	Или Модель                     = Неопределено Тогда
		
		Результат = Ложь;
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Не настроены параметры устройства.
        |Для корректной работы устройства необходимо задать параметры его работы.
        |Сделать это можно при помощи формы ""Настройка параметров"" модели
        |подключаемого оборудования в форме ""Подключение и настройка оборудования"".'
        |;uk='Не настроєні параметри пристрою.
        |Для коректної роботи пристрою необхідно задати параметри його роботи.
        |Зробити це можна за допомогою форми ""Настройка параметрів"" моделі
        |обладнання для підключення в формі ""Підключення та настройка обладнання"".'"));
	ИначеЕсли ТаблицаСоответствийНалоговыхГрупп = Неопределено Тогда
		Результат = Ложь;
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Не заполнены параметры торгового оборудования(параметры налогообложения).';uk='Не заповнені параметри торгового устаткування(параметри оподаткування).'"));
	КонецЕсли;
	// Конец: Проверка настроенных параметров

	Если Результат Тогда
		Если ПараметрыПодключения.ФРПодключен Тогда
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить(НСтр("ru='Фискальный регистратор уже подключен';uk='Фіскальний реєстратор вже підключений'"));
			Результат = Ложь;
		Иначе
			ПараметрыПодключения.ФРПодключен = Истина;

			ВыходныеПараметры.Добавить(""); // Источник событий
			ВыходныеПараметры.Добавить(Неопределено); // Список событий
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Функция осуществляет отключение устройства.
//
// Параметры:
//  ОбъектДрайвера - <*>
//         - ОбъектДрайвера драйвера торгового оборудования.
//
// Возвращаемое значение:
//  <Булево> - Результат работы функции.
//
Функция ОтключитьУстройство(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры) Экспорт

	Результат = Истина;

	//Обязательные выходные
	ВыходныеПараметры = Неопределено;

	ПараметрыПодключения.ФРПодключен = Ложь;

	Возврат Результат;

КонецФункции

// Функция получает, обрабатывает и перенаправляет на исполнение команду к драйверу
//
Функция ВыполнитьКоманду(Команда, ВходныеПараметры = Неопределено, ВыходныеПараметры = Неопределено,
                         ОбъектДрайвера, Параметры, ПараметрыПодключения) Экспорт

	Результат = Истина;

	ВыходныеПараметры = Новый Массив();

	// Открыть смену
	Если Команда = "OpenDay" 
		ИЛИ Команда = "OpenShift" 
									Тогда
									
		Результат = ОткрытьСмену(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);
	ИначеЕсли Команда = "CloseShift" ИЛИ Команда = "ЗакрытьСмену" Тогда
		Результат = НапечататьОтчетБезГашения(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);


	// Печать чека
	ИначеЕсли Команда = "PrintReceipt" Тогда
		Результат = ПечатьЧека(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВходныеПараметры,	ВыходныеПараметры);

	// Печать слип чека
	ИначеЕсли Команда = "PrintText" Тогда
		СтрокаТекста = ВходныеПараметры[0];
		Результат = ПечатьТекста(ОбъектДрайвера, Параметры, ПараметрыПодключения,
		                         СтрокаТекста, ВыходныеПараметры);

	// Печать чека внесения/выемки
	ИначеЕсли Команда = "Encash" Тогда
		ТипИнкассации = ВходныеПараметры[0];
		Сумма         = ВходныеПараметры[1];
		Результат = Инкассация(ОбъектДрайвера, Параметры, ПараметрыПодключения, ТипИнкассации, Сумма, ВыходныеПараметры);

	// Печать отчета без гашения
	ИначеЕсли Команда = "PrintXReport" Тогда
		Результат = НапечататьОтчетБезГашения(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);

	// Печать отчета с гашением
	ИначеЕсли Команда = "PrintZReport" Тогда
		Результат = НапечататьОтчетСГашением(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);
		
	// Открытие денежного ящика
	ИначеЕсли Команда = "OpenCashDrawer" Тогда
		Результат = ОткрытьДенежныйЯщик(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);

	// Печать переодического отчета по датам
	ИначеЕсли Команда = "PrintPReportDate" Тогда
		Результат = НапечататьПериодическийОтчетПоДатам(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВходныеПараметры, ВыходныеПараметры);
		
	// Печать периодического отчета по номерам
	ИначеЕсли Команда = "PrintPReportNumber" Тогда
		Результат = НапечататьПериодическийОтчетПоНомерам(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВходныеПараметры, ВыходныеПараметры);
		
	// Печать нулевого чека
	ИначеЕсли Команда = "PrintNullReceipt" Тогда
		Результат = НапечататьНулевойЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);
		
	// Печать отчета о проданных товарах
	ИначеЕсли Команда = "PrintSoldReport" Тогда
		Результат = НапечататьОтчетОПроданныхТоварах(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);
	// Открываем чек
	ИначеЕсли Команда = "OpenCheck" Тогда
		Результат = ОткрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, Ложь, Ложь, ВыходныеПараметры);
		
	// Указанная команда не поддерживается данным драйвером
	Иначе
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Команда ""%Команда%"" не поддерживается данным драйвером.';uk='Команда ""%Команда%"" не підтримується цим драйвером.'"));
		ВыходныеПараметры[1] = СтрЗаменить(ВыходныеПараметры[1], "%Команда%", Команда);
		Результат = Ложь;

	КонецЕсли;

	Возврат Результат;

КонецФункции

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Функция осуществляет открытие смены
Функция ОткрытьСмену(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;
	СтрокаЛога = "";

	СтрокаЛога = "----- " + НСтр("ru='Смена открыта';uk='Зміна відкрита'") + " -----";

	// Заполнение выходных параметров
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(ТекущаяДата());

	СообщитьПользователю(СтрокаЛога);

	Возврат Результат;

КонецФункции
                    
// Осуществляет печать фискального чека
Функция ПечатьЧека(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВходныеПараметры, ВыходныеПараметры)

	ПараметрыПодключения.СтрокаЛога = "";

	Результат = МенеджерОборудованияКлиентПереопределяемый.ПечатьЧека(ПодключаемоеОборудование1СФискальныйРегистраторЭмулятор,
		ОбъектДрайвера, Параметры, ПараметрыПодключения, ВходныеПараметры, ВыходныеПараметры);
		
	СообщитьПользователю(ПараметрыПодключения.СтрокаЛога);

	Возврат Результат;
	
КонецФункции

// Функция осуществляет печать слип-чека
Функция ПечатьТекста(ОбъектДрайвера, Параметры, ПараметрыПодключения,
                       СтрокаТекста, ВыходныеПараметры)

	Результат  = Истина;

	// Открываем чек
	Результат = ОткрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, Ложь, Ложь, ВыходныеПараметры);

	// Печатаем строки чека
	Если Результат Тогда
		Для НомерСтроки = 1 По СтрЧислоСтрок(СтрокаТекста) Цикл
			ВыделеннаяСтрока = СтрПолучитьСтроку(СтрокаТекста, НомерСтроки);
			Если Найти(ВыделеннаяСтрока, Символ(Параметры.КодСимволаЧастичногоОтреза)) > 0 Тогда
				ТаблицаОплат = Новый Массив();
				Результат = ЗакрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, ТаблицаОплат, ВыходныеПараметры);
				Результат = ОткрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, Ложь, Ложь, ВыходныеПараметры);
			Иначе
				Если НЕ НапечататьНефискальнуюСтроку(ОбъектДрайвера, Параметры, ПараметрыПодключения,
				                                     ВыделеннаяСтрока, ВыходныеПараметры) Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	// Закрываем чек
	Если Результат Тогда
		ТаблицаОплат = Новый Массив();
		Результат = ЗакрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, ТаблицаОплат, ВыходныеПараметры);
	КонецЕсли;

	СообщитьПользователю(ПараметрыПодключения.СтрокаЛога);
	
	Возврат Результат;

КонецФункции

// Функция осуществляет открытие нового чека.
//
Функция ОткрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, ЧекВозврата, ФискальныйЧек, ВыходныеПараметры) Экспорт

	Результат  = Истина;
	СтрокаЛога = "";

	мНомерСмены = 1;
	мНомерЧека  = 1;

	СтрокаЛога = СтрокаЛога + "===== " + ?(ЧекВозврата, НСтр("ru='Открытие чека возврата';uk='Відкриття чека повернення'"), НСтр("ru='Открытие чека продажи';uk='Відкриття чека продажу'")) + " =====" + Символы.ПС;

	СтрокаЛога = СтрокаЛога + "=====" + ?(ФискальныйЧек, "== " + НСтр("ru='Фискальный режим';uk='Фіскальний режим'"), " " + НСтр("ru='Нефискальный режим';uk='Нефіскальний режим'")) + "=====" + Символы.ПС;
	
	СтрокаЛога = СтрокаЛога + НСтр("ru='№ чека';uk='№ чека'")   + ": " + Формат(мНомерЧека,  "ЧЦ=4; ЧН=0; ЧВН=")
	           + "    "
	           + НСтр("ru='№ смены';uk='№ зміни'") + ": " + Формат(мНомерСмены, "ЧЦ=4; ЧН=0; ЧВН=") + Символы.ПС;

	НомерСмены = мНомерСмены;
	НомерЧека  = мНомерЧека;

	// Заполнение выходных параметров
	ВыходныеПараметры.Добавить(НомерСмены);
	ВыходныеПараметры.Добавить(НомерЧека);
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(ТекущаяДата());

	ПараметрыПодключения.Вставить("ФискальныйЧек", ФискальныйЧек);
	ПараметрыПодключения.Вставить("ЧекОткрыт"    , Истина);
	ПараметрыПодключения.Вставить("ИтогЧека"     , 0);

	ПараметрыПодключения.СтрокаЛога = ПараметрыПодключения.СтрокаЛога + СтрокаЛога;
	Возврат Результат;

КонецФункции

// Функция осуществляет печать фискальной строки.
//


Функция НапечататьФискальнуюСтроку(ОбъектДрайвера, Параметры, ПараметрыПодключения,
								   Наименование, Количество, Цена, Сумма,
                                   
                                   НомерСекции, СтавкаНДС, ДопРеквизиты, ВыходныеПараметры) Экспорт

	Результат = Истина;
	СтрокаЛога = "";

	Если Не ПараметрыПодключения.ЧекОткрыт Тогда
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Чек не был открыт';uk='Чек не був відкритий'"));
		Результат = Ложь;
	Иначе
		СтавкаНДССтрокой	= Неопределено;
		ЭтоПодакцизныйТовар = Неопределено;
		КодПоУКТВЭД 		= Неопределено;
		МассивАкцизныхМарок = Неопределено;
		ДопРеквизиты = МенеджерОборудованияВызовСервера.ПолучитьТаблицуДопРеквизитов(ДопРеквизиты);
		
		СтавкаНДССтрокой = ДопРеквизиты[0];
		ЭтоПодакцизныйТовар = ДопРеквизиты[1];
		
		Если ЗначениеЗаполнено(ДопРеквизиты[2]) Тогда
			КодПоУКТВЭД	= ДопРеквизиты[2];
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДопРеквизиты[3]) Тогда    
			МассивАкцизныхМарок = АкцизныеМаркиКлиентПереопределяемый.РазложитьСтрокуВМассивПодстрок(ДопРеквизиты[3]);
		КонецЕсли;
		// Печать строки чека
		СтрокаЛога = ".............................." + Символы.ПС;
		
		Если ЗначениеЗаполнено(КодПоУКТВЭД) Тогда
			СтрокаЛога = СтрокаЛога + НСтр("ru='Код по УКТВЭД';uk='Код по УКТЗЕД'") + ": " + КодПоУКТВЭД + Символы.ПС; 
		КонецЕсли;
		
		Если ЗначениеЗаполнено(МассивАкцизныхМарок) Тогда
			СтрокаЛога = СтрокаЛога + НСтр("ru='Штрихкод марки акцизного налога';uk='Штрихкод марки акцизного податку'") + ": " + Символы.ПС;
			
			Если МассивАкцизныхМарок.Количество() = 1 Тогда
				СтрокаЛога = СтрокаЛога + МассивАкцизныхМарок[0] + Символы.ПС;
			ИначеЕсли МассивАкцизныхМарок.Количество() > 1 Тогда
				Для Индекс = 0 по МассивАкцизныхМарок.ВГраница() Цикл
					СтрокаЛога = СтрокаЛога + МассивАкцизныхМарок[Индекс] + Символы.ПС;
				КонецЦикла;
			КонецЕсли;

		КонецЕсли;

		СтрокаЛога = СтрокаЛога + Наименование + Символы.ПС;
		СтрокаЛога = СтрокаЛога + Строка(Количество) + " X " + Строка(Цена) + " = " + Формат(Количество*Цена, "ЧЦ=15; ЧДЦ=2; ЧН=0; ЧГ=0") + Символы.ПС;
		

		СкидкаНаценка = Сумма - Цена*Количество;
		ПроцентСкидки = 100*Макс(СкидкаНаценка, -СкидкаНаценка)/(Цена*Количество);
		Если СкидкаНаценка < 0 Тогда
			СтрокаЛога = СтрокаЛога + НСтр("ru='Скидка';uk='Знижка'") + ": " + Формат(-СкидкаНаценка, "ЧЦ=15; ЧДЦ=2; ЧН=0; ЧГ=0")
					   + " (" + ПроцентСкидки + "%)" + Символы.ПС;
		Иначе
			СтрокаЛога = СтрокаЛога + НСтр("ru='Наценка';uk='Націнка'") + ": " + Формат(СкидкаНаценка, "ЧЦ=15; ЧДЦ=2; ЧН=0; ЧГ=0")
					   + " (" + ПроцентСкидки + "%)" + Символы.ПС;
		КонецЕсли;
		СтрокаЛога = СтрокаЛога + НСтр("ru='Отдел';uk='Відділ'") + ": №" + НомерСекции + Символы.ПС;
		Если СтавкаНДС <> Неопределено Тогда
		
			СтрокаЛога = СтрокаЛога + НСтр("ru='НДС';uk='ПДВ'") + ": " + СтавкаНДС + "%" + Символы.ПС;
		КонецЕсли;
		Если ЭтоПодакцизныйТовар <> Неопределено Тогда
			СтавкаАкциза = УчетАкцизногоНалога.ПолучитьСтавкуАкцизногоНалога(?(ЭтоПодакцизныйТовар=Истина, ПредопределенноеЗначение("Справочник.СтатьиНалоговыхДеклараций.Акциз_Розница_"), Неопределено));
			СтрокаЛога = СтрокаЛога + "Акциз: " + ?(СтавкаАкциза = Неопределено, "5", СтавкаАкциза*100) + "%" + Символы.ПС;
		КонецЕсли;
		Если СтавкаНДССтрокой <> Неопределено И ЭтоПодакцизныйТовар <> Неопределено Тогда
			НалоговаяГруппа = 0;
			ТаблицаСоответствий = Неопределено;
			Параметры.Свойство("ТаблицаСоответствийНалоговыхГрупп", ТаблицаСоответствий);
			Для каждого Строка из ТаблицаСоответствий Цикл
				Если Строка[1] = ДопРеквизиты[0] И Строка[2] = ДопРеквизиты[1] Тогда
					НалоговаяГруппа = Строка[0];
					Прервать;
				КонецЕсли;
			КонецЦикла;
			СтрокаЛога = СтрокаЛога + "НалоговаяГрупа: " + НалоговаяГруппа + Символы.ПС;
		КонецЕсли;
		СтрокаЛога = СтрокаЛога + ".............................." + Символы.ПС;
		ПараметрыПодключения.ИтогЧека = ПараметрыПодключения.ИтогЧека + Сумма;
	КонецЕсли;

	ПараметрыПодключения.СтрокаЛога = ПараметрыПодключения.СтрокаЛога + СтрокаЛога;
	Возврат Результат;

КонецФункции

// Функция осуществляет печать нефискальной строки.
//
Функция НапечататьНефискальнуюСтроку(ОбъектДрайвера, Параметры, ПараметрыПодключения, СтрокаТекста, ВыходныеПараметры) Экспорт

	Результат = Истина;

	ПараметрыПодключения.СтрокаЛога = ПараметрыПодключения.СтрокаЛога + СтрокаТекста + Символы.ПС;

	Возврат Результат;

КонецФункции

// Функция осуществляет закрытие ранее открытого чека.
//
Функция ЗакрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, ТаблицаОплат, ВыходныеПараметры) Экспорт

	Результат = Истина;
	СтрокаЛога = "";

	СуммаНаличнойОплаты     = 0;
	СуммаБезналичнойОплаты1 = 0;
	СуммаБезналичнойОплаты2 = 0;

	Для ИндексОплаты = 0 По ТаблицаОплат.Количество() - 1 Цикл
		Если ТаблицаОплат[ИндексОплаты][0].Значение = 0 Тогда
			СуммаНаличнойОплаты = СуммаНаличнойОплаты + ТаблицаОплат[ИндексОплаты][1].Значение;
		ИначеЕсли ТаблицаОплат[ИндексОплаты][0].Значение = 1 Тогда
			СуммаБезналичнойОплаты1 = СуммаБезналичнойОплаты1 + ТаблицаОплат[ИндексОплаты][1].Значение;
		Иначе
			СуммаБезналичнойОплаты2 = СуммаБезналичнойОплаты2 + ТаблицаОплат[ИндексОплаты][1].Значение;
		КонецЕсли;
	КонецЦикла;

	Если Не ПараметрыПодключения.ЧекОткрыт Тогда
		ВыходныеПараметры.Очистить();
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Чек не был открыт';uk='Чек не був відкритий'"));
		Результат = Ложь;
	Иначе
		Если ПараметрыПодключения.ФискальныйЧек Тогда
			СтрокаЛога = СтрокаЛога + НСтр("ru='ИТОГО';uk='РАЗОМ'") + ": " + Формат(ПараметрыПодключения.ИтогЧека, "ЧЦ=15; ЧДЦ=2; ЧН=0; ЧГ=0") + Символы.ПС;
			СтрокаЛога = СтрокаЛога + НСтр("ru='Оплачено наличными';uk='Оплачено готівкою'") + ": "      + СуммаНаличнойОплаты + Символы.ПС;
			СтрокаЛога = СтрокаЛога + НСтр("ru='Оплачено безналичными 1';uk='Оплачено безготівковими 1'") + ": " + СуммаБезналичнойОплаты1 + Символы.ПС;
			СтрокаЛога = СтрокаЛога + НСтр("ru='Оплачено безналичными 2';uk='Оплачено безготівковими 2'") + ": " + СуммаБезналичнойОплаты2 + Символы.ПС;
			СтрокаЛога = СтрокаЛога + НСтр("ru='Сдача';uk='Здача'") + ": "
			                        + Строка(СуммаНаличнойОплаты
			                               + СуммаБезналичнойОплаты1
			                               + СуммаБезналичнойОплаты2
			                               - ПараметрыПодключения.ИтогЧека) + Символы.ПС;
		КонецЕсли;
		СтрокаЛога = СтрокаЛога + "===== "
		                        + ?(ПараметрыПодключения.ФискальныйЧек,
		                            НСтр("ru='Закрытие фискального чека';uk='Закриття фіскального чека'"),
		                            НСтр("ru='Закрытие нефискального чека';uk='Закриття нефіскального чека'"))
		                        + " =====" + Символы.ПС;
		ПараметрыПодключения.ЧекОткрыт = Ложь;
	КонецЕсли;

	ПараметрыПодключения.СтрокаЛога = ПараметрыПодключения.СтрокаЛога + СтрокаЛога;
	Возврат Результат;

КонецФункции

// Функция осуществляет внесение или выемку суммы на ФР.
//
Функция Инкассация(ОбъектДрайвера, Параметры, ПараметрыПодключения, ТипИнкассации, Сумма, ВыходныеПараметры)

	Результат = Истина;

	СообщитьПользователю(?(ТипИнкассации = 1, НСтр("ru='Внесение на сумму';uk='Внесення на суму'"), НСтр("ru='Выемка на сумму';uk='Виймання на суму'")) + " " + Сумма);

	// Заполнение выходных параметров
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(ТекущаяДата());

	Возврат Результат;

КонецФункции

// Функция осуществляет снятие отчёта без гашения.
//
Функция НапечататьОтчетБезГашения(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;

	СообщитьПользователю(НСтр("ru='Печать отчета без гашения';uk='Друк звіту без гасіння'"));

	// Заполнение выходных параметров
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(ТекущаяДата());

	Возврат Результат;

КонецФункции

// Функция осуществляет снятие отчёта с гашением.
//
Функция НапечататьОтчетСГашением(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;

	СообщитьПользователю(НСтр("ru='Печать отчета с гашением';uk='Друк звіту з гасінням'") + Символы.ПС
	                      + "----- " + НСтр("ru='Смена закрыта';uk='Зміна закрита'") + " -----");

	// Заполнение выходных параметров
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(ТекущаяДата());

	Возврат Результат;

КонецФункции

// Функция осуществляет открытие денежного ящика
//
Функция ОткрытьДенежныйЯщик(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;

	СообщитьПользователю(НСтр("ru='Денежный ящик открыт';uk='Грошовий ящик відкритий'"));

	Возврат Результат;

КонецФункции


// Функция осуществляет печать периодического отчёта по датам.
//
Функция НапечататьПериодическийОтчетПоДатам(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВходныеПараметры, ВыходныеПараметры)

	Результат = Истина;

	СообщитьПользователю(НСтр("ru='Печать периодического отчета по датам, с даты ';uk='Друк періодичного звіту по датах, з дати '")+Формат(ВходныеПараметры[0], "ДФ=dd.MM.yyyy")+
						 НСтр("ru=' по дату ';uk=' за дату '")+Формат(ВходныеПараметры[1], "ДФ=dd.MM.yyyy"));

	// Заполнение выходных параметров
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(ТекущаяДата());

	Возврат Результат;

КонецФункции

// Функция осуществляет печать периодического отчёта по номерам дневных отчетов.
//
Функция НапечататьПериодическийОтчетПоНомерам(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВходныеПараметры, ВыходныеПараметры)

	Результат = Истина;

	СообщитьПользователю(НСтр("ru='Печать периодического отчета по номерам, с номера ';uk='Друк періодичного звіту за номерами, з номеру'")+ВходныеПараметры[0]+
						 НСтр("ru=' по номер ';uk=' за номер '")+ВходныеПараметры[1]);

	// Заполнение выходных параметров
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(ТекущаяДата());

	Возврат Результат;

КонецФункции

// Функция осуществляет печать нулевого чека.
//
Функция НапечататьНулевойЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	ПараметрыПодключения.СтрокаЛога = "";
	
	ТаблицаОплат = Новый Массив();
		                 
	Результат  = Истина;
	// Открываем чек
	Результат = ОткрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, Ложь, Истина, ВыходныеПараметры);

	// Печатаем строки чека   
	Если Результат Тогда
		ОшибкаПриПечатиСтроки = Ложь;

		Если НЕ ОшибкаПриПечатиСтроки Тогда
		  	// Закрываем чек
			Результат = ЗакрытьЧек(ОбъектДрайвера, Параметры, ПараметрыПодключения, ТаблицаОплат, ВыходныеПараметры);	
		Иначе
			Результат = Ложь;
		КонецЕсли;
		
	КонецЕсли;

	СообщитьПользователю(ПараметрыПодключения.СтрокаЛога);
	
	Возврат Результат;

КонецФункции

// Функция осуществляет печать отчёта о проданных товарах.
//
Функция НапечататьОтчетОПроданныхТоварах(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;

	СообщитьПользователю(НСтр("ru='Печать отчета о проданных товарах';uk='Друк звіту про продані товари'"));

	// Заполнение выходных параметров
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(1);
	ВыходныеПараметры.Добавить(0);
	ВыходныеПараметры.Добавить(ТекущаяДата());

	Возврат Результат;

КонецФункции

// Формирует и выводит сообщение 
//
Процедура СообщитьПользователю(ТекстСообщения)
	
	ПоказатьОповещениеПользователя(НСтр("ru='BAS Emulator';uk='BAS Emulator'"), , ТекстСообщения);
	
КонецПроцедуры
